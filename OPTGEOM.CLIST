PROC 24 MITER() UPDATE() E1ST() MOL() +
           A1() A2() A3() A4() A5() A6() A7() A8() A9() A10() +
           A11() A12() A13() A14() A15() A16() A17() A18() A19() A20()
CONTROL MSG LIST NOF
/* THESE TWO PARAMETERS CAN BE OVERRIDDEN IN THE ARGUMENT LIST
SET &PSIOWN = #ZA0BF9.
SET &T30 = SAVE
IF &MITER EQ &STR() | &DATATYPE(&MITER) NE NUM THEN DO
  WRITE OPTGEOM: YOU MUST GIVE THE MAXIMUM NUMBER OF ITERATIONS AND THE
  WRITE          UPDATE METHOD (BMAT, GNEXT, OR NEWTON) AND THE
  WRITE          THE NAME OF THE ENERGY 1ST DERIVATIVE PROCEDURE
  WRITE          FOLLOWED BY THE ARGUMENTS TO THE FIRST DERIVATIVE
  WRITE          PROCEDURE.  THE FIRST ARGUMENT TO THE FIRST DERIVATIVE
  WRITE          PROCEDURE IS ALWAYS THE NAME OF THE MOLECULE.
  EXIT CODE(877)
  END
IF &UPDATE NE BMAT && &UPDATE NE GNEXTS && &UPDATE NE NEWTON THEN DO
  WRITE OPTGEOM: AFTER THE NUMBER OF ITERATIONS YOU MUST GIVE THE
  WRITE          UPDATE METHOD (BMAT, GNEXTS, OR NEWTON),
  WRITE          THE NAME OF THE ENERGY 1ST DERIVATIVE PROCEDURE
  WRITE          FOLLOWED BY THE ARGUMENTS TO THE FIRST DERIVATIVE
  WRITE          PROCEDURE.  THE FIRST ARGUMENT TO THE FIRST DERIVATIVE
  WRITE          PROCEDURE IS ALWAYS THE NAME OF THE MOLECULE.
  EXIT CODE(877)
  END
IF &E1ST EQ &STR() THEN DO
  WRITE OPTGEOM: THE NAME OF THE ENERGY 1ST DERIVATIVE PROCEDURE MUST
  WRITE          BE GIVEN AS THE THIRD  ARGUMENT WHICH MUST BE FOLLOWED
  WRITE          THE ARGUMENTS TO THE 1ST DERIVATIVE PROCEDURE, THE
  WRITE          FIRST OF WHICH IS THE NAME OF THE MOLECULE.
  EXIT CODE(877)
  END
IF &MOL  EQ &STR() THEN DO
  WRITE OPTGEOM:  THE FIRST DERIVATIVE PROCEDURE ALWAYS REQUIRES THE
  WRITE          NAME OF THE MOLECULE, HENCE THE MOLECULE NAME MUST BE
  WRITE          THE FOURTH ARGUMENT TO OPTGEOM.
  EXIT CODE(877)
  END
SET &LP = &SUBSTR(1:1,&STR(()))
SET &RP = )
SET &ARGS = &STR()
SET &ITER = 1
DO WHILE &ITER LE 20
  IF &LENGTH(&A1) GT 6 THEN DO
    IF &SUBSTR(1:7,&A1) EQ &STR(PSIOWN&LP)  +
       && &SUBSTR(&LENGTH(&A1),&A1) EQ &STR(&RP)   THEN +
         SET &PSIOWN = &SUBSTR(8:&LENGTH(&A1)-1,&A1)
    END
  IF &A1 NE &STR() THEN SET &ARGS = &ARGS,&A1
  IF &LENGTH(&A1) GT 5 THEN DO
    IF    &SUBSTR(1:4,&A1)          EQ &STR(T30&LP)  +
       && &SUBSTR(&LENGTH(&A1),&A1) EQ &STR(&RP)   THEN +
         SET &T30 = &SUBSTR(5:&EVAL(&LENGTH(&A1)-1),&A1)
    END
  SET &A1 = &A2
  SET &A2 = &A3
  SET &A3 = &A4
  SET &A4 = &A5
  SET &A5 = &A6
  SET &A6 = &A7
  SET &A7 = &A8
  SET &A8 = &A9
  SET &A9 = &A10
  SET &A10 = &A11
  SET &A11 = &A12
  SET &A12 = &A13
  SET &A13 = &A14
  SET &A14 = &A15
  SET &A15 = &A16
  SET &A16 = &A17
  SET &A17 = &A18
  SET &A18 = &A19
  SET &A19 = &A20
  SET &ITER = &ITER + 1
  DOEND
 
/***************  HERE'S WHERE THE WORK GETS DONE *****************/
SET &ITER = 1
DO WHILE &ITER LE &MITER
  WRITE OPTGEOM: BEGINNING ITERATION NUMBER &ITER
  EX &PSIOWN.PSI.CLIST:&E1ST '&MOL,&ARGS'
  SET &E1STCC = &LASTCC
  WRITE OPTGEOM: FOR ITER &ITER &&E1STCC = &E1STCC
  IF &E1STCC NE 0 THEN EXIT CODE(&E1STCC)
 
  /* UPDATE THE GEOMETRY */
  IF &UPDATE EQ GNEXTS THEN DO
    EX &PSIOWN.PSI.CLIST:INTCOS '&MOL,T30(&T30)'
    IF &LASTCC NE 0 THEN EXIT CODE(&LASTCC)
    EX &PSIOWN.PSI.CLIST:GNEXTS  '&MOL,T30(&T30)'
    END
 
  IF &UPDATE EQ NEWTON THEN DO
    EX &PSIOWN.PSI.CLIST:INTCOS '&MOL,T30(&T30)'
    IF &LASTCC NE 0 THEN EXIT CODE(&LASTCC)
    EX &PSIOWN.PSI.CLIST:NEWTON '&MOL,T30(&T30)'
    IF &LASTCC NE 0 THEN EXIT CODE(&LASTCC)
    END
 
  IF &UPDATE EQ BMAT   THEN DO
    EX &PSIOWN.PSI.CLIST:BMWRTA '&MOL'
    IF &LASTCC NE 0 THEN EXIT CODE(&LASTCC)
    EX &PSIOWN.PSI.CLIST:BMATIN6 '&MOL'
    IF &LASTCC NE 0 THEN EXIT CODE(&LASTCC)
    EX &PSIOWN.PSI.CLIST:GEOMIU  '&MOL'
    IF &LASTCC NE 0 THEN EXIT CODE(&LASTCC)
    END
 
  SET &ITER = &ITER + 1
  DOEND
WRITE OPTGEOM: MAXIMUM NUMBER OF ITERATIONS REACHED
EXIT CODE(0)
