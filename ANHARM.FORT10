      PROGRAM ANHARM
C*********************************************************
c Moved to PSI distribution disk on 020389 - clj.
C*********************************************************
C***LAST UPDATED ON JANUARY 20, 1988 BY YUKIO YAMAGUCHI***
C*********************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION CC(360000),IA(1)
      DIMENSION I30(200),COORD(3,50)
      LOGICAL GEOINP,ISOMOL
      CHARACTER*1 RAXIS
      CHARACTER*25 RTYPE
      DIMENSION ATM(18)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB103/PARA,WAVE,CONST,CYCL,CONV
      COMMON/VIB104/AIXX,AIYY,AIZZ,SUMW
      COMMON/VIB105/FACT3,FACT4
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB107/ITOP,IVSGL,IVDBL,IVTRP
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB109/CLIMIT,FLIM1,FLIM2
      COMMON/VIB110/ISIGMA
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB202/R(1275)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      COMMON/VIB206/RAXIS(3),RTYPE(6)
      COMMON/VIB207/TABCD(3,3,3,3),PABC(3,3,3)
      COMMON/VIB208/TAABB(3,3),TABAB(3,3)
      COMMON/VIB209/SALP(3)
      EQUIVALENCE (CC,IA)
      DATA ATM / 1.007825D+00 , 4.00260D+00 , 7.01600D+00 ,
     *           9.01218D+00  , 11.00931D+00 , 12.00000D+00 ,
     *           14.00307D+00 , 15.99491D+00 , 18.99840D+00 ,
     *           19.99244D+00 , 22.9898D+00  , 23.98504D+00 ,
     *           26.98153D+00 , 27.97693D+00 , 30.97376D+00 ,
     *           31.97207D+00 , 34.95885D+00 , 39.948D+00      /
      DATA PH,BK,AVN / 6.626176D+00 , 1.380662D+00 , 6.022045D+00 /
      DATA A0,HE,EV / 0.52917706D+00 , 4.359813653D+00 , 1.6021892D+00 /
      DATA CL,TEMP / 2.99792458D+00 , 300.0D+00 /
      DATA PI,ALN / 3.1415926536D+00 , 2.3025850930D+00 /
    1 FORMAT(//,2X,' THE ANHARMONIC CONSTANTS PROGRAM'/)
    2 FORMAT(10I5)
    3 FORMAT(F20.10)
    4 FORMAT(//,2X,' PARAMETERS FOR THE CALCULATION'//
     * 2X,' ISOTOP = ',I5/
     * 2X,' IGEOMT = ',I5/
     * 2X,' ITHREE = ',I5/
     * 2X,' IFOUR  = ',I5/
     * 2X,' IFREQ  = ',I5/
     * 2X,' ICORIO = ',I5/
     * 2X,' IFERM1 = ',I5/
     * 2X,' IFERM2 = ',I5/
     * 2X,' ISIGMA = ',I5/
     * 2X,' IPRNT  = ',I5/
     * 2X,' NATOM  = ',I5/
     * 2X,' N3N    = ',I5/
     * 2X,' NATRI  = ',I5/
     * 2X,' N3TOT  = ',I5/
     * 2X,' N4TOT  = ',I5/)
    5 FORMAT(//,2X,' SCF ENERGY = ',F20.10/)
    6 FORMAT(//,2X,' MOLECULAR GEOMETRY (IN BOHR)'/
     1 5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
    7 FORMAT(4F20.10)
    8 FORMAT(2X,I7,5X,4F20.10)
    9 FORMAT(//,2X,' MOLECULAR GEOMETRY (IN A)'/
     1 5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
   10 FORMAT(F20.10)
   11 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   12 FORMAT('1',//,2X,' THE ANHARMONIC CONSTANTS PROGRAM'/)
C
      CALL TSTART(6)
      CALL NOUNFL
C
C   PHYSICAL CONSTANTS
      PARA=1.0D+00/AVN
      WAVE=1.0D+04*DSQRT(AVN)/(2.0D+00*PI*CL)
      CONST=1.0D+02*(PH*AVN)/(8.0D+00*PI*PI*CL)
      CYCL=1.0D+06*(PH*AVN)/(8.0D+00*PI*PI)
      CONV=1.0D-01*(PH*CL)/(2.0D+00*BK*TEMP)
C   ALAM IS IN AMU-1.A-2
      ALAM=4.0D-02*(PI*PI*CL)/(PH*AVN)
C   SQLAM IS IN AMU-1/2.A-1
      SQLAM=DSQRT(ALAM)
C   FACT3 IS IN CM-1
      FACT3=1.0D+06/(SQLAM*SQLAM*SQLAM*PH*CL)
C   FACT4 IS IN CM-1
      FACT4=1.0D+06/(ALAM*ALAM*PH*CL)
C
      INPUT=5
      ITAPE6=6
      ITAP15=15
      ITAP30=30
      MAXCOR=360000
C
      CALL LOCATE(INPUT,'# ANHARM #',IERR)
C
      WRITE(6,1)
C
C***INPUT PARAMETERS FOR THE CALCULATION***
C     ISOTOP IS A PARAMETER FOR ISOTOPIC MOLECULES
C     ISOTOP =0 AND 1 :USE REGULAR ATOMIC MASS
C     ISOTOP =2 OR MORE :NUMBER OF ISOTOPIC MOLECULES
C     IGEOMT IS A PARAMETER FOR INPUT DATA
C     IGEOMT =0 :READ IN GEOMETRY FROM TAPE 10
C     IGEOMT =1 :READ IN GEOMETRY FROM TAPE 5
C     ITHREE IS A PARAMETER FOR THIRD DERIVATIVES
C     ITHREE =0 :DO NOT READ IN THIRD DERIVATIVES
C     ITHREE =1 :READ IN THIRD DERIVATIVES
C     IFOUR IS A PARAMETER FOR FOURTH DERIVATIVES
C     IFOUR  =0 :DO NOT READ IN FOURTH DERIVATIVES
C     IFOUR  =1 :READ IN FOURTH DERIVATIVES
C     IFREQ  =0 :CALCULATE 3N-6(OR 3N-5) SETS OF ANHARMONIC CONSTANTS
C     IFREQ  =+ :CALCULATE IFREQ SETS OF ANHARMONIC CONSTANTS
C     ICORIO =0 :USE A DEFAULT VALU FOR CORIOLIS RESONANCE
C     ICORIO =1 :READ IN A THRESHOLD VALU FOR CORIOLIS RESONANCE
C     IFERM1 =0 :USE A DEFAULT VALU FOR TYPE 1 FERMI RESONANCE
C                TYPE1----W(I)=W(J)
C     IFERM1 =1 :READ IN A THRESHOLD VALU FOR TYPE 1 FERMI RESONANCE
C     IFERM2 =0 :USE A DEFAULT VALU FOR TYPE 2 FERMI RESONANCE
C                TYPE2---W(I)+W(J)=W(K)
C     IFERM2 =1 :READ IN A THRESHOLD VALU FOR TYPE 2 FERMI RESONANCE
C     ISIGMA =0 :USE A(0') VALUE TO EVALUATE SIGMA VALUE THAT IS NEEDED
C                FOR CENTRIFUGAL DISTORTION CONSTANTS
C     ISIGMA =1 :USE A(E)  VALUE TO EVALUATE SIGMA VALUE THAT IS NEEDED
C                FOR CENTRIFUGAL DISTORTION CONSTANTS
      READ(5,2) ISOTOP,IGEOMT,ITHREE,IFOUR,IFREQ,ICORIO,IFERM1,IFERM2,
     1          ISIGMA,IPRNT
C
      REWIND ITAP15
      READ(ITAP15,2) NATOM,NSTORE
      GEOINP=IGEOMT.NE.0
C
      IOFF(1)=0
      DO 101 I=1,149
      IOFF(I+1)=IOFF(I)+I
  101 CONTINUE
C
      IF(GEOINP) GO TO 301
      CALL RFILE(ITAP30)
      CALL WREADW(ITAP30,I30,200,101,JUNK)
      IEND=I30(1)
      MPOINT=I30(2)
      MCONST=I30(3)
      MCALCS=I30(4)
      NCALCS=I30(5)
      NATOM=I30(19)
      N3N=NATOM*3
      JUNK=101+MCONST
      CALL WREADW(ITAP30,IPT,1,JUNK,JUNK)
      CALL WREADW(ITAP30,CHG,NATOM*2,IPT,JUNK)
      IGEOP=100+MCONST+MPOINT+NCALCS
      CALL WREADW(ITAP30,LOCCAL,1,IGEOP,JUNK)
      IGEOP=LOCCAL+60
      CALL WREADW(ITAP30,I30,20,IGEOP,IGEOP)
      CALL WREADW(ITAP30,COORD,N3N*2,IGEOP,JUNK)
      JUNK=JUNK+INTOWP(1)
      CALL WREADW(ITAP30,ESCF,2,JUNK,JUNK)
      CALL RCLOSE(ITAP30,3)
C
  301 CONTINUE
      N3N=NATOM*3
      NATRI=IOFF(N3N+1)
      N3TOT=N3N*(N3N+1)*(N3N+2)/6
      N4TOT=N3N*(N3N+1)*(N3N+2)*(N3N+3)/24
C
C   SET AN ARRAY FOR ANHARMONICITY CALCULATION
      DO 102 I=1,N3N
  102 NFRQ(I)=I
      CALL LOCATE(INPUT,'# ANHARM #',IERR)
      READ(5,2) ISOTOP,IGEOMT,ITHREE,IFOUR,IFREQ,ICORIO,IFERM1,IFERM2,
     1          ISIGMA,IPRNT
      IF(ISOTOP.EQ.0) ISOTOP=1
      IF(IFREQ.NE.0) THEN
        NFREQ=IABS(IFREQ)
        READ(5,2) (NFRQ(I),I=1,NFREQ)
      END IF
      IF(ICORIO.NE.0) THEN
        READ(5,3) CLIMIT
      ELSE
        CLIMIT=100.0D+00
      END IF
      IF(IFERM1.NE.0) THEN
        READ(5,3) FLIM1
      ELSE
        FLIM1=100.0D+00
      END IF
      IF(IFERM2.NE.0) THEN
        READ(5,3) FLIM2
      ELSE
        FLIM2=100.0D+00
      END IF
C
      WRITE(6,4) ISOTOP,IGEOMT,ITHREE,IFOUR,IFREQ,ICORIO,IFERM1,IFERM2,
     1           ISIGMA,IPRNT,NATOM,N3N,NATRI,N3TOT,N4TOT
C
      WRITE(6,5) ESCF
      WRITE(6,6)
      DO 103 I=1,NATOM
      IF(GEOINP) GO TO 201
      X(I)=COORD(1,I)
      Y(I)=COORD(2,I)
      Z(I)=COORD(3,I)
      II=CHG(I)
      W(I)=ATM(II)
      GO TO 202
  201 CONTINUE
      READ(5,7) COORD(1,I),COORD(2,I),COORD(3,I),W(I)
      X(I)=COORD(1,I)
      Y(I)=COORD(2,I)
      Z(I)=COORD(3,I)
  202 CONTINUE
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  103 CONTINUE
      CALL DIST(1)
      WRITE(6,9)
      DO 104 I=1,NATOM
      X(I)=X(I)*A0
      Y(I)=Y(I)*A0
      Z(I)=Z(I)*A0
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  104 CONTINUE
      CALL DIST(1)
C
C   DYNAMIC ALLOCATION
C   IC1  : FX   (N3N*N3N)
C   IC2  : ELX  (N3N*N3N)
C   IC3  : ELXM (N3N*N3N)
C   IC4  : UX   (N3N*N3N)
C   IC5  : EPX  (N3N*N3N)
C   IC6  : EPXM (N3N*N3N)
C   IC7  : ZETA (N3N*N3N*3)
C   IC8  : AXY  (3*3*N3N)
C   IC9  : BXY  (3*3*N3N)
C   IC10 : CXY  (3*3*N3N)
C   IC11 : BXYZ (3*3*3*N3N)
C   IC12 : CXYZ (3*3*3*N3N)
C   IC13 : F33  (N3TOT)
C   IC14 : F3X  (N3N*N3N*N3N)
C   IC15 : F3Q  (N3N*N3N*N3N)
C   IC16 : F44  (N4TOT)
C   IC17 : F4X  (N3N*N3N*N3N*N3N)
C   IC18 : F4Q  (N3N*N3N*N3N*N3N)
C
C   DYNAMIC ALLOCATION OF CORE MEMORY AND CLEARANCE OF IT
      IC1=1
      IC2=IC1+N3N*N3N
      IC3=IC2+N3N*N3N
      IC4=IC3+N3N*N3N
      IC5=IC4+N3N*N3N
      IC6=IC5+N3N*N3N
      IC7=IC6+N3N*N3N
      IC8=IC7+N3N*N3N*3
      IC9=IC8+3*3*N3N
      IC10=IC9+3*3*N3N
      IC11=IC10+3*3*N3N
      IC12=IC11+3*3*3*N3N
      IC13=IC12+3*3*3*N3N
      IC14=IC13+N3TOT
      IC15=IC14+N3N*N3N*N3N
      IC16=IC15+N3N*N3N*N3N
      IC17=IC16+N4TOT
      IC18=IC17+N3N*N3N*N3N*N3N
      ICKEEP=IC18+N3N*N3N*N3N*N3N
      CALL ZERO(CC(1),ICKEEP)
C
C   FORM THE FORCE CONSTANT MATRIX
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN FCONST'/)
      ICMAX=ICKEEP
      WRITE(3,11) ICMAX,MAXCOR
C.................FX......
      CALL FCONST(CC(IC1))
C
C    CALCULATE THE CENTER OF MASS AND MOMENTS OF INERTIA
C    FOR THE PARENT MOLECULE
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN MOMENT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................UX      EE.......
      CALL MOMENT(CC(IC4),CC(IC19))
C
      ISOMOL=.FALSE.
      NISO=0
  400 NISO=NISO+1
      IF(NISO.EQ.1) GO TO 401
      IF(NISO.GT.ISOTOP) GO TO 220
      WRITE(6,12)
      ISOMOL=.TRUE.
C
C   READ IN ISOTOPIC ATOMIC MASS
      WRITE(6,6)
      DO 105 I=1,NATOM
      READ(5,10) W(I)
      X(I)=COORD(1,I)
      Y(I)=COORD(2,I)
      Z(I)=COORD(3,I)
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  105 CONTINUE
      CALL DIST(1)
      WRITE(6,9)
      DO 106 I=1,NATOM
      X(I)=X(I)*A0
      Y(I)=Y(I)*A0
      Z(I)=Z(I)*A0
      WRITE(6,8) I,X(I),Y(I),Z(I),W(I)
  106 CONTINUE
      CALL DIST(1)
C
C    CALCULATE THE CENTER OF MASS AND MOMENTS OF INERTIA
C    FOR AN ISOTOPIC MOLECULE
      CALL ZERO(CC(IC2),ICKEEP)
      WRITE(3,22)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................UX      EE.......
      CALL MOMENT(CC(IC4),CC(IC19))
C
C   THE NORMAL COORDINATE ANALYSIS
C
C   THE FX MATRIX METHOD
  401 CONTINUE
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN VIBFX'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N
      IC22=IC21+N3N
      IC23=IC22+N3N
      IC24=IC23+NATRI
      ICMAX=IC24+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C................FX      ELX     ELXM    UX      EPX     EPXM....
      CALL VIBFX(CC(IC1),CC(IC2),CC(IC3),CC(IC4),CC(IC5),CC(IC6),
C................EE       VALU     FV1      FV2      FXM      DD.......
     1           CC(IC19),CC(IC20),CC(IC21),CC(IC22),CC(IC23),CC(IC24),
C................CC.......
     2           CC(IC24))
C
C   CORIOLIS COUPLING CONSTANTS
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN CORIOL'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N*N3N
      ICMAX=IC21+N3N*N3N*3
      WRITE(3,11) ICMAX,MAXCOR
C.................EPXM    ZETA    EE       FF       AM.......
      CALL CORIOL(CC(IC6),CC(IC7),CC(IC19),CC(IC20),CC(IC21))
C
C   CALCULATE AXY MATRICES
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN ABCMAT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................EPXM    AXY     BXY     CXY      EE.......
      CALL ABCMAT(CC(IC6),CC(IC8),CC(IC9),CC(IC10),CC(IC19))
C
C   CALCULATE TAU MATRICES
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN TAUMAT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................AXY     BXY.....
      CALL TAUMAT(CC(IC8),CC(IC9))
C
C   CALCULATE BXYZ MATRICES
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN BCXYZ'/)
      ICMAX=ICKEEP
      WRITE(3,11) ICMAX,MAXCOR
C................ZETA    BXY     BXYZ     CXYZ.....
      CALL BCXYZ(CC(IC7),CC(IC9),CC(IC11),CC(IC12))
C
C   CALCULATE CUBIC FORCE CONSTANTS
      IF(ITHREE.EQ.0) GO TO 303
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN FORCE3'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N*N3N
      ICMAX=IC21+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ELX     F33      F3X      F3Q      EE       DD.......
      CALL FORCE3(CC(IC2),CC(IC13),CC(IC14),CC(IC15),CC(IC19),CC(IC20),
C.................GG.......
     1            CC(IC21))
C
C   CALCULATE PHI MATRICES
  303 CONTINUE
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN PHIMAT'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................BXY     CXY      BXYZ     CXYZ     F3Q      EE.......
      CALL PHIMAT(CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC15),CC(IC19))
C
C   CALCULATE QUARTIC FORCE CONSTANTS
      IF(IFOUR.EQ.0) GO TO 304
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN FORCE4'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      IC21=IC20+N3N*N3N
      ICMAX=IC21+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ELX     F44      F4X      F4Q      EE       DD.......
      CALL FORCE4(CC(IC2),CC(IC16),CC(IC17),CC(IC18),CC(IC19),CC(IC20),
C.................GG.......
     1            CC(IC21))
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C%%%ANHARMONICITY ANALYSIS %%%
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C   SELECT THE ROTATIONAL TYPE OF THE MOLECULE
  304 CONTINUE
      GO TO (501,502,503,504,504,505),ITOP
C
C:::::::::::::::::::::::
C:::DIATOMIC MOLECULE:::
C:::::::::::::::::::::::
  501 CONTINUE
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN DIATOM'/)
      IC19=ICKEEP
      ICMAX=IC19+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................F3Q      F4Q      EE.......
      CALL DIATOM(CC(IC15),CC(IC18),CC(IC19))
      GO TO 215
C
C::::::::::::::::::::::::::::::::
C:::LINEAR POLYATOMIC MOLECULE:::
C::::::::::::::::::::::::::::::::
  502 CONTINUE
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN LINEAR'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      ICMAX=IC20+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ZETA    CXY      F3Q      CLTYP....
      CALL LTYPLI(CC(IC7),CC(IC10),CC(IC15),CC(IC19))
C.................ZETA    AXY     F3Q      EE       RVCNST...
      CALL ROVLIR(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C.................BXY     CXY      F3Q      EE.......
      CALL CENTLI(CC(IC9),CC(IC10),CC(IC15),CC(IC19))
      IF(IVDBL.LE.1) GO TO 205
C.................F3Q      F4Q      CLTYP....
      CALL VLTYLI(CC(IC15),CC(IC18),CC(IC19))
  205 CONTINUE
C.................ZETA    F3Q      F4Q      XIJ      GIJ......
      CALL LINEAR(CC(IC7),CC(IC15),CC(IC18),CC(IC19),CC(IC20))
      GO TO 215
C
C:::::::::::::::::::::::::::::
C:::ASYMMETRIC TOP MOLECULE:::
C:::::::::::::::::::::::::::::
  503 CONTINUE
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN ASYTOP'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      ICMAX=IC20+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ZETA    AXY     F3Q      EE       RVCNST...
      CALL ROVASY(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C.................F3Q      EE.......
      CALL CENTAS(CC(IC15),CC(IC19))
C.................ZETA    F3Q      F4Q      XIJ......
      CALL ASYTOP(CC(IC7),CC(IC15),CC(IC18),CC(IC19))
      GO TO 215
C
C::::::::::::::::::::::::::::
C:::SYMMETRIC TOP MOLECULE:::
C::::::::::::::::::::::::::::
  504 CONTINUE
      WRITE(3,35)
   35 FORMAT(//,2X,' NOW YOU ARE IN SYMTOP'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      ICMAX=IC20+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ZETA    AXY     F3Q      EE       CLTYP....
      CALL LTYPSY(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C.................ZETA    AXY     F3Q      EE       RVCNST...
      CALL ROVSYM(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C.................F3Q      EE.......
      CALL CENTSY(CC(IC15),CC(IC19))
      IF(IVDBL.LE.1) GO TO 206
C.................ZETA    AXY     F3Q      F4Q      CLTYP....
      CALL VLTYSY(CC(IC7),CC(IC8),CC(IC15),CC(IC18),CC(IC19))
  206 CONTINUE
C.................ZETA    F3Q      F4Q      XIJ      GIJ......
      CALL SYMTOP(CC(IC7),CC(IC15),CC(IC18),CC(IC19),CC(IC20))
      GO TO 215
C
C::::::::::::::::::::::::::::
C:::SPHERICAL TOP MOLECULE:::
C::::::::::::::::::::::::::::
  505 CONTINUE
      WRITE(3,36)
   36 FORMAT(//,2X,' NOW YOU ARE IN SPHTOP'/)
      IC19=ICKEEP
      IC20=IC19+N3N*N3N
      ICMAX=IC20+N3N*N3N
      WRITE(3,11) ICMAX,MAXCOR
C.................ZETA    AXY     F3Q      EE       RVCNST...
      CALL ROVSPH(CC(IC7),CC(IC8),CC(IC15),CC(IC19),CC(IC20))
C.................F3Q      EE.......
      CALL CENTSP(CC(IC15),CC(IC19))
C.................ZETA    F3Q      F4Q      XIJ      GIJ......
      CALL SPHTOP(CC(IC7),CC(IC15),CC(IC18),CC(IC19),CC(IC20))
C
C##########################################
C   RETURN FOR ANOTHER ISOTOPIC MOLECULE
C##########################################
  215 CONTINUE
      GO TO 400
C
  220 CONTINUE
      CALL TSTOP(6)
      STOP
      END
      SUBROUTINE DIST(IPRNT)
      IMPLICIT REAL*8 (A-H,O-Z)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB202/R(1275)
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' INTERATOMIC DISTANCE MATRIX'/)
C
      IJ=0
      DO 101 I=1,NATOM
      DO 101 J=1,I
      IJ=IJ+1
      R(IJ)=A00
      IF(I.EQ.J) GO TO 101
      XD=X(I)-X(J)
      YD=Y(I)-Y(J)
      ZD=Z(I)-Z(J)
      R(IJ)=DSQRT(XD*XD+YD*YD+ZD*ZD)
  101 CONTINUE
      IF(IPRNT.EQ.0) GO TO 201
      WRITE(6,1)
      CALL PRINT(R,1275,NATOM,6)
  201 RETURN
      END
      SUBROUTINE FCONST(FX)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION FX(N3N,N3N)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      DATA HE,A0 / 4.359813653D+00 , 0.52917706D+00 /
      DATA ITAP15 / 15 /
    1 FORMAT(2I5)
    2 FORMAT(3F20.10)
    3 FORMAT(//,2X,' FX MATRIX (IN HARTREE/BOHR+2)'/)
    4 FORMAT(//,2X,' FX MATRIX (IN MDYN/A)'/)
C
C   FUNIT IS IN MDYN/A
      FUNIT=HE/(A0*A0)
C
C   READ IN ANALYTICAL SECOND DERIVATIVES
C   WITH RESPECT TO NUCLEAR COORDINATE
      REWIND ITAP15
      READ(ITAP15,1) NNATOM,NNSTOR
      READ(ITAP15,2) ((FX(I,J),J=1,N3N),I=1,N3N)
      REWIND ITAP15
      WRITE(6,3)
      CALL MATOUT(FX,N3N,N3N,N3N,N3N,6)
      DO 101 I=1,N3N
      DO 101 J=1,N3N
      FX(I,J)=FX(I,J)*FUNIT
  101 CONTINUE
      WRITE(6,4)
      CALL MATOUT(FX,N3N,N3N,N3N,N3N,6)
C
      RETURN
      END
      SUBROUTINE MOMENT(UX,EE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION UX(N3N,N3N),EE(N3N,N3N)
      DIMENSION T(6),PT(3),EG(3,3),FV1(3),FV2(3)
      CHARACTER*1 RAXIS
      CHARACTER*25 RTYPE
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB103/PARA,WAVE,CONST,CYCL,CONV
      COMMON/VIB104/AIXX,AIYY,AIZZ,SUMW
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB107/ITOP,IVSGL,IVDBL,IVTRP
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB206/RAXIS(3),RTYPE(6)
      DATA A00,ONE,TWO / 0.0D+00 , 1.0D+00 , 2.0D+00 /
      DATA A0 / 0.52917706D+00 /
      DATA DLIMIT / 1.0D-02 /
      DATA RLIMIT / 1.0D-05 /
    1 FORMAT(//,2X,' CENTER OF MASS'/2X,' CMX',11X,' CMY',11X,' CMZ'//
     1 2X,3(5X,F10.7)/)
    2 FORMAT(//,2X,' CARTESIAN COORDINATES W.R.T. CENTER OF MASS (IN A)'
     1/5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
    3 FORMAT(2X,I7,5X,4F20.10)
    4 FORMAT(//,2X,' INERTIA TENSOR'/)
    5 FORMAT(//,2X,' PRINCIPAL MOMENTS OF INERTIA AT EQUILIBRIUM GEOMETR
     1Y'/
     2 4X,5H AXIS,9X,11H IN AMU.A+2,9X,10H IN G.CM+2,10X,
     3 8H IN CM-1,15X,7H IN MHZ/38X,8H (*D+39)/)
    6 FORMAT(8X,A1,3X,3F20.10,F20.5)
    7 FORMAT(//,2X,' EIGENVECTOR OF INERTIA TENSOR'/)
    8 FORMAT(//,2X,' THE ROTATIONAL TYPE OF THIS ISOTOPIC MOLECULE  = ',
     1 2X,A25/)
    9 FORMAT(/,2X,' ASYMMETRY PARAMETERS'/
     1 2X,' B(P)    = ',F10.5/
     2 2X,' B(O)    = ',F10.5/
     3 2X,' KAPPA   = ',F10.5/)
   10 FORMAT(//,2X,' UX MATRIX'/)
   11 FORMAT(//,2X,' UX * UX(T) = E '/)
   12 FORMAT(//,2X,' CARTESIAN COORDINATES W.R.T. PRINCIPAL AXIS (IN BOH
     1R)'/5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
   13 FORMAT(//,2X,' CARTESIAN COORDINATES W.R.T. PRINCIPAL AXIS (IN A)'
     1/5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
C
C   THE CALCULATION OF CENTER OF MASS
      SUMW=A00
      SUMWX=A00
      SUMWY=A00
      SUMWZ=A00
      DO 101 I=1,NATOM
      SUMW=SUMW+W(I)
      SUMWX=SUMWX+W(I)*X(I)
      SUMWY=SUMWY+W(I)*Y(I)
      SUMWZ=SUMWZ+W(I)*Z(I)
  101 CONTINUE
      CMX=SUMWX/SUMW
      CMY=SUMWY/SUMW
      CMZ=SUMWZ/SUMW
      WRITE(6,1) CMX,CMY,CMZ
C   THE CARTESIAN COORDINATES W.R.T. CENTER OF MASS
      WRITE(6,2)
      DO 102 I=1,NATOM
      X(I)=X(I)-CMX
      Y(I)=Y(I)-CMY
      Z(I)=Z(I)-CMZ
      WRITE(6,3) I,X(I),Y(I),Z(I),W(I)
  102 CONTINUE
C
C   THE CALCULATION OF INERTIA TENSOR
      CALL ZERO(T,6)
      DO 104 I=1,NATOM
      T(1)=T(1)+W(I)*(Y(I)**2+Z(I)**2)
      T(3)=T(3)+W(I)*(Z(I)**2+X(I)**2)
      T(6)=T(6)+W(I)*(X(I)**2+Y(I)**2)
      T(2)=T(2)-W(I)*X(I)*Y(I)
      T(4)=T(4)-W(I)*Z(I)*X(I)
      T(5)=T(5)-W(I)*Y(I)*Z(I)
  104 CONTINUE
      AIXX=T(1)
      AIYY=T(3)
      AIZZ=T(6)
      ILIN=0
      IF(AIXX.LE.DLIMIT) ILIN=1
      IF(AIYY.LE.DLIMIT) ILIN=1
      IF(AIZZ.LE.DLIMIT) ILIN=1
      WRITE(6,4)
      CALL PRINT(T,6,3,6)
      NVIB=N3N-6+ILIN
C
C   THE CALCULATION OF PRINCIPAL MOMENTS OF INERTIA
      CALL RSP(3,3,6,T,PT,1,EG,FV1,FV2)
      CALL ZERO(ROTAA,3)
      CALL ZERO(ROTGC,3)
      CALL ZERO(ROTCM,3)
      CALL ZERO(ROTMH,3)
      WRITE(6,5)
      II=0
      DO 106 I=1,3
      PA=A00
      PB=A00
      PC=A00
      IF(DABS(PT(I)).LE.DLIMIT) GO TO 201
      II=II+1
      PA=PT(I)*PARA
      PB=CONST/PT(I)
      PC=CYCL/PT(I)
      ROTAA(II)=PT(I)
      ROTGC(II)=PA
      ROTCM(II)=PB
      ROTMH(II)=PC
  201 CONTINUE
      WRITE(6,6) RAXIS(I),PT(I),PA,PB,PC
  106 CONTINUE
      WRITE(6,7)
      CALL MATOUT(EG,3,3,3,3,6)
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C%  ASSIGN THE ROTATIONAL TYPE OF THE ISOTOPIC MOLECULE  %
C%  ITOP IS A PARAMETER FOR ROTATIONAL TOP               %
C%  ITOP  = 1  : DIATOMIC MOLECULE                       %
C%  ITOP  = 2  : LINEAR POLYATOMIC MOLECULE              %
C%  ITOP  = 3  : ASYMMETRIC TOP                          %
C%  ITOP  = 4  : SYMMETRIC TOP (PROLATE)                 %
C%  ITOP  = 5  : SYMMETRIC TOP (OBLATE)                  %
C%  ITOP  = 6  : SPHERICAL TOP                           %
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      ITOP=3
      IF(DABS(ROTAA(2)-ROTAA(3)).LE.RLIMIT) ITOP=4
      IF(DABS(ROTAA(1)-ROTAA(2)).LE.RLIMIT) ITOP=5
      IF(DABS(ROTAA(1)-ROTAA(2)).LE.RLIMIT.AND.
     *   DABS(ROTAA(2)-ROTAA(3)).LE.RLIMIT) ITOP=6
      IF(ILIN.NE.0) ITOP=2
      IF(NATOM.EQ.2) ITOP=1
      WRITE(6,8) RTYPE(ITOP)
C
C   ASSIGN PRINCIPAL AXIS
      IAXIS(1)=1
      IAXIS(2)=2
      IAXIS(3)=3
C
C   FORM UX MATRIX
      CALL ZERO(UX,N3N*N3N)
C
C%%%%%%%%%%%%%%%%%%
C%  FOR DIATOMIC  %
C%%%%%%%%%%%%%%%%%%
      IF(ITOP.EQ.1) THEN
        CALL ZERO(EG,9)
        EG(1,1)=ONE
        EG(2,2)=ONE
        EG(3,3)=ONE
      END IF
C
C   FOR LINEAR POLYATOMIC
      IF(ITOP.EQ.2) THEN
        DO 108 I=1,3
        TG=EG(I,1)
        EG(I,1)=EG(I,3)
        EG(I,3)=TG
  108 CONTINUE
      END IF
C
C%%%%%%%%%%%%%%%%%%%%%%%%
C%  FOR ASYMMETRIC TOP  %
C%%%%%%%%%%%%%%%%%%%%%%%%
C     FOR PROLATE TOP RAY'S KAPPA = -1
C     FOR OBLATE  TOP RAY'S KAPPA = +1
      IF(ITOP.EQ.3) THEN
        BP=(ROTCM(3)-ROTCM(2))/(ROTCM(1)*TWO-ROTCM(2)-ROTCM(3))
        BO=(ROTCM(1)-ROTCM(2))/(ROTCM(3)*TWO-ROTCM(2)-ROTCM(1))
        RAYK=(ROTCM(2)*TWO-ROTCM(1)-ROTCM(3))/(ROTCM(1)-ROTCM(3))
        WRITE(6,9) BP,BO,RAYK
C     FOR AN ASYMMETRIC TOP THE COORDINATE SYSTEM WILL BE ROTATED
C     TO SATISFY THE FOLLOWING RELATIONSHIPS
C       I(X)<I(Y)<I(Z)
C       I(A)<I(B)<I(C)
C         A >  B >  C
C
        IAXIS(1)=1
        IAXIS(2)=2
        IAXIS(3)=3
      END IF
C
C%%%%%%%%%%%%%%%%%%%%%%%
C%  FOR SYMMETRIC TOP  %
C%%%%%%%%%%%%%%%%%%%%%%%
C     FOR A SYMMETRIC TOP THE MOLECULAR AXIS IS ALONG THE Z AXIS
      IF(ITOP.EQ.4.OR.ITOP.EQ.5) THEN
        CALL ZERO(EG,9)
        EG(1,1)=ONE
        EG(2,2)=ONE
        EG(3,3)=ONE
      END IF
C     FOR A PROLATE TOP
C        I(X)<I(Y)=I(Z)
C        I(A)<I(B)=I(C)
C          A >  B =  C
      IF(ITOP.EQ.4) THEN
        IAXIS(1)=3
        IAXIS(2)=2
        IAXIS(3)=1
      END IF
C
C     FOR AN OBLATE TOP
C        I(X)=I(Y)<I(Z)
C        I(A)=I(B)<I(C)
C          A =  B >  C
      IF(ITOP.EQ.5) THEN
        IAXIS(1)=1
        IAXIS(2)=2
        IAXIS(3)=3
      END IF
C
C%%%%%%%%%%%%%%%%%%%%%%%
C%  FOR SPHERICAL TOP  %
C%%%%%%%%%%%%%%%%%%%%%%%
      IF(ITOP.EQ.6) THEN
        CALL ZERO(EG,9)
        EG(1,1)=ONE
        EG(2,2)=ONE
        EG(3,3)=ONE
        IAXIS(1)=1
        IAXIS(2)=2
        IAXIS(3)=3
      END IF
C
C   FORM THE UX MATRIX
      DO 111 IABC=1,NATOM
      IPOS=(IABC-1)*3
      DO 110 I=1,3
      DO 110 J=1,3
      II=IPOS+I
      JJ=IPOS+J
      UX(II,JJ)=EG(I,J)
  110 CONTINUE
  111 CONTINUE
      WRITE(6,10)
      CALL MATOUT(UX,N3N,N3N,N3N,N3N,6)
C
C   CHECK UNITARITY OF UX MATRIX
      CALL MTXMPY(UX,N3N,UX,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,11)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
C   THE CARTESIAN COORDINATES W.R.T. PRINCIPAL AXIS
      WRITE(6,12)
      DO 112 I=1,NATOM
      XT=EG(1,1)*X(I)+EG(2,1)*Y(I)+EG(3,1)*Z(I)
      YT=EG(1,2)*X(I)+EG(2,2)*Y(I)+EG(3,2)*Z(I)
      ZT=EG(1,3)*X(I)+EG(2,3)*Y(I)+EG(3,3)*Z(I)
      X(I)=XT
      Y(I)=YT
      Z(I)=ZT
      XT=XT/A0
      YT=YT/A0
      ZT=ZT/A0
      WRITE(6,3) I,XT,YT,ZT,W(I)
  112 CONTINUE
      WRITE(6,13)
      DO 113 I=1,NATOM
  113 WRITE(6,3) I,X(I),Y(I),Z(I),W(I)
C
      RETURN
      END
      SUBROUTINE VIBFX(FX,ELX,ELXM,UX,EPX,EPXM,EE,VALU,FV1,FV2,FXM,DD,
     1                 CC)
C   THE NORMAL COORDINATE ANALYSIS FOR CARTESIAN COORDINATE SYSTEM
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION FX(N3N,N3N),ELX(N3N,N3N),ELXM(N3N,N3N)
      DIMENSION UX(N3N,N3N),EPX(N3N,N3N),EPXM(N3N,N3N)
      DIMENSION EE(N3N,N3N),DD(N3N,N3N),CC(1)
      DIMENSION VALU(N3N),FV1(N3N),FV2(N3N),FXM(NATRI)
      DIMENSION IATOMX(10)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB107/ITOP,IVSGL,IVDBL,IVTRP
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
      DATA DLIMIT / 1.0D-02 /
      DATA ELIMIT / 1.0D-04 /
      DATA WLIMIT / 3.0D+00 /
      DATA XLIMIT / 1.0D-05 /
    1 FORMAT(//,2X,' THE NORMAL COORDINATE ANALYSIS BY FX MATRIX METHOD'
     1/)
    2 FORMAT(//,2X,' SECULAR EQUATIONS'/2X,' FXM MATRIX'/)
    3 FORMAT(//,2X,' *****************************'/
     1          2X,' ***VIBRATIONAL FREQUENCIES***'/
     2          2X,' *****************************')
    4 FORMAT(//,2X,' LX MATRIX'/)
    5 FORMAT(//,2X,' LX * LX(T) = M(-1)'/)
    6 FORMAT(//,2X,' LAMDA = LX(T) * FX * LX'/)
    7 FORMAT(//,2X,' LX'' MATRIX'/)
    8 FORMAT(//,2X,' LX'' * LX''(T) = M(-1)'/)
    9 FORMAT(//,2X,' LXM'' MATRIX'/)
   10 FORMAT(//,2X,' LXM'' * LXM''(T) = E'/)
   11 FORMAT(//,2X,' ANALYSIS OF VIBRATIONAL MODES'//
     1          2X,' NO.OF NON-DEGENERATE MODES    = ',I5/
     1          2X,' NO.OF DOUBLY-DEGENERATE MODES = ',I5/
     2          2X,' NO.OF TRIPLY-DEGENERATE MODES = ',I5/
     3          2X,' NO.OF TOTAL VIBRATIONAL MODES = ',I5/)
   12 FORMAT(//,2X,' ROTATED LXM'' MATRIX'/)
C
      IF(IPRNT.NE.0)
     *WRITE(6,1)
C
C   THE FORMATION OF THE SECULAR EQUATION
      DO 101 I=1,NATOM
  101 SQM(I)=DSQRT(W(I))
      DO 102 I=1,N3N
      II=I/3
      IF(I.GT.3*II) II=II+1
      DO 102 J=I,N3N
      JJ=J/3
      IF(J.GT.3*JJ) JJ=JJ+1
      IJ=I+IOFF(J)
      FXM(IJ)=(ONE/SQM(II))*FX(I,J)*(ONE/SQM(JJ))
  102 CONTINUE
      WRITE(6,2)
      CALL PRINT(FXM,NATRI,N3N,6)
C
C   THE NORMAL COORDINATE ANALYSIS
      WRITE(6,3)
      IF(ITOP.EQ.2) THEN
        NAA=IOFF(NATOM+1)
        IC1=1
        IC2=IC1+NAA
        IC3=IC2+NATOM
        IC4=IC3+NATOM*NATOM
        ICMAX=IC4+N3N
C
C...................                         FXMX    VALUX   ELXMX...
        CALL NORMLI(ELXM,EE,VALU,FV1,FV2,FXM,CC(IC1),CC(IC2),CC(IC3),
C...................NORD........
     1              CC(IC4),NAA)
      ELSE
        CALL NORMFX(ELXM,EE,VALU,FV1,FV2,FXM)
      END IF
C
C   THE CALCULATION OF LX MATRIX
      DO 103 I=1,N3N
      II=I/3
      IF(I.GT.3*II) II=II+1
      DO 103 J=1,N3N
      ELX(I,J)=(ONE/SQM(II))*ELXM(I,J)
  103 CONTINUE
      WRITE(6,4)
      CALL FRQOUT(ELX,FREQ,N3N,N3N,N3N,N3N,6)
      IF(IPRNT.LE.2) GO TO 201
      CALL MTXMPY(ELX,N3N,ELX,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,5)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
C   TRANSFORM FX MATRIX FOR A TEST
      CALL MTXMPY(ELX,N3N,FX,N3N,EE,N3N,DD,N3N,N3N,5)
      WRITE(6,6)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
C   THE CALCULATION OF LX' MATRIX
C==============================================
C===FOR ASYMMETRIC TOP AND  LINEAR MOLECULES===
C==============================================
  201 CONTINUE
      CALL MTXMPY(UX,N3N,ELX,N3N,EPX,N3N,33,N3N,N3N,2)
      WRITE(6,7)
      CALL FRQOUT(EPX,FREQ,N3N,N3N,N3N,N3N,6)
      CALL MTXMPY(EPX,N3N,EPX,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,8)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
C   THE CALCULATION OF LXM' MATRIX
      DO 104 I=1,N3N
      II=I/3
      IF(I.GT.3*II) II=II+1
      DO 104 J=1,N3N
      EPXM(I,J)=EPX(I,J)*SQM(II)
  104 CONTINUE
      WRITE(6,9)
      CALL FRQOUT(EPXM,FREQ,N3N,N3N,N3N,N3N,6)
      CALL MTXMPY(EPXM,N3N,EPXM,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,10)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
C   FIND DEGENERACY OF NORMAL MODES
  202 CONTINUE
      DO 105 I=1,N3N
      NDEG(I)=0
      DO 105 J=1,5
      NDAB(I,J)=0
  105 CONTINUE
      DO 107 I=1,NVIB
      FRQI=FREQ(I)
      IF(DABS(FRQI).LE.WLIMIT) GO TO 107
      II=0
      DO 106 J=1,NVIB
      FRQJ=FREQ(J)
      IF(DABS(FRQJ).LE.WLIMIT) GO TO 106
      IF(DABS(FRQI-FRQJ).GT.DLIMIT) GO TO 106
      II=II+1
      NDAB(I,II)=J
  106 CONTINUE
      NDEG(I)=II
  107 CONTINUE
C
C   FIND NUMBERS OF DEGENERATE PAIRS
      IVDBL=0
      IVTRP=0
      DO 108 I=1,N3N
      IF(NDEG(I).EQ.2) IVDBL=IVDBL+1
      IF(NDEG(I).EQ.3) IVTRP=IVTRP+1
  108 CONTINUE
      IVDBL=IVDBL/2
      IVTRP=IVTRP/3
      IVSGL=NVIB-IVDBL*2-IVTRP*3
      WRITE(6,11) IVSGL,IVDBL,IVTRP,NVIB
C
      IF(ITOP.EQ.2) GO TO 205
      IF(IVDBL.EQ.0) GO TO 205
C
C   ROTATE EIGENVECTORS FOR DEGENERATE MODES
C==================================
C===FOR A SYMMETRIC TOP MOLECULE===
C==================================
C   FIND ALL ATOMS ON THE C3V AXIS
      II=0
      DO 110 I=1,NATOM
      XYSUM=X(I)*X(I)+Y(I)*Y(I)
      IF(XYSUM.GT.XLIMIT) GO TO 110
      II=II+1
      IATOMX(II)=I
      WRITE(6,*) ' I,II,IATOMX',I,II,IATOMX(II)
  110 CONTINUE
      IAXTOT=II
      WRITE(6,*) ' IAXTOT',IAXTOT
C
      IASAV=0
      DO 120 IVIB=1,NVIB
      NN=NDEG(IVIB)
      IF(NN.NE.2) GO TO 120
      IA=NDAB(IVIB,1)
      IB=NDAB(IVIB,2)
      IF(IASAV.EQ.IA) GO TO 120
      IASAV=IA
      DO 111 I=1,IAXTOT
      IAX=(IATOMX(I)-1)*3+1
      EP1=EPXM(IAX,IA)
      EP2=EPXM(IAX,IB)
      DNORM=DSQRT(EP1*EP1+EP2*EP2)
      IF(DNORM.LT.ELIMIT) GO TO 111
      XNORM=ONE/DNORM
      EP1=-EP1*XNORM
      EP2= EP2*XNORM
      GO TO 203
  111 CONTINUE
  203 CONTINUE
      DO 113 I=1,N3N
      FV1(I)=EPXM(I,IA)
      FV2(I)=EPXM(I,IB)
  113 CONTINUE
      DO 114 I=1,N3N
      EPXM(I,IA)=EP1*FV1(I)-EP2*FV2(I)
      EPXM(I,IB)=EP2*FV1(I)+EP1*FV2(I)
  114 CONTINUE
      TESTX=EPXM(IAX,IA)+EPXM(IAX+1,IA)
      IF(TESTX.LT.A00) THEN
        DO 115 I=1,N3N
        EPXM(I,IA)=-EPXM(I,IA)
  115   CONTINUE
      END IF
      TESTX=EPXM(IAX,IB)+EPXM(IAX+1,IB)
      IF(TESTX.LT.A00) THEN
        DO 116 I=1,N3N
        EPXM(I,IB)=-EPXM(I,IB)
  116   CONTINUE
      END IF
  120 CONTINUE
      WRITE(6,12)
      CALL FRQOUT(EPXM,FREQ,N3N,N3N,N3N,N3N,6)
      CALL MTXMPY(EPXM,N3N,EPXM,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,10)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
C   THE CALCULATION OF LX' MATRIX
      DO 121 I=1,N3N
      II=I/3
      IF(I.GT.3*II) II=II+1
      DO 121 J=1,N3N
      EPX(I,J)=(ONE/SQM(II))*EPXM(I,J)
  121 CONTINUE
      WRITE(6,7)
      CALL FRQOUT(EPX,FREQ,N3N,N3N,N3N,N3N,6)
      CALL MTXMPY(EPX,N3N,EPX,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,8)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
      DO 122 I=1,N3N
      DO 122 J=1,N3N
      ELX(I,J)=EPX(I,J)
  122 CONTINUE
C
  205 CONTINUE
      RETURN
      END
      SUBROUTINE NORMLI(ELXM,EE,VALU,FV1,FV2,FXM,FXMX,VALUX,ELXMX,NORD,
     1                  NAA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ELXM(N3N,N3N),EE(N3N,N3N)
      DIMENSION VALU(N3N),FV1(N3N),FV2(N3N),FXM(NATRI)
      DIMENSION FXMX(NAA),VALUX(NATOM),ELXMX(NATOM,NATOM)
      DIMENSION NORD(1)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB103/PARA,WAVE,CONST,CYCL,CONV
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' FXMX MATRIX, IXYZ = ',I5/)
    2 FORMAT(//,2X,' ELXM MATRIX BEFORE SORTING'/)
    3 FORMAT(/,2X,' EIGENVALUES AND FREQUENCIES'/
     1 13X,8H IN A.U.,9X,8H IN CM-1/)
    4 FORMAT(2X,I5,F15.6,2X,F15.6)
    5 FORMAT(//,2X,' LXM MATRIX'/)
    6 FORMAT(//,2X,' LXM * LXM(T) = E '/)
C
C   THE CALCULATION OF EIGENVALUE AND EIGENVECTOR
      DO 105 IXYZ=1,3
C
      IIJJ=0
      DO 101 IABC=1,NATOM
      IADD=(IABC-1)*3
      IX=IADD+IXYZ
      DO 101 JABC=1,IABC
      JADD=(JABC-1)*3
      JX=JADD+IXYZ
      IJX=IOFF(IX)+JX
      IIJJ=IIJJ+1
      FXMX(IIJJ)=FXM(IJX)
  101 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1) IXYZ
      CALL PRINT(FXMX,NAA,NATOM,6)
  201 CONTINUE
      CALL RSP(NATOM,NATOM,NAA,FXMX,VALUX,1,ELXMX,FV1,FV2)
C
      DO 103 IABC=1,NATOM
      IADD=(IABC-1)*3
      IX=IADD+IXYZ
      VALU(IX)=VALUX(IABC)
      DO 103 JABC=1,NATOM
      JADD=(JABC-1)*3
      JX=JADD+IXYZ
      ELXM(IX,JX)=ELXMX(IABC,JABC)
  103 CONTINUE
  105 CONTINUE
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,2)
      CALL MATOUT(ELXM,N3N,N3N,N3N,N3N,6)
C
C   SORT EIGENVALUES AND EIGENVECTORS
  202 CONTINUE
      CALL REORDR(VALU,NORD,N3N)
      WRITE(6,3)
      DO 106 I=1,N3N
      ROOT(I)=VALU(I)
      IMAG(I)=0
      IF(VALU(I).LT.A00) IMAG(I)=1
      IF(VALU(I).LT.A00) VALU(I)=-VALU(I)
      AVAL=VALU(I)
      FREQ(I)=WAVE*DSQRT(AVAL)
  106 WRITE(6,4) I,ROOT(I),FREQ(I)
C
C   EIGENVECTOR CORRESPONDS TO LXM VECTOR
      DO 108 II=1,N3N
      NN=NORD(II)
      DO 107 I=1,N3N
  107 EE(I,II)=ELXM(I,NN)
  108 CONTINUE
      DO 109 I=1,N3N
      DO 109 J=1,N3N
      ELXM(I,J)=EE(I,J)
  109 CONTINUE
      WRITE(6,5)
      CALL FRQOUT(ELXM,FREQ,N3N,N3N,N3N,N3N,6)
C
C   THE EXAMINATION OF ABOVE CALCULATION
      IF(IPRNT.LE.2) GO TO 203
      CALL MTXMPY(ELXM,N3N,ELXM,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,6)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
  203 CONTINUE
      RETURN
      END
      SUBROUTINE ORDER(A,NORD,N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(N),NORD(N)
C
      DO 101 I=1,N
      NORD(I)=I
  101 CONTINUE
      IF(N.LE.1) GO TO 201
      N1=N-1
      DO 103 I=1,N1
      IR=I+1
      DO 102 J=IR,N
      IF(A(I).LT.A(J)) GO TO 102
      AA=A(I)
      A(I)=A(J)
      A(J)=AA
      NN=NORD(I)
      NORD(I)=NORD(J)
      NORD(J)=NN
  102 CONTINUE
  103 CONTINUE
  201 RETURN
C
      ENTRY REORDR(A,NORD,N)
      DO 105 I=1,N
  105 NORD(I)=I
      IF(N.LE.1) GO TO 205
      N1=N-1
      DO 107 I=1,N1
      IR=I+1
      DO 106 J=IR,N
      IF(A(I).GE.A(J)) GO TO 106
      AA=A(I)
      A(I)=A(J)
      A(J)=AA
      NN=NORD(I)
      NORD(I)=NORD(J)
      NORD(J)=NN
  106 CONTINUE
  107 CONTINUE
  205 CONTINUE
C
      RETURN
      END
      SUBROUTINE NORMFX(ELXM,EE,VALU,FV1,FV2,FXM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ELXM(N3N,N3N),EE(N3N,N3N)
      DIMENSION VALU(N3N),FV1(N3N),FV2(N3N),FXM(NATRI)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB103/PARA,WAVE,CONST,CYCL,CONV
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,28H EIGENVALUES AND EIGENVECTOR)
    2 FORMAT(/,2X,28H EIGENVALUES AND FREQUENCIES/
     1 13X,8H IN A.U.,9X,8H IN CM-1/)
    3 FORMAT(2X,I5,F15.6,2X,F15.6)
    4 FORMAT(//,2X,' LXM MATRIX'/)
    5 FORMAT(//,2X,' LXM * LXM(T) = E '/)
C
C   THE CALCULATION OF EIGENVALUE AND EIGENVECTOR
      CALL RSP(N3N,N3N,NATRI,FXM,VALU,1,ELXM,FV1,FV2)
C     WRITE(6,1)
      WRITE(6,2)
      DO 101 I=1,N3N
      II=N3N-I+1
      ROOT(I)=VALU(II)
      IMAG(I)=0
      IF(VALU(II).LT.A00) IMAG(I)=1
      IF(VALU(II).LT.A00) VALU(II)=-VALU(II)
      AVAL=VALU(II)
      FREQ(I)=WAVE*DSQRT(AVAL)
  101 WRITE(6,3) I,ROOT(I),FREQ(I)
C
C   EIGENVECTOR CORRESPONDS TO LXM VECTOR
      NN=N3N/2
      DO 102 I=1,N3N
      DO 102 J=1,NN
      JJ=N3N-J+1
      EJ=ELXM(I,J)
      EJJ=ELXM(I,JJ)
      ELXM(I,J)=EJJ
      ELXM(I,JJ)=EJ
  102 CONTINUE
      WRITE(6,4)
      CALL FRQOUT(ELXM,FREQ,N3N,N3N,N3N,N3N,6)
C
C   THE EXAMINATION OF ABOVE CALCULATION
      IF(IPRNT.LE.2) GO TO 201
      CALL MTXMPY(ELXM,N3N,ELXM,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,5)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE CORIOL(EPXM,ZETA,EE,FF,AM)
C   THE CORIOLIS COUPLING CONSTANTS
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*3 AXIS(3)
      DIMENSION EPXM(N3N,N3N),ZETA(N3N,N3N,3)
      DIMENSION EE(N3N,N3N),FF(N3N,N3N),AM(N3N,N3N,3)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB203/IOFF(150),IPRNT
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
      DATA AXIS / '(X)' , '(Y)' , '(Z)' /
    1 FORMAT(//,2X,' *************************************'/
     1          2X,' ***THE CORIOLIS COUPLING CONSTANTS***'/
     2          2X,' *************************************')
    2 FORMAT(//,2X,' LXM'' MATRIX'/)
    3 FORMAT(//,2X,' LXM'' * LXM''(T) = E'/)
    4 FORMAT(//,2X,' MALPH MATRIX',A3/)
    5 FORMAT(//,2X,' ZETA MATRIX ',A3/)
C
      WRITE(6,1)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,2)
      CALL MATOUT(EPXM,N3N,N3N,N3N,N3N,6)
      CALL MTXMPY(EPXM,N3N,EPXM,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,3)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
C
  201 CONTINUE
C****************************************************
C***FORMATION OF ZETA MATRICES                      *
C***AN ALTERNATE EXPRESSION (IN A SIMPLER MANNER)   *
C***GIVES THE SAME RESULTS                          *
C****************************************************
C*    CALL ZERO(AM,N3N*N3N*3)
C*    DO 102 I=1,NATOM
C*    IC=(I-1)*3
C*    IROW=IC+2
C*    ICOL=IC+3
C*    AM(IROW,ICOL,1)=ONE
C*    AM(ICOL,IROW,1)=-ONE
C*    IROW=IC+3
C*    ICOL=IC+1
C*    AM(IROW,ICOL,2)=ONE
C*    AM(ICOL,IROW,2)=-ONE
C*    IROW=IC+1
C*    ICOL=IC+2
C*    AM(IROW,ICOL,3)=ONE
C*    AM(ICOL,IROW,3)=-ONE
C*102 CONTINUE
C*    DO 105 KABC=1,3
C*    IF(IPRNT.LE.2) GO TO 202
C*    WRITE(6,4) AXIS(KABC)
C*    CALL MATOUT(AM(1,1,KABC),N3N,N3N,N3N,N3N,6)
C*202 CONTINUE
C*    CALL MTXMPY(EPXM,N3N,AM(1,1,KABC),N3N,FF,N3N,EE,N3N,N3N,5)
C*    WRITE(6,5) AXIS(KABC)
C*    CALL MATOUT(FF,N3N,N3N,N3N,N3N,6)
C*    DO 103 I=1,N3N
C*    DO 103 J=1,N3N
C*103 ZETA(I,J,KABC)=FF(I,J)
C*105 CONTINUE
C
C   FORM ZETA MATRICES USING AN ALTERNATE EXPRESSION
      CALL ZERO(ZETA,N3N*N3N*3)
      DO 107 IVIB=1,NVIB
      DO 107 JVIB=1,NVIB
      VALUX=A00
      VALUY=A00
      VALUZ=A00
      DO 106 K=1,NATOM
      IX=(K-1)*3+1
      IY=IX+1
      IZ=IX+2
      VALUX=VALUX+EPXM(IY,IVIB)*EPXM(IZ,JVIB)
     1           -EPXM(IZ,IVIB)*EPXM(IY,JVIB)
      VALUY=VALUY+EPXM(IZ,IVIB)*EPXM(IX,JVIB)
     1           -EPXM(IX,IVIB)*EPXM(IZ,JVIB)
      VALUZ=VALUZ+EPXM(IX,IVIB)*EPXM(IY,JVIB)
     1           -EPXM(IY,IVIB)*EPXM(IX,JVIB)
  106 CONTINUE
      ZETA(IVIB,JVIB,1)=VALUX
      ZETA(IVIB,JVIB,2)=VALUY
      ZETA(IVIB,JVIB,3)=VALUZ
  107 CONTINUE
      DO 108 IXYZ=1,3
      WRITE(6,5) AXIS(IXYZ)
      CALL MATOUT(ZETA(1,1,IXYZ),N3N,N3N,NVIB,NVIB,6)
  108 CONTINUE
C
      RETURN
      END
      SUBROUTINE ABCMAT(EPXM,AXY,BXY,CXY,EE)
C   FORM A (XY) MATRICES
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EPXM(N3N,N3N),AXY(3,3,N3N),BXY(3,3,N3N),CXY(3,3,N3N)
      DIMENSION EE(N3N,N3N)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      DATA PH,AVN / 6.626176D+00 , 6.022045D+00 /
      DATA PI,CL / 3.1415926536D+00 , 2.99792458D+00 /
      DATA A00,HALF,ONE,TWO / 0.0D+00 , 0.5D+00 , 1.0D+00 , 2.0D+00 /
      DATA WLIMIT / 1.0D-2 /
    1 FORMAT(//,2X,' ********************************************'/
     1          2X,' ***THE A (XY) , B (XY) AND C(XY) MATRICES***'/
     2          2X,' ********************************************')
    2 FORMAT(//,2X,' LXM'' MATRIX'/)
    3 FORMAT(//,2X,' LXM'' * LXM''(T) = E'/)
    4 FORMAT(//,2X,' CARTESIAN COORDINATES W.R.T. CENTER OF MASS (IN A)'
     1/5X,4H NO.,11X,2H X,18X,2H Y,18X,2H Z,18X,2H W/)
    5 FORMAT(2X,I7,5X,4F15.7)
    6 FORMAT(//,2X,' AXY MATRIX, IVIB = ',I5/)
    7 FORMAT(//,2X,' BXY MATRIX, IVIB = ',I5/)
    8 FORMAT(//,2X,' CXY MATRIX, IVIB = ',I5/)
    9 FORMAT(//,2X,' AXY MATRIX, IVIB = ',I5,18X,' BXY MATRIX, IVIB = ',
     1       I5,18X,' CXY MATRIX, IVIB = ',I5/)
C
      IF(IPRNT.GE.1)
     *WRITE(6,1)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,2)
      CALL MATOUT(EPXM,N3N,N3N,N3N,N3N,6)
      CALL MTXMPY(EPXM,N3N,EPXM,N3N,EE,N3N,EE,N3N,N3N,3)
      WRITE(6,3)
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
      WRITE(6,4)
      DO 101 I=1,NATOM
      WRITE(6,5) I,X(I),Y(I),Z(I),W(I)
  101 CONTINUE
C
C   FORM AXY MATRICES
C   ELEMENTS OF AXY MATRIX ARE IN AMU(1/2).A
  201 CONTINUE
      CALL ZERO(AXY,3*3*N3N)
      DO 105 IVIB=1,NVIB
      CALL ZERO(EE,N3N*N3N)
      DO 103 I=1,NATOM
      IX=(I-1)*3+1
      IY=IX+1
      IZ=IX+2
      EE(1,1)=EE(1,1)+SQM(I)*(Y(I)*EPXM(IY,IVIB)+Z(I)*EPXM(IZ,IVIB))*TWO
      EE(2,2)=EE(2,2)+SQM(I)*(Z(I)*EPXM(IZ,IVIB)+X(I)*EPXM(IX,IVIB))*TWO
      EE(3,3)=EE(3,3)+SQM(I)*(X(I)*EPXM(IX,IVIB)+Y(I)*EPXM(IY,IVIB))*TWO
      EE(1,2)=EE(1,2)-SQM(I)*(X(I)*EPXM(IY,IVIB)+Y(I)*EPXM(IX,IVIB))
      EE(1,3)=EE(1,3)-SQM(I)*(Z(I)*EPXM(IX,IVIB)+X(I)*EPXM(IZ,IVIB))
      EE(2,3)=EE(2,3)-SQM(I)*(Y(I)*EPXM(IZ,IVIB)+Z(I)*EPXM(IY,IVIB))
  103 CONTINUE
      EE(2,1)=EE(1,2)
      EE(3,1)=EE(1,3)
      EE(3,2)=EE(2,3)
      DO 104 I=1,3
      DO 104 J=1,3
      AXY(I,J,IVIB)=EE(I,J)
  104 CONTINUE
      IF(IPRNT.LE.2) GO TO 105
      IF(DABS(FREQ(IVIB)).LE.WLIMIT) GO TO 105
      WRITE(6,6) IVIB
      CALL MATOUT(AXY(1,1,IVIB),3,3,3,3,6)
  105 CONTINUE
C
C   BCONST IS IN CM-1
      BCONST=1.0D+03*((PH*AVN/CL)**(1.5D+00))/(16.0D+00*(PI**3))
C
C   FORM BXY AND CXY MATRICES
C   ELEMENTS OF BXY MATRIX ARE IN CM-1
C   ELEMENTS OF CXY MATRIX DO NOT HAVE UNITS
      CALL ZERO(BXY,3*3*N3N)
      CALL ZERO(CXY,3*3*N3N)
      DO 108 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      IF(DABS(FRQI).LE.WLIMIT) GO TO 108
      FRQIH=DSQRT(FRQI)
      DO 107 IA=1,3
      DO 106 IB=1,3
      IAA=IAXIS(IA)
      IBB=IAXIS(IB)
      DENAB=ROTAA(IAA)*ROTAA(IBB)
      IF(DABS(DENAB).LE.WLIMIT) GO TO 106
      DENAB=-DENAB*FRQIH
      BXY(IA,IB,IVIB)=BCONST*AXY(IA,IB,IVIB)/DENAB
      CXY(IA,IB,IVIB)=-BXY(IA,IB,IVIB)/FRQI
  106 CONTINUE
  107 CONTINUE
      IF(IPRNT.LE.2) GO TO 108
      WRITE(6,7) IVIB
      CALL MATOUT(BXY(1,1,IVIB),3,3,3,3,6)
      WRITE(6,8) IVIB
      CALL MATOUT(CXY(1,1,IVIB),3,3,3,3,6)
  108 CONTINUE
C
      IF(IPRNT.LE.1) GO TO 202
      DO 110 IVIB=1,NVIB
      WRITE(6,9) IVIB,IVIB,IVIB
      CALL MX3OUT(AXY(1,1,IVIB),3,BXY(1,1,IVIB),3,CXY(1,1,IVIB),3,3,6)
  110 CONTINUE
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE TAUMAT(AXY,BXY)
C   THE CALCULATION OF CENTRIFUGAL DISTORTION CONSTANTS
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION AXY(3,3,N3N),BXY(3,3,N3N)
      DIMENSION TAABB(3,3),TABAB(3,3)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      COMMON/VIB207/TABCD(3,3,3,3),PABC(3,3,3)
      DATA WLIMIT / 1.0D-2 /
      DATA PH,AVN / 6.626176D+00 , 6.022045D+00 /
      DATA PI,CL / 3.1415926536D+00 , 2.99792458D+00 /
      DATA A00,HALF,ONE,TWO / 0.0D+00 , 0.5D+00 , 1.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' *****************************'/
     1          2X,' ***THE TAU (ABCD) MATRICES***'/
     2          2X,' *****************************')
    2 FORMAT(//,2X,' AXY MATRIX, IVIB = ',I5/)
    3 FORMAT(//,2X,' BXY MATRIX, IVIB = ',I5/)
    4 FORMAT(//,2X,' TAABB MATRIX FROM AXY MATRICES (IN CM-1)',
     1          2X,' TABAB MATRIX FROM AXY MATRICES (IN CM-1)'/)
    5 FORMAT(//,2X,' TAABB MATRIX FROM BXY MATRICES (IN CM-1)',
     1          2X,' TABAB MATRIX FROM BXY MATRICES (IN CM-1)'/)
C
      WRITE(6,1)
C   TCONST IS IN CM-1
      TCONST=1.0D+06*((PH*AVN/CL)**3)/(128.0D+00*(PI**6))
C
C   ELEMENTS OF AXY MATRIX ARE IN AMU(1/2).A
      IF(IPRNT.LE.2) GO TO 201
      DO 101 IVIB=1,NVIB
      WRITE(6,2) IVIB
      CALL MATOUT(AXY(1,1,IVIB),3,3,3,3,6)
  101 CONTINUE
C
C   ELEMENTS OF BXY MATRIX ARE IN CM-1
      DO 102 IVIB=1,NVIB
      WRITE(6,3) IVIB
      CALL MATOUT(BXY(1,1,IVIB),3,3,3,3,6)
  102 CONTINUE
C
C   FORM TABCD MATRIX USING AXY MATRICES
C   ELEMENTS OF TABCD MATRIX ARE IN CM-1
  201 CONTINUE
      CALL ZERO(TABCD,81)
      DO 105 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      IF(DABS(FRQI).LE.WLIMIT) GO TO 105
      FRQI2=FRQI*FRQI
      DO 103 IA=1,3
      DO 103 IB=1,3
      DO 103 IC=1,3
      DO 103 ID=1,3
      IAA=IAXIS(IA)
      IBB=IAXIS(IB)
      ICC=IAXIS(IC)
      IDD=IAXIS(ID)
      DABCD=ROTAA(IAA)*ROTAA(IBB)*ROTAA(ICC)*ROTAA(IDD)
      IF(DABS(DABCD).LE.WLIMIT) GO TO 103
      FABCD=TCONST*(AXY(IA,IB,IVIB)*AXY(IC,ID,IVIB))/DABCD
      TABCD(IA,IB,IC,ID)=TABCD(IA,IB,IC,ID)-FABCD/FRQI2
  103 CONTINUE
  105 CONTINUE
      DO 106 IA=1,3
      DO 106 IB=1,3
      TAABB(IA,IB)=TABCD(IA,IA,IB,IB)
      TABAB(IA,IB)=TABCD(IA,IB,IA,IB)
  106 CONTINUE
      WRITE(6,4)
      CALL MX2OUT(TAABB,3,TABAB,3,3,6)
C
C   FORM TABCD USING BXY MATRICES
C   ELEMENTS OF TABCD MATRIX ARE IN CM-1
      CALL ZERO(TABCD,81)
      DO 108 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      IF(DABS(FRQI).LE.WLIMIT) GO TO 108
      DO 107 IA=1,3
      DO 107 IB=1,3
      DO 107 IC=1,3
      DO 107 ID=1,3
      FABCD=-TWO*BXY(IA,IB,IVIB)*BXY(IC,ID,IVIB)/FRQI
      TABCD(IA,IB,IC,ID)=TABCD(IA,IB,IC,ID)+FABCD
  107 CONTINUE
  108 CONTINUE
      DO 109 IA=1,3
      DO 109 IB=1,3
      TAABB(IA,IB)=TABCD(IA,IA,IB,IB)
      TABAB(IA,IB)=TABCD(IA,IB,IA,IB)
  109 CONTINUE
      WRITE(6,5)
      CALL MX2OUT(TAABB,3,TABAB,3,3,6)
C
      RETURN
      END
      SUBROUTINE BCXYZ(ZETA,BXY,BXYZ,CXYZ)
C   THE CALCULATION OF CENTRIFUGAL DISTORTION CONSTANTS
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ZETA(N3N,N3N,3),BXY(3,3,N3N)
      DIMENSION BXYZ(3,3,3,N3N),CXYZ(3,3,3,N3N)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      COMMON/VIB207/TABCD(3,3,3,3),PABC(3,3,3)
      DATA WLIMIT / 1.0D-2 /
      DATA PH,AVN / 6.626176D+00 , 6.022045D+00 /
      DATA PI,CL / 3.1415926536D+00 , 2.99792458D+00 /
      DATA A00,HALF,ONE,TWO / 0.0D+00 , 0.5D+00 , 1.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' **************************************'/
     1          2X,' ***THE B (XYZ) AND C (XYZ) MATRICES***'/
     2          2X,' **************************************')
    2 FORMAT(//,2X,' BXY MATRIX, IVIB = ',I5/)
    3 FORMAT(//,2X,' BXYZ (X) MATRIX (IN D-06 CM-1), IVIB = ',I2,
     1          1X,' BXYZ (Y) MATRIX (IN D-06 CM-1), IVIB = ',I2,
     2          1X,' BXYZ (Z) MATRIX (IN D-06 CM-1), IVIB = ',I2/)
    4 FORMAT(//,2X,' CXYZ (X) MATRIX (IN D-08 CM-1), IVIB = ',I2,
     1          1X,' CXYZ (Y) MATRIX (IN D-08 CM-1), IVIB = ',I2,
     2          1X,' CXYZ (Z) MATRIX (IN D-08 CM-1), IVIB = ',I2/)
C
      IF(IPRNT.GE.1)
     *WRITE(6,1)
      IXX=1
      IYY=2
      IZZ=3
C
C   ELEMENTS OF BXY MATRIX ARE IN CM-1
C   ELEMENTS OF CXY MATRIX ARE IN NO UNITS
      IF(IPRNT.LE.2) GO TO 201
      DO 101 IVIB=1,NVIB
      WRITE(6,2) IVIB
      CALL MATOUT(BXY(1,1,IVIB),3,3,3,3,6)
  101 CONTINUE
C
C   FORM BXYZ MATRICES
C   ELEMENTS OF BXYZ MATRIX ARE IN CM(-1/2)
  201 CONTINUE
      CALL ZERO(BXYZ,3*3*3*N3N)
      CALL ZERO(CXYZ,3*3*3*N3N)
      DO 110 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      FRQI2=FRQI*FRQI
      FRQIH=DSQRT(FRQI)
C
C   B(IA,IA,IA,IVIB) AND CXYZ(IA,IA,IA,IVIB) TERMS
      DO 103 IA=1,3
      VALU=A00
      DO 102 JVIB=1,NVIB
      FRQJ=FREQ(JVIB)
      FRQJ2=FRQJ*FRQJ
      FRQJH=DSQRT(FRQJ)
      IAA=IAXIS(IA)
      FACT=BXY(IA,IA,JVIB)*ROTCM(IAA)*ZETA(JVIB,IVIB,IA)
      VALU=VALU+(FACT*FRQJH)*(ONE/FRQJ2+TWO/FRQI2)
  102 CONTINUE
      BXYZ(IA,IA,IA,IVIB)=VALU
      CXYZ(IA,IA,IA,IVIB)=-VALU/FRQIH
  103 CONTINUE
C
C   BXYZ(IA,IA,IB,IVIB) AND CXYZ(IA,IA,IB,IVIB) TERMS
      DO 105 IA=1,3
      DO 105 IB=1,3
      IF(IA.EQ.IB) GO TO 105
      VALU=A00
      DO 104 JVIB=1,NVIB
      FRQJ=FREQ(JVIB)
      FRQJ2=FRQJ*FRQJ
      FRQJH=DSQRT(FRQJ)
      IAA=IAXIS(IA)
      IBB=IAXIS(IB)
      FACT=BXY(IA,IA,JVIB)*ROTCM(IBB)*ZETA(JVIB,IVIB,IB)
     1    +BXY(IA,IB,JVIB)*ROTCM(IAA)*ZETA(JVIB,IVIB,IA)*TWO
      VALU=VALU+(FACT*FRQJH)*(ONE/FRQJ2+TWO/FRQI2)
  104 CONTINUE
      BXYZ(IA,IA,IB,IVIB)=VALU
      BXYZ(IA,IB,IA,IVIB)=VALU
      BXYZ(IB,IA,IA,IVIB)=VALU
      CXYZ(IA,IA,IB,IVIB)=-VALU/FRQIH
      CXYZ(IA,IB,IA,IVIB)=-VALU/FRQIH
      CXYZ(IB,IA,IA,IVIB)=-VALU/FRQIH
  105 CONTINUE
C
C   BXYZ(IA,IB,IC,IVIB) AND CXYZ(IA,IB,IC,IVIB) TERMS
      VALU=A00
      IAA=IAXIS(1)
      IBB=IAXIS(2)
      ICC=IAXIS(3)
      DO 106 JVIB=1,NVIB
      FRQJ=FREQ(JVIB)
      FRQJ2=FRQJ*FRQJ
      FRQJH=DSQRT(FRQJ)
      FACT=BXY(IYY,IZZ,JVIB)*ROTCM(IAA)*ZETA(JVIB,IVIB,IXX)
     1    +BXY(IZZ,IXX,JVIB)*ROTCM(IBB)*ZETA(JVIB,IVIB,IYY)
     2    +BXY(IXX,IYY,JVIB)*ROTCM(ICC)*ZETA(JVIB,IVIB,IZZ)
      VALU=VALU+(FACT*FRQJH)*(ONE/FRQJ2+TWO/FRQI2)
  106 CONTINUE
      BXYZ(IXX,IYY,IZZ,IVIB)=VALU
      BXYZ(IXX,IZZ,IYY,IVIB)=VALU
      BXYZ(IYY,IXX,IZZ,IVIB)=VALU
      BXYZ(IYY,IZZ,IXX,IVIB)=VALU
      BXYZ(IZZ,IXX,IYY,IVIB)=VALU
      BXYZ(IZZ,IYY,IXX,IVIB)=VALU
      CXYZ(IXX,IYY,IZZ,IVIB)=-VALU/FRQIH
      CXYZ(IXX,IZZ,IYY,IVIB)=-VALU/FRQIH
      CXYZ(IYY,IXX,IZZ,IVIB)=-VALU/FRQIH
      CXYZ(IYY,IZZ,IXX,IVIB)=-VALU/FRQIH
      CXYZ(IZZ,IXX,IYY,IVIB)=-VALU/FRQIH
      CXYZ(IZZ,IYY,IXX,IVIB)=-VALU/FRQIH
C
  110 CONTINUE
      IF(IPRNT.LE.1) GO TO 202
      DO 115 IVIB=1,NVIB
      DO 113 IA=1,3
      DO 113 IB=1,3
      DO 113 IC=1,3
      BXYZ(IA,IB,IC,IVIB)=BXYZ(IA,IB,IC,IVIB)*1.0D+06
      CXYZ(IA,IB,IC,IVIB)=CXYZ(IA,IB,IC,IVIB)*1.0D+08
  113 CONTINUE
      WRITE(6,3) IVIB,IVIB,IVIB
      CALL MX3OUT(BXYZ(1,1,1,IVIB),3,BXYZ(1,1,2,IVIB),3,
     1            BXYZ(1,1,3,IVIB),3,3,6)
      WRITE(6,4) IVIB,IVIB,IVIB
      CALL MX3OUT(CXYZ(1,1,1,IVIB),3,CXYZ(1,1,2,IVIB),3,
     1            CXYZ(1,1,3,IVIB),3,3,6)
      DO 114 IA=1,3
      DO 114 IB=1,3
      DO 114 IC=1,3
      BXYZ(IA,IB,IC,IVIB)=BXYZ(IA,IB,IC,IVIB)/1.0D+06
      CXYZ(IA,IB,IC,IVIB)=CXYZ(IA,IB,IC,IVIB)/1.0D+08
  114 CONTINUE
  115 CONTINUE
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE FORCE3(ELX,F33,F3X,F3Q,EE,DD,GG)
C   THE THIRD ORDER ANHARMONICITY ANALYSIS
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ELX(N3N,N3N)
      DIMENSION F33(N3TOT),F3X(N3N,N3N,N3N),F3Q(N3N,N3N,N3N)
      DIMENSION EE(N3N,N3N),DD(N3N,N3N),GG(N3N,N3N)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB105/FACT3,FACT4
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      DATA A00 / 0.0D+00 /
      DATA ITAP20 / 20 /
      DATA A0,HE / 0.52917706D+00 , 4.359813653D+00 /
      DATA WLIMIT / 3.0D+00 /
    1 FORMAT(//,2X,' THE THIRD ORDER ANHARMONICITY ANALYSIS'/)
    2 FORMAT(2I5)
    3 FORMAT(3F20.10)
    4 FORMAT(//,2X,' *************************************************'/
     1          2X,' ***THIRD DERIVATIVES W.R.T. NORMAL COORDINATES***'/
     2          2X,' *************************************************')
    5 FORMAT(//,2X,' F3X MATRIX (IN HARTREE/BOHR+3), KABC = ',I5/)
    6 FORMAT(//,2X,' F3X MATRIX (IN MDYN/A+2), KABC = ',I5/)
    7 FORMAT(//,2X,' LX(T) * F3X * LX MATRIX, KABC = ',I5/)
    8 FORMAT(//,2X,' F3(QI*QJ*QK) MATRIX (IN MDYN/(A+2.AMU-3/2)),  QVIB
     1=',I3,2X,', IVIB = ',I3/)
    9 FORMAT(//,2X,' F3(QI*QJ*QK) MATRIX (IN CM-1),  QVIB = ',I3,2X,
     1      ', IVIB = ',I3/)
C
      IF(IPRNT.NE.0)
     *WRITE(6,1)
C
C   FIND CONVERSION FACTORS
C   FUNIT3 IS IN MDYN/A+2
      FUNIT3=HE/(A0*A0*A0)
C
C   READ IN THIRD DERIVATIVES
      REWIND ITAP20
      READ(ITAP20,2) NNATOM,NF3
      READ(ITAP20,3) (F33(I),I=1,N3TOT)
      REWIND ITAP20
C
C   TRANSFORM THIRD DERIVATIVES
C   FROM CARTESIAN TO NORMAL COORDINATE SYSTEM
      WRITE(6,4)
      LABC=0
      DO 101 IABC=1,N3N
      DO 101 JABC=1,IABC
      DO 101 KABC=1,JABC
      LABC=LABC+1
      VAL=F33(LABC)
      F3X(IABC,JABC,KABC)=VAL
      F3X(IABC,KABC,JABC)=VAL
      F3X(JABC,IABC,KABC)=VAL
      F3X(JABC,KABC,IABC)=VAL
      F3X(KABC,IABC,JABC)=VAL
      F3X(KABC,JABC,IABC)=VAL
  101 CONTINUE
C
      DO 105 KABC=1,N3N
      DO 102 I=1,N3N
      DO 102 J=1,N3N
  102 DD(I,J)=F3X(I,J,KABC)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,5) KABC
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
  201 CONTINUE
      DO 103 I=1,N3N
      DO 103 J=1,N3N
  103 DD(I,J)=DD(I,J)*FUNIT3
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,6) KABC
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
C
  202 CONTINUE
      CALL MTXMPY(ELX,N3N,DD,N3N,EE,N3N,GG,N3N,N3N,5)
      IF(IPRNT.LE.2) GO TO 203
      WRITE(6,7) KABC
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
  203 CONTINUE
      DO 104 I=1,N3N
      DO 104 J=1,N3N
  104 F3X(I,J,KABC)=EE(I,J)
  105 CONTINUE
      DO 107 I=1,N3N
      DO 107 J=1,N3N
      DO 107 K=1,N3N
      VAL=A00
      DO 106 L=1,N3N
  106 VAL=VAL+F3X(I,J,L)*ELX(L,K)
      F3Q(I,J,K)=VAL
  107 CONTINUE
C
C   SET NUMBER OF ANHARMONICITY SETS
C   REGULARLY NVIB=3N-6(OR 3N-5)
C   IF IFREQ IS NOT EQUAL TO 0 THEN NVIB=IFREQ
      IF(IFREQ.NE.0) NVIB=IABS(IFREQ)
C
      IF(IPRNT.LE.1) GO TO 204
      DO 109 II=1,NVIB
      IVIB=NFRQ(II)
      WRITE(6,8) II,IVIB
      DO 108 I=1,N3N
      DO 108 J=1,N3N
  108 DD(I,J)=F3Q(I,J,IVIB)
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
  109 CONTINUE
C
  204 CONTINUE
      DO 112 III=1,N3N
      WK=FREQ(III)
      DO 111 I=1,N3N
      WI=FREQ(I)
      DO 111 J=1,N3N
      WJ=FREQ(J)
      WIJK=WI*WJ*WK
      IF(DABS(WK).LE.WLIMIT) GO TO 205
      IF(DABS(WI).LE.WLIMIT) GO TO 205
      IF(DABS(WJ).LE.WLIMIT) GO TO 205
      IF(DABS(WIJK).LE.WLIMIT) GO TO 205
      SQWS=DSQRT(WIJK)
      FACT=FACT3/SQWS
      GO TO 206
  205 CONTINUE
      FACT=A00
  206 CONTINUE
      F3Q(I,J,III)=F3Q(I,J,III)*FACT
  111 CONTINUE
  112 CONTINUE
      DO 114 III=1,NVIB
      IVIB=NFRQ(III)
      WRITE(6,9) III,IVIB
      DO 113 I=1,NVIB
      II=NFRQ(I)
      DO 113 J=1,NVIB
      JJ=NFRQ(J)
      DD(I,J)=F3Q(II,JJ,IVIB)
  113 CONTINUE
      CALL ANHOUT(DD,N3N,N3N,NVIB,NVIB,6)
  114 CONTINUE
C
      RETURN
      END
      SUBROUTINE PHIMAT(BXY,CXY,BXYZ,CXYZ,F3Q,EE)
C   THE CALCULATION OF PHI MATRICES
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BXY(3,3,N3N),CXY(3,3,N3N)
      DIMENSION BXYZ(3,3,3,N3N),CXYZ(3,3,3,N3N)
      DIMENSION F3Q(N3N,N3N,N3N),EE(N3N,N3N)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB106/ROTAA(3),ROTGC(3),ROTCM(3),ROTMH(3)
      COMMON/VIB108/IAXIS(3),NDEG(150),NDAB(150,5),IMAG(150)
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      COMMON/VIB207/TABCD(3,3,3,3),PABC(3,3,3)
      DATA WLIMIT / 1.0D-02 /
      DATA RLIMIT / 1.0D-05 /
      DATA PH,AVN / 6.626176D+00 , 6.022045D+00 /
      DATA PI,CL / 3.1415926536D+00 , 2.99792458D+00 /
      DATA A00,HALF,ONE,TWO / 0.0D+00 , 0.5D+00 , 1.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' ****************************'/
     1          2X,' ***THE PHI (ABC) MATRICES***'/
     2          2X,' ****************************')
    2 FORMAT(//,2X,' PHI (A) MATIRX (IN D-08 CM-1)',
     1         13X,' PHI (B) MATRIX (IN D-08 CM-1)',
     2         13X,' PHI (C) MATRIX (IN D-08 CM-1)'/)
C
      WRITE(6,1)
      IXX=1
      IYY=2
      IZZ=3
C
C   FORM PHI MATRIX
      CALL ZERO(PABC,3*3*3)
C
C   PHI(IA,IA,IA) ELEMENTS
      DO 105 IA=1,3
      IAA=IAXIS(IA)
      VALU1=A00
      VALU2=A00
      VALU3=A00
      VALU4=A00
      DO 101 IB=1,3
      IBB=IAXIS(IB)
      IF(ROTCM(IBB).LE.RLIMIT) GO TO 101
      FACT1=TABCD(IA,IA,IA,IB)**2
      VALU1=VALU1+FACT1/ROTCM(IBB)
  101 CONTINUE
      DO 102 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      FACT2=CXYZ(IA,IA,IA,IVIB)**2
      VALU2=VALU2-FRQI*FACT2
  102 CONTINUE
      DO 103 IVIB=1,NVIB
      DO 103 JVIB=1,NVIB
      DO 103 KVIB=1,NVIB
      FACT3=CXY(IA,IA,IVIB)*CXY(IA,IA,JVIB)*CXY(IA,IA,KVIB)
      VALU3=VALU3+FACT3*F3Q(IVIB,JVIB,KVIB)
  103 CONTINUE
      DO 104 IB=1,3
      IBB=IAXIS(IB)
      IF(IA.EQ.IB) GO TO 104
      FACT4=TABCD(IA,IA,IA,IB)**2
      ROTAB=ROTCM(IAA)-ROTCM(IBB)
      IF(DABS(ROTAB).LE.RLIMIT) GO TO 104
      VALU4=VALU4+FACT4/ROTAB
  104 CONTINUE
      VALUT=(VALU1*3.0D+00/16.0D+00)+VALU2*TWO
     1      +VALU3/6.0D+00+VALU4/4.0D+00
      PABC(IA,IA,IA)=VALUT
  105 CONTINUE
C
C   PHI(IA,IA,IB) ELEMENTS
      DO 110 IA=1,3
      DO 110 IB=1,3
      IAA=IAXIS(IA)
      IBB=IAXIS(IB)
      IF(IA.EQ.IB) GO TO 110
      VALU1=A00
      VALU2=A00
      VALU3=A00
      VALU4=A00
      VALU5=A00
      VALU6=A00
      DO 106 IC=1,3
      ICC=IAXIS(IC)
      IF(ROTCM(ICC).LE.RLIMIT) GO TO 106
      FAC1=TABCD(IA,IA,IB,IC)+TABCD(IA,IB,IA,IC)*TWO
      FAC2=TABCD(IB,IB,IA,IC)+TABCD(IA,IB,IB,IC)*TWO
      FACT1=FAC1*FAC1+TABCD(IA,IA,IA,IC)*FAC2*TWO
      VALU1=VALU1+FACT1/ROTCM(ICC)
  106 CONTINUE
      DO 107 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      FACT2=CXYZ(IA,IA,IB,IVIB)**2
     1     +CXYZ(IA,IA,IA,IVIB)*CXYZ(IA,IB,IB,IVIB)*TWO
      VALU2=VALU2-FACT2*FRQI
  107 CONTINUE
      DO 108 IVIB=1,NVIB
      DO 108 JVIB=1,NVIB
      DO 108 KVIB=1,NVIB
      FACT3=CXY(IA,IA,IVIB)*(CXY(IA,IA,JVIB)*CXY(IB,IB,KVIB)
     1                      +CXY(IA,IB,JVIB)*CXY(IA,IB,KVIB)*4.0D+00)
      VALU3=VALU3+FACT3*F3Q(IVIB,JVIB,KVIB)
  108 CONTINUE
      ROTAB=ROTCM(IAA)-ROTCM(IBB)
      IF(DABS(ROTAB).LE.RLIMIT) GO TO 201
      FACT4=TABCD(IA,IA,IA,IB)*(TABCD(IB,IB,IB,IA)*4.0D+00
     1                         -TABCD(IA,IA,IA,IB)*3.0D+00)
      VALU4=FACT4/ROTAB
  201 CONTINUE
      DO 109 IC=1,3
      ICC=IAXIS(IC)
      IF(IC.EQ.IA) GO TO 109
      IF(IC.EQ.IB) GO TO 109
      ROTAC=ROTCM(IAA)-ROTCM(ICC)
      ROTAC2=ROTAC*ROTAC
      ROTAB=ROTCM(IAA)-ROTCM(IBB)
      IF(DABS(ROTAC).LE.RLIMIT) GO TO 202
      FACT5=ROTAC*(TABCD(IB,IB,IA,IC)+TABCD(IB,IA,IB,IC)*TWO)
     1     +ROTAB*(TABCD(IC,IC,IC,IA)-TABCD(IA,IA,IA,IC))
      VALU5=VALU5+(FACT5*TABCD(IA,IA,IA,IC))/ROTAC2
  202 CONTINUE
      ROTBC=ROTCM(IBB)-ROTCM(ICC)
      ROTBC2=ROTBC*ROTBC
      IF(DABS(ROTBC).LE.RLIMIT) GO TO 109
      FACT6=ROTBC*(TABCD(IA,IA,IB,IC)+TABCD(IA,IB,IA,IC)*TWO)
     1     +ROTAB*(TABCD(IB,IB,IB,IC)-TABCD(IC,IC,IC,IB))*TWO
      VALU6=VALU6+(FACT6*(TABCD(IA,IA,IB,IC)
     1                   +TABCD(IA,IB,IA,IC)*TWO))/ROTBC2
  109 CONTINUE
      VALUT=(VALU1*3.0D+00/32.0D+00)+VALU2+VALU3/4.0D+00
     1      +VALU4/8.0D+00+VALU5/4.0D+00+VALU6/8.0D+00
      PABC(IA,IA,IB)=VALUT
      PABC(IA,IB,IA)=VALUT
      PABC(IB,IA,IA)=VALUT
  110 CONTINUE
C
C   PHI(IA,IB,IC) ELEMENTS
      VALU1=A00
      VALU2=A00
      VALU3=A00
      VALU4=A00
      VALU5=A00
      VALU6=A00
      DO 111 IA=1,3
      IAA=IAXIS(IA)
      IF(ROTCM(IAA).LE.RLIMIT) GO TO 111
      FACT=TABCD(IA,IXX,IYY,IZZ)+TABCD(IA,IYY,IZZ,IXX)
     1    +TABCD(IA,IZZ,IXX,IYY)
      FAC1=FACT*FACT*TWO
      FAC2=(TABCD(IA,IXX,IYY,IYY)+TABCD(IA,IYY,IXX,IYY)*TWO)
     1    *(TABCD(IA,IXX,IZZ,IZZ)+TABCD(IA,IZZ,IXX,IZZ)*TWO)
      FAC3=(TABCD(IA,IYY,IZZ,IZZ)+TABCD(IA,IZZ,IYY,IZZ)*TWO)
     1    *(TABCD(IA,IYY,IXX,IXX)+TABCD(IA,IXX,IYY,IXX)*TWO)
      FAC4=(TABCD(IA,IZZ,IXX,IXX)+TABCD(IA,IXX,IZZ,IXX)*TWO)
     1    *(TABCD(IA,IZZ,IYY,IYY)+TABCD(IA,IYY,IZZ,IYY)*TWO)
      VALU1=VALU1+(FAC1+FAC2+FAC3+FAC4)/ROTCM(IAA)
  111 CONTINUE
      DO 112 IVIB=1,NVIB
      FRQI=FREQ(IVIB)
      FACT2=CXYZ(IXX,IYY,IZZ,IVIB)**2*TWO
     1     +CXYZ(IYY,IYY,IXX,IVIB)*CXYZ(IZZ,IZZ,IXX,IVIB)
     2     +CXYZ(IZZ,IZZ,IYY,IVIB)*CXYZ(IXX,IXX,IYY,IVIB)
     3     +CXYZ(IXX,IXX,IZZ,IVIB)*CXYZ(IYY,IYY,IZZ,IVIB)
      VALU2=VALU2-FRQI*FACT2
  112 CONTINUE
      DO 113 IVIB=1,NVIB
      DO 113 JVIB=1,NVIB
      DO 113 KVIB=1,NVIB
      FACT3=CXY(IXX,IXX,IVIB)*CXY(IYY,IYY,JVIB)*CXY(IZZ,IZZ,KVIB)
     1     +CXY(IXX,IXX,IVIB)*CXY(IYY,IZZ,JVIB)*CXY(IYY,IZZ,KVIB)*TWO
     2     +CXY(IYY,IYY,IVIB)*CXY(IZZ,IXX,JVIB)*CXY(IZZ,IXX,KVIB)*TWO
     3     +CXY(IZZ,IZZ,IVIB)*CXY(IXX,IYY,JVIB)*CXY(IXX,IYY,KVIB)*TWO
     4     +CXY(IYY,IZZ,IVIB)*CXY(IZZ,IXX,JVIB)*CXY(IXX,IYY,KVIB)
     5     *8.0D+00
      VALU3=VALU3+FACT3*F3Q(IVIB,JVIB,KVIB)
  113 CONTINUE
      IAA=IAXIS(1)
      IBB=IAXIS(2)
      ICC=IAXIS(3)
      ROTYZ=ROTCM(IBB)-ROTCM(ICC)
      ROTYZ2=ROTYZ*ROTYZ
      ROTZX=ROTCM(ICC)-ROTCM(IAA)
      ROTZX2=ROTZX*ROTZX
      ROTXY=ROTCM(IAA)-ROTCM(IBB)
      ROTXY2=ROTXY*ROTXY
      IF(DABS(ROTYZ).LE.RLIMIT) GO TO 203
      FACT4=ROTZX*TABCD(IYY,IYY,IYY,IZZ)
     1     +ROTXY*TABCD(IZZ,IZZ,IZZ,IYY)
      VALU4=(TABCD(IYY,IYY,IYY,IZZ)-TABCD(IZZ,IZZ,IZZ,IYY))*FACT4
     1      /ROTYZ2
  203 CONTINUE
      IF(DABS(ROTZX).LE.RLIMIT) GO TO 204
      FACT5=ROTXY*TABCD(IZZ,IZZ,IZZ,IXX)
     1     +ROTYZ*TABCD(IXX,IXX,IXX,IZZ)
      VALU5=(TABCD(IZZ,IZZ,IZZ,IXX)-TABCD(IXX,IXX,IXX,IZZ))*FACT5
     1      /ROTZX2
  204 CONTINUE
      IF(DABS(ROTXY).LE.RLIMIT) GO TO 205
      FACT6=ROTYZ*TABCD(IXX,IXX,IXX,IYY)
     1     +ROTZX*TABCD(IYY,IYY,IYY,IXX)
      VALU6=(TABCD(IXX,IXX,IXX,IYY)-TABCD(IYY,IYY,IYY,IXX))*FACT6
     1      /ROTXY2
  205 CONTINUE
      VALUT=(VALU1*3.0D+00/16.0D+00)+VALU2*TWO+VALU3*HALF
     1     +(VALU4*3.0D+00)/4.0D+00+(VALU5*3.0D+00)/4.0D+00
     2     +(VALU6*3.0D+00)/4.0D+00
      PABC(IXX,IYY,IZZ)=VALUT
      PABC(IXX,IZZ,IYY)=VALUT
      PABC(IYY,IXX,IZZ)=VALUT
      PABC(IYY,IZZ,IXX)=VALUT
      PABC(IZZ,IXX,IYY)=VALUT
      PABC(IZZ,IYY,IXX)=VALUT
C
      DO 115 IA=1,3
      DO 115 IB=1,3
      DO 115 IC=1,3
      PABC(IA,IB,IC)=PABC(IA,IB,IC)*1.0D+08
  115 CONTINUE
      WRITE(6,2)
      CALL MX3OUT(PABC(1,1,1),3,PABC(1,1,2),3,PABC(1,1,3),3,3,6)
      DO 116 IA=1,3
      DO 116 IB=1,3
      DO 116 IC=1,3
      PABC(IA,IB,IC)=PABC(IA,IB,IC)/1.0D+08
  116 CONTINUE
C
      RETURN
      END
      SUBROUTINE FORCE4(ELX,F44,F4X,F4Q,EE,DD,GG)
C   THE FOURTH ORDER ANHARMONICITY ANALYSIS
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ELX(N3N,N3N)
      DIMENSION F44(N4TOT),F4X(N3N,N3N,N3N,N3N),F4Q(N3N,N3N,N3N,N3N)
      DIMENSION EE(N3N,N3N),DD(N3N,N3N),GG(N3N,N3N)
      COMMON/VIB101/NATOM,N3N,NATRI,ILIN,NVIB
      COMMON/VIB102/ITHREE,IFOUR,N3TOT,N4TOT
      COMMON/VIB105/FACT3,FACT4
      COMMON/VIB203/IOFF(150),IPRNT
      COMMON/VIB204/SQM(150),ROOT(150),FREQ(150)
      COMMON/VIB205/IFREQ,NFRQ(150)
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
      DATA WLIMIT / 3.0D+00 /
      DATA ITAP24 / 24 /
      DATA A0,HE / 0.52917706D+00 , 4.359813653D+00 /
    1 FORMAT(//,2X,' THE FOURTH ORDER ANHARMONICITY ANALYSIS'/)
    2 FORMAT(2I5)
    3 FORMAT(3F20.10)
    4 FORMAT(//,2X,' **************************************************'
     1        /,2X,' ***FOURTH DERIVATIVES W.R.T. NORMAL COORDINATES***'
     2        /,2X,' **************************************************'
     3        /)
    5 FORMAT(//,2X,' F4X MATRIX (IN HARTREE/BOHR+4), KABC = ',I5/)
    6 FORMAT(//,2X,' F4X MATRIX (IN MDYN/A+3), KABC = ',I5,
     1          5X,' LABC = ',I5/)
    7 FORMAT(//,2X,' LX(T) * F4X * LX MATRIX, KABC = ',I5,
     1          5X,' LABC = ',I5/)
    8 FORMAT(//,2X,' LX(T)*LX(T) * F4X *LX*LX MATRIX, KABC = ',I5,
     1          5X,' LABC = ',I5/)
    9 FORMAT(//,2X,' F4(QI*QJ*QK*QL) MATRIX (IN MDYN/(A+2.AMU-3/2))',
     1 1X,' QVIB (',I3,',',I3,')',2X,',',' IVIB (',I3,',',I3,')'/)
   10 FORMAT(//,2X,' F4(QI*QJ*QK*QL) MATRIX (IN CM-1),',
     1 1X,' QVIB (',I3,',',I3,')',2X,',',' IVIB (',I3,',',I3,')'/)
C
      IF(IPRNT.NE.0)
     *WRITE(6,1)
C
C   FIND CONVERSION FACTORS
C   FUNIT4 IS IN MDYN/A+3
      FUNIT4=HE/(A0*A0*A0*A0)
C
C   READ IN FOURTH DERIVATIVES
      REWIND ITAP24
      READ(ITAP24,2) NNATOM,NF3
      READ(ITAP24,3) (F44(I),I=1,N4TOT)
      REWIND ITAP24
C
C   TRANSFORM FOURTH DERIVATIVES
C   FROM CARTESIAN TO NORMAL COORDINATE SYSTEM
      WRITE(6,4)
      MABC=0
      DO 101 IABC=1,N3N
      DO 101 JABC=1,IABC
      DO 101 KABC=1,JABC
      DO 101 LABC=1,KABC
      MABC=MABC+1
      VAL=F44(MABC)
C
      F4X(IABC,JABC,KABC,LABC)=VAL
      F4X(IABC,JABC,LABC,KABC)=VAL
      F4X(IABC,KABC,JABC,LABC)=VAL
      F4X(IABC,KABC,LABC,JABC)=VAL
      F4X(IABC,LABC,JABC,KABC)=VAL
      F4X(IABC,LABC,KABC,JABC)=VAL
C
      F4X(JABC,IABC,KABC,LABC)=VAL
      F4X(JABC,IABC,LABC,KABC)=VAL
      F4X(JABC,KABC,IABC,LABC)=VAL
      F4X(JABC,KABC,LABC,IABC)=VAL
      F4X(JABC,LABC,IABC,KABC)=VAL
      F4X(JABC,LABC,KABC,IABC)=VAL
C
      F4X(KABC,IABC,JABC,LABC)=VAL
      F4X(KABC,IABC,LABC,JABC)=VAL
      F4X(KABC,JABC,IABC,LABC)=VAL
      F4X(KABC,JABC,LABC,IABC)=VAL
      F4X(KABC,LABC,IABC,JABC)=VAL
      F4X(KABC,LABC,JABC,IABC)=VAL
C
      F4X(LABC,IABC,JABC,KABC)=VAL
      F4X(LABC,IABC,KABC,JABC)=VAL
      F4X(LABC,JABC,IABC,KABC)=VAL
      F4X(LABC,JABC,KABC,IABC)=VAL
      F4X(LABC,KABC,IABC,JABC)=VAL
      F4X(LABC,KABC,JABC,IABC)=VAL
  101 CONTINUE
C
      DO 105 KABC=1,N3N
      DO 105 LABC=1,N3N
      DO 102 I=1,N3N
      DO 102 J=1,N3N
  102 DD(I,J)=F4X(I,J,KABC,LABC)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,5) KABC,LABC
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
  201 CONTINUE
      DO 103 I=1,N3N
      DO 103 J=1,N3N
  103 DD(I,J)=DD(I,J)*FUNIT4
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,6) KABC,LABC
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
C
  202 CONTINUE
      CALL MTXMPY(ELX,N3N,DD,N3N,EE,N3N,GG,N3N,N3N,5)
      IF(IPRNT.LE.2) GO TO 203
      WRITE(6,7) KABC,LABC
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
  203 CONTINUE
      DO 104 I=1,N3N
      DO 104 J=1,N3N
  104 F4X(I,J,KABC,LABC)=EE(I,J)
  105 CONTINUE
C
      DO 110 KABC=1,N3N
      DO 110 LABC=1,N3N
      DO 106 I=1,N3N
      DO 106 J=1,N3N
  106 DD(I,J)=F4X(KABC,LABC,I,J)
      IF(IPRNT.LE.2) GO TO 204
      WRITE(6,7) KABC,LABC
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
  204 CONTINUE
C
      CALL MTXMPY(ELX,N3N,DD,N3N,EE,N3N,GG,N3N,N3N,5)
      IF(IPRNT.LE.2) GO TO 206
      WRITE(6,8) KABC,LABC
      CALL MATOUT(EE,N3N,N3N,N3N,N3N,6)
  206 CONTINUE
      DO 108 I=1,N3N
      DO 108 J=1,N3N
  108 F4Q(KABC,LABC,I,J)=EE(I,J)
  110 CONTINUE
C
C   SET NUMBER OF ANHARMONICITY SETS
C   REGULARLY NVIB=3N-6(OR 3N-5)
C   IF IFREQ IS NOT EQUAL TO 0 THEN NVIB=IFREQ
      IF(IFREQ.NE.0) NVIB=IABS(IFREQ)
C
      IF(IPRNT.LE.2) GO TO 207
      DO 112 II=1,NVIB
      IVIB=NFRQ(II)
      DO 112 JJ=1,NVIB
      JVIB=NFRQ(JJ)
      WRITE(6,9) II,JJ,IVIB,JVIB
      DO 111 I=1,N3N
      DO 111 J=1,N3N
  111 DD(I,J)=F4Q(I,J,IVIB,JVIB)
      CALL MATOUT(DD,N3N,N3N,N3N,N3N,6)
  112 CONTINUE
C
  207 CONTINUE
      DO 115 III=1,N3N
      WK=FREQ(III)
      DO 115 JJJ=1,N3N
      WL=FREQ(JJJ)
      DO 114 I=1,N3N
      WI=FREQ(I)
      DO 114 J=1,N3N
      WJ=FREQ(J)
      WIJKL=WI*WJ*WK*WL
      IF(DABS(WK).LE.WLIMIT) GO TO 208
      IF(DABS(WL).LE.WLIMIT) GO TO 208
      IF(DABS(WI).LE.WLIMIT) GO TO 208
      IF(DABS(WJ).LE.WLIMIT) GO TO 208
      IF(DABS(WIJKL).LE.WLIMIT) GO TO 208
      SQWS=DSQRT(WIJKL)
      FACT=FACT4/SQWS
      GO TO 209
  208 CONTINUE
      FACT=A00
  209 CONTINUE
      F4Q(I,J,III,JJJ)=F4Q(I,J,III,JJJ)*FACT
  114 CONTINUE
  115 CONTINUE
      DO 120 III=1,NVIB
      IVIB=NFRQ(III)
      DO 120 JJJ=1,NVIB
      JVIB=NFRQ(JJJ)
      WRITE(6,10) III,JJJ,IVIB,JVIB
      DO 116 I=1,NVIB
      II=NFRQ(I)
      DO 116 J=1,NVIB
      JJ=NFRQ(J)
      DD(I,J)=F4Q(II,JJ,IVIB,JVIB)
  116 CONTINUE
      CALL ANHOUT(DD,N3N,N3N,NVIB,NVIB,6)
  120 CONTINUE
C
      RETURN
      END
      SUBROUTINE MTXMPY(A,NAD,B,NBD,C,NCD,D,NDD,N,IMPY)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(NAD,NAD),B(NBD,NBD),C(NCD,NCD),D(NDD,NDD)
      DATA A00 / 0.0D+00 /
C
      CALL ZERO(C,NCD*NCD)
      GO TO (201,202,203,204,205,206),IMPY
C     C=A*B
  201 DO 102 I=1,N
      DO 102 J=1,N
      DO 102 K=1,N
  102 C(I,J)=C(I,J)+A(I,K)*B(K,J)
      RETURN
C     C=TA*B
  202 DO 103 I=1,N
      DO 103 J=1,N
      DO 103 K=1,N
  103 C(I,J)=C(I,J)+A(K,I)*B(K,J)
      RETURN
C     C=A*TB
  203 DO 104 I=1,N
      DO 104 J=1,N
      DO 104 K=1,N
  104 C(I,J)=C(I,J)+A(I,K)*B(J,K)
      RETURN
C     C=TA*TB
  204 DO 105 I=1,N
      DO 105 J=1,N
      DO 105 K=1,N
  105 C(I,J)=C(I,J)+A(K,I)*B(J,K)
      RETURN
C     C=TA*B*A
  205 DO 106 I=1,N
      DO 106 J=1,N
      D(I,J)=A00
      DO 106 K=1,N
  106 D(I,J)=D(I,J)+A(K,I)*B(K,J)
      DO 107 I=1,N
      DO 107 J=1,N
      DO 107 K=1,N
  107 C(I,J)=C(I,J)+D(I,K)*A(K,J)
      RETURN
C     C=A*B*TA
  206 DO 108 I=1,N
      DO 108 J=1,N
      D(I,J)=A00
      DO 108 K=1,N
  108 D(I,J)=D(I,J)+A(I,K)*B(K,J)
      DO 109 I=1,N
      DO 109 J=1,N
      DO 109 K=1,N
  109 C(I,J)=C(I,J)+D(I,K)*A(J,K)
      RETURN
      END
      SUBROUTINE ANHOUT(A,NAD,NBD,M,N,IOUT)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*6 LINE(21)
      DIMENSION A(NAD,NBD)
      DATA LINE / 21*'------' /
    1 FORMAT(2X,10(7X,I5))
    2 FORMAT(2X,21A6)
    3 FORMAT(2X,I2,2X,10F12.3)
    4 FORMAT(/)
C
      II=0
      JJ=0
  200 II=II+1
      JJ=JJ+1
      KK=10*JJ
      NN=N
      IF(N.GT.KK) NN=KK
      LL=2*(NN-II+1)+1
      WRITE (IOUT,1) (I,I=II,NN)
      WRITE (IOUT,2) (LINE(I),I=1,LL)
      DO 101 I=1,M
      WRITE (IOUT,3) I,(A(I,J),J=II,NN)
  101 CONTINUE
      IF(N.LE.KK) GO TO 201
      WRITE (IOUT,4)
      II=KK
      GO TO 200
  201 RETURN
      END
      SUBROUTINE MX2OUT(A,NAD,B,NBD,M,IOUT)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*6 LINE(7)
      DIMENSION A(NAD,NAD),B(NBD,NBD)
      DATA LINE / 7*'------' /
    1 FORMAT(3X,3(7X,I5),7X,3(7X,I5))
    2 FORMAT(3X,7A6,1X,7A6,1X,7A6)
    3 FORMAT(3X,I2,2X,3F12.7,3X,I2,2X,3F12.7)
    4 FORMAT(/)
C
      WRITE (IOUT,1) (I,I=1,3),(I,I=1,3)
      WRITE (IOUT,2) (LINE(I),I=1,7),(LINE(I),I=1,7)
      DO 101 I=1,M
      WRITE (IOUT,3) I,(A(I,J),J=1,3),I,(B(I,J),J=1,3)
  101 CONTINUE
      WRITE (IOUT,4)
      RETURN
      END
      SUBROUTINE MX3OUT(A,NAD,B,NBD,C,NCD,M,IOUT)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*6 LINE(7)
      DIMENSION A(NAD,NAD),B(NBD,NBD),C(NCD,NCD)
      DATA LINE / 7*'------' /
    1 FORMAT(3X,3(7X,I5),7X,3(7X,I5),7X,3(7X,I5))
    2 FORMAT(3X,7A6,1X,7A6,1X,7A6)
    3 FORMAT(3X,I2,2X,3F12.7,3X,I2,2X,3F12.7,3X,I2,2X,3F12.7)
    4 FORMAT(/)
C
      WRITE (IOUT,1) (I,I=1,3),(I,I=1,3),(I,I=1,3)
      WRITE (IOUT,2) (LINE(I),I=1,7),(LINE(I),I=1,7),(LINE(I),I=1,7)
      DO 101 I=1,M
      WRITE (IOUT,3) I,(A(I,J),J=1,3),I,(B(I,J),J=1,3),I,(C(I,J),J=1,3)
  101 CONTINUE
      WRITE (IOUT,4)
      RETURN
      END
      BLOCK DATA
      CHARACTER*1  RAXIS
      CHARACTER*25 RTYPE
      COMMON/VIB206/RAXIS(3),RTYPE(6)
C
      DATA RAXIS(1) / 'A' /
      DATA RAXIS(2) / 'B' /
      DATA RAXIS(3) / 'C' /
      DATA RTYPE(1) / ' DIATOMIC                ' /
      DATA RTYPE(2) / ' LINEAR POLYATOMIC       ' /
      DATA RTYPE(3) / ' ASYMMETRIC TOP          ' /
      DATA RTYPE(4) / ' SYMMETRIC TOP (PROLATE) ' /
      DATA RTYPE(5) / ' SYMMETRIC TOP (OBLATE)  ' /
      DATA RTYPE(6) / ' SPHERICAL TOP           ' /
      END
