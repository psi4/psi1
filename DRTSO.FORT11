      SUBROUTINE FENTRY(CC,IA,MAXCOR)
C   THIS PROGRAM REORDERS MO INTEGRALS AND TWO-PDMS IN DRT ORDER
C   TO THOSE IN SORTED-SCF ORDER
C**********************************************************************
C*  MODIFICATION FOR FENTRY                                           *
C*  BY: YUKIO YAMAGUCHI                                               *
C*  DATE: FEBRUARY 17, 1989                                           *
C**********************************************************************
C***LAST UPDATED ON MAY 02, 1988 BY YUKIO YAMAGUCHI                   *
C**********************************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
C2-19-89 DIMENSION CC(300000),IA(600000)
      DIMENSION CC(MAXCOR),IA(1)
      DIMENSION LBLDRT(26),NDUM(20)
      CHARACTER*8 CITYPE,TRANSL
      LOGICAL PDM,MOINT,BOTH
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/DIMES/ISYMRB,NUMIJ,NBF,NORBS,NMAX
      COMMON/FUNCS/ICITYP
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/MFSEC/MASTER,NSECT
      COMMON/ORDCI/IDRSYM(256),IORDRT(256)
      COMMON/ORDSO/IORSO(256),IDRTSO(256)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP35,ITAP52,ITAP53,ITAP56,ITAP58
C2-17-89  EQUIVALENCE (CC,IA)
    1 FORMAT(//,2X,' THE DRT-SO PROGRAM'//)
    2 FORMAT(A8,2X,A8,2X)
    3 FORMAT(5I5)
    4 FORMAT(2X,' DRT LABEL = ',26A3)
    5 FORMAT(//,2X,' PARAMETERS'/
     1 2X,' NBF     = ',I8/
     2 2X,' NSYM    = ',I8/
     2 2X,' NORBS   = ',I8/
     3 2X,' NWKS    = ',I8/
     4 2X,' NMAX    = ',I8/
     5 2X,' NUMIJ   = ',I8/
     6 2X,' NGROUP  = ',I8/
     7 2X,' ISYMRB  = ',I8/
     8 2X,' ICITYP  = ',I8/
     9 2X,' IMOPDM  = ',I8/
     A 2X,' IPRNT   = ',I8/)
    6 FORMAT(//,2X,' NUMBER OF ORBITALS DOES NOT MATCH',
     1             ' NBF = ',I5,' ISUM = ',I5/)
    7 FORMAT(//,2X,' I , IORDRT , IORSO , IDRTSO'/)
    8 FORMAT(2X,5I5)
    9 FORMAT(//,2X,' WRONG INPUT',A8/)
C
      CALL TSTART(6)
      CALL NOUNFL
      CALL INITMF(1)
C
C     ITAP35 = CONTAINS THE TWO ELECTRON INTEGRALS (SORTED SO)
C     ITAP52 = CONTAINS THE MO ONE AND TWO ELECTRON INTEGRALS (DRT)
C     ITAP53 = CONTAINS THE ONE AND TWO PDM'S IN THE MO BASIS (DRT)
C     ITAP56 = CONTAINS TWO PDM'S IN THE MO BASIS (SORTED SO)
C     ITAP58 = CONTAINS THE DRT INFORMATION
C
      ITAPE3=3
      INPUT=5
      ITAP35=35
      ITAP52=52
      ITAP53=53
      ITAP56=56
      ITAP58=58
C2-17-89  MAXCOR=300000
C
      CALL LOCATE(INPUT,'# DRTSO ##',IERR)
C
      WRITE(6,1)
      READ(5,2) CITYPE,TRANSL
      ICITYP=0
      IF(CITYPE.EQ.'CI      ') ICITYP=1
      IF(CITYPE.EQ.'MCSCF   ') ICITYP=2
      IF(CITYPE.EQ.'MCSCFCI ') ICITYP=3
      IMOPDM=0
      IF(TRANSL.EQ.'MOINTS  ') MOINT=.TRUE.
      IF(TRANSL.EQ.'MOINT   ') MOINT=.TRUE.
      IF(TRANSL.EQ.'MO      ') MOINT=.TRUE.
      IF(TRANSL.EQ.'TWOPDM  ') PDM=.TRUE.
      IF(TRANSL.EQ.'PDM     ') PDM=.TRUE.
      IF(TRANSL.EQ.'TWO     ') PDM=.TRUE.
      IF(TRANSL.EQ.'MOPDM   ') BOTH=.TRUE.
      IF(TRANSL.EQ.'PDMMO   ') BOTH=.TRUE.
      IF(BOTH) PDM=.TRUE.
      IF(BOTH) MOINT=.TRUE.
      IF(MOINT) IMOPDM=1
      IF(PDM) IMOPDM=2
      IF(BOTH) IMOPDM=3
      READ(5,3) IPRNT
C
      IOFF(1)=0
      DO 101 I=1,255
  101 IOFF(I+1)=IOFF(I)+I
C
C   READ DIMENSIONS FROM DRT TAPE
C
      CALL RFILE(ITAP58)
      CALL WREADW(ITAP58,DRTVER,2,1,IEND)
      CALL WREADW(ITAP58,LBLDRT,26,IEND,IEND)
      CALL WREADW(ITAP58,NBF,1,IEND,IEND)
      CALL WREADW(ITAP58,NSYM,1,IEND,IEND)
      CALL WREADW(ITAP58,NORBS,1,IEND,IEND)
      CALL WREADW(ITAP58,NDUM,6,IEND,IEND)
      CALL WREADW(ITAP58,NWKS,1,IEND,IEND)
      CALL WREADW(ITAP58,NDUM,3,IEND,IEND)
      CALL WREADW(ITAP58,ISYMRB,1,IEND,IEND)
      CALL WREADW(ITAP58,NUMIJ,1,IEND,IEND)
      CALL WREADW(ITAP58,NGROUP,1,IEND,IEND)
      CALL WREADW(ITAP58,NUMINT,1,IEND,IEND)
      CALL WREADW(ITAP58,NMAX,1,IEND,IEND)
      IEND=IEND+3
C
      WRITE(6,4) LBLDRT
      WRITE(6,5) NBF,NSYM,NORBS,NWKS,NMAX,NUMIJ,NGROUP,ISYMRB,
     1           ICITYP,IMOPDM,IPRNT
C
      IC1=1
      IA1=1
      IC2=IC1+ISYMRB
      IA2=IC2+IC2-1
      IC3=IC2+ISYMRB
      IA3=IC3+IC3-1
      IC4=IC3+NUMIJ
      IA4=IC4+IC4-1
      ICSAVE=IC4+NUMIJ
C
C.................KADD    LADD    IJADD   IJGRP...............
      CALL GETDRT(IA(IA1),IA(IA2),IA(IA3),IA(IA4),IEND,NGROUP)
C
C   FORM AN ARRAY TO RELATE DRT AND SO ORDERINGS
      CALL MREAD(IORSO,4)
      ISUM=0
      DO 103 I=1,NBF
      II=IORDRT(I)
      DO 102 J=1,NBF
      JJ=IORSO(J)
      IF(II.NE.JJ) GO TO 102
      ISUM=ISUM+1
      IDRTSO(I)=J
  102 CONTINUE
  103 CONTINUE
      IF(ISUM.EQ.NBF) GO TO 301
      WRITE(6,6) NBF,ISUM
      STOP
  301 CONTINUE
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,7)
      DO 104 I=1,NBF
  104 WRITE(6,8) I,IORDRT(I),IORSO(I),IDRTSO(I)
C
C   READ IN MO INTEGRALS IN DRT ORDER AND REORDER THEM IN SORTED-SCF
C   ORDER
C
  302 CONTINUE
      IF(.NOT.MOINT)  GO TO 201
      WRITE(6,*) ' MO INTEGRALS'
      IC5=ICSAVE
      IC6=IC5+NUMIJ
      IC7=IC6+NMAX
      IA7=IC7+IC7-1
      ICMAX=IC7+MAXBF2
C................KADD    LADD    IJADD   IJGRP   HQ      BUFI....
      CALL GETIT(IA(IA1),IA(IA2),IA(IA3),IA(IA4),CC(IC5),CC(IC6),
C.............. .LBLIO   BUFO..................
     1           IA(IA7),CC(IC7),ITAP52,ITAP35)
C
C   READ IN PDM MATRICES IN DRT ORDER AND REORDER THEM IN SORTED-SCF
C   ORDER
C
  201 CONTINUE
      IF(.NOT.PDM) GO TO 400
      WRITE(6,*) '         '
      WRITE(6,*) ' TWO PDMS'
      IC5=ICSAVE
      IC6=IC5+NUMIJ
      IC7=IC6+NMAX
      IA7=IC7+IC7-1
      ICMAX=IC7+MAXBF2
C................KADD    LADD    IJADD   IJGRP   HQ      BUFI....
      CALL GETIT(IA(IA1),IA(IA2),IA(IA3),IA(IA4),CC(IC5),CC(IC6),
C.............. .LBLIO   BUFO..................
     1           IA(IA7),CC(IC7),ITAP53,ITAP56)
C
  400 CONTINUE
      CALL RCLOSE(MASTER,3)
      CALL RCLOSE(ITAP58,3)
      CALL TSTOP(6)
C
      RETURN
C2-17-89  STOP
      END
      SUBROUTINE GETDRT(KADD,LADD,IJADD,IJGRP,IEND,NGROUP)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION KADD(ISYMRB),LADD(ISYMRB),IJADD(NUMIJ),IJGRP(NUMIJ)
      COMMON/DIMES/ISYMRB,NUMIJ,NBF,NORBS,NMAX
      COMMON/ORDCI/IDRSYM(256),IORDRT(256)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP35,ITAP52,ITAP53,ITAP56,ITAP58
    1 FORMAT(//,2X,' INFORMATION IN GETDRT'/)
    2 FORMAT(2X,20I5)
C
      CALL WREADW(ITAP58,KADD,ISYMRB,IEND,IEND)
      CALL WREADW(ITAP58,LADD,ISYMRB,IEND,IEND)
      CALL WREADW(ITAP58,IJADD,NUMIJ,IEND,IEND)
      CALL WREADW(ITAP58,IJGRP,NUMIJ,IEND,IEND)
      IEND=IEND+4*NORBS+NGROUP+NBF
      CALL WREADW(ITAP58,IDRSYM,NORBS,IEND,IEND)
      IEND=IEND+NBF*2
      CALL WREADW(ITAP58,IORDRT,NORBS,IEND,IEND)
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      WRITE(6,*) ' KADD ',KADD
      WRITE(6,*) ' LADD ',LADD
      WRITE(6,*) ' IJADD ',IJADD
      WRITE(6,*) ' IJGRP ',IJGRP
      WRITE(6,*) ' IDRSYM '
      WRITE(6,2) (IDRSYM(I),I=1,NBF)
      WRITE(6,*) ' IORDRT '
      WRITE(6,2) (IORDRT(I),I=1,NBF)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE GETIT(KADD,LADD,IJADD,IJGRP,HQ,BUFI,LBLO,BUFO,
     1                 ITAPE,NTAPE)
      IMPLICIT REAL*8 (A-H,O-Z)
      LOGICAL MOINT
      DIMENSION KADD(ISYMRB),LADD(ISYMRB),IJADD(NUMIJ),IJGRP(NUMIJ)
      DIMENSION HQ(NUMIJ),BUFI(NMAX),LBLO(MAXBF4),BUFO(MAXBF2)
      DIMENSION DUMS(56)
      LOGICAL IJEQ,IJKEQ,JKEQ
      COMMON/DIMES/ISYMRB,NUMIJ,NBF,NORBS,NMAX
      COMMON/FUNCS/ICITYP
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/ORDCI/IDRSYM(256),IORDRT(256)
      COMMON/ORDSO/IORSO(256),IDRTSO(256)
      COMMON/SIGNS/IOFF(256),IPRNT
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
      DATA D8, D4, D2 / 0.125D0, 0.25D0, 0.5D0 /
    1 FORMAT(//,2X,' GET DRT MATRICES'/)
    2 FORMAT(1X,7F18.8)
    3 FORMAT(1X,'  IN GETDM, IJKLT UNKNOWN TYPE ',I5/'  ORBITALS :',4I5/
     * '  BASIS FUNCTIONS :',4I5/'   SYMMETRIES :',4I5)
    4 FORMAT(1X,'  IN GETDM, LAD TOO BIG :',I6)
    5 FORMAT(2X,4I5,3X,I5,3X,3F20.10)
    6 FORMAT(//,2X,' HQ MATRIX'/)
C
      CALL ZERO(HQ,NUMIJ)
C
C   READ IN THE ONE AND TWO PARTICLE DENSITY MATRICES IN DRT ORDERING
C
      MOINT=.FALSE.
      CALL RFILE(ITAPE)
      CALL RFILE(NTAPE)
      IF(ITAPE.NE.52) GO TO 201
      MOINT=.TRUE.
      CALL SREAD(ITAPE,DUMS,112)
  201 CONTINUE
      NMAX2=NMAX*2
      IBL=0
      ISRIN=0
      IGRP=0
      DO 140 IOR=NORBS,1,-1
      ISM=IDRSYM(IOR)
      ITYP=1
      DO 130 JOR=IOR,1,-1
      IF (IGRP .NE. IJGRP(IOFF(IOR)+JOR)) THEN
      CALL SREAD(ITAPE,BUFI,NMAX2)
      IF(IPRNT.GT.4)
     *WRITE(6,2) (BUFI(IOP), IOP=1,NMAX)
      ISRIN=ISRIN+NMAX/112
      IGRP=IJGRP(IOFF(IOR)+JOR)
      END IF
      JSM=IDRSYM(JOR)
      IJEQ=IOR.EQ.JOR
      IJT=ITYP
      IF (IJEQ) IJT=IJT+2
      IJSYM=IEOR((ISM-1),(JSM-1))
      KOFF=IJSYM*NORBS
      JAD=IJADD(IOFF(IOR)+JOR)
      DO 120 KOR=1,JOR
      KSM=IDRSYM(KOR)
      JKEQ=JOR.EQ.KOR
      IJKT=IJT
      IF (JKEQ) IJKT=IJKT+1
      IJKEQ=IJEQ.AND.JKEQ
      IJKSYM=IEOR(IJSYM,(KSM-1))
      LOFF=IJKSYM*NORBS
      KAD=JAD+KADD(KOFF+KOR)
      DO 110 LOR=1,KOR
      IF (JOR.EQ.KOR .AND. KOR.EQ.LOR .AND. LOR.LT.IOR) GO TO 310
      LSM=IDRSYM(LOR)
      IF ((LSM-1) .NE. IJKSYM) GO TO 310
      IJKLT=IJKT
      IF (IJKEQ .AND. KOR.GT.LOR) IJKLT=IJKLT+1
      IF (KOR.EQ.LOR) IJKLT=IJKLT+3
      LAD=KAD+LADD(LOFF+LOR)
      IF (IJKLT.GT.7 .OR. IJKLT.LT.1) THEN
      WRITE(6,3)IJKLT,IOR,JOR,KOR,LOR,IBF,JBF,KBF,LBF,ISM,JSM,KSM,LSM
          STOP
      END IF
      IF (LAD .GT. NMAX) THEN
      WRITE(6,4) LAD
      STOP
      END IF
C
      GOTO (301,302,303,304,305,306,307) IJKLT
C
  301 CONTINUE
      FAC=D8
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+1)*FAC
      CALL ORDER(VAL,IOR,KOR,JOR,LOR,IBL,LBLO,BUFO,NTAPE)
      VAL=BUFI(LAD+2)*FAC
      CALL ORDER(VAL,IOR,JOR,KOR,LOR,IBL,LBLO,BUFO,NTAPE)
      VAL=BUFI(LAD+3)*FAC
      CALL ORDER(VAL,IOR,LOR,JOR,KOR,IBL,LBLO,BUFO,NTAPE)
      GO TO 310
C
  302 CONTINUE
      FAC=D8
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+1)*FAC
      CALL ORDER(VAL,IOR,JOR,JOR,LOR,IBL,LBLO,BUFO,NTAPE)
      FAC=D4
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+2)*FAC
      CALL ORDER(VAL,IOR,LOR,JOR,JOR,IBL,LBLO,BUFO,NTAPE)
      GO TO 310
C
  303 CONTINUE
      FAC=D8
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+1)*FAC
      CALL ORDER(VAL,IOR,KOR,IOR,LOR,IBL,LBLO,BUFO,NTAPE)
      FAC=D4
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+2)*FAC
      CALL ORDER(VAL,IOR,IOR,KOR,LOR,IBL,LBLO,BUFO,NTAPE)
      GO TO 310
C
  304 CONTINUE
      FAC=D8
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+1)*FAC
      CALL ORDER(VAL,IOR,LOR,JOR,LOR,IBL,LBLO,BUFO,NTAPE)
      FAC=D4
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+2)*FAC
      CALL ORDER(VAL,IOR,JOR,LOR,LOR,IBL,LBLO,BUFO,NTAPE)
      GO TO 310
C
  305 CONTINUE
      FAC=D4
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+1)*FAC
      CALL ORDER(VAL,IOR,IOR,IOR,LOR,IBL,LBLO,BUFO,NTAPE)
      VAL=BUFI(LAD+2)*FAC
      CALL ORDER(VAL,IOR,LOR,LOR,LOR,IBL,LBLO,BUFO,NTAPE)
      FAC=D2
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+3)*FAC
      II=IDRTSO(IOR)
      LL=IDRTSO(LOR)
      IILL=IOFF(MAX0(II,LL))+MIN0(II,LL)
      HQ(IILL)=VAL
      GO TO 310
C
  306 CONTINUE
      FAC=D4
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+1)*FAC
      CALL ORDER(VAL,IOR,LOR,IOR,LOR,IBL,LBLO,BUFO,NTAPE)
      FAC=D2
      IF(MOINT) FAC=ONE
      VAL=BUFI(LAD+2)*FAC
      CALL ORDER(VAL,IOR,IOR,LOR,LOR,IBL,LBLO,BUFO,NTAPE)
      GO TO 310
C
  307 CONTINUE
      VAL=BUFI(LAD+1)
      CALL ORDER(VAL,IOR,IOR,IOR,IOR,IBL,LBLO,BUFO,NTAPE)
      VAL=BUFI(LAD+2)
      II=IDRTSO(IOR)
      IIII=IOFF(II+1)
      HQ(IIII)=VAL
C
  310 CONTINUE
C*    IF(IPRNT.LE.4) GO TO 110
      WRITE(6,5) IOR,JOR,KOR,LOR,IJKLT,
     1           BUFI(LAD+1),BUFI(LAD+2),BUFI(LAD+3)
C
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
  140 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,6)
      CALL PRINT(HQ,NUMIJ,NBF,6)
C
C   WRITE THE LAST BUFFER
  202 CONTINUE
      IBL=IBL+1
      LBLO(IBL+IBL-1)=0
      LBLO(IBL+IBL)=0
      BUFO(IBL+MAXBUF)=A00
      CALL SWRIT(NTAPE,LBLO,MAXBF4)
C
C   STORE ONE ELECTRON MO MATRIX ON THE MASTER FILE
      IF(.NOT.MOINT) GO TO 203
      CALL MWRIT(HQ,16)
      CALL MWRIT(HQ,23)
C
C   STORE ONE PDM ON THE MASTER FILE
  203 CONTINUE
      IPOS=35
      IF(ICITYP.EQ.2) IPOS=39
      CALL MWRIT(HQ,IPOS)
C
      CALL RCLOSE(ITAPE,3)
      CALL RCLOSE(NTAPE,3)
      RETURN
      END
      SUBROUTINE ORDER(VAL,I,J,K,L,IBL,LBLO,BUFO,NTAPE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION LBLO(MAXBF4),BUFO(MAXBF2)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/ORDSO/IORSO(256),IDRTSO(256)
      COMMON/SIGNS/IOFF(256),IPRNT
    1 FORMAT(2X,4I5,5X,4I5,5X,F20.10)
C
      IT=IDRTSO(I)
      JT=IDRTSO(J)
      KT=IDRTSO(K)
      LT=IDRTSO(L)
      IJT=IOFF(MAX0(IT,JT))+MIN0(IT,JT)
      KLT=IOFF(MAX0(KT,LT))+MIN0(KT,LT)
      IF(IJT.LT.KLT) GO TO 201
      II=MAX0(IT,JT)
      JJ=MIN0(IT,JT)
      KK=MAX0(KT,LT)
      LL=MIN0(KT,LT)
      GO TO 202
  201 CONTINUE
      II=MAX0(KT,LT)
      JJ=MIN0(KT,LT)
      KK=MAX0(IT,JT)
      LL=MIN0(IT,JT)
  202 CONTINUE
      CALL PACK(II,JJ,KK,LL,IX,JA,JAA)
      IBL=IBL+1
      IF(IPRNT.LE.3) GO TO 203
      WRITE(6,1) I,J,K,L,II,JJ,KK,LL,VAL
  203 CONTINUE
      LBLO(IBL+IBL-1)=JA
      LBLO(IBL+IBL)=JAA
      BUFO(IBL+MAXBUF)=VAL
      IF(IBL.LT.MAXBUF) GO TO 205
      IBL=0
      CALL SWRIT(NTAPE,LBLO,MAXBF4)
C
  205 CONTINUE
      RETURN
      END
