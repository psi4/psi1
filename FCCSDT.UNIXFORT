      PROGRAM CCSDT
C
C    ************************************************
C    * THE CLOSED-SHELL COUPLED CLUSTER SDT PROGRAM *
C    * LAST UPDATE  JULY 28    , 1986  BY GES       *
C    *         CCSDT UPDATE OCT 23, 87.             *
C    ************************************************
C
C   SEPTEMBER 19, 1988
C   SET UP DIIS ON DISK INCLUDING T3 AMPLITUDES.
C   SET UP RESTART OPTION INCLUDING T3 AMPLITUDES.
C
C   MAY 24,   1988
C   T3 SYMMETRY-PACKED, REDUCTION OF CHI INTERMEDIATES
C      AND FROZEN ORBITALS IMPLEMENTED.
C
C   APRIL 28, 1988
C   FULL CCSDT DEBUGGED! NON-SCF TERMS PARTIALLY INCLUDED.
C
C   MARCH 31, 1988
C   INCLUDE ALL NON-SCF CCSD TERMS AND DEBUG WITH FCI CASES.
C
C *************************************************************
C *- THIS VERSION WORKS WITH CCTRANS (NO CCSORT NEEDED).      *
C *- ALL 2E INTS READ FROM FILE78 AND KEPT IN CORE IN COMPLETE*
C *  CANONICAL ORDERING.                                      *
C *- T2 IS SYMMETRY PACKED (INTEGRALS NOT).                   *
C *- ALL CCSD LOOPS ARE SYMMETRY ADAPTED.                     *
C *- DIIS IN CORE. OPTIONAL PARAMETERS AVAILABLE.             *
C *- THERE IS AN ETEST (NEEDS FILE8 FOR 1E INTS) NOT WORKING. *
C *                                                           *
C *  VERSION DESIGNED FOR DEBUGGING PURPOSES.                 *
C *                                                           *
C *  UPDATED JUNE 19, 1987 BY GES.                            *
C *************************************************************
C
C
      IMPLICIT INTEGER (A-Z)
      REAL*8 TIMLIM,E,CC,AZERO,ONE,   WTEMP(120),HALF,ENUC,ESCF,ECORR
      REAL*4 EECPU,EETIME,EETCPU
      CHARACTER*32 DATTIM
      CHARACTER* 4 LABEL(20), RSTR
      COMMON/TIEMPO/EECPU,EETIME,EETCPU
      COMMON/RESTAR/TIMLIM
      DIMENSION E(120),NLAMDA(8),ITEMP(1),NC(8),ITYP(8),FLOV(8,4),
     .          ORBSYM(120),ITEP(8)
      DIMENSION FZO(50),FZV(50)
      DIMENSION NOSYM(8),NVSYM(8),NFZO(8),NFZV(8),NDOC(8),NUOC(8)
      INTEGER PTOCC(120)
      EQUIVALENCE (CC,IC)
      DIMENSION CC(1),IC(7000000)
C     DIMENSION CC(1),IC(3000000)
      call drum
cets  CALL NOUNFL
      IMXCOR=3500000
C     IMXCOR=1500000
      INPUT=5
      IW = 6
      ITAP30=30
      ITAP78=78
      ITAP98=98
      ITAP99=99
cets  CALL DATETM(DATTIM,EECPU,EETIME,EETCPU)
      CALL TSTART(6)
      WRITE(IW,6000)
      AZERO=0.0D0
      HALF=0.5D0
      ONE =1.0D0
      INTBUF=SEC2I(100)/INTOWP(1)
C     WRITE (6,*) ' AT RFILES'
      CALL RFILE(ITAP30)
      CALL RFILE(ITAP78)
      CALL RFILE(ITAP98)
      CALL RFILE(ITAP99)
C     WRITE (6,*) ' BEFORE READ OF TAPE 30 CONSTANTS'
      CALL WREADW(ITAP30,IC,200,101,LWORD)
      MPOINT=IC(2)
      MCONST=IC(3)
      MCALCS=IC(4)
      NCALCS=IC(5)
      NT=IC(18)
      NTAO=IC(22)
      NIRRED=IC(28)
      NSYMHF=IC(41)
      MXCOEF=IC(42)
C     WRITE (*,*) ' NSYMHF=',NSYMHF
C     WRITE (*,*) ' MCALCS=',MCALCS
C     WRITE (*,*) ' MXCOEF=',MXCOEF
      CALL ZERO(CC,IMXCOR)
      CALL READ30(ITAP30,MPOINT,MCONST,MCALCS,NCALCS,NT,NSYMHF,
     .            MXCOEF,E,NLAMDA,ITEMP,6,NC,NO,WTEMP,PTOCC,ITYP,
     .            ORBSYM,FLOV,NIRRED,ITEP,ENUC,ESCF)
      NV=NT-NO
      WRITE(IW,6020)
 6020 FORMAT(//,2X,'I',3X,'IRREP',10X,'SCF MO ENERGY',/)
 6021 FORMAT(I3,4X,A4,5X,F18.12)
      DO 556 I=1,NT
      WRITE (IW,6021) I,ITEP(ORBSYM(I)+1),E(I)
  556 CONTINUE
      NO2=(NO*(NO+1))/2
      NV2=(NV*(NV+1))/2
      NT2=(NT*(NT+1))/2
      NONO=NO*NO
      NVNV=NV*NV
      NM =NO*NV
C     WRITE(*,*) 'MAIN NM= ',NM
      NMNM=NM*NM
      NTNT=NT*NT
      NM2=(NM*(NM+1))/2
      NGOO=(NO2*(NO2+1))/2
      NGVV=(NV2*(NV2+1))/2
      NGOV=(NM*(NO+1)*(NV+1))/4
      NHOV=(NM*(NM+1))/2
      NLOV=NO2*NM
      NLVO=NV2*NM
      NINT=NGOO+NGVV+NGOV+NHOV+NLOV+NLVO
      NTIN=(NT2*(NT2+1))/2
C     WRITE(*,*) '  NTIN = ',NTIN
      NONO2=NO*NO2
      NONV2=NO*NV2
      NONM=NO*NM
      NVNO2=NV*NO2
      NVNV2=NV*NV2
      NVNM=NV*NM
      NOV2=MAX0(NO2,NV2)
      NOV =MAX0(NO ,NV )
      IMX=MAX0(NM,NV2)
      NOVNOV=NOV*NOV
      WRITE(IW,6030)NO,NV,NT,NM,NO2,NV2,NT2,NM2,NGOO,NGVV,NHOV,NGOV,
     .              NLOV,NLVO,NINT
      CALL LOCATE (INPUT,'# CCSD ###',IERR)
      IF(IERR.NE.0) THEN
      WRITE (IW,6007)
      WRITE ( 4,6007)
 6007 FORMAT(//,2X,' WARNING! NO INPUT TO CCSD HAS BEEN FOUND.',/,
     .          2X,'          DEFAULT PARAMETERS WILL BE USED.')
      EPSI=5
      NGO=2
      NMIN =2
      MINDIM=2
      MAXDIM=8
      MAXIT=20
      CONVI=7
      RSTR='NO  '
C     DEFAULT LEVEL=0 (CCSD)
      LEVEL=0
      DO 622 I=1,NIRRED
      NFZO(I)=0
  622 NFZV(I)=0
      ELSE
      READ(5,6004)LABEL
 6004 FORMAT(20A4)
      WRITE(6,6024)LABEL
 6024 FORMAT(//,'# CCSD ###  LABEL :  ',20A4)
      READ(5,6002)EPSI,NGO,NMIN ,MINDIM,MAXDIM,FLDIIS,IRSTR
      IF(EPSI  .EQ.0)EPSI  =5
      IF(NGO   .EQ.0)NGO   =2
      IF(NMIN  .EQ.0)NMIN  =2
      IF(MINDIM.EQ.0)MINDIM=2
      IF(MAXDIM.EQ.0)MAXDIM=8
      IF(IRSTR.NE.0)RSTR='YES '
      IF(IRSTR.EQ.0)RSTR='NO  '
      READ(5,6002)CONVI,MAXIT,LEVEL
      IF(CONVI .EQ.0)CONVI =7
      IF(MAXIT .EQ.0)MAXIT =20
 6002 FORMAT(16I5)
      DO 623 I=1,NIRRED
  623 READ(INPUT,6051)NFZO(I),NFZV(I)
 6051 FORMAT(I2,1X,I2)
      END IF
 6005 FORMAT(/, '****** FLDIIS=2, NO DIIS! *****',/)
      WRITE(IW,6003)EPSI,NGO,NMIN ,MINDIM,MAXDIM,CONVI,MAXIT,RSTR,LEVEL
 6003 FORMAT(//,2X,'***** DIIS PARAMETERS *****',
     .        /,2X,'EPSI   =',I5,
     .        /,2X,'NGO    =',I5,
     .        /,2X,'NMIN   =',I5,
     .        /,2X,'MINDIM =',I5,
     .        /,2X,'MAXDIM =',I5,
     .       //,2X,'***** CCSD PARAMETERS *****',
     .        /,2X,'CONVI  =',I5,
     .        /,2X,'MAXIT  =',I5,
     .        /,2X,'RSTR   =',A4,
     .        /,2X,'LEVEL  =',I5)
  749 CONTINUE
      IF(FLDIIS.EQ.2)THEN
      WRITE(6,6005)
      ENDIF
      CALL FROZEN (FLOV,NOSYM,NVSYM,NFZO,NFZV,NDOC,NUOC,FZO,FZV,
     .             ITEP,NIRRED,NO,NV,IW)
      CALL NCOUNT(ORBSYM,FLOV,NIRRED,NO,NV,NT,NDIMT2,NDIMT1,FZO,FZV,
     .            NDIMT3)
      WRITE(IW,6012)
      WRITE(IW,6013)NDIMT1
      WRITE(IW,6014)NDIMT2
      WRITE(IW,6016)NDIMT3
      NDIMTT=NDIMT1+NDIMT2+NDIMT3+1
      WRITE(IW,6015)NDIMTT
 6012 FORMAT(//,2X,'*****  # WALKS  *******************',/)
 6013 FORMAT(2X,'THE NUMBER OF SINGLE EXCITATIONS IS',I12)
 6014 FORMAT(2X,'THE NUMBER OF DOUBLE EXCITATIONS IS',I12)
 6016 FORMAT(2X,'THE NUMBER OF TRIPLE EXCITATIONS IS',I12)
 6015 FORMAT(2X,'THE TOTAL NUMBER OF  CC  WALKS   IS',I12)
C  CORE ALLOCATION
      IPV=1
      IPO=IPV+NM
      ITRV=IPO+NM
      ITRT=ITRV+NM
      ITR=ITRT+NM
      IPL=ITR+NV
      IPQ=IPL+NT
      ITRO=IPQ+NT2
      UOFF=ITRO+IMX
      VADD=UOFF+NO*NO*2
      ZLX= VADD+NV*NV
      IOUT=ZLX+NV*NV
      X3OFF=IOUT+NT
      X3ADD=X3OFF+NO*NO*NO
      II=IADTWP(X3ADD+NV*NV*NV)
      BUF=II+NTIN
      IBUF=WPADTI(BUF)
      AAA=BUF
C
C     NOTE THAT BUF SCRATCHES FOLLOWING POSITIONS
C
      T2=AAA
      TAU=T2+NDIMT2
      S=TAU+NDIMT2
      T1O=S+NDIMT2
      T1N=T1O+NM
      FO=T1N+NM
      FV=FO+NONO
      XA=FV+NVNV
      XB=XA+NO
      XC=XB+NO
      FOCK=XC+NO
      FFO=FOCK+NT2
      FFV=FFO+NO*NO
      DVEC=FFV+NV*NV
      WVEC=DVEC+NT*NT
      XVEC=WVEC+NT*NT
      XARR=XVEC+NT*NT
      Z=XARR+NT
      F11=Z+NO*NV
      F12=F11+NM
      T2P1=F12+NM
      D1=T2P1+NM
      ICC=D1+NM
      IBB=ICC+MAXDIM
      IB2=IBB+(MAXDIM+1)*(MAXDIM+2)
      ILAST=IB2+(MAXDIM+1)*(MAXDIM+2)
      WRITE(*,*)'CORE BEFORE CCSDT',ILAST
C
C     ALLOCATE CORE FOR FULL CCSDT
C
      CHI=ILAST
      F1=CHI+NT*NT
      F2=F1+NO*NV*NV*NV
      F3=F2+NO*NO*NO*NV
      F4=F3+NO*NV*NO*NV
      F5=F4+NO*NV*NO*NV
      F6=F5+NO*NV*NO*NV
      CHI1=F6+NO*NV*NO*NV
      CHI2=CHI1+NO*NV*NV*NV
      CHIO=CHI2+NO*NO*NO*NV
      CHIV=CHIO+NO*NO
      FFF =CHIV+NV*NV
      Y3=FFF+NO*NV
      Z3=Y3+NDIMT3
      ILAST=Z3+NDIMT3
C
      IMCOR=IMXCOR*8/1000
      WRITE(IW,6009)
 6009 FORMAT(/,2X,'DIIS WILL BE DONE IN DISK')
      ICOR =ILAST *8/1000
      WRITE(IW,6001)IMCOR,IMXCOR,ICOR,ILAST
      IF(ILAST.GT.IMXCOR) THEN
      WRITE(IW,*)' SORRY BUD, GET A COMPUTER WITH MORE MEMORY !!'
      STOP 'NOT ENOUGH CORE '
      END IF
      IF(FLDIIS.EQ.2)THEN
      DIISFL=2
      ELSE
      DIISFL=0
      ENDIF
C********* CONSTRUCT INDICES TO BE USED
      DO 140  I=1,NM
      I1=I-1
      IC(IPV+I1)=NO+1+I1/NO
      IC(IPO+I1)=I-(IC(IPV+I1)-NO-1)*NO
      IC(ITRV+I1)=I1*NV2
  140 IC(ITRT+I1)=I1*NM
      DO 141 I=1,NV
      I1=I-1
  141 IC(ITR+I1)=I1*NO
      DO 142 I=1,NT
      I1=I-1
  142 IC(IPL+I1)=I1*NT
      IC(IPQ)=0
      DO 143 I=1,NT2-1
  143 IC(IPQ+I)=IC(IPQ+I-1)+I
      DO 144 I=1,IMX
      I1=I-1
  144 IC(ITRO+I1)=I1*NO2
C
      CALL SYMARR(ORBSYM,FLOV,NIRRED,NO,NV,NT,IC(UOFF),IC(VADD),IC(ZLX),
     .            FZO,FZV,IC(X3OFF),IC(X3ADD))
C     WRITE(*,*)'IN MAIN AFTER CALL SYMARR',(IC(X3ADD+I),I=1,100)
C
C     CALL ZERO(CC(II),NTIN)
      CALL RDINTS(ITAP78,CC(II),NTIN,NT,6,CC(BUF),IC(IBUF),INTBUF,
     .            IC(IPQ),NT2)
C     WRITE(*,*)'IN MAIN AFTER CALL RDINTS',(IC(X3ADD+I),I=1,100)
C
CGES  CALL ETEST(CC(II),NTIN,CC(HINTS),NT2,NT,NO)
C
C     WRITE(IW,*)  'GOO'
C     WRITE(IW,6070) (I,CC(I),I=GOO,GVV-1)
C     WRITE(IW,6040)
C     WRITE(IW,*)  'GVV'
C     WRITE(IW,6070) (I,CC(I),I=GVV,HOV-1)
C     WRITE(IW,6040)
C     WRITE(IW,*)  'HOV'
C     WRITE(IW,6070) (I,CC(I),I=HOV,GOV-1)
C     WRITE(IW,6040)
C     WRITE(IW,*)  'GOV'
C     WRITE(IW,6070) (I,CC(I),I=GOV,LOV-1)
C     WRITE(IW,6040)
C     WRITE(IW,*)  'LOV'
C     WRITE(IW,6070) (I,CC(I),I=LOV,LVO-1)
C     WRITE(IW,6040)
C     WRITE(IW,*)  'LVO'
C     WRITE(IW,6070) (I,CC(I),I=LVO,AAA-1)
C     WRITE(IW,6040)
C********CLOSED-SHELL COUPLED CLUSTER CALCULATION
      CALL FCLUS(CC(II),NTIN,EPSI,NGO,NMIN ,MINDIM,MAXDIM,MAXIT,CONVI,
     .          IC(IPQ),IC(IPL),IC(ITR),IC(ITRO),IC(ITRV),IC(ITRT),
     .          NO,NV,NT,NM,NV2,NT2,IMX,NONO,NVNV,NDIMT2,
     .          CC(T2),CC(TAU),CC(S),CC(T1O),CC(T1N),CC(FO),CC(FV),
     .          CC(XA),CC(XB),CC(XC),CC(F11),CC(F12),CC(T2P1),
     .          CC(D1),E,ORBSYM,FLOV,NSYMHF,NIRRED,
     .          IC(UOFF),IC(VADD),IC(ZLX),
     .          CC(ICC),CC(IBB),CC(IB2),
     .          CC(TT1),CC(DT1),CC(TT2),CC(DT2),DIISFL,
     .          FZO,FZV,ECORR,LEVEL,CC(FOCK),CC(FFO),CC(FFV),
     .          CC(DVEC),CC(XVEC),CC(WVEC),CC(Z),CC(XARR),IC(IOUT),ESCF,
     .          ENUC,RSTR,
     .          CC(F1),CC(F2),CC(F3),CC(F4),CC(F5),CC(F6),
     .          CC(CHI1),CC(CHI2),
     .          CC(CHIO),CC(CHIV),CC(FFF),
     .          IC(X3OFF),IC(X3ADD),CC(Y3),NDIMT3,CC(Z3),CC(CHI),
     .          ITAP98,ITAP99)
      WRITE(IW,6074)
 6074 FORMAT(//,'CALCULATION RESULTS',/)
      WRITE(IW,6071) ESCF
      WRITE( 4,6071) ESCF
      WRITE(IW,6072) ECORR
      WRITE( 4,6072) ECORR
      ESCF=ESCF+ECORR
      WRITE(IW,6073) ESCF
      WRITE( 4,6073) ESCF
 6071 FORMAT('  SCF  ENERGY = ',F20.10)
 6072 FORMAT(' CORR. ENERGY = ',F20.10)
 6073 FORMAT(' TOTAL ENERGY = ',F20.10)
 
 
C
      CALL TSTOP (6)
C
 6000 FORMAT(//,42('-'),/,42('-'),/,
     .' THE CLOSED-SHELL  FULL   CCSDT    PROGRAM',/,
     .'         (GES VERSION MAY 1988)',/,42('-'),/,42('-'),//)
 6001 FORMAT(//,' MAXIMUM  CORE = ',I7,2X,'K BYTES  OR  ',I9 ,' REAL',
     .         ' WORDS',/,
     .          ' REQUIRED CORE = ',I7,2X,'K BYTES  OR  ',I9 ,' REAL',
     .         ' WORDS')
 6030 FORMAT(//,'NO  =',I8,6X,'NV  =',I8,6X,'NT  =',I8,6X,'NM  =',I8,/,
     .          'NO2 =',I8,6X,'NV2 =',I8,6X,'NT2 =',I8,6X,'NM2 =',I8,/,
     .          'NGOO=',I8,6X,'NGVV=',I8,6X,'NHOV=',I8,6X,'NGOV=',I8,/,
     .          'NLOV=',I8,6X,'NLVO=',I8,/,'NINT=',I8,//)
 6040 FORMAT(//)
 6070 FORMAT(6(1X,I6,2X,F12.6))
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE DIISC (T1O,T1N,T2,S,NO,NV,NDIMT2,NIT,IT,IECORR,
     .                  EPSI,NGO,NDIIS,MINDIM,MAXDIM,
     .                  CC,BB,TT1,DT1,TT2,DT2)
      IMPLICIT INTEGER (A-Z)
cets  REAL * 16 DET
      real * 8 det
      REAL * 8 BB,TT1,TT2,EPS,CC,ONE,XM,XMAX,XEN,DT1,DT2,XON,XLAM
      REAL * 8 T1N,T1O,S,T2,ZERO,XFAC,XADD
      DIMENSION CC(MAXDIM),BB(MAXDIM+1,MAXDIM+2),
     .          TT1(MAXDIM,NO,NV),DT1(MAXDIM,NO,NV),
     .          TT2(MAXDIM,NDIMT2),DT2(MAXDIM,NDIMT2)
      DIMENSION T1O(NO,NV),T1N(NO,NV),T2(NDIMT2),S(NDIMT2)
C
      ZERO=0.0D0
      ONE=1.D0
      EPS=10.D0 **(-EPSI)
      IECORR=0
C
C     DIIS EXTRAPOLATION
C
      IF(NIT.LT.NDIIS) GO TO 2400
      IF(IFLAG.EQ.1) THEN
      IFLAG=0
      NCOUNT=0
      GO TO 2400
      END IF
      NCOUNT=NCOUNT+1
      IT=IT+1
      IF(IT.LE.MAXDIM) GO TO 2040
      DO 2030 N=1,MAXDIM-1
      DO 2010 I=1,NO
      DO 2010 A=1,NV
      DT1(N,I,A)=DT1(N+1,I,A)
 2010 TT1(N,I,A)=TT1(N+1,I,A)
      DO 2020 UVBG=1,NDIMT2
      DT2(N,UVBG)=DT2(N+1,UVBG)
 2020 TT2(N,UVBG)=TT2(N+1,UVBG)
 2030 CONTINUE
      IT=MAXDIM
 2040 CONTINUE
      DO 2050 I=1,NO
      DO 2050 A=1,NV
      DT1(IT,I,A)=T1N(I,A)-T1O(I,A)
 2050 TT1(IT,I,A)=T1O(I,A)
      DO 2060 UVBG=1,NDIMT2
      DT2(IT,UVBG)=S(UVBG)-T2(UVBG)
 2060 TT2(IT,UVBG)=T2(UVBG)
      IF(IT.LT.MINDIM) GO TO 2400
      DO 2090 N=1,IT
      DO 2090 M=1,N
      BB(N,M)=ZERO
      DO 2070 I=1,NO
      DO 2070 A=1,NV
 2070 BB(N,M)=BB(N,M)+DT1(N,I,A)*DT1(M,I,A)
      DO 2080 UVBG=1,NDIMT2
 2080 BB(N,M)=BB(N,M)+DT2(N,UVBG)*DT2(M,UVBG)
      BB(M,N)=BB(N,M)
 2090 CONTINUE
C     FIND THE MAXIMUM AND SCALE
      XM=DABS(BB(1,1))
      DO 2094 N=1,IT
      DO 2094 M=1,N
      XFAC=DABS(BB(N,M))
      XMAX=DMAX1(XM,XFAC)
      XM=XMAX
 2094 CONTINUE
      DO 2095 N=1,IT
      DO 2095 M=1,IT
 2095 BB(N,M)=BB(N,M)/XM
      IT1=IT+1
      IT2=IT+2
      DO 2100 N=1,IT
      BB(N,IT1)=-ONE
 2100 BB(IT1,N)=-ONE
      BB(IT1,IT1)= ZERO
      DO 2110 N=1,IT
 2110 BB(N,IT2)= ZERO
      BB(IT1,IT2)=-ONE
      CALL FLINQ(BB,9,IT1,1,DET)
C     WRITE(6,*)'DET=',DET
C     WRITE(4,*)'DET=',DET
      XADD=ZERO
      DO 2120 N=1,IT
      CC(N)= BB(N,IT2)
      XADD=XADD+CC(N)
C     WRITE(6,*)'N=',N,'CC(N)=',CC(N)
 2120 CONTINUE
      XEN=BB(IT1,IT2)*XM
      XEN=DSQRT(XEN)
      XON=ZERO
C     WRITE(6,*)'CSUM=',XADD
C     WRITE(4,*)'CSUM=',XADD
      DO 2124 I=1,NO
      DO 2124 A=1,NV
      XON = XON + DT1(IT,I,A)*DT1(IT,I,A)
 2124 CONTINUE
      DO 2125 UVBG=1,NDIMT2
      XON = XON + DT2(IT,UVBG)*DT2(IT,UVBG)
 2125 CONTINUE
      XON=DSQRT(XON)
      WRITE(6,*)'XEN =',XEN
      WRITE(4,*)'XEN =',XEN
      WRITE(6,*)'XON =',XON
      WRITE(4,*)'XON =',XON
      IF(DABS(XEN).GT.EPS) GO TO 2400
      IF(NCOUNT.LE.NGO) GO TO 2400
      DO 2140 I=1,NO
      DO 2140 A=1,NV
 2140 T1N(I,A)= ZERO
      DO 2160 UVBG=1,NDIMT2
 2160 S(UVBG)= ZERO
      DO 2200 N=1,IT
      DO 2170 I=1,NO
      DO 2170 A=1,NV
 2170 T1N(I,A)= T1N(I,A)+CC(N)*TT1(N,I,A)
      DO 2180 UVBG=1,NDIMT2
 2180 S(UVBG)= S(UVBG)+CC(N)*TT2(N,UVBG)
 2200 CONTINUE
      IFLAG=1
      IECORR=1
 2400 CONTINUE
C
      END
C
C----------------------------------------------------------------------
C
      SUBROUTINE DIISD3(T1O,T1N,T2,S,NO,NV,NDIMT2,IT,MAXDIM,
     .                  CC,BB,ITAP98,ITAP99,ITC,BB2,
     .                  T3,T3O,NDIMT3)
      IMPLICIT INTEGER (A-Z)
cets  REAL * 16 DET
      real*8 det
      REAL*8 ONE,XM,XMAX,XEN,XON,A0,XFAC,XADD
      REAL*8 CC(MAXDIM),BB(MAXDIM+1,MAXDIM+2),BB2(MAXDIM+1,MAXDIM+2),
     .       T1O(NO,NV),T1N(NO,NV),T2(NDIMT2),S(NDIMT2),
     .       T3(NDIMT3),T3O(NDIMT3)
C
      A0 = 0.0D0
      ONE= 1.0D0
C     WRITE(6,*)'IN DIISD3'
C     WRITE(6,*)'ITAP98=',ITAP98, ' ITAP99=',ITAP99
C     WRITE(6,*)'NDIMT3=',NDIMT3
      IT=IT+1
      IF(IT.GT.MAXDIM) IT = MAXDIM
      ITC = ITC + 1
      IF(ITC.GT.MAXDIM) ITC = 1
C
      DO 2050 I=1,NO
      DO 2051 A=1,NV
      T1O(I,A) = T1N(I,A) - T1O(I,A)
 2051 CONTINUE
 2050 CONTINUE
C
      DO 2060 UVBG=1,NDIMT2
      T2(UVBG) = S(UVBG) - T2(UVBG)
 2060 CONTINUE
C
      DO 2065 UVBG=1,NDIMT3
      T3O(UVBG) =T3(UVBG) - T3O(UVBG)
 2065 CONTINUE
C
      PT1 = (ITC-1)*INTOWP(NO*NV + NDIMT2 + NDIMT3) + 1
      PT2 = PT1 + INTOWP(NO*NV)
      PT3 = PT2 + INTOWP(NDIMT2)
      CALL WWRITW(ITAP99,T1O,INTOWP(NO*NV),PT1,JUNK)
      CALL WWRITW(ITAP98,T1N,INTOWP(NO*NV),PT1,JUNK)
      CALL WWRITW(ITAP99,T2,INTOWP(NDIMT2),PT2,JUNK)
      CALL WWRITW(ITAP98,S,INTOWP(NDIMT2),PT2,JUNK)
      CALL WWRITW(ITAP99,T3O,INTOWP(NDIMT3),PT3,JUNK)
      CALL WWRITW(ITAP98,T3 ,INTOWP(NDIMT3),PT3,JUNK)
C
      DO 2090 N=1,IT
C
      PT1 = (N-1)*INTOWP(NO*NV + NDIMT2 + NDIMT3) + 1
      PT2 = PT1 + INTOWP(NO*NV)
      PT3 = PT2 + INTOWP(NDIMT2)
      CALL WREADW(ITAP99,T1N,INTOWP(NO*NV),PT1,JUNK)
      CALL WREADW(ITAP99,S,INTOWP(NDIMT2),PT2,JUNK)
      CALL WREADW(ITAP99,T3,INTOWP(NDIMT3),PT3,JUNK)
C
      BB2(N,ITC) = A0
C
      DO 2070 I=1,NO
      DO 2071 A=1,NV
      BB2(N,ITC) = BB2(N,ITC) + T1N(I,A)*T1O(I,A)
 2071 CONTINUE
 2070 CONTINUE
C
      DO 2080 UVBG=1,NDIMT2
      BB2(N,ITC) = BB2(N,ITC) + S(UVBG)*T2(UVBG)
 2080 CONTINUE
      DO 2085 UVBG=1,NDIMT3
      BB2(N,ITC) = BB2(N,ITC) +T3(UVBG)*T3O(UVBG)
 2085 CONTINUE
      BB2(ITC,N) = BB2(N,ITC)
 2090 CONTINUE
C
C  TRANSFER INTO THE BB ARRAY
C
      DO 10 N = 1,IT
      DO 20 M = 1,N-1
      BB(N,M) = BB2(N,M)
      BB(M,N) = BB2(M,N)
  20  CONTINUE
      BB(N,N) = BB2(N,N)
  10  CONTINUE
C
C     FIND THE MAXIMUM AND SCALE
C
      XM=DABS(BB(1,1))
      DO 2094 N=1,IT
      DO 2094 M=1,N
      XFAC=DABS(BB(N,M))
      XMAX=DMAX1(XM,XFAC)
      XM=XMAX
 2094 CONTINUE
      DO 2095 N=1,IT
      DO 2095 M=1,IT
 2095 BB(N,M)=BB(N,M)/XM
      IT1=IT+1
      IT2=IT+2
      DO 2100 N=1,IT
      BB(N,IT1)=-ONE
 2100 BB(IT1,N)=-ONE
      BB(IT1,IT1)= A0
      DO 2110 N=1,IT
 2110 BB(N,IT2)= A0
      BB(IT1,IT2)=-ONE
      CALL FLINQ(BB,9,IT1,1,DET)
C     WRITE(6,*)'DET=',DET
C     WRITE(4,*)'DET=',DET
      XADD=A0
      DO 2120 N=1,IT
      CC(N)= BB(N,IT2)
      XADD=XADD+CC(N)
C     WRITE(6,*)'N=',N,'CC(N)=',CC(N)
 2120 CONTINUE
      XEN=BB(IT1,IT2)*XM
      XEN=DSQRT(XEN)
      XON=A0
C     WRITE(6,*)'CSUM=',XADD
C     WRITE(4,*)'CSUM=',XADD
C     WRITE(6,*)'  XEN = ',XEN,'  XON = ',XON
C
      CALL ZERO(T1N,NO*NV)
      CALL ZERO(S,NDIMT2)
      CALL ZERO(T3,NDIMT3)
C
      DO 2200 N=1,IT
C
      PT1 = (N-1)*INTOWP(NO*NV + NDIMT2 + NDIMT3) + 1
      PT2 = PT1 + INTOWP(NO*NV)
      PT3 = PT2 + INTOWP(NDIMT2)
      CALL WREADW(ITAP98,T1O,INTOWP(NO*NV),PT1,JUNK)
      CALL WREADW(ITAP98,T2,INTOWP(NDIMT2),PT2,JUNK)
      CALL WREADW(ITAP98,T3O,INTOWP(NDIMT3),PT3,JUNK)
C
      DO 2170 I=1,NO
      DO 2171 A=1,NV
      T1N(I,A) = T1N(I,A) + CC(N)*T1O(I,A)
 2171 CONTINUE
 2170 CONTINUE
C
      DO 2180 UVBG=1,NDIMT2
      S(UVBG) = S(UVBG) + CC(N)*T2(UVBG)
 2180 CONTINUE
C
      DO 2186 UVBG=1,NDIMT3
      T3(UVBG) = T3(UVBG) + CC(N)*T3O(UVBG)
 2186 CONTINUE
C
 2200 CONTINUE
 2400 CONTINUE
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE ECORR(TAU,T2,T1N,II,XE,UOFF,VADD,IPQ,ORBSYM,FLOV,
     .                 NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK)
      IMPLICIT INTEGER (A-Z)
      REAL * 8 XE,TAU,II,T2,T1N,FOCK(NT2)
      DIMENSION ORBSYM(NT),FLOV(NIRRED,4),IPQ(NT2),UOFF(NO,NO,2),
     .          VADD(NV,NV),II(NTIN),TAU(NDIMT2),T2(NDIMT2),
     .          T1N(NO,NV),FZO(NO),FZV(NV)
C
C     CONSTRUCT TAU
C
      DO 70 I=1,NO
      IF(FZO(I).EQ.1)GO TO 70
      ISYM=ORBSYM(I)
      DO 69 J=1,NO
      IF(FZO(J).EQ.1)GO TO 69
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      DO 68 A=1,NV
      IF(FZV(A).EQ.1)GO TO 68
      ASYM=ORBSYM(A+NO)
      BSYM=IEOR(IJSYM,ASYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      IF(LB.GT.A)LB=A
      DO 66 B=FB,LB
      IJAB=UOFF(I,J,1)+VADD(A,B)
      TAU(IJAB)=T2(IJAB)+T1N(I,A)*T1N(J,B)
   66 CONTINUE
   68 CONTINUE
   69 CONTINUE
   70 CONTINUE
C
C     CALCULATE THE CORRELATION ENERGY
C
      XE=AZERO
      DO 81 I=1,NO
      IF(FZO(I).EQ.1)GO TO 81
      ISYM=ORBSYM(I)
      DO 80 J=1,NO
      IF(FZO(J).EQ.1)GO TO 80
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      DO 78 A=1,NV
      IF(FZV(A).EQ.1)GO TO 78
      ASYM=ORBSYM(A+NO)
      BSYM=IEOR(IJSYM,ASYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      IF(LB.GT.A-1)LB=A-1
      IA=IPQ(A+NO)+I
      DO 76 B=FB,LB
      JB=IPQ(B+NO)+J
      IAJB=IPQ(MAX0(IA,JB))+MIN0(IA,JB)
      IJAB=UOFF(I,J,1)+VADD(A,B)
      JIAB=UOFF(J,I,1)+VADD(A,B)
      XE=XE+II(IAJB)*(TAU(IJAB)+TAU(IJAB)-TAU(JIAB))
   76 CONTINUE
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      IF(FB.LT.A)FB=A
      DO 77 B=FB,LB
      JB=IPQ(B+NO)+J
      IAJB=IPQ(MAX0(IA,JB))+MIN0(IA,JB)
      IJAB=UOFF(J,I,1)+VADD(B,A)
      JIAB=UOFF(I,J,1)+VADD(B,A)
      XE=XE+II(IAJB)*(TAU(IJAB)+TAU(IJAB)-TAU(JIAB))
   77 CONTINUE
   78 CONTINUE
   80 CONTINUE
   81 CONTINUE
C
C     ADD FOCK MATRIX DEPENDENT TERMS
C
      DO 95 I=1,NO
      IF(FZO(I).EQ.1)GO TO 95
      DO 94 A=1,NV
      IF(FZV(A).EQ.1)GO TO 94
      IA=IPQ(A+NO)+I
      XE=XE+2.D0*FOCK(IA)*T1N(I,A)
   94 CONTINUE
   95 CONTINUE
C
      WRITE(6,9000)
      WRITE(6,9010) NIT,XE,XE
      WRITE(4,9010) NIT,XE,XE
 9000 FORMAT(///)
 9010 FORMAT('NIT =',I3,5X,'CORREL.ENERGY =',F16.12,3X,D23.16)
      END
C
C=======================================================================
C
      SUBROUTINE ETEST(INTS,NTIN,H,NIJ,NT,NO)
      IMPLICIT REAL*8 (A-H,O-Z)
C
      REAL*8 INTS(NTIN),H(NIJ)
      DATA A0,A2 /0.0D0,2.0D0/
C
C     WRITE(*,*) '   NIJ,NT,NO,NTIN ',NIJ,NT,NO,NTIN
      ITAP8 = 8
      REWIND ITAP8
      IJ = 0
      DO 10 I = 1,NT
        DO 20 J = 1,I
          IJ = IJ + 1
          READ(ITAP8,*) H(IJ)
   20 CONTINUE
   10 CONTINUE
C
CTJL  WRITE(*,*) '    THE ONE ELECTRON INTEGRALS '
CTJL  CALL PRINT(H,NIJ,NT,6)
C
      ESCF = A0
      ONE = A0
      TWO = A0
      DO 30 I = 1,NO
        II = (I+1)*I/2
        ONE = ONE + H(II)*A2
C
        DO 40 J = 1,NO
          JJ = (J+1)*J/2
          MAXIJ = MAX(I,J)
          MINIJ = MIN(I,J)
          IJ = (MAXIJ-1)*MAXIJ/2 + MINIJ
          MAXIIJ = MAX(II,JJ)
          MINIIJ = MIN(II,JJ)
          IIJJ = (MAXIIJ-1)*MAXIIJ/2 + MINIIJ
          IJIJ = (IJ+1)*IJ/2
          TWO = TWO + (A2*INTS(IIJJ)-INTS(IJIJ))
   40   CONTINUE
   30 CONTINUE
C
      ESCF = ONE + TWO
      WRITE(6,600)
  600 FORMAT(//,2X,'** ENERGIES FROM FILE78 **',/)
      WRITE(*,*) '  THE ONE ELECTRON ENERGY ',ONE
      WRITE(*,*) '  THE TWO ELECTRON ENERGY ',TWO
      WRITE(*,*) '  THE SCF ELECTRON ENERGY ',ESCF
C     WRITE(4,*) '  THE ONE ELECTRON ENERGY ',ONE
C     WRITE(4,*) '  THE TWO ELECTRON ENERGY ',TWO
C     WRITE(4,*) '  THE SCF ELECTRON ENERGY ',ESCF
      RETURN
      END
      SUBROUTINE FCLUS(II,NTIN,EPSI,NGO,NDIIS,MINDIM,MAXDIM,MAXIT,CONVI,
     .                IPQ,IPL,ITR,ITRO,ITRV,ITRT,
     .                NO,NV,NT,NM,NV2,NT2,IMX,NONO,NVNV,NDIMT2,
     .                T2,TAU,S,T1O,T1N,FO,FV,
     .                XA,XB,XC,F11,F12,T2P1,
     .                D1,EIG,ORBSYM,FLOV,NSYMHF,NIRRED,UOFF,VADD,ZLX,
     .            CCC,BBB,BB2,TT1,DT1,TT2,DT2,DIISFL,FZO,FZV,XE,LEVEL,
     .                FOCK,FFO,FFV,DVEC,XVEC,WVEC,Z,XARR,IOUT,ESCF,
     .                ENUC,RSTR,
     .                F1,F2,F3,F4,F5,F6,
     .                CHI1,CHI2,
     .                CHIO,CHIV,FFF,
     .                X3OFF,X3ADD,Y3,NDIMT3,Z3,CHI,ITAP98,ITAP99)
      IMPLICIT INTEGER (A-Z)
      REAL * 8  S,TAU,T2,T1O,T1N,F11,F12,AZERO,FO,FV,XA,XB,XC,XFAC,XADD,
     .          GOO,GVV,GOV,HOV,LOV,LVO,AX,BX,CX,DX,D1,T2P1,XE,EIG,
     .          XEO,CONV,II,HALF,CCC,BBB,TT1,DT1,TT2,DT2,BB2
      REAL * 8  DUVBG,DIUV,DDIJ,DDAI,DDAJ,DDBI,DDBJ
      REAL*8 FOCK(NT2),FFO(NO,NO),FFV(NV,NV)
      REAL*8 EREF,ESCF,ENUC,T1NORM,DELT
      REAL*8 DVEC(NT*NT),XVEC(NT*NT),WVEC(NT*NT),XARR(NT),Z(NO*NV)
      REAL*8 VAL,VAL1,VAL2,VAL3
      REAL*8 CHI1(NV,NV,NO,NV),CHI2(NV,NO,NO,NO)
      REAL*8 F1(NV,NO,NV,NV),F2(NV,NO,NO,NO),F3(NV,NO,NO,NV),
     .       F4(NV,NO,NV,NO),F5(NO,NV,NV,NO),F6(NO,NV,NO,NV)
      REAL*8 CHIO(NO,NO),CHIV(NV,NV),FFF(NO,NV)
      REAL*8 Y3(NDIMT3),Z3(NDIMT3)
      REAL*8 CHI(NT,NT)
      INTEGER X3OFF(NO,NO,NO),X3ADD(NV,NV,NV)
      DIMENSION S(NDIMT2),T2(NDIMT2),TAU(NDIMT2),T1O(NO,NV),
     .          F11(NV,NO),F12(NV,NO),FO(NO,NO),FV(NV,NV),XA(NO),
     .          XB(NO),XC(NO),T2P1(NO,NV),D1(NO,NV),T1N(NO,NV),EIG(NT)
      DIMENSION CCC(MAXDIM),BBB(MAXDIM+1,MAXDIM+2),
     .                      BB2(MAXDIM+1,MAXDIM+2),
     .          TT1(MAXDIM,NO,NV),DT1(MAXDIM,NO,NV),
     .          TT2(MAXDIM,NDIMT2),DT2(MAXDIM,NDIMT2)
      DIMENSION II(NTIN),ORBSYM(NT),FLOV(NIRRED,4),
     .          UOFF(NO,NO,2),VADD(NV,NV),ZLX(NV,NV)
      DIMENSION IPQ(NT2),IPL(NT),ITR(NV),ITRO(IMX),ITRV(NM),ITRT(NM)
      DIMENSION FZO(NO),FZV(NV)
      DIMENSION IOUT(NT)
      CHARACTER * 4 RSTR
C
      NIT=0
      AZERO=0.0D0
      HALF= 0.5D0
      CONV=10.D0**(-CONVI)
      XEO=AZERO
      IT=0
      IFLAG=0
      NCOUNT=0
      ITAP69=69
C
C     FOCK MATRIX IN MO BASIS
C
      CALL LOCATE (5,'# MONGO ###',IERR)
      IF(IERR.NE.0) THEN
      WRITE ( 6,6007)
      WRITE ( 4,6007)
 6007 FORMAT(//,2X,' NO INPUT TO MONGO HAS BEEN FOUND.',/,
     .          2X,' DIAGONAL FOCK MATRIX IS ASSUMED. ',//)
      FLAG=0
      ELSE
      READ(5,6004)FLAG
 6004 FORMAT(I5)
      END IF
      CALL ZERO (FOCK,NT2)
C
C     FLAG=0 (DEFAULT) USE MO ENERGIES IN THE FOCK MATRIX
C
      IF (FLAG.EQ.0) THEN
      EREF=ESCF
      DO 42 I=1,NT
      IJ=IPQ(I)+I
   42 FOCK(IJ)=EIG(I)
      ELSE
      WRITE(*,*)'NON-DIAGONAL FOCK MATRIX IS CONSTRUCTED '
      CALL FOCKI (DVEC,IOUT,XVEC,WVEC,XARR,NT,Z,FLAG,FOCK,EREF,ENUC,
     .                 II,NTIN,BUF,IBUF,INTBUF,
     .       NSGOO,NSGOV,NSHOV,NSLOV,IPQ,ITR,ITV,ITRO,ITRV,FZO,FZV,
     .            NT2,NO,NV,NM,IMX,NONO,NVNV,NOV)
      ESCF=EREF
      ENDIF
C
      IF(RSTR.EQ.'YES ')THEN
      CALL RFILE(ITAP69)
      CALL SREAD(ITAP69,JU1,1)
      CALL SREAD(ITAP69,JU2,1)
      CALL SREAD(ITAP69,JU3,1)
      CALL SREAD(ITAP69,T1N,INTOWP(NO*NV))
      CALL SREAD(ITAP69,S,INTOWP(NDIMT2))
      CALL SREAD(ITAP69,Z3,INTOWP(NDIMT3))
      CALL RCLOSE(ITAP69,3)
      GO TO 41
      END IF
C
C     SET INITIAL VALUES OF T2, T1N=0, Z3=0
C
      DO 25 U=1,NO
      DO 25 BE=1,NV
   25 T1N(U,BE)=AZERO
      DO 40 U=1,NO
      UU=IPQ(U)+U
      IF(FZO(U).EQ.1)GO TO 40
      USYM=ORBSYM(U)
      DO 39 V=1,NO
      VV=IPQ(V)+V
      IF(FZO(V).EQ.1)GO TO 39
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      DO 38 BE=1,NV
      BEBE=IPQ(BE+NO)+BE+NO
      IF(FZV(BE).EQ.1)GO TO 38
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(UVSYM,BESYM)+1
      BEU=IPQ(NO+BE)+U
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      IF(LGA.GT.BE)LGA=BE
      DO 36 GA=FGA,LGA
      GAGA=IPQ(GA+NO)+GA+NO
      GAV=IPQ(NO+GA)+V
      BEUGAV=IPQ(MAX0(BEU,GAV))+MIN0(BEU,GAV)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VAL=FOCK(UU)+FOCK(VV)-FOCK(BEBE)-FOCK(GAGA)
      S(UVBEGA)=II(BEUGAV)/VAL
   36 CONTINUE
   38 CONTINUE
   39 CONTINUE
   40 CONTINUE
      CALL ZERO (Z3,NDIMT3)
   41 CONTINUE
C
C     MP2 CORRELATION ENERGY
C
      CALL ECORR(TAU,S,T1N,II,XE,UOFF,VADD,IPQ,ORBSYM,FLOV,
     .           NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK)
C
   50 CONTINUE
C
      NIT=NIT+1
 9000 FORMAT(////)
      CALL TIMIT(4,6)
C
C     TRANSFER T AMPLITUDES TO BEGIN NEW ITERATION
C
      XEO=XE
      DO 90 I=1,NO
      DO 90 A=1,NV
      T1O(I,A)=T1N(I,A)
   90 CONTINUE
      DO 1400 UVBEGA=1,NDIMT2
      T2(UVBEGA)=S(UVBEGA)
 1400 CONTINUE
      DO 1401 INDEX=1,NDIMT3
      Y3(INDEX)=Z3(INDEX)
 1401 CONTINUE
C
C     AZERO ALL THE CORE
C
      DO 93 U=1,NO
      DO 93 V=1,NO
   93 FO(U,V)=AZERO
      DO 95 UVBEGA=1,NDIMT2
   95 S(UVBEGA)=AZERO
      DO 96 U=1,NO
      DO 96 A=1,NV
   96 T1N(U,A)=AZERO
      DO 97 B=1,NV
      DO 97 A=1,NV
   97 FV(B,A)=AZERO
      CALL ZERO (Z3,NDIMT3)
C
C     A TERMS
C
      DO 190 U=1,NO
      IF(FZO(U).EQ.1) GO TO 190
      USYM=ORBSYM(U)
      DO 125 V=1,U-1
      IF(FZO(V).EQ.1) GO TO 125
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      DO 120 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 120
      GASYM=ORBSYM(GA+NO)
      DO 118 BE=GA,NV
      IF(FZV(BE).EQ.1) GO TO 118
      BESYM=ORBSYM(BE+NO)
      BGSYM=IEOR(BESYM,GASYM)
      IF(UVSYM.NE.BGSYM) GO TO 115
      DO 110 IRI=1,NIRRED
      FI=FLOV(IRI,1)
      LI=FLOV(IRI,2)
      JSYM=IEOR(UVSYM,IRI-1)+1
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      DO 110 I=FI,LI
      UI=IPQ(MAX0(U,I))+MIN0(U,I)
      DO 110 J=FJ,LJ
      VJ=IPQ(MAX0(V,J))+MIN0(V,J)
      UIVJ=IPQ(MAX0(UI,VJ))+MIN0(UI,VJ)
      IJBEGA=UOFF(I,J,1)+VADD(BE,GA)
      JIBEGA=UOFF(J,I,1)+VADD(BE,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VUBEGA=UOFF(V,U,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)+II(UIVJ)*TAU(IJBEGA)
      S(VUBEGA)=S(VUBEGA)+II(UIVJ)*TAU(JIBEGA)
  110 CONTINUE
  115 CONTINUE
  118 CONTINUE
  120 CONTINUE
  125 CONTINUE
      DO 140 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 140
      GASYM=ORBSYM(GA+NO)
      DO 138 BE=GA,NV
      IF(FZV(BE).EQ.1) GO TO 138
      BESYM=ORBSYM(BE+NO)
      BGSYM=IEOR(BESYM,GASYM)
      IF(IJSYM.NE.BGSYM) GO TO 135
      DO 130 IRI=1,NIRRED
      FI=FLOV(IRI,1)
      LI=FLOV(IRI,2)
      JSYM=IEOR(UUSYM,IRI-1)+1
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      DO 130 I=FI,LI
      UI=IPQ(MAX0(U,I))+MIN0(U,I)
      DO 130 J=FJ,LJ
      IJBEGA=UOFF(I,J,1)+VADD(BE,GA)
      UUBEGA=UOFF(U,U,1)+VADD(BE,GA)
      UJ=IPQ(MAX0(U,J))+MIN0(U,J)
      UIUJ=IPQ(MAX0(UI,UJ))+MIN0(UI,UJ)
C     UIUJ=IPQ(UI)+UJ
      S(UUBEGA)=S(UUBEGA)+II(UIUJ)*TAU(IJBEGA)
  130 CONTINUE
  135 CONTINUE
  138 CONTINUE
  140 CONTINUE
  190 CONTINUE
      IF(NIT.EQ.3)THEN
      WRITE(6,*)'END A TERMS'
      CALL TIMIT(1,6)
      END IF
C
C     B TERMS
C
      DO 290 BE=1,NV
      IF(FZV(BE).EQ.1) GO TO 290
      BESYM=ORBSYM(BE+NO)
      DO 288 GA=1,BE
      IF(FZV(GA).EQ.1) GO TO 288
      GASYM=ORBSYM(GA+NO)
      BEGASM=IEOR(BESYM,GASYM)
      DO 280 U=1,NO
      IF(FZO(U).EQ.1) GO TO 280
      USYM=ORBSYM(U)
      UBEGAS=IEOR(USYM,BEGASM)
      DO 278 V=1,NO
      IF(FZO(V).EQ.1) GO TO 278
      VSYM=ORBSYM(V)
      IF(VSYM.NE.UBEGAS) GO TO 275
      DO 240 IRA = 1,NIRRED
      FA=FLOV(IRA,3)-NO
      LA=FLOV(IRA,4)-NO
      BSYM = IEOR(IRA-1,BEGASM) + 1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 235 A=FA,LA
      BEA=IPQ(MAX0(NO+BE,NO+A))+MIN0(NO+BE,NO+A)
      DO 235 B=FB,LB
      GAB=IPQ(MAX0(NO+GA,NO+B))+MIN0(NO+GA,NO+B)
C     BEAGAB=IPQ(BEA)+GAB
      BEAGAB=IPQ(MAX0(BEA,GAB))+MIN0(BEA,GAB)
      ZL=ZLX(A,B)
      UVAB=UOFF(U,V,ZL)+VADD(A,B)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)+II(BEAGAB)*TAU(UVAB)
  235 CONTINUE
  240 CONTINUE
  275 CONTINUE
  278 CONTINUE
  280 CONTINUE
  288 CONTINUE
  290 CONTINUE
      IF(NIT.EQ.3)THEN
      WRITE(6,*)'END B TERMS'
      CALL TIMIT(1,6)
      END IF
C
C     F TERMS
C
      DO 390 BE=1,NV
      IF(FZV(BE).EQ.1) GO TO 390
      BESYM=ORBSYM(BE+NO)
      DO 388  I=1,NO
      IF(FZO(I).EQ.1) GO TO 388
      ISYM=ORBSYM(I)
      BEISYM=IEOR(BESYM,ISYM)
      DO 310 B=BE,NV
      IF(FZV(B).EQ.1) GO TO 310
      F11(B,I)=AZERO
      F12(B,I)=AZERO
      BSYM=ORBSYM(B+NO)
      BBEIS=IEOR(BEISYM,BSYM)
      IB=IPQ(NO+B)+I
      BEB=IPQ(MAX0(NO+BE,NO+B))+MIN0(NO+BE,NO+B)
      FU=FLOV(BBEIS+1,1)
      LU=FLOV(BBEIS+1,2)
      FA=FLOV(BBEIS+1,3)-NO
      LA=FLOV(BBEIS+1,4)-NO
      DO 308 U=FU,LU
      F11(B,U)=AZERO
      F12(B,U)=AZERO
      DO 305 A=FA,LA
      BEA=IPQ(MAX0(NO+BE,NO+A))+MIN0(NO+BE,NO+A)
      IA=IPQ(NO+A)+I
      IABEB=IPQ(MAX0(IA,BEB))+MIN0(IA,BEB)
      IBBEA=IPQ(MAX0(IB,BEA))+MIN0(IB,BEA)
C     IBBEA=IPQ(IB)+BEA
      F11(B,U)=F11(B,U)+II(IABEB)*T1O(U,A)
      F12(B,U)=F12(B,U)+II(IBBEA)*T1O(U,A)
  305 CONTINUE
      IUBBE=UOFF(I,U,1)+VADD(B,BE)
      S(IUBBE)=S(IUBBE)+F12(B,U)
  308 CONTINUE
      IF(B.EQ.BE) GO TO 309
      FV(B,BE)=FV(B,BE)+F11(B,I)+F11(B,I)-F12(B,I)
  309 CONTINUE
  310 CONTINUE
C
      DO 320 B=1,BE
      IF(FZV(B).EQ.1) GO TO 320
      F11(B,I)=AZERO
      F12(B,I)=AZERO
      BSYM=ORBSYM(B+NO)
      BBEIS=IEOR(BEISYM,BSYM)
      IB=IPQ(NO+B)+I
      BEB=IPQ(MAX0(NO+BE,NO+B))+MIN0(NO+BE,NO+B)
      FU=FLOV(BBEIS+1,1)
      LU=FLOV(BBEIS+1,2)
      FA=FLOV(BBEIS+1,3)-NO
      LA=FLOV(BBEIS+1,4)-NO
      DO 318 U=FU,LU
      F11(B,U)=AZERO
      F12(B,U)=AZERO
      DO 315 A=FA,LA
      BEA=IPQ(MAX0(NO+BE,NO+A))+MIN0(NO+BE,NO+A)
      IA=IPQ(NO+A)+I
      IABEB=IPQ(MAX0(IA,BEB))+MIN0(IA,BEB)
      IBBEA=IPQ(MAX0(IB,BEA))+MIN0(IB,BEA)
      F11(B,U)=F11(B,U)+II(IABEB)*T1O(U,A)
      F12(B,U)=F12(B,U)+II(IBBEA)*T1O(U,A)
  315 CONTINUE
      UIBEB=UOFF(U,I,1)+VADD(BE,B)
      S(UIBEB)=S(UIBEB)+F12(B,U)
  318 CONTINUE
      FV(B,BE)=FV(B,BE)+F11(B,I)+F11(B,I)-F12(B,I)
  320 CONTINUE
C
      DO  360 U=1,NO
      IF(FZO(U).EQ.1) GO TO 360
      USYM=ORBSYM(U)
      DO 359 B=1,NV
      IF(FZV(B).EQ.1) GO TO 359
      BSYM=ORBSYM(B+NO)
      IBSYM=IEOR(ISYM,BSYM)
      UBSYM=IEOR(USYM,BSYM)
      IF(UBSYM.NE.BEISYM)GO TO 358
      DO 345 IRGA=1,NIRRED
      FGA=FLOV(IRGA,3)-NO
      LGA=FLOV(IRGA,4)-NO
      IF(LGA.GT.BE)LGA=BE
      VSYM=IEOR(IRGA-1,IBSYM)+1
      FIV=FLOV(VSYM,1)
      LV=FLOV(VSYM,2)
      DO 345 GA=FGA,LGA
      BEGA=IPQ(BE)+GA
      DO 340 V=FIV,LV
      ZL=ZLX(B,GA)
      IVBGA=UOFF(I,V,ZL)+VADD(B,GA)
      VIBGA=UOFF(V,I,ZL)+VADD(B,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VUBEGA=UOFF(V,U,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)+F12(B,U)*(T2(IVBGA)+T2(IVBGA)-T2(VIBGA))
     .                     -F11(B,U)*T2(IVBGA)
      S(VUBEGA)=S(VUBEGA)-F11(B,U)*T2(VIBGA)
  340 CONTINUE
  345 CONTINUE
      DO 355 IRGA=1,NIRRED
      FGA=FLOV(IRGA,3)-NO
      LGA=FLOV(IRGA,4)-NO
      IF(FGA.LT.BE)FGA=BE
      VSYM=IEOR(IRGA-1,IBSYM)+1
      FIV=FLOV(VSYM,1)
      LV=FLOV(VSYM,2)
      DO 355 GA=FGA,LGA
      GABE=IPQ(GA)+BE
      DO 350 V=FIV,LV
      ZL=ZLX(B,GA)
      IVBGA=UOFF(I,V,ZL)+VADD(B,GA)
      VIBGA=UOFF(V,I,ZL)+VADD(B,GA)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      UVGABE=UOFF(U,V,1)+VADD(GA,BE)
      S(VUGABE)=S(VUGABE)+F12(B,U)*(T2(IVBGA)+T2(IVBGA)-T2(VIBGA))
     .                     -F11(B,U)*T2(IVBGA)
      S(UVGABE)=S(UVGABE)-F11(B,U)*T2(VIBGA)
  350 CONTINUE
  355 CONTINUE
  358 CONTINUE
  359 CONTINUE
  360 CONTINUE
C
      DO 370 V=1,NO
      IF(FZO(V).EQ.1) GO TO 370
      VSYM=ORBSYM(V)
      IF(VSYM.NE.BESYM)GO TO 367
      DO 365 IRGA=1,NIRRED
      FGA=FLOV(IRGA,3)-NO
      LGA=FLOV(IRGA,4)-NO
      BSYM=IEOR(IRGA-1,BEISYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 364 GA=FGA,LGA
      BEGAI=IPQ(MAX0(BE,GA)+NO)+NO+MIN0(BE,GA)
      DO 363 B=FB,LB
      IB=IPQ(NO+B)+I
      IBBEGA=IPQ(MAX(IB,BEGAI))+MIN0(IB,BEGAI)
      ZL=ZLX(B,GA)
      IVBGA=UOFF(I,V,ZL)+VADD(B,GA)
      VIBGA=UOFF(V,I,ZL)+VADD(B,GA)
      XFAC=T2(IVBGA)+T2(IVBGA)-T2(VIBGA)
      T1N(V,BE)=T1N(V,BE)-II(IBBEGA)*XFAC
  363 CONTINUE
  364 CONTINUE
  365 CONTINUE
  367 CONTINUE
  370 CONTINUE
C
      DO 380 U=1,NO
      IF(FZO(U).EQ.1) GO TO 380
      USYM=ORBSYM(U)
      DO 379 V=1,NO
      IF(FZO(V).EQ.1) GO TO 379
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      IF(UVSYM.NE.BEISYM) GO TO 378
      AX=AZERO
      DO 374 IRA=1,NIRRED
      FA=FLOV(IRA,3)-NO
      LA=FLOV(IRA,4)-NO
      BSYM=IEOR(IRA-1,BEISYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 374 A=FA,LA
      IA=IPQ(NO+A)+I
      BEA=IPQ(MAX0(NO+BE,NO+A))+MIN0(NO+BE,NO+A)
      DO 372 B=FB,LB
      IB=IPQ(NO+B)+I
      BEB=IPQ(MAX0(NO+BE,NO+B))+MIN0(NO+BE,NO+B)
C     IABEB=IPQ(MAX0(IA,BEB))+MIN0(IA,BEB)
      IBBEA=IPQ(MAX0(IB,BEA))+MIN0(IB,BEA)
      ZL=ZLX(A,B)
      UVAB=UOFF(U,V,ZL)+VADD(A,B)
      AX=AX+II(IBBEA)*TAU(UVAB)
  372 CONTINUE
  374 CONTINUE
      FGA=FLOV(ISYM+1,3)-NO
      LGA=FLOV(ISYM+1,4)-NO
      IF(LGA.GT.BE)LGA=BE
      DO 376 GA=FGA,LGA
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
  376 S(UVBEGA)=S(UVBEGA)-AX*T1O(I,GA)
      LGA=FLOV(ISYM+1,4)-NO
      IF(FGA.LT.BE)FGA=BE
      DO 377 GA=FGA,LGA
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
  377 S(VUGABE)=S(VUGABE)-AX*T1O(I,GA)
  378 CONTINUE
  379 CONTINUE
  380 CONTINUE
C
  388 CONTINUE
  390 CONTINUE
      IF(NIT.EQ.3)THEN
      WRITE(6,*)'END F TERMS'
      CALL TIMIT(1,6)
C     CALL TIMIT(1,4)
      END IF
C
C     D TERMS
C
C     D2A,D2B,D2C
      DO 458 A=1,NV
      IF(FZV(A).EQ.1) GO TO 458
      ASYM=ORBSYM(A+NO)
      DO 457 I=1,NO
      IF(FZO(I).EQ.1) GO TO 457
      ISYM=ORBSYM(I)
      AISYM= IEOR(ASYM,ISYM)
C     READ  (AI,JB) AND (AJ,IB) ALL JB
      AI=IPQ(NO+A)+I
      DO 401 J=1,NO
      IF(FZO(J).EQ.1) GO TO 401
      DO 400 B=1,NV
      IF(FZV(B).EQ.1) GO TO 400
      BJ=IPQ(NO+B)+J
      AIBJ=IPQ(MAX0(AI,BJ))+MIN0(AI,BJ)
      T1N(I,A)=T1N(I,A)-(II(AIBJ)+II(AIBJ))*T1O(J,B)
  400 CONTINUE
  401 CONTINUE
C     CONSTRUCT T2P1(*I,A*)
      DO 404 K=1,NO
      IF(FZO(K).EQ.1) GO TO 404
      DO 402 C=1,A
      IF(FZV(C).EQ.1) GO TO 402
      KIAC=UOFF(K,I,1)+VADD(A,C)
      T2P1(K,C)=T2(KIAC)*HALF+T1O(K,A)*T1O(I,C)
  402 CONTINUE
      DO 403 C=A+1,NV
      IF(FZV(C).EQ.1) GO TO 403
      KIAC=UOFF(I,K,1)+VADD(C,A)
      T2P1(K,C)=T2(KIAC)*HALF+T1O(K,A)*T1O(I,C)
  403 CONTINUE
  404 CONTINUE
      DO 440 V=1,NO
      IF(FZO(V).EQ.1) GO TO 440
      VSYM=ORBSYM(V)
      AIVSYM=IEOR(VSYM,AISYM)
      DO 415 GA=A,NV
      IF(FZV(GA).EQ.1) GO TO 415
      GASYM=ORBSYM(GA+NO)
      IF(GASYM.NE.AIVSYM)GO TO 415
      GAA=IPQ(GA)+A
      AX=AZERO
      BX=AZERO
      CX=AZERO
      DO 405 IRJ=1,NIRRED
      BSYM=IEOR(AISYM,IRJ-1)+1
      FJ=FLOV(IRJ,1)
      LJ=FLOV(IRJ,2)
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 405 J=FJ,LJ
      AJ=IPQ(NO+A)+J
      DO 405 B=FB,LB
      BI=IPQ(NO+B)+I
      BJ=IPQ(NO+B)+J
      AJBI=IPQ(MAX0(AJ,BI))+MIN0(AJ,BI)
C     AIBJ=IPQ(AI)+BJ
      AIBJ=IPQ(MAX0(AI,BJ))+MIN0(AI,BJ)
      ZL=ZLX(GA,B)
      VJGAB=UOFF(V,J,ZL)+VADD(GA,B)
      JVGAB=UOFF(J,V,ZL)+VADD(GA,B)
      AX=AX+II(AIBJ)*(T2(VJGAB)+T2(VJGAB)-T2(JVGAB))
      BX=BX+II(AJBI)* T2(VJGAB)
      CX=CX+II(AJBI)* T2(JVGAB)
  405 CONTINUE
      VIGAA=UOFF(V,I,1)+VADD(GA,A)
      S(VIGAA)=S(VIGAA)+AX
      IF(A.EQ.GA) GO TO 416
      T1N(V,GA)=T1N(V,GA)-(AX-BX+AX-BX+CX)*T1O(I,A)
      CX=CX*HALF
      DO 411 IRU=1,NIRRED
      FU=FLOV(IRU,1)
      LU=FLOV(IRU,2)
      BESYM=IEOR(AISYM,IRU-1)+1
      FBE=FLOV(BESYM,3)-NO
      LBE=FLOV(BESYM,4)-NO
      FBE2=FBE
      LBE2=LBE
      IF(FBE.LT.GA)FBE2=GA
      IF(LBE.GT.GA)LBE2=GA
      DO 411 U=FU,LU
      DO 407 BE=FBE2,LBE
      BEGA=IPQ(BE)+GA
      ZL=ZLX(BE,A)
      UIBEA=UOFF(U,I,ZL)+VADD(BE,A)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VUBEGA=UOFF(V,U,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)+(AX-BX)*(T2(UIBEA)-T2P1(U,BE))+
     .                       CX*T2(UIBEA)
      S(VUBEGA)=S(VUBEGA)+ (CX+CX)*T2P1(U,BE)
  407 CONTINUE
      DO 409 BE=FBE,LBE2
      GABE=IPQ(GA)+BE
      ZL=ZLX(BE,A)
      UIBEA=UOFF(U,I,ZL)+VADD(BE,A)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      UVGABE=UOFF(U,V,1)+VADD(GA,BE)
      S(VUGABE)=S(VUGABE)+(AX-BX)*(T2(UIBEA)-T2P1(U,BE))+
     .                       CX*T2(UIBEA)
  409 S(UVGABE)=S(UVGABE)+ (CX+CX)*T2P1(U,BE)
  411 CONTINUE
  416 CONTINUE
  415 CONTINUE
      DO 435 GA=1,A
      IF(FZV(GA).EQ.1) GO TO 435
      GASYM=ORBSYM(GA+NO)
      IF(GASYM.NE.AIVSYM)GO TO 435
      AGA=IPQ(A)+GA
      AX=AZERO
      BX=AZERO
      CX=AZERO
      DO 425 IRJ=1,NIRRED
      BSYM=IEOR(AISYM,IRJ-1)+1
      FJ=FLOV(IRJ,1)
      LJ=FLOV(IRJ,2)
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 425 J=FJ,LJ
      AJ=IPQ(NO+A)+J
      DO 425 B=FB,LB
      BI=IPQ(NO+B)+I
      BJ=IPQ(NO+B)+J
C     AJBI=IPQ(AJ)+BI
      AJBI=IPQ(MAX0(AJ,BI))+MIN0(AJ,BI)
C     AIBJ=IPQ(AI)+BJ
      AIBJ=IPQ(MAX0(AI,BJ))+MIN0(AI,BJ)
      ZL=ZLX(GA,B)
      VJGAB=UOFF(V,J,ZL)+VADD(GA,B)
      JVGAB=UOFF(J,V,ZL)+VADD(GA,B)
      AX=AX+II(AIBJ)*(T2(VJGAB)+T2(VJGAB)-T2(JVGAB))
      BX=BX+II(AJBI)* T2(VJGAB)
      CX=CX+II(AJBI)* T2(JVGAB)
  425 CONTINUE
      IVAGA=UOFF(I,V,1)+VADD(A,GA)
      S(IVAGA)=S(IVAGA)+AX
      T1N(V,GA)=T1N(V,GA)-(AX-BX+AX-BX+CX)*T1O(I,A)
      CX=CX*HALF
      DO 431 IRU=1,NIRRED
      FU=FLOV(IRU,1)
      LU=FLOV(IRU,2)
      BESYM=IEOR(AISYM,IRU-1)+1
      FBE=FLOV(BESYM,3)-NO
      LBE=FLOV(BESYM,4)-NO
      FBE2=FBE
      LBE2=LBE
      IF(FBE.LT.GA)FBE2=GA
      IF(LBE.GT.GA)LBE2=GA
      DO 431 U=FU,LU
      DO 427 BE=FBE2,LBE
      BEGA=IPQ(BE)+GA
      ZL=ZLX(BE,A)
      UIBEA=UOFF(U,I,ZL)+VADD(BE,A)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VUBEGA=UOFF(V,U,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)+(AX-BX)*(T2(UIBEA)-T2P1(U,BE))+
     .                       CX*T2(UIBEA)
  427 S(VUBEGA)=S(VUBEGA)+ (CX+CX)*T2P1(U,BE)
      DO 429 BE=FBE,LBE2
      GABE=IPQ(GA)+BE
      ZL=ZLX(BE,A)
      UIBEA=UOFF(U,I,ZL)+VADD(BE,A)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      UVGABE=UOFF(U,V,1)+VADD(GA,BE)
      S(VUGABE)=S(VUGABE)+(AX-BX)*(T2(UIBEA)-T2P1(U,BE))+
     .                       CX*T2(UIBEA)
  429 S(UVGABE)=S(UVGABE)+(CX+CX)*T2P1(U,BE)
  431 CONTINUE
  435 CONTINUE
  440 CONTINUE
C
C     D2'*
      DO 444 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 444
      GASYM=ORBSYM(GA+NO)
      IF(GASYM.NE.ASYM)GO TO 444
      AX=AZERO
      DO 442 IRJ=1,NIRRED
      FJ=FLOV(IRJ,1)
      LJ=FLOV(IRJ,2)
      BSYM=IEOR(AISYM,IRJ-1)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 442 J=FJ,LJ
      AJ=IPQ(NO+A)+J
      DO 442 B=FB,LB
      BI=IPQ(NO+B)+I
      BJ=IPQ(NO+B)+J
C     AIBJ=IPQ(AI)+BJ
      AIBJ=IPQ(MAX0(AI,BJ))+MIN0(AI,BJ)
C     AJBI=IPQ(AJ)+BI
      AJBI=IPQ(MAX0(AJ,BI))+MIN0(AJ,BI)
      ZL=ZLX(B,GA)
      IJBGA=UOFF(I,J,ZL)+VADD(B,GA)
      AX=AX+(II(AIBJ)-II(AJBI)-II(AJBI))*TAU(IJBGA)
  442 CONTINUE
      FV(A,GA)=FV(A,GA)+AX
  444 CONTINUE
C
C     D1
      DO 445 C=1,NV
      DO 445 K=1,NO
  445 D1(K,C)=AZERO
      DO 452 V=1,NO
      IF(FZO(V).EQ.1) GO TO 452
      VSYM=ORBSYM(V)
      DO 451 J=1,NO
      IF(FZO(J).EQ.1) GO TO 451
      JSYM=ORBSYM(J)
      VJSYM=IEOR(VSYM,JSYM)
      IF(VJSYM.NE.AISYM)GO TO 451
      FB=FLOV(VSYM+1,3)-NO
      LB=FLOV(VSYM+1,4)-NO
      AJ=IPQ(NO+A)+J
      AX=AZERO
      BX=AZERO
      DO 446 B=FB,LB
      BJ=IPQ(NO+B)+J
      BI=IPQ(NO+B)+I
C     AJBI=IPQ(AJ)+BI
      AJBI=IPQ(MAX0(AJ,BI))+MIN0(AJ,BI)
C     AIBJ=IPQ(AI)+BJ
      AIBJ=IPQ(MAX0(AI,BJ))+MIN0(AI,BJ)
      AX=AX+II(AIBJ)*T1O(V,B)
      BX=BX+II(AJBI)*T1O(V,B)
  446 CONTINUE
      FGA=FLOV(JSYM+1,3)-NO
      LGA=FLOV(JSYM+1,4)-NO
      IF(FGA.LT.A)FGA=A
      DO 448 GA=FGA,LGA
C     GAA=IPQ(GA)+A
      XFAC=AX*T1O(J,GA)
      XADD=BX*T1O(J,GA)
      D1(V,GA)=D1(V,GA)+XADD-XFAC-XFAC
      VIGAA=UOFF(V,I,1)+VADD(GA,A)
      S(VIGAA)=S(VIGAA)-XFAC
  448 CONTINUE
      FGA=FLOV(JSYM+1,3)-NO
      IF(LGA.GT.A)LGA=A
      DO 450 GA=FGA,LGA
C     AGA=IPQ(A)+GA
      XFAC=AX*T1O(J,GA)
      XADD=BX*T1O(J,GA)
      D1(V,GA)=D1(V,GA)+XADD-XFAC-XFAC
      IVAGA=UOFF(I,V,1)+VADD(A,GA)
      S(IVAGA)=S(IVAGA)-XFAC
  450 CONTINUE
      D1(V,A)=D1(V,A)-(BX-AX-AX)*T1O(J,A)
  451 CONTINUE
  452 CONTINUE
      DO 454 V=1,NO
      IF(FZO(V).EQ.1) GO TO 454
      DO 453 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 453
      T1N(V,GA)=T1N(V,GA)+D1(V,GA)*T1O(I,A)
  453 CONTINUE
  454 CONTINUE
  457 CONTINUE
  458 CONTINUE
C
C     D2'
      DO 495 I=1,NO
      IF(FZO(I ).EQ.1) GO TO 495
      ISYM=ORBSYM(I)
      IISYM=IEOR(ISYM,ISYM)
      DO 475 J=1,I-1
      IF(FZO(J ).EQ.1) GO TO 475
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
C     READ I.GT.J  (IA,JB) ALL A B
      DO 473 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 473
      VSYM=ORBSYM(V)
      DO 472 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 472
      USYM=ORBSYM(U)
      UVSYM=IEOR(USYM,VSYM)
      AX=AZERO
      BX=AZERO
      IF(IJSYM.NE.UVSYM)GO TO 471
      DO 460 IRA=1,NIRRED
      FA=FLOV(IRA,3)-NO
      LA=FLOV(IRA,4)-NO
      BSYM=IEOR(IRA-1,IJSYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 460 A=FA,LA
      AI=IPQ(NO+A)+I
      AJ=IPQ(NO+A)+J
      DO 460 B=FB,LB
      BI=IPQ(NO+B)+I
      BJ=IPQ(NO+B)+J
C     AIBJ=IPQ(AI)+BJ
      AIBJ=IPQ(MAX0(AI,BJ))+MIN0(AI,BJ)
C     AJBI=IPQ(AJ)+BI
      AJBI=IPQ(MAX0(AJ,BI))+MIN0(AJ,BI)
      ZL=ZLX(A,B)
      UVAB=UOFF(U,V,ZL)+VADD(A,B)
      AX=AX+II(AIBJ)*TAU(UVAB)
  460 BX=BX+II(AJBI)*TAU(UVAB)
      IF(V.NE.J) GO TO 464
      FO(U,I)=FO(U,I)+AX+AX-BX
  464 CONTINUE
      IF(V.NE.I) GO TO 468
      FO(U,J)=FO(U,J)+BX+BX-AX
  468 CONTINUE
      DO 470 IBE=1,NIRRED
      FBE=FLOV(IBE,3)-NO
      LBE=FLOV(IBE,4)-NO
      GASYM=IEOR(IBE-1,IJSYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      DO 470 BE=FBE,LBE
      LGA2=LGA
      IF(LGA.GT.BE)LGA2=BE
      DO 470 GA=FGA,LGA2
C     BEGAS=IPQ(BE)+GA
      IJBEGA=UOFF(I,J,1)+VADD(BE,GA)
      JIBEGA=UOFF(J,I,1)+VADD(BE,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
  470 S(UVBEGA)=S(UVBEGA)+AX*TAU(IJBEGA)+BX*TAU(JIBEGA)
  471 CONTINUE
  472 CONTINUE
  473 CONTINUE
  475 CONTINUE
C     READ (IA,IB) ALL A B
      DO 493 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 493
      VSYM=ORBSYM(V)
      DO 492 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 492
      USYM=ORBSYM(U)
      UVSYM=IEOR(USYM,VSYM)
      AX=AZERO
      IF(UVSYM.NE.IISYM) GO TO 491
      DO 480 IRA=1,NIRRED
      FA=FLOV(IRA,3)-NO
      LA=FLOV(IRA,4)-NO
      BSYM=IEOR(IRA-1,IISYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 480 A=FA,LA
      AI=IPQ(NO+A)+I
      DO 480 B=FB,LB
      BI=IPQ(NO+B)+I
      AIBI=IPQ(MAX0(AI,BI))+MIN0(AI,BI)
      ZL=ZLX(A,B)
      UVAB=UOFF(U,V,ZL)+VADD(A,B)
  480 AX=AX+II(AIBI)*TAU(UVAB)
      IF(V.NE.I) GO TO 484
      FO(U,I)=FO(U,I)+AX
  484 CONTINUE
      DO 490 IBE=1,NIRRED
      FBE=FLOV(IBE,3)-NO
      LBE=FLOV(IBE,4)-NO
      GASYM=IEOR(IBE-1,IISYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
      DO 490 BE=FBE,LBE
      LGA2 = LGA
      IF(LGA.GT.BE)LGA2=BE
      DO 490 GA=FGA,LGA2
C     BEGAS=IPQ(BE)+GA
      IIBEGA=UOFF(I,I,1)+VADD(BE,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
  490 S(UVBEGA)=S(UVBEGA)+AX*TAU(IIBEGA)
  491 CONTINUE
  492 CONTINUE
  493 CONTINUE
  495 CONTINUE
      IF(NIT.EQ.3)THEN
      WRITE(6,*)'END D TERMS'
      CALL TIMIT(1,6)
C     CALL TIMIT(1,4)
      END IF
C
C     C TERMS
C
      DO 540 BE=1,NV
      IF(FZV(BE).EQ.1) GO TO 540
      BESYM=ORBSYM(BE+NO)
      DO 539 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 539
      USYM=ORBSYM(U)
      BEUSYM=IEOR(BESYM,USYM)
      DO 525 GA=1,BE
      IF(FZV(GA).EQ.1) GO TO 525
      GASYM=ORBSYM(GA+NO)
      GBUSYM=IEOR(GASYM,BEUSYM)
C     BEGA=IPQ(BE)+GA
      DO 524 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 524
      VSYM=ORBSYM(V)
      IF(VSYM.NE.GBUSYM)GO TO 523
      DO 520 IRI=1,NIRRED
      FI=FLOV(IRI,1)
      LI=FLOV(IRI,2)
      BSYM=IEOR(BEUSYM,IRI-1)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 520 I=FI,LI
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
      DO 520 B=FB,LB
      BEB=IPQ(MAX0(NO+BE,NO+B))+MIN0(NO+BE,NO+B)
      BEBIU=IPQ(MAX0(BEB,IU))+MIN0(BEB,IU)
      ZL=ZLX(B,GA)
      IVBGA=UOFF(I,V,ZL)+VADD(B,GA)
      VIBGA=UOFF(V,I,ZL)+VADD(B,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VUBEGA=UOFF(V,U,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)-II(BEBIU)*T2(IVBGA)
      S(VUBEGA)=S(VUBEGA)-II(BEBIU)*TAU(VIBGA)
  520 CONTINUE
  523 CONTINUE
  524 CONTINUE
  525 CONTINUE
      DO 530 GA=BE,NV
      IF(FZV(GA).EQ.1) GO TO 530
      GASYM=ORBSYM(GA+NO)
      GBUSYM=IEOR(GASYM,BEUSYM)
      DO 529 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 529
      VSYM=ORBSYM(V)
      IF(VSYM.NE.GBUSYM)GO TO 528
      DO 527 IRI=1,NIRRED
      FI=FLOV(IRI,1)
      LI=FLOV(IRI,2)
      BSYM=IEOR(BEUSYM,IRI-1)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 527 I=FI,LI
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
      DO 527 B=FB,LB
      BEB=IPQ(MAX0(NO+BE,NO+B))+MIN0(NO+BE,NO+B)
C     BEBIU=IPQ(BEB)+IU
      BEBIU=IPQ(MAX0(BEB,IU))+MIN0(BEB,IU)
      ZL=ZLX(B,GA)
      IVBGA=UOFF(I,V,ZL)+VADD(B,GA)
      VIBGA=UOFF(V,I,ZL)+VADD(B,GA)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      UVGABE=UOFF(U,V,1)+VADD(GA,BE)
      S(VUGABE)=S(VUGABE)-II(BEBIU)*T2(IVBGA)
      S(UVGABE)=S(UVGABE)-II(BEBIU)*TAU(VIBGA)
  527 CONTINUE
  528 CONTINUE
  529 CONTINUE
  530 CONTINUE
      DO 535 B=1,NV
      IF(FZV(B ).EQ.1) GO TO 535
      BEB=IPQ(MAX0(NO+BE,NO+B))+MIN0(NO+BE,NO+B)
      DO 534 I=1,NO
      IF(FZO(I ).EQ.1) GO TO 534
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
C     BEBIU=IPQ(BEB)+IU
      BEBIU=IPQ(MAX0(BEB,IU))+MIN0(BEB,IU)
      T1N(I,BE)=T1N(I,BE)+II(BEBIU)*T1O(U,B)
  534 CONTINUE
  535 CONTINUE
  539 CONTINUE
  540 CONTINUE
      IF(NIT.EQ.3)THEN
      WRITE(6,*)'END C TERMS'
      CALL TIMIT(1,6)
C     CALL TIMIT(1,4)
      END IF
C
C     E TERMS
C
      DO 690 I=1,NO
      IF(FZO(I ).EQ.1) GO TO 690
      XA(I)=AZERO
      XB(I)=AZERO
      XC(I)=AZERO
      ISYM=ORBSYM(I)
      DO 689 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 689
      USYM=ORBSYM(U)
      IUSYM=IEOR(ISYM,USYM)
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
C     READ   (IU,BJ) AND (IB,UJ)  ALL BJ
      DO 650 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 650
      GASYM=ORBSYM(GA+NO)
      DO 640 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 640
      VSYM=ORBSYM(V)
      GAVSYM=IEOR(GASYM,VSYM)
      XA(V)=AZERO
      XB(V)=AZERO
      XC(V)=AZERO
      IF(GAVSYM.NE.IUSYM)GO TO 640
      DO 610 IRJ=1,NIRRED
      FJ=FLOV(IRJ,1)
      LJ=FLOV(IRJ,2)
      BSYM=IEOR(IUSYM,IRJ-1)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 610 J=FJ,LJ
      UJ=IPQ(MAX0(U,J))+MIN0(U,J)
      DO 610 B=FB,LB
      IB=IPQ(NO+B)+I
      BJ=IPQ(NO+B)+J
      IUBJ=IPQ(MAX0(IU,BJ))+MIN0(IU,BJ)
      IBUJ=IPQ(MAX0(IB,UJ))+MIN0(IB,UJ)
C     IBUJ=IPQ(IB)+UJ
      ZL=ZLX(GA,B)
      VJGAB=UOFF(V,J,ZL)+VADD(GA,B)
      JVGAB=UOFF(J,V,ZL)+VADD(GA,B)
      XA(V)=XA(V)+II(IUBJ)*(T2(VJGAB)+T2(VJGAB)-T2(JVGAB))
      XB(V)=XB(V)+II(IBUJ)* T2(VJGAB)
      XC(V)=XC(V)+II(IBUJ)* T2(JVGAB)
  610 CONTINUE
      FBE=FLOV(ISYM+1,3)-NO
      LBE=FLOV(ISYM+1,4)-NO
      IF(FBE.LT.GA)FBE=GA
      DO 620 BE=FBE,LBE
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      VUBEGA=UOFF(V,U,1)+VADD(BE,GA)
      S(UVBEGA)=S(UVBEGA)-(XA(V)-XB(V))*T1O(I,BE)
      S(VUBEGA)=S(VUBEGA)+ XC(V)      *T1O(I,BE)
  620 CONTINUE
      FBE=FLOV(ISYM+1,3)-NO
      IF(LBE.GT.GA)LBE=GA
      DO 630 BE=FBE,LBE
      UVGABE=UOFF(U,V,1)+VADD(GA,BE)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      S(VUGABE)=S(VUGABE)-(XA(V)-XB(V))*T1O(I,BE)
      S(UVGABE)=S(UVGABE)+ XC(V)      *T1O(I,BE)
  630 CONTINUE
  640 CONTINUE
      T1N(U,GA)=T1N(U,GA)+(XA(I)-XB(I)+XC(I)+XC(I))*HALF
  650 CONTINUE
C
      DO 670 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 670
      GASYM=ORBSYM(GA+NO)
      DO 668 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 668
      VSYM=ORBSYM(V)
      GAVSYM=IEOR(GASYM,VSYM)
      IF(IUSYM.NE.GAVSYM)GO TO 668
      VGA=IPQ(NO+GA)+V
      IUVGA=IPQ(MAX0(IU,VGA))+MIN0(IU,VGA)
      FBE=FLOV(ISYM+1,3)-NO
      LBE=FLOV(ISYM+1,4)-NO
      IF(FBE.LT.GA)FBE=GA
      DO 655 BE=FBE,LBE
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
  655 S(UVBEGA)=S(UVBEGA)-II(IUVGA)*T1O(I,BE)
      FBE=FLOV(ISYM+1,3)-NO
      LBE=FLOV(ISYM+1,4)-NO
      IF(LBE.GT.GA)LBE=GA
      DO 660 BE=FBE,LBE
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
  660 S(VUGABE)=S(VUGABE)-II(IUVGA)*T1O(I,BE)
  668 CONTINUE
  670 CONTINUE
C
      DO 687 J=1,NO
      IF(FZO(J ).EQ.1) GO TO 687
      XA(J)=AZERO
      JSYM=ORBSYM(J)
      IUJSYM=IEOR(IUSYM,JSYM)
      IJSYM=IEOR(ISYM,JSYM)
      UJ=IPQ(MAX0(U,J))+MIN0(U,J)
      BX=AZERO
      FB=FLOV(IUJSYM+1,3)-NO
      LB=FLOV(IUJSYM+1,4)-NO
      DO 672 B=FB,LB
      IB=IPQ(NO+B)+I
C     IBUJ=IPQ(IB)+UJ
      IBUJ=IPQ(MAX0(IB,UJ))+MIN0(IB,UJ)
  672 BX=BX+II(IBUJ)*T1O(J,B)
      FIV=FLOV(IUJSYM+1,1)
      LV=FLOV(IUJSYM+1,2)
      DO 685 V=FIV,LV
      XA(V)=AZERO
      DO 675 B=FB,LB
      BJ=IPQ(NO+B)+J
C     IUBJ=IPQ(BJ)+IU
      IUBJ=IPQ(MAX0(IU,BJ))+MIN0(IU,BJ)
  675 XA(V)=XA(V)+II(IUBJ)*T1O(V,B)
      DO 680 IGA=1,NIRRED
      FGA=FLOV(IGA,3)-NO
      LGA=FLOV(IGA,4)-NO
      BESYM=IEOR(IGA-1,IJSYM)+1
      FBE=FLOV(BESYM,3)-NO
      LBE=FLOV(BESYM,4)-NO
      DO 680 GA=FGA,LGA
      FBE2=FBE
      IF(FBE.LT.GA)FBE2=GA
      DO 677 BE=FBE2,LBE
      IJBEGA=UOFF(I,J,1)+VADD(BE,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
  677 S(UVBEGA)=S(UVBEGA)+XA(V)*TAU(IJBEGA)
      LBE2=LBE
      IF(LBE.GT.GA)LBE2=GA
      DO 679 BE=FBE,LBE2
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      IJBEGA=UOFF(I,J,2)+VADD(BE,GA)
  679 S(VUGABE)=S(VUGABE)+XA(V)*TAU(IJBEGA)
  680 CONTINUE
  685 CONTINUE
      FO(U,I)=FO(U,I)+XA(J)+XA(J)-BX
  687 CONTINUE
  689 CONTINUE
  690 CONTINUE
      IF(NIT.EQ.3)THEN
      WRITE(6,*)'END E TERMS'
      CALL TIMIT(1,6)
C     CALL TIMIT(1,4)
      END IF
C
C     ADD T3 TERMS TO T1 AND T2 (BEFORE DIVIDING BY DELTA EIG)
C
      IF(LEVEL.GT.0)THEN
      DO 3190 U=1,NO
      USYM=ORBSYM(U)
      IF(FZO(U).EQ.1)GO TO 3190
      FBE=FLOV(USYM+1,3)-NO
      LBE=FLOV(USYM+1,4)-NO
      DO 3185 BE=FBE,LBE
      DO 3180 I=1,NO
      IF(FZO(I).EQ.1)GO TO 3180
      ISYM=ORBSYM(I)
      DO 3175 J=1,NO
      IF(FZO(J).EQ.1)GO TO 3175
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      DO 3170 A=1,NV
      IF(FZV(A).EQ.1)GO TO 3170
      ASYM=ORBSYM(A+NO)
      IA=IPQ(A+NO)+I
      JA=IPQ(A+NO)+J
      BSYM=IEOR(IJSYM,ASYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 3165 B=FB,LB
      IB=IPQ(B+NO)+I
      JB=IPQ(B+NO)+J
      IAJB=IPQ(MAX0(IA,JB))+MIN0(IA,JB)
      IBJA=IPQ(MAX0(IB,JA))+MIN0(IB,JA)
      IDX1=X3OFF(I,J,U)+X3ADD(A,BE,B)
      IDX2=X3OFF(I,J,U)+X3ADD(A,B,BE)
      T1N(U,BE)=T1N(U,BE)
     .         +(II(IAJB)+II(IAJB)-II(IBJA))*(Y3(IDX1)-Y3(IDX2))
 3165 CONTINUE
 3170 CONTINUE
 3175 CONTINUE
 3180 CONTINUE
 3185 CONTINUE
 3190 CONTINUE
C
      DO 3290 U=1,NO
      IF(FZO(U).EQ.1)GO TO 3290
      USYM=ORBSYM(U)
      DO 3285 V=1,NO
      IF(FZO(V).EQ.1)GO TO 3285
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      DO 3280 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 3280
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(UVSYM,BESYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
C
      LGA2=LGA
      IF(LGA.GT.BE)LGA2=BE
      DO 3276 GA=FGA,LGA2
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      DO 3271 I=1,NO
      IF(FZO(I).EQ.1)GO TO 3271
      ISYM=ORBSYM(I)
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
      DO 3266 A=1,NV
      IF(FZV(A).EQ.1)GO TO 3266
      ASYM=ORBSYM(A+NO)
      IASYM=IEOR(ISYM,ASYM)
      IA=IPQ(A+NO)+I
      BEA=IPQ(MAX0(BE,A)+NO)+MIN0(BE,A)+NO
      JSYM=IEOR(IASYM,USYM)+1
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      DO 3241 J=FJ,LJ
      JU=IPQ(MAX0(J,U))+MIN0(J,U)
      JA=IPQ(A+NO)+J
      IUJA=IPQ(MAX0(IU,JA))+MIN0(IU,JA)
      IAJU=IPQ(MAX0(IA,JU))+MIN0(IA,JU)
      IDX1=X3OFF(I,J,V)+X3ADD(BE,A,GA)
      IDX2=X3OFF(I,J,V)+X3ADD(BE,GA,A)
      S(UVBEGA)=S(UVBEGA)-(II(IUJA)+II(IUJA)-II(IAJU))*Y3(IDX1)
     .                   + II(IUJA) * Y3(IDX2)
 3241 CONTINUE
      BSYM=IEOR(IASYM,BESYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 3251 B=FB,LB
      IB=IPQ(B+NO)+I
      BEB=IPQ(MAX0(BE,B)+NO)+MIN0(BE,B)+NO
      GAB=IPQ(MAX0(GA,B)+NO)+MIN0(GA,B)+NO
      BEBIA=IPQ(MAX0(BEB,IA))+MIN0(BEB,IA)
      GABIA=IPQ(MAX0(GAB,IA))+MIN0(GAB,IA)
      BAIB=IPQ(MAX0(BEA,IB))+MIN0(BEA,IB)
      GAIB=IPQ(MAX0(GAA,IB))+MIN0(GAA,IB)
      IDX1=X3OFF(U,I,V)+X3ADD(A,B,GA)
      IDX2=X3OFF(U,I,V)+X3ADD(A,GA,B)
      S(UVBEGA)=S(UVBEGA)+(II(BAIB)+II(BAIB)-II(BEBIA))*Y3(IDX1)
     .                   - II(BAIB) * Y3(IDX2)
 3251 CONTINUE
 3266 CONTINUE
 3271 CONTINUE
 3276 CONTINUE
C
      FGA2=FGA
      IF(FGA.LT.BE)FGA2=BE
      DO 3376 GA=FGA2,LGA
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      DO 3371 I=1,NO
      IF(FZO(I).EQ.1)GO TO 3371
      ISYM=ORBSYM(I)
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
      DO 3366 A=1,NV
      IF(FZV(A).EQ.1)GO TO 3366
      ASYM=ORBSYM(A+NO)
      IASYM=IEOR(ISYM,ASYM)
      IA=IPQ(A+NO)+I
      BEA=IPQ(MAX0(BE,A)+NO)+MIN0(BE,A)+NO
      JSYM=IEOR(IASYM,USYM)+1
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      DO 3341 J=FJ,LJ
      JU=IPQ(MAX0(J,U))+MIN0(J,U)
      JA=IPQ(A+NO)+J
      IUJA=IPQ(MAX0(IU,JA))+MIN0(IU,JA)
      IAJU=IPQ(MAX0(IA,JU))+MIN0(IA,JU)
      IDX1=X3OFF(I,J,V)+X3ADD(BE,A,GA)
      IDX2=X3OFF(I,J,V)+X3ADD(BE,GA,A)
      S(VUGABE)=S(VUGABE)-(II(IUJA)+II(IUJA)-II(IAJU))*Y3(IDX1)
     .                   + II(IUJA) * Y3(IDX2)
 3341 CONTINUE
      BSYM=IEOR(IASYM,BESYM)+1
      FB=FLOV(BSYM,3)-NO
      LB=FLOV(BSYM,4)-NO
      DO 3351 B=FB,LB
      IB=IPQ(B+NO)+I
      BEB=IPQ(MAX0(BE,B)+NO)+MIN0(BE,B)+NO
      BEBIA=IPQ(MAX0(BEB,IA))+MIN0(BEB,IA)
      BAIB=IPQ(MAX0(BEA,IB))+MIN0(BEA,IB)
      IDX1=X3OFF(U,I,V)+X3ADD(A,B,GA)
      IDX2=X3OFF(U,I,V)+X3ADD(A,GA,B)
      S(VUGABE)=S(VUGABE)+(II(BAIB)+II(BAIB)-II(BEBIA))*Y3(IDX1)
     .                   - II(BAIB) * Y3(IDX2)
 3351 CONTINUE
 3366 CONTINUE
 3371 CONTINUE
 3376 CONTINUE
C
 3280 CONTINUE
 3285 CONTINUE
 3290 CONTINUE
C
C     ADD NON-LINEAR T3*T1 CONTRIBUTION TO T2 EQUATION (CCSDT-1B)
C
C
      DO 6398 I=1,NO
      IF(FZO(I).EQ.1)GO TO 6398
      ISYM=ORBSYM(I)
      FA=FLOV(ISYM+1,3)-NO
      LA=FLOV(ISYM+1,4)-NO
      DO 6395 A=FA,LA
      IA=IPQ(A+NO)+I
      VAL=0.0D0
      DO 6350 J=1,NO
      IF(FZO(J).EQ.1)GO TO 6350
      JSYM=ORBSYM(J)
      FB=FLOV(JSYM+1,3)-NO
      LB=FLOV(JSYM+1,4)-NO
      JA=IPQ(A+NO)+J
      DO 6345 B=FB,LB
      JB=IPQ(B+NO)+J
      IAJB=IPQ(MAX0(IA,JB))+MIN0(IA,JB)
      IB=IPQ(B+NO)+I
      IBJA=IPQ(MAX0(IB,JA))+MIN0(IB,JA)
      VAL=VAL+(II(IAJB)+II(IAJB)-II(IBJA))*T1O(J,B)
 6345 CONTINUE
 6350 CONTINUE
      DO 6390 U=1,NO
      IF(FZO(U).EQ.1)GO TO 6390
      USYM=ORBSYM(U)
      DO 6385 V=1,NO
      IF(FZO(V).EQ.1)GO TO 6385
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      DO 6380 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 6380
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(UVSYM,BESYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
C
      LGA2=LGA
      IF(LGA.GT.BE)LGA2=BE
      DO 6375 GA=FGA,LGA2
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      IDX1=X3OFF(U,V,I)+X3ADD(BE,GA,A)
      IDX2=X3OFF(U,V,I)+X3ADD(A,GA,BE)
      S(UVBEGA)=S(UVBEGA)+VAL* (Y3(IDX1)-Y3(IDX2))
 6375 CONTINUE
C
      FGA2=FGA
      IF(FGA.LT.BE)FGA2=BE
      DO 6377 GA=FGA2,LGA
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      IDX1=X3OFF(U,V,I)+X3ADD(BE,GA,A)
      IDX2=X3OFF(U,V,I)+X3ADD(A,GA,BE)
      S(VUGABE)=S(VUGABE)+VAL*(Y3(IDX1)-Y3(IDX2))
 6377 CONTINUE
C
 6380 CONTINUE
 6385 CONTINUE
 6390 CONTINUE
 6395 CONTINUE
 6398 CONTINUE
C
      DO 6498 I=1,NO
      IF(FZO(I).EQ.1)GO TO 6498
      ISYM=ORBSYM(I)
      DO 6495 A=1,NV
      IF(FZV(A).EQ.1)GO TO 6495
      ASYM=ORBSYM(A+NO)
      IA=IPQ(A+NO)+I
      IASYM=IEOR(ISYM,ASYM)
      DO 6490 J=1,NO
      IF(FZO(J).EQ.1)GO TO 6490
      JA=IPQ(A+NO)+J
      JSYM=ORBSYM(J)
      VSYM=IEOR(IASYM,JSYM)
      IV=FLOV(VSYM+1,1)
      LV=FLOV(VSYM+1,2)
      FB=FLOV(VSYM+1,3)-NO
      LB=FLOV(VSYM+1,4)-NO
      DO 6480 V=IV,LV
      VAL1=0.0D0
      VAL2=0.0D0
      DO 6445 B=FB,LB
      JB=IPQ(B+NO)+J
      IAJB=IPQ(MAX0(IA,JB))+MIN0(IA,JB)
      IB=IPQ(B+NO)+I
      IBJA=IPQ(MAX0(IB,JA))+MIN0(IB,JA)
      VAL1=VAL1+(II(IAJB)+II(IAJB)-II(IBJA))*T1O(V,B)
      VAL2=VAL2+ II(IAJB)                   *T1O(V,B)
 6445 CONTINUE
      DO 6470 U=1,NO
      IF(FZO(U).EQ.1)GO TO 6470
      USYM=ORBSYM(U)
      UVSYM=IEOR(USYM,VSYM)
      DO 6460 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 6460
      BESYM=ORBSYM(BE+NO)
      GASYM=IEOR(UVSYM,BESYM)+1
      FGA=FLOV(GASYM,3)-NO
      LGA=FLOV(GASYM,4)-NO
C
      LGA2=LGA
      IF(LGA.GT.BE)LGA2=BE
      DO 6455 GA=FGA,LGA2
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      IDX1=X3OFF(U,I,J)+X3ADD(BE,A,GA)
      IDX2=X3OFF(U,I,J)+X3ADD(A,BE,GA)
      S(UVBEGA)=S(UVBEGA)-VAL1* Y3(IDX1)
     .                   +VAL2* Y3(IDX2)
 6455 CONTINUE
C
      FGA2=FGA
      IF(FGA.LT.BE)FGA2=BE
      DO 6457 GA=FGA2,LGA
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      IDX1=X3OFF(U,I,J)+X3ADD(BE,A,GA)
      IDX2=X3OFF(U,I,J)+X3ADD(A,BE,GA)
      S(VUGABE)=S(VUGABE)-VAL1* Y3(IDX1)
     .                   +VAL2* Y3(IDX2)
 6457 CONTINUE
C
 6460 CONTINUE
 6470 CONTINUE
 6480 CONTINUE
 6490 CONTINUE
 6495 CONTINUE
 6498 CONTINUE
C
      DO 6598 I=1,NO
      IF(FZO(I).EQ.1)GO TO 6598
      ISYM=ORBSYM(I)
      DO 6595 A=1,NV
      IF(FZV(A).EQ.1)GO TO 6595
      ASYM=ORBSYM(A+NO)
      IA=IPQ(A+NO)+I
      IASYM=IEOR(ISYM,ASYM)
      DO 6590 B=1,NV
      IF(FZV(B).EQ.1)GO TO 6590
      IB=IPQ(B+NO)+I
      BSYM=ORBSYM(B+NO)
      GASYM=IEOR(IASYM,BSYM)
      FJ=FLOV(GASYM+1,1)
      LJ=FLOV(GASYM+1,2)
      FGA=FLOV(GASYM+1,3)-NO
      LGA=FLOV(GASYM+1,4)-NO
      DO 6580 GA=FGA,LGA
      VAL1=0.0D0
      VAL2=0.0D0
      DO 6545 J=FJ,LJ
      JB=IPQ(B+NO)+J
      JA=IPQ(A+NO)+J
      IAJB=IPQ(MAX0(IA,JB))+MIN0(IA,JB)
      IBJA=IPQ(MAX0(IB,JA))+MIN0(IB,JA)
      VAL1=VAL1+(II(IAJB)+II(IAJB)-II(IBJA))*T1O(J,GA)
      VAL2=VAL2+ II(IAJB)                   *T1O(J,GA)
 6545 CONTINUE
      DO 6570 U=1,NO
      IF(FZO(U).EQ.1)GO TO 6570
      USYM=ORBSYM(U)
      DO 6560 V=1,NO
      IF(FZO(V).EQ.1)GO TO 6560
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      BESYM=IEOR(UVSYM,GASYM)+1
      FBE=FLOV(BESYM,3)-NO
      LBE=FLOV(BESYM,4)-NO
C
      LBE2=LBE
      IF(LBE.GT.GA)LBE2=GA
      DO 6555 BE=FBE,LBE2
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
      IDX1=X3OFF(U,V,I)+X3ADD(BE,B,A)
      IDX2=X3OFF(U,V,I)+X3ADD(A,B,BE)
      S(VUGABE)=S(VUGABE)-VAL1* Y3(IDX1) +VAL2* Y3(IDX2)
 6555 CONTINUE
C
      FBE2=FBE
      IF(FBE.LT.GA)FBE2=GA
      DO 6557 BE=FBE2,LBE
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      IDX1=X3OFF(U,V,I)+X3ADD(BE,B,A)
      IDX2=X3OFF(U,V,I)+X3ADD(A,B,BE)
      S(UVBEGA)=S(UVBEGA)-VAL1* Y3(IDX1) +VAL2* Y3(IDX2)
 6557 CONTINUE
C
 6560 CONTINUE
 6570 CONTINUE
 6580 CONTINUE
 6590 CONTINUE
 6595 CONTINUE
 6598 CONTINUE
C
      ENDIF
C
C     ADD EXTRA TERMS
C
      DO 1050 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 1050
      USYM=ORBSYM(U)
      DO 1048 BE=1,NV
      IF(FZV(BE).EQ.1) GO TO 1048
      BESYM=ORBSYM(BE+NO)
      UBESYM=IEOR(USYM,BESYM)
      XADD=AZERO
      DO 1030 I=1,NO
      IF(FZO(I ).EQ.1) GO TO 1030
      ISYM=ORBSYM(I)
      ASYM=IEOR(ISYM,UBESYM)+1
      FA=FLOV(ASYM,3)-NO
      LA=FLOV(ASYM,4)-NO
      DO 1028 A=FA,LA
      ZL=ZLX(BE,A)
      UIBEA=UOFF(U,I,ZL)+VADD(BE,A)
      UIABE=UOFF(I,U,ZL)+VADD(BE,A)
      AI=IPQ(A+NO)+I
      XADD =XADD -FOCK(AI)*(2.D0*T2(UIBEA)-TAU(UIABE))
 1028 CONTINUE
      VAL=FO(U,I)
      IU=IPQ(MAX0(I,U))+MIN0(I,U)
      IF(I.NE.U)VAL=VAL+FOCK(IU)
      T1N(U,BE)=T1N(U,BE)+VAL*T1O(I,BE)
 1030 CONTINUE
      DO 1040 A=1,NV
      IF(FZV(A ).EQ.1) GO TO 1040
      VAL=FV(A,BE)
      ABE=IPQ(MAX0(A,BE)+NO)+MIN0(A,BE)+NO
      IF(A.NE.BE)VAL=VAL+FOCK(ABE)
      T1N(U,BE)=T1N(U,BE)-VAL*T1O(U,A)
 1040 CONTINUE
      UBE=IPQ(NO+BE)+U
      T1N(U,BE)=T1N(U,BE)+XADD-FOCK(UBE)
 1048 CONTINUE
 1050 CONTINUE
 1055 CONTINUE
C
      CALL ZERO(FFV,NV*NV)
      DO 1120 A=1,NV
      IF(FZV(A).EQ.1)GO TO 1120
      DO 1118 GA=1,NV
      IF(FZV(GA).EQ.1)GO TO 1118
      DO 1116 I=1,NO
      IF(FZO(I).EQ.1)GO TO 1116
      IA=IPQ(NO+A)+I
      FFV(A,GA)=FFV(A,GA)+FOCK(IA)*T1O(I,GA)
 1116 CONTINUE
 1118 CONTINUE
 1120 CONTINUE
      CALL ZERO(FFO,NO*NO)
      DO 1130 I=1,NO
      IF(FZO(I).EQ.1)GO TO 1130
      DO 1128 U=1,NO
      IF(FZO(U).EQ.1)GO TO 1128
      DO 1126 A=1,NV
      IF(FZV(A).EQ.1)GO TO 1126
      IA=IPQ(NO+A)+I
      FFO(I,U)=FFO(I,U)+FOCK(IA)*T1O(U,A)
 1126 CONTINUE
 1128 CONTINUE
 1130 CONTINUE
      DO 1290 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 1290
      USYM=ORBSYM(U)
      DO 1288 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 1288
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      DO 1240 IRA=1,NIRRED
      FA=FLOV(IRA,3)-NO
      LA=FLOV(IRA,4)-NO
      UVASYM=IEOR(UVSYM,IRA-1)
      FBE=FLOV(UVASYM+1,3)-NO
      LBE=FLOV(UVASYM+1,4)-NO
      DO 1240 A=FA,LA
      DO 1230 GA=FA,LA
      VAL=FV(A,GA)-FFV(A,GA)
      AGA=IPQ(MAX0(A,GA)+NO)+MIN0(A,GA)+NO
      IF(A.NE.GA)VAL=VAL+FOCK(AGA)
      FBE2=FBE
      IF(FBE.LT.GA)FBE2=GA
      DO 1210 BE=FBE2,LBE
      ZL=ZLX(BE,A)
      UVBEA=UOFF(U,V,ZL)+VADD(BE,A)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
 1210 S(UVBEGA)=S(UVBEGA)+VAL*T2(UVBEA)
      LBE2=LBE
      IF(LBE.GT.GA)LBE2=GA
      DO 1220 BE=FBE,LBE2
      ZL=ZLX(BE,A)
      UVBEA=UOFF(U,V,ZL)+VADD(BE,A)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
 1220 S(VUGABE)=S(VUGABE)+VAL*T2(UVBEA)
 1230 CONTINUE
 1240 CONTINUE
      FI=FLOV(VSYM+1,1)
      LI=FLOV(VSYM+1,2)
      DO 1280 I=FI,LI
      VAL=FO(V,I)+FFO(V,I)
      VI=IPQ(MAX0(V,I))+MIN0(V,I)
      IF(V.NE.I)VAL=VAL+FOCK(VI)
      DO 1270 IRGA=1,NIRRED
      FGA=FLOV(IRGA,3)-NO
      LGA=FLOV(IRGA,4)-NO
      BESYM=IEOR(IRGA-1,UVSYM)+1
      FBE=FLOV(BESYM,3)-NO
      LBE=FLOV(BESYM,4)-NO
      DO 1270 GA=FGA,LGA
      FBE2=FBE
      IF(FBE.LT.GA)FBE2=GA
      DO 1250 BE=FBE2,LBE
      UIBEGA=UOFF(U,I,1)+VADD(BE,GA)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
 1250 S(UVBEGA)=S(UVBEGA)-VAL*T2(UIBEGA)
      LBE2=LBE
      IF(LBE.GT.GA)LBE2=GA
      DO 1260 BE=FBE,LBE2
      UIBEGA=UOFF(U,I,2)+VADD(BE,GA)
      VUGABE=UOFF(V,U,1)+VADD(GA,BE)
 1260 S(VUGABE)=S(VUGABE)-VAL*T2(UIBEGA)
 1270 CONTINUE
 1280 CONTINUE
 1288 CONTINUE
 1290 CONTINUE
 1295 CONTINUE
C
      DO 1390 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 1390
      USYM=ORBSYM(U)
      DO 1380 V=1,NO
      IF(FZO(V ).EQ.1) GO TO 1380
      VSYM=ORBSYM(V)
      UVSYM=IEOR(USYM,VSYM)
      DO 1370 GA=1,NV
      IF(FZV(GA).EQ.1) GO TO 1370
      GASYM=ORBSYM(GA+NO)
      GAV=IPQ(GA+NO)+V
      DO 1360 BE=GA,NV
      IF(FZV(BE).EQ.1) GO TO 1360
      BESYM=ORBSYM(BE+NO)
      BEGSYM=IEOR(BESYM,GASYM)
      IF(UVSYM.NE.BEGSYM) GO TO 1320
      BEU=IPQ(BE+NO)+U
      BEUGAV=IPQ(MAX0(BEU,GAV))+     MIN0(BEU,GAV)
      UVBEGA=UOFF(U,V,1)+VADD(BE,GA)
      UU=IPQ(U)+U
      VV=IPQ(V)+V
      BEBE=IPQ(BE+NO)+BE+NO
      GAGA=IPQ(GA+NO)+GA+NO
      VAL=FOCK(UU)+FOCK(VV)-FOCK(BEBE)-FOCK(GAGA)
      S(UVBEGA)=(S(UVBEGA)+II(BEUGAV))/VAL
 1320 CONTINUE
 1360 CONTINUE
 1370 CONTINUE
 1380 CONTINUE
 1390 CONTINUE
      DO 1450 U=1,NO
      IF(FZO(U ).EQ.1) GO TO 1450
      DO 1448 BE=1,NV
      IF(FZV(BE).EQ.1) GO TO 1448
      UU=IPQ(U)+U
      BEBE=IPQ(BE+NO)+BE+NO
      VAL=-FOCK(UU)+FOCK(BEBE)
      T1N(U,BE)=T1N(U,BE)/VAL
 1448 CONTINUE
 1450 CONTINUE
C
C
C     GO INTO CCSDT IFF LEVEL.GT.0
C
      IF(LEVEL.GT.0)THEN
      CALL ZERO (FFF,NO*NV)
      CALL ZERO (F1,NV*NO*NV*NV)
      CALL ZERO (F2,NV*NO*NO*NO)
      CALL ZERO (F3,NV*NO*NO*NV)
      CALL ZERO (F4,NV*NO*NV*NO)
      CALL ZERO (F5,NO*NV*NV*NO)
      CALL ZERO (F6,NO*NV*NO*NV)
      IF(LEVEL.GT.1)THEN
C    WATCH OUT! THERE IS A NON-SCF TERM FOR CCSDT2, NOT CODED.
      IF(LEVEL.GT.2)THEN
      DO 2085 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2085
      DO 2080 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2080
      ME=IPQ(E+NO)+M
      VAL=0.0D0
      DO 2075 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2075
      MF=IPQ(F+NO)+M
      DO 2070 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2070
      NF=IPQ(F+NO)+N
      NE=IPQ(E+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      MFNE=IPQ(MAX0(MF,NE))+MIN0(MF,NE)
      VAL=VAL+(II(MENF)+II(MENF)-II(MFNE))*T1O(N,F)
 2070 CONTINUE
 2075 CONTINUE
      FFF(M,E)=VAL
 2080 CONTINUE
 2085 CONTINUE
      ENDIF
C
C     >F1<
C
      DO 2190 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2190
      DO 2185 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2185
      DO 2180 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2180
      AE=IPQ(MAX0(A,E)+NO)+MIN0(A,E)+NO
      DO 2175 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2175
      MF=IPQ(F+NO)+M
      AEMF=IPQ(MAX0(AE,MF))+MIN0(AE,MF)
      F1(A,M,E,F)=II(AEMF)
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2170 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2170
      NE=IPQ(E+NO)+N
      NEMF=IPQ(MAX0(NE,MF))+MIN0(NE,MF)
      VAL=VAL+II(NEMF)*T1O(N,A)
 2170 CONTINUE
      F1(A,M,E,F)=F1(A,M,E,F)-VAL
      ENDIF
C
 2175 CONTINUE
 2180 CONTINUE
 2185 CONTINUE
 2190 CONTINUE
C
C     >F2<
C
      DO 2290 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2290
      DO 2285 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2285
      DO 2280 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2280
      ME=IPQ(E+NO)+M
      DO 2275 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2275
      IN=IPQ(MAX0(I,N))+MIN0(I,N)
      EMIN=IPQ(MAX0(ME,IN))+MIN0(ME,IN)
      F2(E,I,M,N)=II(EMIN)
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2270 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2270
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      VAL=VAL+II(MENF)*T1O(I,F)
 2270 CONTINUE
      F2(E,I,M,N)=F2(E,I,M,N)+VAL
      ENDIF
C
 2275 CONTINUE
 2280 CONTINUE
 2285 CONTINUE
 2290 CONTINUE
C
C     >F3<
C
      DO 2390 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2390
      DO 2385 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2385
      DO 2380 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2380
      AI=IPQ(A+NO)+I
      DO 2375 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2375
      ME=IPQ(E+NO)+M
      AIME=IPQ(MAX0(ME,AI))+MIN0(ME,AI)
      F3(A,M,I,E)=II(AIME)
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2370 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2370
      AF=IPQ(MAX0(A,F)+NO)+MIN0(A,F)+NO
      MEAF=IPQ(MAX0(ME,AF))+MIN0(ME,AF)
      VAL=VAL+II(MEAF)*T1O(I,F)
 2370 CONTINUE
      F3(A,M,I,E)=F3(A,M,I,E)+VAL
      ENDIF
C
 2375 CONTINUE
 2380 CONTINUE
 2385 CONTINUE
 2390 CONTINUE
C
C     >F4<
C
      DO 2490 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2490
      DO 2485 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2485
      DO 2480 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2480
      AE=IPQ(MAX0(A,E)+NO)+MIN0(A,E)+NO
      DO 2475 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2475
      IM=IPQ(MAX0(I,M))+MIN0(I,M)
      AEIM=IPQ(MAX0(AE,IM))+MIN0(AE,IM)
      F4(A,M,E,I)=II(AEIM)
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2470 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2470
      MF=IPQ(F+NO)+M
      AEMF=IPQ(MAX0(AE,MF))+MIN0(AE,MF)
      VAL=VAL+II(AEMF)*T1O(I,F)
 2470 CONTINUE
      F4(A,M,E,I)=F4(A,M,E,I)+VAL
      ENDIF
C
 2475 CONTINUE
 2480 CONTINUE
 2485 CONTINUE
 2490 CONTINUE
C
C     >F5<
C
      DO 2590 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2590
      DO 2585 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2585
      DO 2580 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2580
      IA=IPQ(A+NO)+I
      DO 2575 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2575
      EM=IPQ(E+NO)+M
      IAEM=IPQ(MAX0(IA,EM))+MIN0(IA,EM)
      F5(I,E,A,M)=II(IAEM)
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2570 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2570
      IN=IPQ(MAX0(I,N))+MIN0(I,N)
      INEM=IPQ(MAX0(IN,EM))+MIN0(IN,EM)
      VAL=VAL+II(INEM)*T1O(N,A)
 2570 CONTINUE
      F5(I,E,A,M)=F5(I,E,A,M)-VAL
      ENDIF
C
 2575 CONTINUE
 2580 CONTINUE
 2585 CONTINUE
 2590 CONTINUE
C
C     >F6<
C
      DO 2690 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2690
      DO 2685 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2685
      DO 2680 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2680
      IM=IPQ(MAX0(I,M))+MIN0(I,M)
      DO 2675 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2675
      EA=IPQ(MAX0(E,A)+NO)+MIN0(E,A)+NO
      IMEA=IPQ(MAX0(IM,EA))+MIN0(IM,EA)
      F6(I,E,M,A)=II(IMEA)
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2670 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2670
      EN=IPQ(E+NO)+N
      IMEN=IPQ(MAX0(IM,EN))+MIN0(IM,EN)
      VAL=VAL+II(IMEN)*T1O(N,A)
 2670 CONTINUE
      F6(I,E,M,A)=F6(I,E,M,A)-VAL
      ENDIF
C
 2675 CONTINUE
 2680 CONTINUE
 2685 CONTINUE
 2690 CONTINUE
      ENDIF
C
C     >>CHI1<<
C
      CALL ZERO(CHI1,NV*NV*NO*NV)
      DO 2790 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2790
      ASYM=ORBSYM(A+NO)
      DO 2785 B=1,NV
      IF(FZV(B).EQ.1)GO TO 2785
      BSYM=ORBSYM(B+NO)
      ABSYM=IEOR(ASYM,BSYM)
      ZLAB=ZLX(A,B)
      DO 2780 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2780
      ISYM=ORBSYM(I)
      IASYM=IEOR(ISYM,ASYM)
      IBSYM=IEOR(ISYM,BSYM)
      AI=IPQ(A+NO)+I
      ESYM=IEOR(ISYM,ABSYM)+1
      FE=FLOV(ESYM,3)-NO
      LE=FLOV(ESYM,4)-NO
      DO 2775 E=FE,LE
      BE=IPQ(MAX0(B,E)+NO)+MIN0(B,E)+NO
      AIBE=IPQ(MAX0(AI,BE))+MIN0(AI,BE)
      CHI1(A,B,I,E)=II(AIBE)
C
      IF(LEVEL.GT.1)THEN
      VAL=0.0D0
      DO 2760 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2760
      NSYM=ORBSYM(N)
      MSYM=IEOR(ABSYM,NSYM)+1
      FM=FLOV(MSYM,1)
      LM=FLOV(MSYM,2)
      DO 2759 M=FM,LM
      NMAB=UOFF(N,M,ZLAB)+VADD(A,B)
      VAL=VAL+F2(E,I,M,N)*T2(NMAB)
 2759 CONTINUE
 2760 CONTINUE
      CHI1(A,B,I,E)=CHI1(A,B,I,E)+VAL
C
      VAL=0.0D0
      DO 2750 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2750
      ZLAF=ZLX(A,F)
      ZLBF=ZLX(B,F)
      FSYM=ORBSYM(F+NO)
      MSYM=IEOR(IASYM,FSYM)+1
      FM=FLOV(MSYM,1)
      LM=FLOV(MSYM,2)
      DO 2748 M=FM,LM
      IMAF=UOFF(I,M,ZLAF)+VADD(A,F)
      MIAF=UOFF(M,I,ZLAF)+VADD(A,F)
      VAL=VAL+(F1(B,M,E,F)+F1(B,M,E,F)-F1(B,M,F,E))*T2(IMAF)
      VAL=VAL- F1(B,M,E,F)*T2(MIAF)
 2748 CONTINUE
      MSYM=IEOR(IBSYM,FSYM)+1
      FM=FLOV(MSYM,1)
      LM=FLOV(MSYM,2)
      DO 2749 M=FM,LM
      MIBF=UOFF(M,I,ZLBF)+VADD(B,F)
      VAL=VAL- F1(A,M,F,E) *T2(MIBF)
 2749 CONTINUE
 2750 CONTINUE
      CHI1(A,B,I,E)=CHI1(A,B,I,E)+VAL
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2762 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2762
      NSYM=ORBSYM(N)
      MSYM=IEOR(ABSYM,NSYM)+1
      FM=FLOV(MSYM,1)
      LM=FLOV(MSYM,2)
      DO 2769 M=FM,LM
      VAL=VAL+F2(E,I,M,N)*T1O(N,A)*T1O(M,B)
 2769 CONTINUE
 2762 CONTINUE
      CHI1(A,B,I,E)=CHI1(A,B,I,E)+VAL
C
      VAL=0.0D0
      DO 2745 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2745
      VAL=VAL+F3(A,M,I,E)*T1O(M,B)+F4(B,M,E,I)*T1O(M,A)
 2745 CONTINUE
      CHI1(A,B,I,E)=CHI1(A,B,I,E)-VAL
C
      VAL=0.0D0
      DO 2740 F=1,NV
      IF(FZV(F).EQ.1)GO TO 2740
      AF=IPQ(MAX0(A,F)+NO)+MIN0(A,F)+NO
      AFBE=IPQ(MAX0(AF,BE))+MIN0(AF,BE)
      VAL=VAL+II(AFBE)*T1O(I,F)
 2740 CONTINUE
      CHI1(A,B,I,E)=CHI1(A,B,I,E)+VAL
      ENDIF
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      DO 2763 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2763
      MSYM=ORBSYM(M)
      ME=IPQ(E+NO)+M
      DO 2768 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2768
      NSYM=ORBSYM(N)
      MNSYM=IEOR(MSYM,NSYM)
      FSYM=IEOR(MNSYM,ESYM-1)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 2767 F=FF,LF
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      IDX1=X3OFF(I,M,N)+X3ADD(A,B,F)
      IDX2=X3OFF(I,M,N)+X3ADD(F,B,A)
      IDX3=X3OFF(I,M,N)+X3ADD(A,F,B)
      VAL=VAL+II(MENF)*(Y3(IDX1)+Y3(IDX1)-Y3(IDX2)-Y3(IDX3))
 2767 CONTINUE
 2768 CONTINUE
 2763 CONTINUE
      CHI1(A,B,I,E)=CHI1(A,B,I,E)-VAL
      ENDIF
C
      ENDIF
 2775 CONTINUE
 2780 CONTINUE
 2785 CONTINUE
 2790 CONTINUE
C
C     >>CHI2<<
C
      CALL ZERO(CHI2,NO*NO*NO*NV)
      DO 2890 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2890
      ASYM=ORBSYM(A+NO)
      DO 2885 M=1,NO
      IF(FZO(M).EQ.1)GO TO 2885
      MSYM=ORBSYM(M)
      AMSYM=IEOR(ASYM,MSYM)
      DO 2880 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2880
      ISYM=ORBSYM(I)
      IASYM=IEOR(ISYM,ASYM)
      IMSYM=IEOR(ISYM,MSYM)
      AI=IPQ(A+NO)+I
      JSYM=IEOR(AMSYM,ISYM)+1
      IJSYM=IEOR(ISYM,JSYM-1)
      AJSYM=IEOR(ASYM,JSYM-1)
      FJ=FLOV(JSYM,1)
      LJ=FLOV(JSYM,2)
      DO 2875 J=FJ,LJ
      JM=IPQ(MAX0(J,M))+MIN0(J,M)
      AIJM=IPQ(MAX0(AI,JM))+MIN0(AI,JM)
      CHI2(A,M,I,J)=II(AIJM)
C
      IF(LEVEL.GT.1)THEN
      VAL=0.0D0
      DO 2860 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2860
      ESYM=ORBSYM(E+NO)
      FSYM=IEOR(IJSYM,ESYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 2859 F=FF,LF
      ZL=ZLX(E,F)
      IJEF=UOFF(I,J,ZL)+VADD(E,F)
      VAL=VAL+F1(A,M,E,F)*T2 (IJEF)
 2859 CONTINUE
 2860 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)+VAL
C
      VAL=0.0D0
      DO 2850 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2850
      ZLAE=ZLX(A,E)
      ESYM=ORBSYM(E+NO)
      NSYM=IEOR(IASYM,ESYM)+1
      FN=FLOV(NSYM,1)
      LN=FLOV(NSYM,2)
      DO 2848 N=FN,LN
      INAE=UOFF(I,N,ZLAE)+VADD(A,E)
      NIAE=UOFF(N,I,ZLAE)+VADD(A,E)
      VAL=VAL+(F2(E,J,N,M)+F2(E,J,N,M)-F2(E,J,M,N))*T2(INAE)
      VAL=VAL- F2(E,J,N,M)*T2(NIAE)
 2848 CONTINUE
      NSYM=IEOR(AJSYM,ESYM)+1
      FN=FLOV(NSYM,1)
      LN=FLOV(NSYM,2)
      DO 2849 N=FN,LN
      NJAE=UOFF(N,J,ZLAE)+VADD(A,E)
      VAL=VAL- F2(E,I,M,N) *T2(NJAE)
 2849 CONTINUE
 2850 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)+VAL
C
      VAL=0.0D0
      EESYM=IEOR(JSYM-1,IASYM)+1
      FE=FLOV(EESYM,3)-NO
      LE=FLOV(EESYM,4)-NO
      DO 2865 E=FE,LE
      ZL=ZLX(A,E)
      IJAE=UOFF(I,J,ZL)+VADD(A,E)
      VAL=VAL+FFF(M,E)*T2(IJAE)
 2865 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)+VAL
C
      IF(LEVEL.GT.2)THEN
      VAL=0.0D0
      DO 2861 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2861
      ESYM=ORBSYM(E+NO)
      FSYM=IEOR(IJSYM,ESYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 2862 F=FF,LF
      VAL=VAL+F1(A,M,E,F)*T1O(I,E)*T1O(J,F)
 2862 CONTINUE
 2861 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)+VAL
C
      VAL=0.0D0
      DO 2845 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2845
      VAL=VAL+F5(I,E,A,M)*T1O(J,E)+F6(J,E,M,A)*T1O(I,E)
 2845 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)+VAL
C
      VAL=0.0D0
      DO 2840 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2840
      IN=IPQ(MAX0(I,N))+MIN0(I,N)
      INJM=IPQ(MAX0(IN,JM))+MIN0(IN,JM)
      VAL=VAL+II(INJM)*T1O(N,A)
 2840 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)-VAL
      ENDIF
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      DO 2870 N=1,NO
      IF(FZO(N).EQ.1)GO TO 2870
      NSYM=ORBSYM(N)
      DO 2869 E=1,NV
      IF(FZV(E).EQ.1)GO TO 2869
      ESYM=ORBSYM(E+NO)
      ME=IPQ(E+NO)+M
      NESYM=IEOR(NSYM,ESYM)
      FSYM=IEOR(NESYM,MSYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 2868 F=FF,LF
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      IDX1=X3OFF(I,J,N)+X3ADD(A,E,F)
      IDX2=X3OFF(I,J,N)+X3ADD(F,E,A)
      IDX3=X3OFF(I,J,N)+X3ADD(A,F,E)
      VAL=VAL+II(MENF)*(Y3(IDX1)+Y3(IDX1)-Y3(IDX2)-Y3(IDX3))
C     VAL=VAL+II(MENF)*(T3(I,J,N,A,E,F)+T3(I,J,N,A,E,F)-
C    .                  T3(I,J,N,F,E,A)-T3(I,J,N,A,F,E))
 2868 CONTINUE
 2869 CONTINUE
 2870 CONTINUE
      CHI2(A,M,I,J)=CHI2(A,M,I,J)+VAL
      ENDIF
C
      ENDIF
 2875 CONTINUE
 2880 CONTINUE
 2885 CONTINUE
 2890 CONTINUE
C
C     CALCULATE FIRST SET OF CONTRIBUTIONS
C
      DO 2990 I=1,NO
      IF(FZO(I).EQ.1)GO TO 2990
      ISYM=ORBSYM(I)
      DO 2980 J=1,NO
      IF(FZO(J).EQ.1)GO TO 2980
      JSYM=ORBSYM(J)
      DO 2970 K=1,NO
      IF(FZO(K).EQ.1)GO TO 2970
      KSYM=ORBSYM(K)
      JKSYM=IEOR(JSYM,KSYM)
      IJKSYM=IEOR(ISYM,JKSYM)
      DO 2960 A=1,NV
      IF(FZV(A).EQ.1)GO TO 2960
      ASYM=ORBSYM(A+NO)
      DO 2950 B=1,NV
      IF(FZV(B).EQ.1)GO TO 2950
      BSYM=ORBSYM(B+NO)
      ABSYM=IEOR(ASYM,BSYM)
      CSYM=IEOR(IJKSYM,ABSYM)
      FC=FLOV(CSYM+1,3)-NO
      LC=FLOV(CSYM+1,4)-NO
      BCSYM=IEOR(BSYM,CSYM)
      ESYM=IEOR(JKSYM,CSYM)+1
      FE=FLOV(ESYM,3)-NO
      LE=FLOV(ESYM,4)-NO
      MSYM=IEOR(BCSYM,KSYM)+1
      FM=FLOV(MSYM,1)
      LM=FLOV(MSYM,2)
      DO 2940 C=FC,LC
      VAL=0.0D0
      ZLBC=ZLX(B,C)
C
      DO 2930 E=FE,LE
      ZLCE=ZLX(C,E)
      KJCE=UOFF(K,J,ZLCE)+VADD(C,E)
      VAL=VAL+CHI1(A,B,I,E)*T2(KJCE)
 2930 CONTINUE
C
      DO 2920 M=FM,LM
      MKBC=UOFF(M,K,ZLBC)+VADD(B,C)
      VAL=VAL-CHI2(A,M,I,J)*T2(MKBC)
 2920 CONTINUE
C
      IDX1=X3OFF(I,J,K)+X3ADD(A,B,C)
      IDX2=X3OFF(J,K,I)+X3ADD(B,C,A)
      IDX3=X3OFF(K,I,J)+X3ADD(C,A,B)
      IDX4=X3OFF(I,K,J)+X3ADD(A,C,B)
      IDX5=X3OFF(K,J,I)+X3ADD(C,B,A)
      IDX6=X3OFF(J,I,K)+X3ADD(B,A,C)
      Z3(IDX1)=Z3(IDX1)+VAL
      Z3(IDX2)=Z3(IDX2)+VAL
      Z3(IDX3)=Z3(IDX3)+VAL
      Z3(IDX4)=Z3(IDX4)+VAL
      Z3(IDX5)=Z3(IDX5)+VAL
      Z3(IDX6)=Z3(IDX6)+VAL
 2940 CONTINUE
 2950 CONTINUE
 2960 CONTINUE
 2970 CONTINUE
 2980 CONTINUE
 2990 CONTINUE
C
C
C     GO FOR THE REST OF CCSDT
C
      IF(LEVEL.GT.3)THEN
C
C     >>CHIO<<
C
      CALL ZERO(CHIO,NO*NO)
      DO 4090 I=1,NO
      IF(FZO(I).EQ.1)GO TO 4090
      ISYM=ORBSYM(I)
      DO 4080 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4080
      MSYM=ORBSYM(M)
      IF(MSYM.NE.ISYM)GO TO 4080
      IM=IPQ(MAX0(I,M))+MIN0(I,M)
CJN   IF(I.NE.M)  CHIO(I,M)=CHIO(I,M)+EIG(I)
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      DO 4072 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4072
      IN=IPQ(MAX0(I,N))+MIN0(I,N)
      NSYM=ORBSYM(N)
      ESYM=NSYM
      FE=FLOV(ESYM+1,3)-NO
      LE=FLOV(ESYM+1,4)-NO
      DO 4070 E=FE,LE
      EM=IPQ(E+NO)+M
      EN=IPQ(E+NO)+N
      IMEN=IPQ(MAX0(IM,EN))+MIN0(IM,EN)
      INEM=IPQ(MAX0(IN,EM))+MIN0(IN,EM)
      VAL=VAL+(II(IMEN)+II(IMEN)-II(INEM))*T1O(N,E)
 4070 CONTINUE
 4072 CONTINUE
      CHIO(I,M)=CHIO(I,M)+VAL
C
      VAL=0.0D0
      DO 4079 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4079
      NSYM=ORBSYM(N)
      MNSYM=IEOR(MSYM,NSYM)
      DO 4077 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4077
      ME=IPQ(E+NO)+M
      NE=IPQ(E+NO)+N
      ESYM=ORBSYM(E+NO)
      FSYM=IEOR(MNSYM,ESYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 4075 F=FF,LF
      MF=IPQ(F+NO)+M
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      MFNE=IPQ(MAX0(MF,NE))+MIN0(MF,NE)
      ZL=ZLX(E,F)
      INEF=UOFF(I,N,ZL)+VADD(E,F)
      VAL=VAL+(II(MENF)+II(MENF)-II(MFNE))*TAU(INEF)
 4075 CONTINUE
 4077 CONTINUE
 4079 CONTINUE
      CHIO(I,M)=CHIO(I,M)+VAL
C
      ENDIF
 4080 CONTINUE
 4090 CONTINUE
C
C     >>CHIV<<
C
      CALL ZERO(CHIV,NV*NV)
      DO 4190 A=1,NV
      IF(FZV(A).EQ.1)GO TO 4190
      ASYM=ORBSYM(A+NO)
      ESYM=ASYM
      FE=FLOV(ESYM+1,3)-NO
      LE=FLOV(ESYM+1,4)-NO
      DO 4180 E=FE,LE
      AE=IPQ(MAX0(A,E)+NO)+MIN0(A,E)+NO
CJN   IF(A.EQ.E)  CHIV(A,E)=CHIV(A,E)+EIG(A+NO)
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      DO 4172 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4172
      ME=IPQ(E+NO)+M
      MSYM=ORBSYM(M)
      FSYM=MSYM
      FF=FLOV(FSYM+1,3)-NO
      LF=FLOV(FSYM+1,4)-NO
      DO 4170 F=FF,LF
      MF=IPQ(F+NO)+M
      AF=IPQ(MAX0(A,F)+NO)+MIN0(A,F)+NO
      AEMF=IPQ(MAX0(AE,MF))+MIN0(AE,MF)
      AFME=IPQ(MAX0(AF,ME))+MIN0(AF,ME)
      VAL=VAL+(II(AEMF)+II(AEMF)-II(AFME))*T1O(M,F)
 4170 CONTINUE
 4172 CONTINUE
      CHIV(A,E)=CHIV(A,E)+VAL
C
      VAL=0.0D0
      DO 4179 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4179
      NE=IPQ(E+NO)+N
      NSYM=ORBSYM(N)
      DO 4177 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4177
      ME=IPQ(E+NO)+M
      MSYM=ORBSYM(M)
      MNSYM=IEOR(MSYM,NSYM)
      FSYM=IEOR(MNSYM,ESYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 4175 F=FF,LF
      MF=IPQ(F+NO)+M
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      MFNE=IPQ(MAX0(MF,NE))+MIN0(MF,NE)
      ZL=ZLX(A,F)
      MNAF=UOFF(M,N,ZL)+VADD(A,F)
      VAL=VAL+(II(MENF)+II(MENF)-II(MFNE))*TAU(MNAF)
 4175 CONTINUE
 4177 CONTINUE
 4179 CONTINUE
      CHIV(A,E)=CHIV(A,E)-VAL
C
      ENDIF
 4180 CONTINUE
 4190 CONTINUE
C
C     >>CHI3<<    O O O O
C
      CALL ZERO(CHI,NT*NT)
      DO 4291 J=1,NO
      IF(FZO(J).EQ.1)GO TO 4291
      JSYM=ORBSYM(J)
      DO 4286 K=1,NO
      IF(FZO(K).EQ.1)GO TO 4286
      KSYM=ORBSYM(K)
      JKSYM=IEOR(JSYM,KSYM)
      DO 4231 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4231
      JM=IPQ(MAX0(J,M))+MIN0(J,M)
      MSYM=ORBSYM(M)
      NSYM=IEOR(JKSYM,MSYM)+1
      FN=FLOV(NSYM,1)
      LN=FLOV(NSYM,2)
      DO 4226 N=FN,LN
      KN=IPQ(MAX0(K,N))+MIN0(K,N)
      JMKN=IPQ(MAX0(JM,KN))+MIN0(JM,KN)
      CHI(M,N)=II(JMKN)
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      DO 4217 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4217
      ME=IPQ(E+NO)+M
      ESYM=ORBSYM(E+NO)
      FSYM=IEOR(JKSYM,ESYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 4216 F=FF,LF
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      ZL=ZLX(E,F)
      JKEF=UOFF(J,K,ZL)+VADD(E,F)
      VAL=VAL+II(MENF)*TAU(JKEF)
 4216 CONTINUE
 4217 CONTINUE
      CHI(M,N)=CHI(M,N)+VAL
C
      VAL=0.0D0
      DO 4221 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4221
      EN=IPQ(E+NO)+N
      JMEN=IPQ(MAX0(JM,EN))+MIN0(JM,EN)
      EM=IPQ(E+NO)+M
      EMKN=IPQ(MAX0(EM,KN))+MIN0(EM,KN)
      VAL=VAL+II(JMEN)*T1O(K,E)+II(EMKN)*T1O(J,E)
 4221 CONTINUE
      CHI(M,N)=CHI(M,N)+VAL
C
      ENDIF
 4226 CONTINUE
 4231 CONTINUE
C
      DO 4280 I=1,NO
      IF(FZO(I).EQ.1)GO TO 4280
      ISYM=ORBSYM(I)
      IJKSYM=IEOR(ISYM,JKSYM)
      DO 4265 A=1,NV
      IF(FZV(A).EQ.1)GO TO 4265
      ASYM=ORBSYM(A+NO)
      DO 4260 B=1,NV
      IF(FZV(B).EQ.1)GO TO 4260
      BSYM=ORBSYM(B+NO)
      ABSYM=IEOR(ASYM,BSYM)
      CSYM=IEOR(ABSYM,IJKSYM)+1
      FC=FLOV(CSYM,3)-NO
      LC=FLOV(CSYM,4)-NO
      DO 4255 C=FC,LC
      VAL=0.0D0
      DO 4240 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4240
      MSYM=ORBSYM(M)
      NSYM=IEOR(MSYM,JKSYM)
      FN=FLOV(NSYM+1,1)
      LN=FLOV(NSYM+1,2)
      DO 4235 N=FN,LN
      IDX=X3OFF(I,M,N)+X3ADD(A,B,C)
      VAL=VAL+CHI(M,N)*Y3(IDX)
 4235 CONTINUE
 4240 CONTINUE
      IDX1=X3OFF(I,J,K)+X3ADD(A,B,C)
      IDX2=X3OFF(K,J,I)+X3ADD(C,B,A)
      IDX3=X3OFF(J,I,K)+X3ADD(B,A,C)
      Z3(IDX1)=Z3(IDX1)+VAL
      Z3(IDX2)=Z3(IDX2)+VAL
      Z3(IDX3)=Z3(IDX3)+VAL
 4255 CONTINUE
 4260 CONTINUE
 4265 CONTINUE
 4280 CONTINUE
 4286 CONTINUE
 4291 CONTINUE
C
C     >>CHI4<<    V V V V
C
      CALL ZERO(CHI,NT*NT)
      DO 4390 B=1,NV
      IF(FZV(B).EQ.1)GO TO 4390
      BSYM=ORBSYM(B+NO)
      DO 4385 C=1,NV
      IF(FZV(C).EQ.1)GO TO 4385
      CSYM=ORBSYM(C+NO)
      BCSYM=IEOR(BSYM,CSYM)
      DO 4340 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4340
      BE=IPQ(MAX0(B,E)+NO)+MIN0(B,E)+NO
      ESYM=ORBSYM(E+NO)
      FSYM=IEOR(BCSYM,ESYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 4335 F=FF,LF
      CF=IPQ(MAX0(C,F)+NO)+MIN0(C,F)+NO
      BECF=IPQ(MAX0(BE,CF))+MIN0(BE,CF)
      CHI(E,F)=II(BECF)
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      DO 4317 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4317
      ME=IPQ(E+NO)+M
      MSYM=ORBSYM(M)
      NSYM=IEOR(BCSYM,MSYM)+1
      FN=FLOV(NSYM,1)
      LN=FLOV(NSYM,2)
      DO 4315 N=FN,LN
      NF=IPQ(F+NO)+N
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      ZL=ZLX(B,C)
      MNBC=UOFF(M,N,ZL)+VADD(B,C)
      VAL=VAL+II(MENF)*TAU(MNBC)
 4315 CONTINUE
 4317 CONTINUE
      CHI(E,F)=CHI(E,F)+VAL
C
      VAL=0.0D0
      DO 4320 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4320
      MF=IPQ(F+NO)+M
      BEMF=IPQ(MAX0(BE,MF))+MIN0(BE,MF)
      ME=IPQ(E+NO)+M
      MECF=IPQ(MAX0(ME,CF))+MIN0(ME,CF)
      VAL=VAL+II(BEMF)*T1O(M,C)+II(MECF)*T1O(M,B)
 4320 CONTINUE
      CHI(E,F)=CHI(E,F)-VAL
C
      ENDIF
 4335 CONTINUE
 4340 CONTINUE
C
      DO 4380 A=1,NV
      IF(FZV(A).EQ.1)GO TO 4380
      ASYM=ORBSYM(A+NO)
      ABCSYM=IEOR(ASYM,BCSYM)
      DO 4365 I=1,NO
      IF(FZO(I).EQ.1)GO TO 4365
      ISYM=ORBSYM(I)
      DO 4360 J=1,NO
      IF(FZO(J).EQ.1)GO TO 4360
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      KSYM=IEOR(IJSYM,ABCSYM)+1
      FK=FLOV(KSYM,1)
      LK=FLOV(KSYM,2)
      DO 4355 K=FK,LK
      VAL=0.0D0
      DO 4350 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4350
      ESYM=ORBSYM(E+NO)
      FSYM=IEOR(ESYM,BCSYM)
      IF=FLOV(FSYM+1,3)-NO
      LF=FLOV(FSYM+1,4)-NO
      DO 4348 F=IF,LF
      IDX=X3OFF(I,J,K)+X3ADD(A,E,F)
      VAL=VAL+CHI(E,F)*Y3(IDX)
      IDX=X3OFF(I,J,K)+X3ADD(A,E,F)
 4348 CONTINUE
 4350 CONTINUE
      IDX1=X3OFF(I,J,K)+X3ADD(A,B,C)
      IDX2=X3OFF(K,J,I)+X3ADD(C,B,A)
      IDX3=X3OFF(J,I,K)+X3ADD(B,A,C)
      Z3(IDX1)=Z3(IDX1)+VAL
      Z3(IDX2)=Z3(IDX2)+VAL
      Z3(IDX3)=Z3(IDX3)+VAL
 4355 CONTINUE
 4360 CONTINUE
 4365 CONTINUE
 4380 CONTINUE
 4385 CONTINUE
 4390 CONTINUE
C
C     >>CHI5<<    V O V O
C
      CALL ZERO(CHI,NT*NT)
      DO 4490 A=1,NV
      IF(FZV(A).EQ.1)GO TO 4490
      ASYM=ORBSYM(A+NO)
      DO 4485 I=1,NO
      IF(FZO(I).EQ.1)GO TO 4485
      ISYM=ORBSYM(I)
      AISYM=IEOR(ASYM,ISYM)
      DO 4435 M=1,NO
      IF(FZO(M).EQ.1)GO TO 4435
      MSYM=ORBSYM(M)
      MI=IPQ(MAX0(M,I))+MIN0(M,I)
      ESYM=IEOR(AISYM,MSYM)
      FE=FLOV(ESYM+1,3)-NO
      LE=FLOV(ESYM+1,4)-NO
      DO 4430 E=FE,LE
      AE=IPQ(MAX0(A,E)+NO)+MIN0(A,E)+NO
      AEMI=IPQ(MAX0(AE,MI))+MIN0(AE,MI)
      CHI(M,E)=II(AEMI)
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      MESYM=IEOR(MSYM,ESYM)
      DO 4417 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4417
      NE=IPQ(E+NO)+N
      NSYM=ORBSYM(N)
      FSYM=IEOR(MESYM,NSYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 4415 F=FF,LF
      MF=IPQ(F+NO)+M
      MFNE=IPQ(MAX0(MF,NE))+MIN0(MF,NE)
      ZL=ZLX(F,A)
      INFA=UOFF(I,N,ZL)+VADD(F,A)
      VAL=VAL+II(MFNE)*TAU(INFA)
 4415 CONTINUE
 4417 CONTINUE
      CHI(M,E)=CHI(M,E)-VAL
C
      VAL=0.0D0
      DO 4420 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4420
      NE=IPQ(E+NO)+N
      NEMI=IPQ(MAX0(NE,MI))+MIN0(NE,MI)
      VAL=VAL+II(NEMI)*T1O(N,A)
 4420 CONTINUE
      CHI(M,E)=CHI(M,E)-VAL
C
      VAL=0.0D0
      DO 4422 F=1,NV
      IF(FZV(F).EQ.1)GO TO 4422
      MF=IPQ(F+NO)+M
      AEMF=IPQ(MAX0(AE,MF))+MIN0(AE,MF)
      VAL=VAL+II(AEMF)*T1O(I,F)
 4422 CONTINUE
      CHI(M,E)=CHI(M,E)+VAL
C
      ENDIF
 4430 CONTINUE
 4435 CONTINUE
C
      DO 4480 B=1,NV
      IF(FZV(B).EQ.1)GO TO 4480
      BSYM=ORBSYM(B+NO)
      AIBSYM=IEOR(BSYM,AISYM)
      DO 4465 J=1,NO
      IF(FZO(J).EQ.1)GO TO 4465
      JSYM=ORBSYM(J)
      DO 4460 C=1,NV
      IF(FZV(C).EQ.1)GO TO 4460
      CSYM=ORBSYM(C+NO)
      JCSYM=IEOR(CSYM,JSYM)
      KSYM=IEOR(JCSYM,AIBSYM)+1
      FK=FLOV(KSYM,1)
      LK=FLOV(KSYM,2)
      DO 4455 K=FK,LK
      VAL1=0.0D0
      VAL2=0.0D0
      VAL3=0.0D0
      DO 4440 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4440
      ESYM=ORBSYM(E+NO)
      MSYM=IEOR(ESYM,AISYM)
      FM=FLOV(MSYM+1,1)
      LM=FLOV(MSYM+1,2)
      DO 4438 M=FM,LM
      IDX1=X3OFF(M,J,K)+X3ADD(E,B,C)
      VAL1=VAL1-CHI(M,E)* Y3(IDX1)
      IDX2=X3OFF(M,J,K)+X3ADD(B,E,C)
      VAL2=VAL2-CHI(M,E)* Y3(IDX2)
      IDX3=X3OFF(M,J,K)+X3ADD(C,B,E)
      VAL3=VAL3-CHI(M,E)* Y3(IDX3)
 4438 CONTINUE
 4440 CONTINUE
      IDX1=X3OFF(I,J,K)+X3ADD(A,B,C)
      IDX2=X3OFF(K,J,I)+X3ADD(C,B,A)
      IDX3=X3OFF(J,I,K)+X3ADD(B,A,C)
      Z3(IDX1)=Z3(IDX1)+VAL1
      Z3(IDX2)=Z3(IDX2)+VAL1
      Z3(IDX3)=Z3(IDX3)+VAL1
      IDX1=X3OFF(I,J,K)+X3ADD(B,A,C)
      IDX2=X3OFF(K,J,I)+X3ADD(C,A,B)
      IDX3=X3OFF(J,I,K)+X3ADD(A,B,C)
      Z3(IDX1)=Z3(IDX1)+VAL2
      Z3(IDX2)=Z3(IDX2)+VAL2
      Z3(IDX3)=Z3(IDX3)+VAL2
      IDX1=X3OFF(I,J,K)+X3ADD(C,B,A)
      IDX2=X3OFF(K,J,I)+X3ADD(A,B,C)
      IDX3=X3OFF(J,I,K)+X3ADD(B,C,A)
      Z3(IDX1)=Z3(IDX1)+VAL3
      Z3(IDX2)=Z3(IDX2)+VAL3
      Z3(IDX3)=Z3(IDX3)+VAL3
C
 4455 CONTINUE
 4460 CONTINUE
 4465 CONTINUE
 4480 CONTINUE
 4485 CONTINUE
 4490 CONTINUE
C
C     >>CHI6<<    V O O V
C
      CALL ZERO(CHI,NT*NT)
      DO 4590 A=1,NV
      IF(FZV(A).EQ.1)GO TO 4590
      ASYM=ORBSYM(A+NO)
      DO 4585 I=1,NO
      IF(FZO(I).EQ.1)GO TO 4585
      ISYM=ORBSYM(I)
      AISYM=IEOR(ASYM,ISYM)
      AI=IPQ(A+NO)+I
      DO 4530 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4530
      ESYM=ORBSYM(E+NO)
      MSYM=IEOR(AISYM,ESYM)
      FM=FLOV(MSYM+1,1)
      LM=FLOV(MSYM+1,2)
      DO 4525 M=FM,LM
      ME=IPQ(E+NO)+M
      AIME=IPQ(MAX0(AI,ME))+MIN0(AI,ME)
      CHI(M,E)=II(AIME)
C
      IF(LEVEL.GT.4)THEN
      VAL=0.0D0
      MESYM=IEOR(MSYM,ESYM)
      DO 4517 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4517
      NE=IPQ(E+NO)+N
      ME=IPQ(E+NO)+M
      NSYM=ORBSYM(N)
      FSYM=IEOR(MESYM,NSYM)+1
      FF=FLOV(FSYM,3)-NO
      LF=FLOV(FSYM,4)-NO
      DO 4515 F=FF,LF
      MF=IPQ(F+NO)+M
      NF=IPQ(F+NO)+N
      MFNE=IPQ(MAX0(MF,NE))+MIN0(MF,NE)
      MENF=IPQ(MAX0(ME,NF))+MIN0(ME,NF)
      ZL=ZLX(F,A)
      INFA=UOFF(I,N,ZL)+VADD(F,A)
      INAF=UOFF(N,I,ZL)+VADD(F,A)
      VAL=VAL+(II(MENF)+II(MENF)-II(MFNE))*T2(INAF)
     .                          -II(MENF) *TAU(INFA)
 4515 CONTINUE
 4517 CONTINUE
      CHI(M,E)=CHI(M,E)+VAL
C
      VAL=0.0D0
      DO 4520 N=1,NO
      IF(FZO(N).EQ.1)GO TO 4520
      NI=IPQ(MAX0(N,I))+MIN0(N,I)
      NIME=IPQ(MAX0(NI,ME))+MIN0(NI,ME)
      VAL=VAL+II(NIME)*T1O(N,A)
 4520 CONTINUE
      CHI(M,E)=CHI(M,E)-VAL
C
      VAL=0.0D0
      EM=IPQ(E+NO)+M
      DO 4522 F=1,NV
      IF(FZV(F).EQ.1)GO TO 4522
      AF=IPQ(MAX0(A,F)+NO)+MIN0(A,F)+NO
      AFEM=IPQ(MAX0(AF,EM))+MIN0(AF,EM)
      VAL=VAL+II(AFEM)*T1O(I,F)
 4522 CONTINUE
      CHI(M,E)=CHI(M,E)+VAL
C
      ENDIF
 4525 CONTINUE
 4530 CONTINUE
C
      DO 4580 B=1,NV
      IF(FZV(B).EQ.1)GO TO 4580
      BSYM=ORBSYM(B+NO)
      AIBSYM=IEOR(BSYM,AISYM)
      DO 4565 J=1,NO
      IF(FZO(J).EQ.1)GO TO 4565
      JSYM=ORBSYM(J)
      DO 4560 C=1,NV
      IF(FZV(C).EQ.1)GO TO 4560
      CSYM=ORBSYM(C+NO)
      JCSYM=IEOR(CSYM,JSYM)
      KSYM=IEOR(JCSYM,AIBSYM)+1
      FK=FLOV(KSYM,1)
      LK=FLOV(KSYM,2)
      DO 4555 K=FK,LK
      VAL=0.0D0
      DO 4540 E=1,NV
      IF(FZV(E).EQ.1)GO TO 4540
      ESYM=ORBSYM(E+NO)
      MSYM=IEOR(ESYM,AISYM)
      FM=FLOV(MSYM+1,1)
      LM=FLOV(MSYM+1,2)
      DO 4538 M=FM,LM
      IDX1=X3OFF(M,J,K)+X3ADD(E,B,C)
      IDX2=X3OFF(M,J,K)+X3ADD(B,E,C)
      IDX3=X3OFF(M,J,K)+X3ADD(C,B,E)
      VAL=VAL+CHI(M,E)*(Y3(IDX1)+Y3(IDX1)-Y3(IDX2)-Y3(IDX3))
 4538 CONTINUE
 4540 CONTINUE
      IDX1=X3OFF(I,J,K)+X3ADD(A,B,C)
      IDX2=X3OFF(K,J,I)+X3ADD(C,B,A)
      IDX3=X3OFF(J,I,K)+X3ADD(B,A,C)
      Z3(IDX1)=Z3(IDX1)+VAL
      Z3(IDX2)=Z3(IDX2)+VAL
      Z3(IDX3)=Z3(IDX3)+VAL
C
 4555 CONTINUE
 4560 CONTINUE
 4565 CONTINUE
 4580 CONTINUE
 4585 CONTINUE
 4590 CONTINUE
C
C     ADD THE 2ND SET OF CONTRIBUTIONS
C
      DO 5590 I=1,NO
      IF(FZO(I).EQ.1)GO TO 5590
      ISYM=ORBSYM(I)
      DO 5580 J=1,NO
      IF(FZO(J).EQ.1)GO TO 5580
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      DO 5570 K=1,NO
      IF(FZO(K).EQ.1)GO TO 5570
      KSYM=ORBSYM(K)
      JKSYM=IEOR(JSYM,KSYM)
      IJKSYM=IEOR(IJSYM,KSYM)
      DO 5560 A=1,NV
      IF(FZV(A).EQ.1)GO TO 5560
      ASYM=ORBSYM(A+NO)
      AISYM=IEOR(ASYM,ISYM)
      DO 5550 B=1,NV
      IF(FZV(B).EQ.1)GO TO 5550
      BSYM=ORBSYM(B+NO)
      BISYM=IEOR(BSYM,ISYM)
      ABSYM=IEOR(ASYM,BSYM)
      CSYM=IEOR(IJKSYM,ABSYM)
      BCSYM=IEOR(BSYM,CSYM)
      CISYM=IEOR(CSYM,ISYM)
      FC=FLOV(CSYM+1,3)-NO
      LC=FLOV(CSYM+1,4)-NO
      DO 5540 C=FC,LC
      VAL=0.0D0
C
      FE=FLOV(ASYM+1,3)-NO
      LE=FLOV(ASYM+1,4)-NO
      DO 5000 E=FE,LE
      IDX=X3OFF(I,J,K)+X3ADD(E,B,C)
      VAL=VAL+CHIV(A,E)*Y3(IDX)
 5000 CONTINUE
C
      FM=FLOV(ISYM+1,1)
      LM=FLOV(ISYM+1,2)
      DO 5100 M=FM,LM
      IDX=X3OFF(M,J,K)+X3ADD(A,B,C)
      VAL=VAL-CHIO(I,M)*Y3(IDX)
 5100 CONTINUE
C
      IDX1=X3OFF(I,J,K)+X3ADD(A,B,C)
      IDX2=X3OFF(K,J,I)+X3ADD(C,B,A)
      IDX3=X3OFF(J,I,K)+X3ADD(B,A,C)
      Z3(IDX1)=Z3(IDX1)+VAL
      Z3(IDX2)=Z3(IDX2)+VAL
      Z3(IDX3)=Z3(IDX3)+VAL
 5540 CONTINUE
 5550 CONTINUE
 5560 CONTINUE
 5570 CONTINUE
 5580 CONTINUE
 5590 CONTINUE
C
C
C     ENDING LEVEL.GT.3 (2ND SET OF CONTRIBUTIONS)
      ENDIF
C
C     DIVIDE BY MO ENERGIES
C
      DO 3590 I=1,NO
      IF(FZO(I).EQ.1)GO TO 3590
      ISYM=ORBSYM(I)
      IY=IPQ(I)+I
      DO 3580 J=1,NO
      IF(FZO(J).EQ.1)GO TO 3580
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      JJ=IPQ(J)+J
      DO 3570 K=1,NO
      IF(FZO(K).EQ.1)GO TO 3570
      KSYM=ORBSYM(K)
      IJKSYM=IEOR(IJSYM,KSYM)
      KK=IPQ(K)+K
      DO 3560 A=1,NV
      IF(FZV(A).EQ.1)GO TO 3560
      ASYM=ORBSYM(A+NO)
      AA=IPQ(A+NO)+A+NO
      DO 3550 B=1,NV
      IF(FZV(B).EQ.1)GO TO 3550
      BSYM=ORBSYM(B+NO)
      ABSYM=IEOR(ASYM,BSYM)
      BB=IPQ(B+NO)+B+NO
      CSYM=IEOR(IJKSYM,ABSYM)+1
      FC=FLOV(CSYM,3)-NO
      LC=FLOV(CSYM,4)-NO
      DO 3540 C=FC,LC
      CC=IPQ(C+NO)+C+NO
      VAL=FOCK(IY)+FOCK(JJ)+FOCK(KK)-FOCK(AA)-FOCK(BB)-FOCK(CC)
      IDX=X3OFF(I,J,K)+X3ADD(A,B,C)
      Z3(IDX)=Z3(IDX)/VAL
 3540 CONTINUE
 3550 CONTINUE
 3560 CONTINUE
 3570 CONTINUE
 3580 CONTINUE
 3590 CONTINUE
C
C
C     ENDING CCSDT LEVEL.GT.0
      ENDIF
C
C     STORE T AMPLITUDES IN FILE69 (NOTICE SPECIAL CCSDT FORMAT!)
C
      CALL RFILE(ITAP69)
      CALL SWRIT(ITAP69,NM,1)
      CALL SWRIT(ITAP69,NDIMT2,1)
      CALL SWRIT(ITAP69,NDIMT3,1)
      CALL SWRIT(ITAP69,T1N,INTOWP(NM))
      CALL SWRIT(ITAP69,S ,INTOWP(NDIMT2))
      CALL SWRIT(ITAP69,Z3,INTOWP(NDIMT3))
      CALL SWRIT(ITAP69,EREF,INTOWP(1))
      CALL SWRIT(ITAP69,XE,INTOWP(1))
      CALL RCLOSE(ITAP69,3)
C
C
C     CORRELATION ENERGY
C
      CALL ECORR(TAU,S,T1N,II,XE,UOFF,VADD,IPQ,ORBSYM,FLOV,
     .           NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK)
C
C     CHECK CONVERGENCE
C
      CALL TDIFF(S,T1N,T2,T1O,T1NORM,DELT,NO,NV,NDIMT2,
     .           ORBSYM,FLOV,NIRRED,UOFF,VADD,ZLX,FZO,FZV,
     .           Z3,Y3,NDIMT3,X3OFF,X3ADD)
      WRITE(6,6011)T1NORM,DELT
      WRITE(4,6011)T1NORM,DELT
 6011 FORMAT(13X,'T1 NORM =',D15.8,5X,'DELT=',D15.8)
      IF(DELT.LT.CONV) THEN
      WRITE(6,9020) CONV
 9020 FORMAT(///,32('*'),/,'* CORRELATION ENERGY CONVERGED *',3X,D12.2,
     . /,32('*'))
      RETURN
      END IF
      IF(NIT.LT.MAXIT) GO TO 81
      WRITE(6,9030)
 9030 FORMAT(///,'ITERATIONS EXHAUSTED & DID NOT CONVERGE',///)
      RETURN
   81 CONTINUE
C
C     DIIS EXTRAPOLATION
C
      IF(DIISFL.EQ.2) GO TO 1490
      CALL DIISD3(T1O,T1N,T2,S,NO,NV,NDIMT2,IT,MAXDIM,
     .            CCC,BBB,ITAP98,ITAP99,ITC,BB2,
     .            Z3,Y3,NDIMT3)
      CALL ECORR(TAU,S,T1N,II,XE,UOFF,VADD,IPQ,ORBSYM,FLOV,
     .           NIT,NO,NV,NT,NT2,NTIN,NDIMT2,NIRRED,FZO,FZV,FOCK)
 1490 CONTINUE
C
      GO TO 50
C
      END
C
C=======================================================================
C
      SUBROUTINE FOCKI (D,IOUT,XVEC,WVEC,XARR,NT,Z,FLAG,FOCK,EREF,ENUC,
     .                 II,NTIN,BUF,IBUF,INTBUF,
     .            NGOO,NGOV,NHOV,NLOV,IPQ,ITR,ITV,ITRO,ITRV,FZO,FZV,
     .            NT2,NO,NV,NM,IMX,NONO,NVNV,NOV)
      IMPLICIT INTEGER (A-Z)
      REAL * 8 D(NT,NT),XVEC(NT,NT),WVEC(NT*NT),ACRCY
      REAL * 8 EREF,ENUC,FOCK(NT2)
      REAL * 8 Z(NO*NV),XARR(NT)
      REAL * 8 DUM,DAM,VAL,BUF
      REAL * 8  II(NTIN)
      DIMENSION IPQ(NT2),ITR(NOV),ITRO(IMX),ITRV(NM),
     .          ITV(NV)
      DIMENSION FZO(NO),FZV(NV)
      DIMENSION BUF(INTBUF),IBUF(INTBUF*2)
      DIMENSION IOUT(NT)
      CHARACTER*4 IREWR
      ITAP67=67
      ACRCY=1.D-15
C
C     GET VECTOR FROM FILE30
C
      IREWR='READ'
      CALL VECS(D,IOUT,ACRCY,XVEC,WVEC,IREWR,NT,NT)
      WRITE(*,*)'EIGENVECTOR TO CONSTRUCT GENERALIZED FOCK MATRIX'
      CALL MATOUT(D,NT,NT,NT,NT,6)
C
C     GET S AND H FROM FILE67
C
      CALL RFILE(ITAP67)
      CALL SREAD (ITAP67,WVEC,INTOWP(NT2))
C     WRITE(*,*)'AO OVERLAP MATRIX'
C     CALL PRINT(WVEC,NT2,NT,6)
      CALL SREAD (ITAP67,WVEC,INTOWP(NT2))
      CALL RCLOSE(ITAP67,3)
C     WRITE(*,*)'AO ONE E H MATRIX'
C     CALL PRINT(WVEC,NT2,NT,6)
      CALL ZERO (FOCK,NT2)
      DO 148  I=1,NT
      DO 146  J=1,I
      DO 140 MU=1,NT
      DO 140 NU=1,NT
      MA=MAX0(MU,NU)
      MI=MIN0(MU,NU)
      MUNU=(MA*(MA-1))/2+MI
      IJ=IPQ(I)+J
      FOCK(IJ)=FOCK(IJ)+D(MU,I)*D(NU,J)*WVEC(MUNU)
  140 CONTINUE
  146 CONTINUE
  148 CONTINUE
C     WRITE(*,*)'ONE E HAMILTONIAN MO BASIS'
C     CALL PRINT(FOCK,NT2,NT,6)
      EREF=0.0D0
      DO 200 I=1,NO
      FII=IPQ(I)+I
      EREF=EREF+FOCK(FII)
  200 CONTINUE
C
C     CALL RDINTS(60,II,NTIN,NGOO,6,BUF,IBUF,INTBUF,
C    .            IPQ,NT2,ITR,NV,ITRO,IMX,ITRV,NM,NO,FZO,FZV,
C    .            AOFF,AADD,NONO,NONO,ITR,NOV,ITR,NOV)
C     WRITE(*,645)(II(I),I=1,NGOO)
  645 FORMAT(7(2X,F8.4))
      DO 248  I=1,NO
      DO 246  M=1,I
      IM=IPQ(MAX0(I,M))+MIN0(I,M)
      FIM=IPQ(I)+M
      DO 240  J=1,NO
      IJ=IPQ(MAX0(I,J))+MIN0(I,J)
      MJ=IPQ(MAX0(M,J))+MIN0(M,J)
      JJ=IPQ(J)+J
      IMJJ=IPQ (MAX0(IM,JJ))+    (MIN0(IM,JJ))
      IJMJ=IPQ (MAX0(IJ,MJ))+    (MIN0(IJ,MJ))
      FOCK(FIM)=FOCK(FIM)+2.D0*II(IMJJ)-II(IJMJ)
  240 CONTINUE
  246 CONTINUE
  248 CONTINUE
C
C     CALL RDINTS(64,II,NTIN,NLOV,6,BUF,IBUF,INTBUF,
C    .            IPQ,NT2,ITR,NV,ITRO,IMX,ITRV,NM,NO,FZO,FZV,
C    .            EOFF,EADD,NM  ,NONO,ITR,NOV,ITR,NOV)
C     DO 348  I=1,NO
C     DO 346  M=1,NV
C     IM=ITR(M)+I
C     FIM=IP
C     DO 340  J=1,NO
C     IJ=ITR(MAX0(I,J))+MIN0(I,J)
C     MJ=ITR(M)+J
C     JJ=ITR(J)+J
C     IMJJ=EOFF(IM)+EADD(JJ)
C     IJMJ=EOFF(MJ)+EADD(IJ)
C     FOCK(I,M+NO)=FOCK(I,M+NO)+2.D0*II(IMJJ)-II(IJMJ)
C 340 CONTINUE
C 346 CONTINUE
C 348 CONTINUE
      DO 448  I=1,NV
      DO 446  M=1,NO
      IM=IPQ(I+NO)+M
      FIM=IPQ(I+NO)+M
      DO 440  J=1,NO
      IJ=IPQ(I+NO)+J
      MJ=IPQ(MAX0(M,J))+MIN0(M,J)
      JJ=IPQ(J)+J
      IMJJ=IPQ(MAX0(IM,JJ))+MIN0(IM,JJ)
      IJMJ=IPQ(MAX0(IJ,MJ))+MIN0(IJ,MJ)
      FOCK(FIM)=FOCK(FIM)+2.D0*II(IMJJ)-II(IJMJ)
  440 CONTINUE
  446 CONTINUE
  448 CONTINUE
C
C     CALL RDINTS(62,II,NTIN,NGOV,6,BUF,IBUF,INTBUF,
C    .            IPQ,NT2,ITR,NV,ITRO,IMX,ITRV,NM,NO,FZO,FZV,
C    .            COFF,CADD,NVNV,NONO,ITV,NV,ITR,NOV)
C     WRITE(*,*)'NGOV',NGOV
C     CALL MATOUT(FOCK,NT,NT,NT,NT,6)
C     WRITE(*,645)(II(I),I=1,NGOV)
      DO 548  I=1,NV
      DO 546  M=1,I
      FIM=IPQ(I+NO)+M+NO
      IM=IPQ(MAX0(I,M)+NO)+MIN0(I,M)+NO
      DO 540  J=1,NO
      JJ=IPQ(J)+J
      IMJJ=IPQ(MAX0(IM,JJ))+MIN0(IM,JJ)
      FOCK(FIM)=FOCK(FIM)+2.D0*II(IMJJ)
  540 CONTINUE
  546 CONTINUE
  548 CONTINUE
C
C     CALL RDINTS(63,II,NTIN,NHOV,6,BUF,IBUF,INTBUF,
C    .            IPQ,NT2,ITR,NV,ITRO,IMX,ITRV,NM,NO,FZO,FZV,
C    .            DOFF,DADD,NM  ,NM  ,ITR,NOV,ITR,NOV)
C     WRITE(*,*)'NHOV',NHOV
C     CALL MATOUT(FOCK,NT,NT,NT,NT,6)
C     WRITE(*,645)(II(I),I=1,NHOV)
      DO 648  I=1,NV
      DO 646  M=1,I
      FIM=IPQ(I+NO)+M+NO
      DO 640  J=1,NO
      IJ=IPQ(I+NO)+J
      MJ=IPQ(M+NO)+J
      IJMJ=IPQ(MAX0(IJ,MJ))+MIN0(IJ,MJ)
      FOCK(FIM)=FOCK(FIM)-II(IJMJ)
  640 CONTINUE
  646 CONTINUE
  648 CONTINUE
C
      WRITE(*,*)'FOCK MATRIX IN MO BASIS'
      CALL PRINT(FOCK,NT2,NT,6)
C
      DO 700 I=1,NO
      FII=IPQ(I)+I
      EREF=EREF+FOCK(FII)
  700 CONTINUE
      EREF=EREF+ENUC
      WRITE(*,*)'NEW EREF', EREF
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE FROZEN (FLOV,NOSYM,NVSYM,NFZO,NFZV,NDOC,NUOC,FZO,FZV,
     .                   ITEP,NIRRED,NO,NV,IW)
      IMPLICIT INTEGER(A-Z)
      DIMENSION FLOV(NIRRED,4),NOSYM(NIRRED),NVSYM(NIRRED),NFZO(NIRRED),
     .          NFZV(NIRRED),NDOC(NIRRED),NUOC(NIRRED),FZO(NO),FZV(NV),
     .          ITEP(NIRRED)
C     DO 558 I=1,NIRRED
C     WRITE(*,*)'IRREP=',I,'F1',FLOV(I,1)
C     WRITE(*,*)'IRREP=',I,'F2',FLOV(I,2)
C     WRITE(*,*)'IRREP=',I,'F3',FLOV(I,3)
C 558 WRITE(*,*)'IRREP=',I,'F4',FLOV(I,4)
      DO 625 I=1,NIRRED
      NOSYM(I)=FLOV(I,2)-FLOV(I,1)+1
C     WRITE(*,*)'I=',I,'NOSYM(I)=',NOSYM(I)
      IF(FLOV(I,2).LT.FLOV(I,1))NOSYM(I)=0
      NVSYM(I)=FLOV(I,4)-FLOV(I,3)+1
C     WRITE(*,*)'I=',I,'NVSYM(I)=',NVSYM(I)
      IF(FLOV(I,4).LT.FLOV(I,3))NVSYM(I)=0
  625 CONTINUE
      WRITE(IW,6000)
 6000 FORMAT(2X,//,'******* CORES & VIRTUALS *******',/)
      DO 627 I=1,NIRRED
      NDOC(I)=NOSYM(I)-NFZO(I)
      NUOC(I)=NVSYM(I)-NFZV(I)
  627 WRITE(IW,6052)ITEP(I),NFZO(I),NDOC(I),NUOC(I),NFZV(I)
 6052 FORMAT(2X,A4,3X,I3,'COR',I3,'DOC',I3,'UOC',I3,'VIR')
      JO=0
      DO 633 IS=1,NIRRED
      DO 631 I=1,NFZO(IS)
      JO=JO+1
  631 FZO(JO)=1
      DO 632 I=1,NDOC(IS)
      JO=JO+1
  632 FZO(JO)=0
  633 CONTINUE
      JV=0
      DO 636 IS=1,NIRRED
      DO 634 I=1,NUOC(IS)
      JV=JV+1
  634 FZV(JV)=0
      DO 635 I=1,NFZV(IS)
      JV=JV+1
  635 FZV(JV)=1
  636 CONTINUE
C     DO 650 I=1,NO
C 650 WRITE(*,*)'I=',I,'FZO(I)=',FZO(I)
C     DO 660 I=1,NV
C 660 WRITE(*,*)'I=',I,'FZV(I)=',FZV(I)
      DO 670 I=1,NIRRED
C     WRITE(*,*)I,FLOV(I,1),FLOV(I,2)
      FLOV(I,1)=FLOV(I,1)+NFZO(I)
C     WRITE(*,*)I,FLOV(I,1),FLOV(I,2)
C     WRITE(*,*)I,FLOV(I,3),FLOV(I,4)
      FLOV(I,4)=FLOV(I,4)-NFZV(I)
C     WRITE(*,*)I,FLOV(I,3),FLOV(I,4)
  670 CONTINUE
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE NCOUNT (ORBSYM,FLOV,NIRRED,NO,NV,NT,NDT2,NDT1,FZO,FZV,
     .                   NDIMT3)
      IMPLICIT INTEGER (A-Z)
      DIMENSION FLOV(NIRRED,4),ORBSYM(NT),FZO(NO),FZV(NV)
C
C     COUNT SYMMETRY NON-ZERO T1
C
      NODD=0
      DO 220 USYM=1,NIRRED
      FU=FLOV(USYM,1)
      LU=FLOV(USYM,2)
      DO 220  U=FU,LU
      FBE=FLOV(USYM,3)-NO
      LBE=FLOV(USYM,4)-NO
      DO 220 BE=FBE,LBE
      NODD=NODD+1
  220 CONTINUE
      NDT1=NODD
C
C     COUNT SYMMETRY NON-ZERO T2
C
      NOFF=0
      DO 330 TSYM=1,NIRRED
      DO 330 USYM=1,NIRRED
      DO 330 VSYM=1,NIRRED
      UVSYM=IEOR(USYM-1,VSYM-1)+1
      IF(UVSYM.NE.TSYM) GO TO 325
      DO 320 U=FLOV(USYM,1),FLOV(USYM,2)
      DO 320 V=FLOV(VSYM,1),FLOV(VSYM,2)
      DO 318 BE=1,NV
      IF(FZV(BE).EQ.1)GO TO 318
      BESYM=ORBSYM(BE+NO)
      DO 317 GA=1,BE
      IF(FZV(GA).EQ.1)GO TO 317
      GASYM=ORBSYM(GA+NO)
      BEGSYM=IEOR(BESYM,GASYM)+1
      IF(BEGSYM.NE.TSYM)GO TO 315
      NOFF=NOFF+1
  315 CONTINUE
  317 CONTINUE
  318 CONTINUE
  320 CONTINUE
  325 CONTINUE
  330 CONTINUE
      NDT2=NOFF
C
C     COUNT T3
C
      NOFF=0
      DO 590 TSYM=1,NIRRED
      DO 585 USYM=1,NIRRED
      DO 580 VSYM=1,NIRRED
      DO 578 WSYM=1,NIRRED
      UVSYM=IEOR(USYM-1,VSYM-1)
      UVWSYM=IEOR(UVSYM,WSYM-1)+1
      IF(UVWSYM.NE.TSYM) GO TO 575
      DO 570 U=FLOV(USYM,1),FLOV(USYM,2)
      DO 565 V=FLOV(VSYM,1),FLOV(VSYM,2)
      DO 563 W=FLOV(WSYM,1),FLOV(WSYM,2)
C     NADD=0
C     XOFF(U,V,W)=NOFF
      DO 560 BESYM=1,NIRRED
      DO 555 GASYM=1,NIRRED
      DO 553 EPSYM=1,NIRRED
      BEGAS=IEOR(BESYM-1,GASYM-1)
      BEGAEP=IEOR(BEGAS,EPSYM-1)+1
      IF(BEGAEP.NE.TSYM) GO TO 550
      DO 545 BE=FLOV(BESYM,3)-NO,FLOV(BESYM,4)-NO
      DO 540 GA=FLOV(GASYM,3)-NO,FLOV(GASYM,4)-NO
      DO 538 EP=FLOV(EPSYM,3)-NO,FLOV(EPSYM,4)-NO
      NOFF=NOFF+1
C     NADD=NADD+1
C     XADD(BE,GA,EP)=NADD
C     TOT=XOFF(U,V,W)+XADD(BE,GA,EP)
C     WRITE(6,658) U,V,W,XOFF(U,V,W),BE,GA,EP,XADD(BE,GA,EP),TOT
  538 CONTINUE
  540 CONTINUE
  545 CONTINUE
  550 CONTINUE
  553 CONTINUE
  555 CONTINUE
  560 CONTINUE
  563 CONTINUE
  565 CONTINUE
  570 CONTINUE
  575 CONTINUE
  578 CONTINUE
  580 CONTINUE
  585 CONTINUE
  590 CONTINUE
      NDIMT3=NOFF
      WRITE(*,*)'NDIMT3 IN INE NCOUNT',NDIMT3
      END
C
C=======================================================================
C
      SUBROUTINE PAK
      IMPLICIT REAL*8 (A-H,O-Z)
C
      COMMON /PAKQ/ IX(2),I5(2)
C
      INTEGER*2 I5
C     LOGICAL*4 TRANS(2)
C     EQUIVALENCE (IX(1),TRANS(1))
C
      DO 1 I=1,2
         IF (IX(I) .GT. 65535) STOP ' PACKING PROBLEM '
         I5(I)=IX(I)
    1 CONTINUE
C     I5=ITEMP
C
      RETURN
C
C----UNPAK-----------------------------------------------UNPAK
C
      ENTRY UNPAK
C
C     ITEMP=I5
      DO 2 I=1,2
         IX(I)=I5(I)
    2 CONTINUE
C
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE PRN11 (ENUC,ESCF)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 A30(100)
      INTEGER IA30(400),IPTR30(400)
      INTEGER IALBEL(20),IBLBEL(20),LBLDRT(26)
      DIMENSION ZAN(50),GEOM(3,50)
      EQUIVALENCE (IA30,A30)
      ITAP11 = 11
      IT30 = 30
      NOUT = 6
C
C     ----- GET CONSTANTS FROM TAPE30 -----
C
      CALL SREW(IT30)
      CALL WREADW(IT30,IA30,200,101,JUNK)
C
      IEND   =  IA30(1)
      MPOINT =  IA30(2)
      MCONST =  IA30(3)
      MCALCS =  IA30(4)
      NCALCS =  IA30(5)
      NAT    = IA30(19)
      NUM    = IA30(22)
      NNP    = IA30(23)
      NSHELL = IA30(27)
      NIRRED = IA30(28)
      NT     = IA30(29)
      NPRIM  = IA30(32)
      MXT    = IA30(36)
      NISO   = IA30(40)
C
C     ----- READ POINTERS FROM TAPE30 -----
C
      JUNK   = 101+MCONST
      CALL WREADW(IT30,IPTR30,MPOINT,JUNK,JUNK)
C
C     ----- NOW GO FOR THE GUSTO -----
C
      CALL WREADW(IT30,   ZAN,    INTOWP(NAT),IPTR30(1),JUNK)
C
C     ----- AND GET LOCATION OF INDIVIDUAL CALCULATION TO GET GEOM
C
      JUNK   = 101+MCONST+MPOINT+NCALCS-1
      CALL WREADW(IT30, LOCCAL, 1,JUNK,JUNK)
      CALL WREADW(IT30, IA30, 60,LOCCAL,LOCCAL)
      DO 500  ILBL=1,20
         IALBEL(ILBL) =  IA30(ILBL)
  500 CONTINUE
      LOCCAL=LOCCAL+20
      CALL WREADW(IT30,  GEOM,  INTOWP(3*NAT),LOCCAL,LOCCAL)
C
C     ----- GET ENERGIES AND WRITE OUT -----
C
      CALL WREADW(IT30,IA30,INTOWP(10),LOCCAL,LOCCAL)
      ESCF = A30(2)
      ENUC=A30(1)
      EE=ESCF-ENUC
      WRITE (NOUT,900)
  900 FORMAT (//,1X,'*** ENERGIES FROM FILE30 ***',/)
      WRITE (NOUT,901) ENUC
  901 FORMAT (1X,' NUCLEAR REPULSION ENERGY =',F20.12)
      WRITE (NOUT,902) ESCF
  902 FORMAT (1X,' SCF ENERGY               =',F20.12)
      WRITE (NOUT,903) EE
  903 FORMAT (1X,' TOTAL ELECTRONIC  ENERGY =',F20.12)
      CALL RCLOSE(IT30,3)
      IF(DIISFL.NE.2)THEN
      CALL RCLOSE (ITAP98,3)
      CALL RCLOSE (ITAP99,3)
      ELSE
      CALL RCLOSE (ITAP98,3)
      CALL RCLOSE (ITAP99,3)
      ENDIF
C
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE RDINTS (IDISK,MOINTS,LENINT,IDIM,JOUT,BUF,IBUF,LENBUF,
     .                   IPQ,NT2)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 MOINTS(LENINT),BUF(LENBUF)
      DIMENSION IBUF(2*LENBUF),IPQ(NT2)
      COMMON/PAKQ/I1,I2,I3
      CALL SREW(IDISK)
      ITEMP=255
      INTLEN=(INTOWP(LENBUF)-2)/INTOWP(1)
C     WRITE (JOUT,*) ' LENBUF=',LENBUF
      MAXVAL=INTOWP(INTLEN)/(1+INTOWP(1))
      IBOFF=(MAXVAL+2)/INTOWP(1)
  111 CALL SREAD(IDISK,BUF,INTOWP(LENBUF))
C     WRITE (JOUT,*) ' SREAD 78'
      CALL SETMBF(IFLG,IBUF(1))
C     WRITE (JOUT,*) ' IFLG',IFLG,IBUF(1)
      CALL SETMBF(MBUF,IBUF(2))
      DO 12 II=1,MBUF
      CALL SETMBF(IJKL,IBUF(II+2))
         I=ISHFT(IJKL,-24)
         J=IAND(ITEMP,ISHFT(IJKL,-16))
         IJ=IPQ(I)+J
         K=IAND(ITEMP,ISHFT(IJKL,- 8))
         L=IAND(ITEMP,IJKL)
         KL=IPQ(K)+L
         IJKL=IPQ(IJ)+KL
         MOINTS(IJKL)=BUF(II+IBOFF)
   12 CONTINUE
      IF (IFLG.EQ.0) GOTO 111
C     CALL MATOUT(MOINTS,IDIM,IDIM,IDIM,IDIM,JOUT)
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE READ30(ITAP30,MPOINT,MCONST,MCALCS,NCALCS,NT,
     &                  NSYMHF,MXCOEF,EIGVAL,NLAMDA,ITEMP,JOUT,NC,NO,
     .                  WTEMP,PTOCC,ITYP,ORBSYM,FLOV,NIRRED,ITEP,
     .                  ENUC,ESCF)
C
      IMPLICIT REAL*8 (A-H,O-Z)
C
      CHARACTER*4 ITYP(NSYMHF),ICSYM,ITEP(NIRRED)
      REAL*8 EIGVAL(NT),WTEMP(NT)
      INTEGER NLAMDA(NSYMHF),ITEMP(MCALCS),NC(NSYMHF),PTOCC(NT),
     .        ORBSYM(NT),FLOV(NIRRED,4),SYMORB
C
      JPOINT = 101 + MCONST + MPOINT
C
      CALL WREADW (ITAP30,ITEMP,MCALCS,JPOINT,JPOINT)
      LOCCAL = ITEMP(NCALCS)
      JPOINT = LOCCAL + 60
      CALL WREADW (ITAP30,LOCVEC,1,JPOINT,JPOINT)
      LOCVEC=LOCVEC+INTOWP(MXCOEF)
      CALL WREADW (ITAP30,EIGVAL,INTOWP(NT),LOCVEC,LOCVEC)
      CALL WREADW (ITAP30,ITYP,NSYMHF,LOCVEC,LOCVEC)
C     LOCVEC = LOCVEC + NSYMHF
C     WRITE(*,*) ' LOCVEC,MXCOEF = ',LOCVEC,MXCOEF
      CALL WREADW (ITAP30,NLAMDA,NSYMHF,LOCVEC,LOCVEC)
      CALL WREADW (ITAP30,NC,NSYMHF,LOCVEC,LOCVEC)
      NO=0
      DO 555 I=1,NSYMHF
         NO=NO+NC(I)
C        WRITE (6,*) ' I,NC=',I,NC(I)
  555 CONTINUE
      ICNT=0
      IOF = 0
      DO 558 I=1,NSYMHF
      IF(I.NE.1) IOF = IOF + NLAMDA(I-1)
      NOI=NC(I)
      DO 557 J=1,NOI
      ICNT=ICNT+1
      IPT=IOF+J
C     WRITE(6,*)'IPT=',IPT,ICNT
  557 PTOCC(IPT)=ICNT
  558 CONTINUE
C     WRITE(*,*) '  VIRTUALS'
      IOF = 0
      DO 658 I=1,NSYMHF
      IF(I.NE.1) IOF = IOF + NLAMDA(I-1)
      NVI=NLAMDA(I)-NC(I)
      DO 657 J=1,NVI
      ICNT=ICNT+1
      IPT=IOF+NC(I) + J
C     WRITE(6,*) IOF,NC(I),J
C     WRITE(6,*)'IPT=',IPT,ICNT
  657 PTOCC(IPT)=ICNT
  658 CONTINUE
      DO 559 I=1,NT
      IPT=PTOCC(I)
  559 WTEMP(IPT)=EIGVAL(I)
      DO 569 I=1,NT
      EIGVAL(I)=WTEMP(I)
C     WRITE(6,*)'I=',I,'E(I)=',EIGVAL(I)
  569 CONTINUE
C
      DO 805 ISYM = 1,NIRRED
      FLOV(ISYM,1) = 0
      FLOV(ISYM,2) = -1
      FLOV(ISYM,3) = 0
      FLOV(ISYM,4) = -1
  805 CONTINUE
C
      ICNT=0
      DO 705 ISYM=1,NSYMHF
      ICSYM=ITYP(ISYM)
      ICNTH=ICNT+1
      NOI=NC(ISYM)
      DO 700 I=1,NOI
      ICNT=ICNT+1
      IF(ICSYM.EQ.'A1  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'A2  ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'B1  ')ORBSYM(ICNT)=2
      IF(ICSYM.EQ.'B2  ')ORBSYM(ICNT)=3
      IF(ICSYM.EQ.'A   ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'B   ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'A''  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'A"  ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'AG  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'B1G ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'B2G ')ORBSYM(ICNT)=2
      IF(ICSYM.EQ.'B3G ')ORBSYM(ICNT)=3
      IF(ICSYM.EQ.'AU  ')ORBSYM(ICNT)=4
      IF(ICSYM.EQ.'B1U ')ORBSYM(ICNT)=5
      IF(ICSYM.EQ.'B2U ')ORBSYM(ICNT)=6
      IF(ICSYM.EQ.'B3U ')ORBSYM(ICNT)=7
  700 CONTINUE
      IF(ICNTH-1.NE.ICNT) THEN
      SYMORB = ORBSYM(ICNT) + 1
      FLOV(SYMORB,1)=ICNTH
      FLOV(SYMORB,2)=ICNT
      END IF
C     WRITE(*,*)'ISYM',ISYM,'F1',FLOV(ISYM,1)
C     WRITE(*,*)'ISYM',ISYM,'F2',FLOV(ISYM,2)
  705 CONTINUE
      DO 715 ISYM=1,NSYMHF
      ICSYM=ITYP(ISYM)
      ICNTH=ICNT+1
      NVI=NLAMDA(ISYM)-NC(ISYM)
      DO 710 I=1,NVI
      ICNT=ICNT+1
      IF(ICSYM.EQ.'A1  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'A2  ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'B1  ')ORBSYM(ICNT)=2
      IF(ICSYM.EQ.'B2  ')ORBSYM(ICNT)=3
      IF(ICSYM.EQ.'A   ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'B   ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'A''  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'A"  ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'AG  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'B1G ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'B2G ')ORBSYM(ICNT)=2
      IF(ICSYM.EQ.'B3G ')ORBSYM(ICNT)=3
      IF(ICSYM.EQ.'AU  ')ORBSYM(ICNT)=4
      IF(ICSYM.EQ.'B1U ')ORBSYM(ICNT)=5
      IF(ICSYM.EQ.'B2U ')ORBSYM(ICNT)=6
      IF(ICSYM.EQ.'B3U ')ORBSYM(ICNT)=7
  710 CONTINUE
      IF(ICNTH-1.NE.ICNT) THEN
      SYMORB = ORBSYM(ICNT) + 1
      FLOV(SYMORB,3)=ICNTH
      FLOV(SYMORB,4)=ICNT
      END IF
C     WRITE(*,*)'ISYM',ISYM,'F3',FLOV(ISYM,3)
C     WRITE(*,*)'ISYM',ISYM,'F4',FLOV(ISYM,4)
  715 CONTINUE
C     WRITE(*,*)'ORBSYM',ORBSYM
      DO 800 ISYM=1,NSYMHF
      ICSYM=ITYP(ISYM)
      IF(ICSYM.EQ.'A1  ')ITEP(1)=ICSYM
      IF(ICSYM.EQ.'A2  ')ITEP(2)=ICSYM
      IF(ICSYM.EQ.'B1  ')ITEP(3)=ICSYM
      IF(ICSYM.EQ.'B2  ')ITEP(4)=ICSYM
      IF(ICSYM.EQ.'A   ')ITEP(1)=ICSYM
      IF(ICSYM.EQ.'B   ')ITEP(2)=ICSYM
      IF(ICSYM.EQ.'A''  ')ORBSYM(ICNT)=0
      IF(ICSYM.EQ.'A"  ')ORBSYM(ICNT)=1
      IF(ICSYM.EQ.'AG  ')ITEP(1)=ICSYM
      IF(ICSYM.EQ.'B1G ')ITEP(2)=ICSYM
      IF(ICSYM.EQ.'B2G ')ITEP(3)=ICSYM
      IF(ICSYM.EQ.'B3G ')ITEP(4)=ICSYM
      IF(ICSYM.EQ.'AU  ')ITEP(5)=ICSYM
      IF(ICSYM.EQ.'B1U ')ITEP(6)=ICSYM
      IF(ICSYM.EQ.'B2U ')ITEP(7)=ICSYM
      IF(ICSYM.EQ.'B3U ')ITEP(8)=ICSYM
  800 CONTINUE
      CALL PRN11(ENUC,ESCF)
      END
C
C=======================================================================
C
      SUBROUTINE SETMBF(MBUF,IBUF)
      IMPLICIT REAL*8 (A-H,O-Z)
      MBUF=IBUF
      RETURN
      END
C
C=======================================================================
C
      SUBROUTINE SYMARR(ORBSYM,FLOV,NIRRED,NO,NV,NT,UOFF,VADD,ZLX,
     .                  FZO,FZV,XOFF,XADD)
      IMPLICIT INTEGER (A-Z)
      DIMENSION FLOV(NIRRED,4),ORBSYM(NT),UOFF(NO,NO,2),VADD(NV,NV),
     .          ZLX(NV,NV),FZO(NO),FZV(NV)
      INTEGER XOFF(NO,NO,NO),XADD(NV,NV,NV)
C     SYMMETRY ARRAYS
      NOFF=0
      DO 330 TSYM=1,NIRRED
      DO 330 USYM=1,NIRRED
      DO 330 VSYM=1,NIRRED
      UVSYM=IEOR(USYM-1,VSYM-1)+1
      IF(UVSYM.NE.TSYM) GO TO 325
      DO 320 U=FLOV(USYM,1),FLOV(USYM,2)
      DO 320 V=FLOV(VSYM,1),FLOV(VSYM,2)
      NADD=0
      UOFF(U,V,1)=NOFF
      DO 319 BE=1,NV
      BESYM=ORBSYM(BE+NO)
      IF(FZV(BE).EQ.1) GO TO 319
      DO 318 GA=1,BE
      IF(FZV(GA).EQ.1) GO TO 318
      GASYM=ORBSYM(GA+NO)
      BEGSYM=IEOR(BESYM,GASYM)+1
      IF(BEGSYM.NE.TSYM)GO TO 315
      NOFF=NOFF+1
      NADD=NADD+1
      VADD(BE,GA)=NADD
      TOT=UOFF(U,V,1)+VADD(BE,GA)
C     WRITE(6,657) U,V,UOFF(U,V,1),BE,GA,VADD(BE,GA),TOT
  315 CONTINUE
  318 CONTINUE
  319 CONTINUE
  320 CONTINUE
  325 CONTINUE
  330 CONTINUE
      DO 350 U=1,NO
      DO 350 V=1,NO
  350 UOFF(V,U,2)=UOFF(U,V,1)
      DO 360 A=1,NV
      DO 360 B=1,A
      ZLX(B,A)=2
      ZLX(A,B)=1
  360 VADD(B,A)=VADD(A,B)
C
C     T3  SYMMETRY  OFFSETS
C
      NOFF=0
      DO 590 TSYM=1,NIRRED
      DO 585 USYM=1,NIRRED
      DO 580 VSYM=1,NIRRED
      DO 578 WSYM=1,NIRRED
      UVSYM=IEOR(USYM-1,VSYM-1)
      UVWSYM=IEOR(UVSYM,WSYM-1)+1
      IF(UVWSYM.NE.TSYM) GO TO 575
      DO 570 U=FLOV(USYM,1),FLOV(USYM,2)
      DO 565 V=FLOV(VSYM,1),FLOV(VSYM,2)
      DO 563 W=FLOV(WSYM,1),FLOV(WSYM,2)
      NADD=0
      XOFF(U,V,W)=NOFF
      DO 560 BESYM=1,NIRRED
      DO 555 GASYM=1,NIRRED
      DO 553 EPSYM=1,NIRRED
      BEGAS=IEOR(BESYM-1,GASYM-1)
      BEGAEP=IEOR(BEGAS,EPSYM-1)+1
      IF(BEGAEP.NE.TSYM) GO TO 550
      DO 545 BE=FLOV(BESYM,3)-NO,FLOV(BESYM,4)-NO
      DO 540 GA=FLOV(GASYM,3)-NO,FLOV(GASYM,4)-NO
      DO 538 EP=FLOV(EPSYM,3)-NO,FLOV(EPSYM,4)-NO
      NOFF=NOFF+1
      NADD=NADD+1
      XADD(BE,GA,EP)=NADD
      TOT=XOFF(U,V,W)+XADD(BE,GA,EP)
C     WRITE(6,658) U,V,W,XOFF(U,V,W),BE,GA,EP,XADD(BE,GA,EP),TOT
  538 CONTINUE
  540 CONTINUE
  545 CONTINUE
  550 CONTINUE
  553 CONTINUE
  555 CONTINUE
  560 CONTINUE
  563 CONTINUE
  565 CONTINUE
  570 CONTINUE
  575 CONTINUE
  578 CONTINUE
  580 CONTINUE
  585 CONTINUE
  590 CONTINUE
C
C     WRITE (*,*)'TOTAL IN SYMARR',TOT
C     WRITE(*,*)'IN SYMARR XADD',XADD
  657 FORMAT(' U=',I3,' V=',I3,' UOFF=',I6,' BE=',I3,' GA=',I3,
     .                        ' VADD=',I6,' TOT= ',I6)
  658 FORMAT(3I4,' UOFF=',I6,3I4,' VADD=',I6,' TOT= ',I6)
C
      END
      SUBROUTINE TDIFF(T2,T1,T2O,T1O,T1NORM,DELT,NO,NV,NDIMT2,
     .                 ORBSYM,FLOV,NIRRED,TOFF,TADD,ZLX,FZO,FZV,
     .                 Z3,Y3,NDIMT3,X3OFF,X3ADD)
      IMPLICIT INTEGER(A-Z)
      REAL*8 T2(NDIMT2),T1(NO,NV),T2O(NDIMT2),T1O(NO,NV),
     .       XE,XEO,DELE,T1NORM,DELT
      INTEGER ORBSYM(NO+NV),FLOV(NIRRED,4),TOFF(NO,NO,2),TADD(NV,NV),
     .        ZLX(NV,NV),FZO(NO),FZV(NV)
      REAL*8 Z3(*),Y3(*)
      INTEGER X3OFF(NO,NO,NO),X3ADD(NV,NV,NV)
C
      T1NORM=0.0D+00
      DELT=0.0D+00
      DO 10 U=1,NO
      IF(FZO(U).EQ.1)GO TO 10
      USYM=ORBSYM(U)
         DO 20 BE=1,NV
         IF(FZV(BE).EQ.1)GO TO 20
         BESYM=ORBSYM(BE+NO)
            IF(USYM.EQ.BESYM)THEN
            DELT=DELT+(T1(U,BE)-T1O(U,BE))*(T1(U,BE)-T1O(U,BE))
            T1NORM=T1NORM+T1(U,BE)*T1(U,BE)
            ENDIF
         UBESYM=IEOR(USYM,BESYM)
            DO 30 V=1,NO
            IF(FZO(V).EQ.1)GO TO 30
            VSYM=ORBSYM(V)
            GASYM=IEOR(UBESYM,VSYM)+1
            FGA=FLOV(GASYM,3)-NO
            LGA=FLOV(GASYM,4)-NO
               DO 40 GA=FGA,LGA
               ZL=ZLX(BE,GA)
               UVBEGA=TOFF(U,V,ZL)+TADD(BE,GA)
                  DELT=DELT
     .   +      (T2O(UVBEGA)-T2(UVBEGA))*(T2O(UVBEGA)-T2(UVBEGA))
   40          CONTINUE
   30       CONTINUE
   20    CONTINUE
   10 CONTINUE
      DO 3590 I=1,NO
      IF(FZO(I).EQ.1)GO TO 3590
      ISYM=ORBSYM(I)
      DO 3580 J=1,I
      IF(FZO(J).EQ.1)GO TO 3580
      JSYM=ORBSYM(J)
      IJSYM=IEOR(ISYM,JSYM)
      DO 3570 K=1,J
      IF(FZO(K).EQ.1)GO TO 3570
      KSYM=ORBSYM(K)
      IJKSYM=IEOR(IJSYM,KSYM)
      DO 3560 A=1,NV
      IF(FZV(A).EQ.1)GO TO 3560
      ASYM=ORBSYM(A+NO)
      DO 3550 B=1,A
      IF(FZV(B).EQ.1)GO TO 3550
      BSYM=ORBSYM(B+NO)
      ABSYM=IEOR(ASYM,BSYM)
      CSYM=IEOR(IJKSYM,ABSYM)+1
      FC=FLOV(CSYM,3)-NO
      LC=FLOV(CSYM,4)-NO
      IF(LC.GT.B)LC=B
      DO 3540 C=FC,LC
      IDX=X3OFF(I,J,K)+X3ADD(A,B,C)
      DELT=DELT+(Z3(IDX)-Y3(IDX))*(Z3(IDX)-Y3(IDX))
 3540 CONTINUE
 3550 CONTINUE
 3560 CONTINUE
 3570 CONTINUE
 3580 CONTINUE
 3590 CONTINUE
      DELT=DSQRT(DELT)
      T1NORM=DSQRT(T1NORM)
      RETURN
      END
C
C
      SUBROUTINE VECS(D,IOUT,ACRCY,XVEC,WVEC,IREWR,NBF,NBFAO)
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER IOUT(NBF),NLAMDA(8),OUT,CHK
      REAL*8 D(NBFAO,NBF),XVEC(1),WVEC(NBFAO,NBFAO),ACRCY
      CHARACTER * 4 IREWR
      DIMENSION I30(200)
      OUT=6
C
C     WRITE(OUT,701) ACRCY
  701 FORMAT (1X,'THRESHOLD FOR ELIMINATION OF VECTOR ELEMENTS = ',
     #1PE10.2,/)
      CALL ZERO(D,NBF**2)
C
C     ----- OPEN TAPE30 AND GET CONSTANTS AND POINTER TO CALC ----
C
      IT30=30
      CALL RFILE(IT30)
      CALL WREADW(IT30,I30,200,101,JUNK)
      MPOINT=I30(2)
      MCONST=I30(3)
      MCALCS=I30(4)
      NCALCS=I30(5)
      MSFRU = I30(18)
      NUM = I30(22)
      NSYMHF=I30(41)
      MXCF=I30(42)
C
      JUNK=101+MCONST+MPOINT
      CALL WREADW(IT30,I30,MCALCS,JUNK,JUNK)
C
      LOCCAL=I30(NCALCS)
      JUNK=LOCCAL+60
      CALL WREADW(IT30,LOCVEC,1,JUNK,JUNK)
C
C     ----- READ OR WRITE VECTOR -----
C
      IF(IREWR.EQ.'WRIT')THEN
      CALL WWRITW(IT30,WVEC,INTOWP(NBF*NBF),LOCVEC,LOCVEC)
      CALL RCLOSE(IT30,3)
      WRITE(*,*)'WRITING NEW MO VECTOR IN FILE 30'
      CALL MATOUT(WVEC,NBFAO,NBF,NBFAO,NBF,6)
      RETURN
      ENDIF
      CALL WREADW(IT30,XVEC,INTOWP(MXCF),LOCVEC,LOCVEC)
COUT  WRITE(*,*)'EIGENVECTOR FROM TAPE 30'
COUT  CALL MATOUT(XVEC,NBFAO,NBF,NBFAO,NBF,6)
C
      LOCVEC=LOCVEC+INTOWP(NBF)+NSYMHF
      CALL WREADW(IT30,NLAMDA,NSYMHF,LOCVEC,LOCVEC)
C
C     ----- TRANSFER VECTOR TO D MATRIX -----
C
      DO 1 I=1,MXCF
           IF (ABS(XVEC(I)).LT.ACRCY) XVEC(I)=0.0D+00
    1 CONTINUE
      IOF=0
      IJ=0
      DO 10 ISYM=1,NSYMHF
           N=NLAMDA(ISYM)
           DO 9 J=1,N
                DO 8 I=1,N
                     IJ=IJ+1
                     D(IOF+I,IOF+J)=XVEC(IJ)
    8           CONTINUE
    9      CONTINUE
           IOF=IOF+N
   10 CONTINUE
C
      JUNK=101+MCONST
      CALL WREADW(IT30,I30,MPOINT,JUNK,JUNK)
C
      LAOTSO=I30(29)
      CALL WREADW(IT30,XVEC,INTOWP(NUM*MSFRU),LAOTSO,LAOTSO)
CGES  WRITE(*,*) ' SO TO AO TRANSFORMATION MATRIX '
CGES  CALL MATOUT(XVEC,NBFAO,NBF,NBFAO,NBF,6)
C
      CALL ZERO(WVEC,NBFAO*NBFAO)
      CALL MXMB(XVEC,1,NBFAO,D,1,NBF,WVEC,1,NBFAO,NBFAO,NBF,NBF)
CGES  WRITE(*,*) ' AO TO MO EIGENVECTOR FROM TAPE 30 '
CGES  CALL MATOUT(WVEC,NBFAO,NBF,NBFAO,NBF,6)
C
      DO 20 IBF = 1,NBF
      NIBF = IOUT(IBF)
      DO 30 IAO = 1,NBFAO
      D(IAO,NIBF) = WVEC(IAO,IBF)
   30 CONTINUE
   20 CONTINUE
C
COUT  WRITE(*,*) ' AO TO MO EIGENVECTOR FROM TAPE 30 '
COUT  CALL MATOUT(D,NBFAO,NBF,NBFAO,NBF,6)
      CALL RCLOSE(IT30,3)
      RETURN
      END
