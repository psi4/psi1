      SUBROUTINE FENTRY(CC,IA,MAXCOR)
C***********************************************************************
C*  MODIFICATION FOR FENTRY                                            *
C*  BY YUKIO YAMAGUCHI                                                 *
C*  DATE FEBRUARY 16, 1989                                             *
C***********************************************************************
C*  MODIFICATION FOR IMS VERSION                                       *
C*  BY YUKIO YAMAGUCHI                                                 *
C*  DATE FEBRUARY 01, 1989                                             *
C***********************************************************************
C***LAST UPDATED ON SEPTEMBER 24, 1985 BY YUKIO YAMAGUCHI              *
C***LAST UPDATED ON JANUARY   23, 1987 BY YUKIO YAMAGUCHI              *
C***********************************************************************
      IMPLICIT REAL*8 (A-H,O-Z)
CCC   DIMENSION CC(1200000),IA(1)
C2-16-89 DIMENSION CC(650000),IA(1)
      DIMENSION CC(MAXCOR),IA(1)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/MFSEC/MASTER,NSECT
      COMMON/SAVES/IUNIT,IUNIT4,INTOT,ISAVE(500,4)
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IBF(150),IUF(150)
      COMMON/SECTS/ISS(465),IFS(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP38,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IHOMO,ILUMO
C2-16-89 EQUIVALENCE (CC,IA)
    1 FORMAT(//,2X,' THIRD DERIVATIVE PROGRAM FOR CLOSED SHELL SCF IN AO
     1 BASIS (VERSION OF 2/01/89)'/)
    2 FORMAT(5I5)
    3 FORMAT(2X,10I6)
    4 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' NBASIS = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NTRI   = ',I8/
     * 2X,' NATOMS = ',I8/
     * 2X,' N3N    = ',I8/
     * 2X,' NATRI  = ',I8/
     * 2X,' N3TOT  = ',I8/
     * 2X,' N3NP3  = ',I8/
     * 2X,' N3NXX  = ',I8/
     * 2X,' IHOMO  = ',I8/
     * 2X,' ILUMO  = ',I8/
     * 2X,' IUNIT  = ',I8/
     * 2X,' INTOT  = ',I8/
     * 2X,' IPOL   = ',I8/
     * 2X,' IPRNT  = ',I8/)
    5 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
    6 FORMAT(2I5)
    7 FORMAT(3F20.10)
    8 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
    9 FORMAT(//,2X,' REQUIRED MEMORY EXCEEDS MAXCOR'/
     1          2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
C
C   ITAP38 = TAPE FOR AO FIRST DERIVATIVES
C   ITAP44 = TAPE FOR FIRST DERIVATIVES
C   ITAP46 = TAPE FOR THIRD DERIVATIVES
C   JTAPE1 = WORKING TAPE FOR SECOND DERIVATIVES
C
      CALL TSTART(6)
      CALL NOUNFL
      CALL INITMF(1)
C
      ITAPE3=3
      ITAPE6=6
      INPUT=5
      ITAP20=20
      ITAP38=38
      ITAP44=44
      ITAP45=45
      ITAP46=46
      JTAPE1=91
CCC   MAXCOR=1200000
C2-16-89  MAXCOR=650000
C
      CALL LOCATE(INPUT,'# SCF3RD #',IERR)
C
C   LOCATION OF MATRICES IN ITAP44
C   (1) SA (N3N*NTRIL)
C   (2) HA (N3N*NTRIL)
C   (3) FA (N3N*NTRIL)
C   (4) BA (N3N*NTRIL)
C   (5) UA (N3N*NBASQL)
C
C   LOCATION OF MATRICES IN JTAPE1
C   (1) SAB (NATRI*NTRIL)
C   (2) FAB (NATRI*NTRIL)
C   (3) SSAB (NATRI*NTRIL)
C   (4) BAB (NATRI*NTRIL)
C   (5) UAB (NATRI*NBASQL)
C
      IOFF(1)=0
      DO 101 I=1,465
  101 IOFF(I+1)=IOFF(I)+I
C
      WRITE(6,1)
      WRITE(3,1)
C
C   READIN PARAMETERS
      READ(5,2) IPOL,IPRNT
C
C   READIN SCF INFORMATION
      NBASIS=LPARA(3)
      NTRI=LPARA(4)
      NTRI2=NTRI*2
      NTOT=IOFF(NTRI+1)
      IHOMO=LPARA(7)
      NATOMS=LPARA(10)
      NBFAO=LPARA(11)
      ILUMO=IHOMO+1
      N3N=LPARA(17)
      NATRI=IOFF(N3N+1)
      N3TOT=N3N*(N3N+1)*(N3N+2)/6
      NTRIL=((NTRI2-1)/NSECT+1)
      NBASQ=NBASIS*NBASIS*2
      NBASQL=((NBASQ-1)/NSECT+1)
      N3NP3=N3N+3
      N3NXX=N3NP3
      IF(IPOL.NE.0) N3NXX=N3N
      N3TRL=N3NXX*NTRIL
      N3TRTL=NATRI*NTRIL
      DO 102 I=1,N3NXX
      ISF(I)=(I-1)*NTRIL+1
      IHF(I)=(I-1)*NTRIL+N3TRL+1
      IFF(I)=(I-1)*NTRIL+N3TRL+N3TRL+1
      IBF(I)=(I-1)*NTRIL+N3TRL+N3TRL+N3TRL+1
      IUF(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+N3TRL+1
      WRITE(3,3) I,ISF(I),IHF(I),IFF(I),IBF(I),IUF(I)
  102 CONTINUE
      DO 103 I=1,NATRI
      ISS(I)=(I-1)*NTRIL+1
      IFS(I)=(I-1)*NTRIL+N3TRTL+1
      ILS(I)=(I-1)*NTRIL+N3TRTL+N3TRTL+1
      IBS(I)=(I-1)*NTRIL+N3TRTL+N3TRTL+N3TRTL+1
      IUS(I)=(I-1)*NBASQL+N3TRTL+N3TRTL+N3TRTL+N3TRTL+1
      WRITE(3,3) I,ISS(I),IFS(I),ILS(I),IBS(I),IUS(I)
  103 CONTINUE
C
      CALL RFILE(ITAP38)
      CALL RREAD(ITAP38,ISAVE,2000,1)
      IUNIT=ISAVE(1,1)
      INTOT=ISAVE(500,1)-1
      IUNIT2=IUNIT*2
      IUNIT4=IUNIT*4
C
      WRITE(6,4) NBASIS,NBFAO,NTRI,NATOMS,N3N,NATRI,N3TOT,N3NP3,N3NXX,
     1           IHOMO,ILUMO,IUNIT,INTOT,IPOL,IPRNT
C
C   DYNAMIC ALLOCATION
C   IC1   : OCC (NBASIS)
C   IC2   : EIG (NBASIS)
C   IC3   : EAO (NBFAO,NBFAO)
C   IC4   : F3A (N3TOT)
C   IC5   : F3M (N3TOT)
C   IC6   : F3X (N3TOT)
C   IC7   : UA  (NBASIS,NBASIS,N3N)
C   IC8   : SA  (NTRI,N3N)
C
C   READ IN SO-MO EIGENVECTORS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      CALL MREAD(CC(IC1),17)
      CALL MREAD(CC(IC2),18)
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,5)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C   READ IN AO THIRD DERIVATIVES
  301 CONTINUE
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN AOPART'/)
      IC4=IC3+NBFAO*NBASIS
      IC5=IC4+N3TOT
      IC6=IC5+N3TOT
      ICMAX=IC6+N3TOT
      WRITE(3,8) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................F3A     F3X.....
      CALL AOPART(CC(IC4),CC(IC6))
C
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN FABMAT'/)
      IC7=IC6+N3TOT
      IC8=IC7+NTRI*NATRI
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NTRI
      IC12=IC11+NBFAO*NBFAO
      ICMAX=IC12+NBFAO*NBFAO
      WRITE(3,8) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     HAB     SAB     TAB     FAB      U........
      CALL FABMAT(CC(IC3),CC(IC7),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C.................T........
     1            CC(IC12))
C
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN SSMAT'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      ICMAX=IC9+NTRI*NATRI
      WRITE(3,8) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................UA      SA      SS......
      CALL SSMAT(CC(IC7),CC(IC8),CC(IC9))
C
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN SCF3RD'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      IC10=IC9+NTRI*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      IC16=IC15+NTRI
      IC17=IC16+NTRI
      IC18=IC17+NTRI
      ICMAX=IC18+NTRI
      WRITE(3,8) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EIG     F3A     F3M     UA      SA......
      CALL SCF3RD(CC(IC1),CC(IC4),CC(IC5),CC(IC7),CC(IC8),
C.................BA      SAB      SBC      SCA      FAB      FBC......
     1            CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C.................FCA      SSAB     SSBC     SSCA.....
     2            CC(IC15),CC(IC16),CC(IC17),CC(IC18))
C
      WRITE(3,25)
   25 FORMAT(//,2X, ' NOW YOU ARE IN SCFADD'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBFAO*NBASIS*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+IUNIT*N3N
      IA11=IC11+IC11-1
      IC12=IC11+IUNIT2
      IC13=IC12+N3N
      IC14=IC13+NTRI*N3N
      ICMAX=IC14+NTRI*NATRI
      WRITE(3,8) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     F3A     F3M     F3X     UA      U.......
      CALL SCFADD(CC(IC3),CC(IC4),CC(IC5),CC(IC6),CC(IC7),CC(IC8),
C.................T       FDER     LABEL    XINT     DP       DM.......
     1            CC(IC9),CC(IC10),IA(IA11),CC(IC12),CC(IC13),CC(IC14))
C
C   STORE THIRD DERIVATIVES IN TAPE20
      REWIND ITAP20
      WRITE(ITAP20,6) NATOMS,N3TOT
      WRITE(ITAP20,7) (CC(IC6+I-1),I=1,N3TOT)
      REWIND ITAP20
      GO TO 400
C
  399 WRITE(6,9) ICMAX,MAXCOR
  400 CONTINUE
      CALL RCLOSE(MASTER,3)
      CALL RCLOSE(ITAP38,3)
      CALL RCLOSE(ITAP44,3)
      CALL RCLOSE(ITAP45,3)
      CALL RCLOSE(ITAP46,3)
      CALL RCLOSE(JTAPE1,3)
      CALL TSTOP(6)
C
      RETURN
C2-16-89  STOP
      END
      SUBROUTINE AOPART(F3A,F3X)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION F3A(N3TOT),F3X(N3TOT)
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/MFSEC/MASTER,NSECT
      COMMON/POSIT/KST(7,7,7),IREV3(21,21,21)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP38,ITAP44,ITAP45,ITAP46
    1 FORMAT(2X,3I5,3X,I5,5X,F10.6)
C
      CALL RFILE(ITAP46)
      CALL SREAD(ITAP46,F3X,N3TOT*2)
C
C  FORM AN ARRAY FOR AO THIRD DERIVATIVE ALLOCATION
      CALL SETUP
C
      LABC=0
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::B COORDINATE:::
      DO 120 JABC=1,IABC
C
C:::C COORDINATE:::
      DO 110 KABC=1,JABC
      LABC=LABC+1
C
      IPOS=IREV3(IABC,JABC,KABC)
      VALU=F3X(IPOS)
      F3A(LABC)=VALU
      IF(IPRNT.LE.3) GO TO 110
      WRITE(6,1) IABC,JABC,KABC,LABC,VALU
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      CALL SREW(ITAP46)
      RETURN
      END
      SUBROUTINE SETUP
      IMPLICIT INTEGER (A-Z)
      DIMENSION IIIADD(10,3),IIJADD(18,3),IJKADD(27,3)
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/POSIT/KST(7,7,7),IREV3(21,21,21)
      DATA IIIADD / 1,2,2,2,3,3,3,3,3,3,
     *              1,1,2,2,1,2,2,3,3,3,
     *              1,1,1,2,1,1,2,1,2,3/
      DATA IIJADD / 1,1,1,2,2,2,3,3,3,2,2,2,3,3,3,3,3,3,
     *              1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,3,3,3,
     *              1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3/
      DATA IJKADD/1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,2,2,2,3,3,3,3,3,3,3,3,3,
     *            1,1,1,2,2,2,3,3,3,1,1,1,2,2,2,3,3,3,1,1,1,2,2,2,3,3,3,
     *            1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3/
C
C SET UP THE KST ARRAY FOR THIRD DERIVATIVES
C
      KTOT=1
      IADD=10
      KST(1,1,1)=1
      DO 300 I=2,NATOMS
        DO 200 J=1,I
          DO 100 K=1,J
            KST(I,J,K)=KTOT + IADD
            IF(I .EQ. J) THEN
              IF(I .EQ. K) THEN
                IADD=10
              ELSE
                IADD=18
              END IF
            ELSE
              IF(J .EQ. K) THEN
                IADD=18
              ELSE
                IADD=27
              END IF
            END IF
            KTOT=KST(I,J,K)
  100     CONTINUE
  200   CONTINUE
  300 CONTINUE
C
      DO 450 IATOM=1,NATOMS
        II=(IATOM-1)*3
        IIISTR=KST(IATOM,IATOM,IATOM)-1
        DO 400 I=1,10
          I3=IIISTR + I
          IX=II + IIIADD(I,1)
          IY=II + IIIADD(I,2)
          IZ=II + IIIADD(I,3)
          IREV3(IX,IY,IZ)=I3
  400   CONTINUE
  450 CONTINUE
C
      DO 575 IATOM=2,NATOMS
        II=(IATOM-1)*3
        IATOM1=IATOM-1
        DO 550 JATOM=1,IATOM1
          JJ=(JATOM-1)*3
          IIJSTR=KST(IATOM,IATOM,JATOM)-1
          IJJSTR=KST(IATOM,JATOM,JATOM)-1
          DO 500 I=1,18
            I3=IIJSTR + I
            J3=IJJSTR + I
            IX=II + IIJADD(I,1)
            IY=II + IIJADD(I,2)
            IZ=JJ + IIJADD(I,3)
            IREV3(IX,IY,IZ)=I3
C
            IX=II + IIJADD(I,3)
            IY=JJ + IIJADD(I,1)
            IZ=JJ + IIJADD(I,2)
            IREV3(IX,IY,IZ)=J3
  500     CONTINUE
  550   CONTINUE
  575 CONTINUE
C
      DO 750 IATOM=3,NATOMS
        II=(IATOM-1)*3
        IATOM1=IATOM-1
        DO 700 JATOM=2,IATOM1
          JJ=(JATOM-1)*3
          JATOM1=JATOM-1
          DO 650 KATOM=1,JATOM1
            KK=(KATOM-1)*3
            IJKSTR=KST(IATOM,JATOM,KATOM)-1
            DO 600 I=1,27
              I3=IJKSTR + I
              IX=II + IJKADD(I,1)
              IY=JJ + IJKADD(I,2)
              IZ=KK + IJKADD(I,3)
              IREV3(IX,IY,IZ)=I3
  600       CONTINUE
  650     CONTINUE
  700   CONTINUE
  750 CONTINUE
C
      RETURN
      END
      SUBROUTINE FABMAT(EAO,HAB,SAB,TAB,FAB,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EAO(NBFAO,NBFAO),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION HAB(NTRI,NATRI),SAB(NTRI),TAB(NTRI),FAB(NTRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTS/ISS(465),IFS(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP38,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1
    1 FORMAT(//,2X,' SAB MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' HAB MATRIX, IABC = ',I5/)
    3 FORMAT(//,2X,' TAB MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' FAB MATRIX, IABC = ',I5/)
C
C   CALL BACK HM AND TM MATRICES AND FORM FA MATRICES
      CALL RFILE(ITAP45)
      CALL RFILE(JTAPE1)
C
      DO 101 IABC=1,NATRI
      ISX=ISS(IABC)
      CALL SREAD(ITAP45,SAB,NTRI2)
      CALL MOCONS(SAB,NTRI,SAB,NTRI,EAO,U,T)
      CALL RWRIT(JTAPE1,SAB,NTRI2,ISX)
  101 CONTINUE
C
      DO 102 IABC=1,NATRI
      CALL SREAD(ITAP45,HAB(1,IABC),NTRI2)
      CALL MOCONS(HAB(1,IABC),NTRI,HAB(1,IABC),NTRI,EAO,U,T)
  102 CONTINUE
      DO 105 IABC=1,NATRI
      ITS=IFS(IABC)
      CALL SREAD(ITAP45,TAB,NTRI2)
      CALL MOCONS(TAB,NTRI,TAB,NTRI,EAO,U,T)
      DO 104 I=1,NTRI
  104 FAB(I)=HAB(I,IABC)+TAB(I)
      CALL RWRIT(JTAPE1,FAB,NTRI2,ITS)
  105 CONTINUE
C
      CALL SREW(ITAP45)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE SSMAT(UA,SA,SS)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION UA(NBASIS,NBASIS,N3N),SA(NTRI,N3N),SS(NTRI,NATRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IBF(150),IUF(150)
      COMMON/SECTS/ISS(465),IFS(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP38,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' SS MATRIX'/)
C
      CALL RFILE(ITAP44)
      CALL SREW(JTAPE1)
C
      DO 101 IABC=1,N3N
      ISA=ISF(IABC)
      IUA=IUF(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,ISA)
      CALL RREAD(ITAP44,UA(1,1,IABC),NBASQ,IUA)
  101 CONTINUE
C
C   FORM SS MATRIX
      KABC=0
      DO 110 IABC=1,N3N
      DO 110 JABC=1,IABC
      KABC=KABC+1
      IABS=ISS(KABC)
      IABX=ILS(KABC)
      CALL RREAD(JTAPE1,SS(1,KABC),NTRI2,IABS)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      VALU1=A00
      VALU2=A00
      DO 103 M=1,NBASIS
      IM=IOFF(MAX0(I,M))+MIN0(I,M)
      JM=IOFF(MAX0(J,M))+MIN0(J,M)
      VALU1=VALU1+UA(I,M,IABC)*UA(J,M,JABC)+UA(J,M,IABC)*UA(I,M,JABC)
      VALU2=VALU2-SA(IM,IABC)*SA(JM,JABC)-SA(JM,IABC)*SA(IM,JABC)
  103 CONTINUE
      SS(IJ,KABC)=SS(IJ,KABC)+VALU1+VALU2
  105 CONTINUE
      CALL RWRIT(JTAPE1,SS(1,KABC),NTRI2,IABX)
C
  110 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(SS,NTRI,NATRI,NTRI,NATRI,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE SCF3RD(EIG,F3A,F3M,UA,SA,BA,
     1                  SAB,SBC,SCA,FAB,FBC,FCA,SSAB,SSBC,SSCA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),F3A(N3TOT),F3M(N3TOT)
      DIMENSION SA(NTRI,N3N),BA(NTRI,N3N)
      DIMENSION UA(NBASIS,NBASIS,N3N)
      DIMENSION SAB(NTRI),SBC(NTRI),SCA(NTRI)
      DIMENSION FAB(NTRI),FBC(NTRI),FCA(NTRI)
      DIMENSION SSAB(NTRI),SSBC(NTRI),SSCA(NTRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IBF(150),IUF(150)
      COMMON/SECTS/ISS(465),IFS(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP38,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IHOMO,ILUMO
      DATA A00,TWO,FOUR / 0.0D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(2X,4I5,5X,10F10.6)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,N3N
      IBA=IBF(IABC)
      CALL RREAD(ITAP44,BA(1,IABC),NTRI2,IBA)
  101 CONTINUE
C
      CALL SREW(JTAPE1)
      LABC=0
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::B COORDINATE:::
      DO 120 JABC=1,IABC
      IJABC=IOFF(IABC)+JABC
      ISAB=ISS(IJABC)
      IFAB=IFS(IJABC)
      ILAB=ILS(IJABC)
      CALL RREAD(JTAPE1,SAB,NTRI2,ISAB)
      CALL RREAD(JTAPE1,FAB,NTRI2,IFAB)
      CALL RREAD(JTAPE1,SSAB,NTRI2,ILAB)
C
C:::C COORDINATE:::
      DO 110 KABC=1,JABC
      LABC=LABC+1
      JKABC=IOFF(JABC)+KABC
      ISBC=ISS(JKABC)
      IFBC=IFS(JKABC)
      ILBC=ILS(JKABC)
      CALL RREAD(JTAPE1,SBC,NTRI2,ISBC)
      CALL RREAD(JTAPE1,FBC,NTRI2,IFBC)
      CALL RREAD(JTAPE1,SSBC,NTRI2,ILBC)
C
      IKABC=IOFF(IABC)+KABC
      ISCA=ISS(IKABC)
      IFCA=IFS(IKABC)
      ILCA=ILS(IKABC)
      CALL RREAD(JTAPE1,SCA,NTRI2,ISCA)
      CALL RREAD(JTAPE1,FCA,NTRI2,IFCA)
      CALL RREAD(JTAPE1,SSCA,NTRI2,ILCA)
C
      VALU1=F3A(LABC)
      VALU2=A00
      VALU3=A00
      VALU4=A00
      VALU5=A00
      VALU6=A00
      VALU7=A00
      VALU8=A00
C
      DO 104 I=1,IHOMO
      II=IOFF(I+1)
      VALU2=VALU2-SSBC(II)*BA(II,IABC)
     1           -SSCA(II)*BA(II,JABC)
     2           -SSAB(II)*BA(II,KABC)
      DO 103 J=1,NBASIS
      JJ=IOFF(J+1)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU3=VALU3+UA(J,I,IABC)*FBC(IJ)
     1           +UA(J,I,JABC)*FCA(IJ)
     2           +UA(J,I,KABC)*FAB(IJ)
      VALU4=VALU4+UA(J,I,IABC)*UA(J,I,JABC)*BA(JJ,KABC)
     1           +UA(J,I,JABC)*UA(J,I,KABC)*BA(JJ,IABC)
     2           +UA(J,I,KABC)*UA(J,I,IABC)*BA(JJ,JABC)
      FAC5=SBC(IJ)*SA(IJ,IABC)
     1    +SCA(IJ)*SA(IJ,JABC)
     2    +SAB(IJ)*SA(IJ,KABC)
      VALU5=VALU5+FAC5*EIG(I)
      FAC6=SSBC(IJ)*UA(I,J,IABC)
     1    +SSCA(IJ)*UA(I,J,JABC)
     2    +SSAB(IJ)*UA(I,J,KABC)
      VALU6=VALU6+FAC6*EIG(I)
C
      DO 102 M=1,NBASIS
      IM=IOFF(MAX0(I,M))+MIN0(I,M)
      JM=IOFF(MAX0(J,M))+MIN0(J,M)
      FAC7=UA(J,I,IABC)*(UA(M,I,JABC)*UA(M,J,KABC)
     1                  +UA(M,J,JABC)*UA(M,I,KABC))
     2    +UA(J,I,JABC)*(UA(M,I,KABC)*UA(M,J,IABC)
     3                  +UA(M,J,KABC)*UA(M,I,IABC))
     4    +UA(J,I,KABC)*(UA(M,I,IABC)*UA(M,J,JABC)
     5                  +UA(M,J,IABC)*UA(M,I,JABC))
      VALU7=VALU7-FAC7*EIG(M)
      FAC8=SA(IJ,IABC)*SA(IM,JABC)*SA(JM,KABC)
     1    +SA(IJ,JABC)*SA(IM,KABC)*SA(JM,IABC)
     2    +SA(IJ,KABC)*SA(IM,IABC)*SA(JM,JABC)
      VALU8=VALU8-FAC8*EIG(I)
  102 CONTINUE
C
  103 CONTINUE
  104 CONTINUE
      VALU2=VALU2*TWO
      VALU3=VALU3*FOUR
      VALU4=VALU4*FOUR
      VALU5=VALU5*FOUR
      VALU6=VALU6*FOUR
      VALU7=VALU7*FOUR
      VALU8=VALU8*FOUR
      VALUT=VALU2+VALU3+VALU4+VALU5+VALU6+VALU7+VALU8
      F3M(LABC)=VALUT
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,1) IABC,JABC,KABC,LABC,VALU1,VALU2,VALU3,VALU4,VALU5,
     1           VALU6,VALU7,VALU8,VALUT
C
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE SCFADD(EAO,F3A,F3M,F3X,UA,U,T,FDER,LABEL,XINT,DP,DM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EAO(NBFAO,NBASIS),UA(NBASIS,NBASIS,N3N)
      DIMENSION F3A(N3TOT),F3M(N3TOT),F3X(N3TOT)
      DIMENSION U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION FDER(N3N,IUNIT),LABEL(IUNIT4),XINT(N3N)
      DIMENSION DP(NTRI,N3N),DM(NTRI,NATRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/SAVES/IUNIT,IUNIT4,INTOT,ISAVE(500,4)
      COMMON/SECTS/ISS(465),IFS(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/CI101/IHOMO,ILUMO
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DM MATRIX IN SCFADD'/)
    2 FORMAT(2X,4I5,10F10.6)
C
C   FORM DENSITY LIKE MATRIX IN SO BASIS
      CALL DAMAT(EAO,UA,DP)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   DP ARRAY CONTAINS DENSITY LIKE MATRIX IN SO BASIS
C   DM ARRAY STORES HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PDDMAT(FDER,LABEL,XINT,DP,DM)
C
C   FINAL TRANSFORMATION
      DO 101 IABC=1,NATRI
  101 CALL MOCONS(DM(1,IABC),NTRI,DM(1,IABC),NTRI,EAO,U,T)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DM,NTRI,NATRI,NTRI,NATRI,6)
C
  201 CONTINUE
      LABC=0
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::B COORDINATE:::
      DO 120 JABC=1,IABC
      IJABC=IOFF(IABC)+JABC
C
C:::C COORDINATE:::
      DO 110 KABC=1,JABC
      LABC=LABC+1
C
      JKABC=IOFF(JABC)+KABC
      IKABC=IOFF(IABC)+KABC
C
      VALU1=A00
      VALU2=A00
      VALU3=A00
C
      DO 102 I=1,IHOMO
      DO 102 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU1=VALU1+UA(J,I,IABC)*DM(IJ,JKABC)
      VALU2=VALU2+UA(J,I,JABC)*DM(IJ,IKABC)
      VALU3=VALU3+UA(J,I,KABC)*DM(IJ,IJABC)
  102 CONTINUE
C
      VALU1=VALU1*TWO
      IF(JABC.EQ.KABC) VALU1=VALU1*TWO
      VALU2=VALU2*TWO
      IF(IABC.EQ.KABC) VALU2=VALU2*TWO
      VALU3=VALU3*TWO
      IF(IABC.EQ.JABC) VALU3=VALU3*TWO
      VALUT=VALU1+VALU2+VALU3
      F3M(LABC)=F3M(LABC)+VALUT
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,2) IABC,JABC,KABC,LABC,VALU1,VALU2,VALU3,VALUT
C
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
C   SUM UP BOTH CONTRIBUTIONS
      DO 140 I=1,N3TOT
  140 F3X(I)=F3A(I)+F3M(I)
C
      RETURN
      END
      SUBROUTINE DAMAT(EAO,UA,DP)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EAO(NBFAO,NBASIS),UA(NBASIS,NBASIS,N3N),DP(NTRI,N3N)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IBF(150),IUF(150)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/CI101/IHOMO,ILUMO
    1 FORMAT(//,2X,' DP MATRIX IN DAMAT'/)
C
C   FORM DENSITY LIKE MATRICES
      CALL ZERO(DP,NTRI*N3N)
C
      IJ=0
      DO 104 I=1,NBASIS
      DO 104 J=1,I
      IJ=IJ+1
      DO 103 K=1,IHOMO
      DO 103 L=1,NBASIS
      FAC=EAO(I,K)*EAO(J,L)+EAO(I,L)*EAO(J,K)
      DO 102 IABC=1,N3N
      DP(IJ,IABC)=DP(IJ,IABC)+UA(L,K,IABC)*FAC
  102 CONTINUE
  103 CONTINUE
  104 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,N3N,NTRI,N3N,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE PDDMAT(FDER,LABEL,XINT,DA,DM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION FDER(N3N,IUNIT),LABEL(IUNIT4),XINT(N3N)
      DIMENSION DA(NTRI,N3N),DM(NTRI,NATRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/FUNCS/NATOMS,N3N,NATRI,N3TOT
      COMMON/SAVES/IUNIT,IUNIT4,INTOT,ISAVE(500,4)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP38,ITAP44,ITAP45,ITAP46
      DATA XLIMIT / 1.0D-10 /
    1 FORMAT(2X,7I4,10F10.5)
    2 FORMAT(//,2X,' DA MATRIX IN PDDMAT'/)
    3 FORMAT(//,2X,' DM MATRIX IN PDDMAT'/)
C
      ISIZED=IUNIT*N3N*2
      ISIZEL=IUNIT4
C
C   FORM DM MATRICES
      CALL ZERO(DM,NATRI*NTRI)
C
      DO 180 IBUF=1,INTOT
      IPLUS=IBUF+1
      ISECD=ISAVE(IPLUS,1)
      CALL RREAD(ITAP38,FDER,ISIZED,ISECD)
      ISECL=ISAVE(IPLUS,4)
      CALL RREAD(ITAP38,LABEL,ISIZEL,ISECL)
      DO 170 JBUF=1,IUNIT
      JX=(JBUF-1)*4
      II=LABEL(JX+1)
      JJ=LABEL(JX+2)
      KK=LABEL(JX+3)
      LL=LABEL(JX+4)
      IF(II.EQ.0) GO TO 170
      DO 102 I=1,N3N
  102 XINT(I)=FDER(I,JBUF)
      CALL PACK(II,JJ,KK,LL,IX,JA,JAA)
      IF(IPRNT.LE.5) GO TO 301
      WRITE(6,1) IBUF,JBUF,II,JJ,KK,LL,IX,(XINT(I),I=1,N3N)
  301 CONTINUE
      GO TO (201,202,203,204,205,206,207,208,209,210,211,212,212,212),IX
C
C (II/II)   1=(11/11)
C
  201 IIII=IOFF(II+1)
      DO 112 IABC=1,N3N
      DO 111 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 111
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC)=DM(IIII,KABC)+DA(IIII,IABC)*VALU
  111 CONTINUE
  112 CONTINUE
      GO TO 160
C
C  (II/KK)  2=(22/11)
C
  202 IIII=IOFF(II+1)
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      DO 114 IABC=1,N3N
      DO 113 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 113
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC)=DM(IIII,KABC)+DA(KKKK,IABC)*VAL2
      DM(KKKK,KABC)=DM(KKKK,KABC)+DA(IIII,IABC)*VAL2
      DM(IIKK,KABC)=DM(IIKK,KABC)-DA(IIKK,IABC)*VALU
  113 CONTINUE
  114 CONTINUE
      GO TO 160
C
C  (II/IL)  3=(22/21)
C
  203 IIII=IOFF(II+1)
      IILL=IOFF(II)+LL
      DO 116 IABC=1,N3N
      DO 115 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 115
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC)=DM(IIII,KABC)+DA(IILL,IABC)*VAL2
      DM(IILL,KABC)=DM(IILL,KABC)+DA(IIII,IABC)*VALU
  115 CONTINUE
  116 CONTINUE
      GO TO 160
C
C  (IJ/JJ)  4=(21/11)
C
  204 IIJJ=IOFF(II)+JJ
      JJJJ=IOFF(JJ+1)
      DO 118 IABC=1,N3N
      DO 117 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 117
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(JJJJ,IABC)*VALU
      DM(JJJJ,KABC)=DM(JJJJ,KABC)+DA(IIJJ,IABC)*VAL2
  117 CONTINUE
  118 CONTINUE
      GO TO 160
C
C  (IJ/IJ)  5=(21/21)
C
  205 IIJJ=IOFF(II)+JJ
      IIII=IOFF(II+1)
      JJJJ=IOFF(JJ+1)
      DO 120 IABC=1,N3N
      DO 119 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 119
      VAL3=VALU+VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(IIJJ,IABC)*VAL3
      DM(IIII,KABC)=DM(IIII,KABC)-DA(JJJJ,IABC)*VALU
      DM(JJJJ,KABC)=DM(JJJJ,KABC)-DA(IIII,IABC)*VALU
  119 CONTINUE
  120 CONTINUE
      GO TO 160
C
C  (II/KL)  6=(33/21)
C
  206 IIII=IOFF(II+1)
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      DO 122 IABC=1,N3N
      DO 121 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 121
      VAL2=VALU+VALU
      VAL4=VAL2+VAL2
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC)=DM(IIII,KABC)+DA(KKLL,IABC)*VAL4
      DM(KKLL,KABC)=DM(KKLL,KABC)+DA(IIII,IABC)*VAL2
      DM(IIKK,KABC)=DM(IIKK,KABC)-DA(IILL,IABC)*VALU
      DM(IILL,KABC)=DM(IILL,KABC)-DA(IIKK,IABC)*VALU
  121 CONTINUE
  122 CONTINUE
      GO TO 160
C
C  (IJ/KK)  7=(32/11)
C
  207 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=IOFF(JJ)+KK
      DO 124 IABC=1,N3N
      DO 123 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 123
      VAL2=VALU+VALU
      VAL4=VAL2+VAL2
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(KKKK,IABC)*VAL2
      DM(KKKK,KABC)=DM(KKKK,KABC)+DA(IIJJ,IABC)*VAL4
      DM(IIKK,KABC)=DM(IIKK,KABC)-DA(JJKK,IABC)*VALU
      DM(JJKK,KABC)=DM(JJKK,KABC)-DA(IIKK,IABC)*VALU
  123 CONTINUE
  124 CONTINUE
      GO TO 160
C
C  (IJ/KK)  8=(31/22)
C
  208 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=JJ+IOFF(KK)
      DO 126 IABC=1,N3N
      DO 125 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 125
      VAL2=VALU+VALU
      VAL4=VAL2+VAL2
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(KKKK,IABC)*VAL2
      DM(KKKK,KABC)=DM(KKKK,KABC)+DA(IIJJ,IABC)*VAL4
      DM(IIKK,KABC)=DM(IIKK,KABC)-DA(JJKK,IABC)*VALU
      DM(JJKK,KABC)=DM(JJKK,KABC)-DA(IIKK,IABC)*VALU
  125 CONTINUE
  126 CONTINUE
      GO TO 160
C
C (IJ/IL)  9=(32/31)
C
  209 IIJJ=IOFF(II)+JJ
      IILL=IOFF(II)+LL
      IIII=IOFF(II+1)
      JJLL=IOFF(JJ)+LL
      DO 128 IABC=1,N3N
      DO 127 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 127
      VAL2=VALU+VALU
      VAL3=VAL2+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(IILL,IABC)*VAL3
      DM(IILL,KABC)=DM(IILL,KABC)+DA(IIJJ,IABC)*VAL3
      DM(IIII,KABC)=DM(IIII,KABC)-DA(JJLL,IABC)*VAL2
      DM(JJLL,KABC)=DM(JJLL,KABC)-DA(IIII,IABC)*VALU
  127 CONTINUE
  128 CONTINUE
      GO TO 160
C
C  (IJ/JL)  10=(32/21)
C
  210 IIJJ=IOFF(II)+JJ
      JJLL=IOFF(JJ)+LL
      IILL=IOFF(II)+LL
      JJJJ=IOFF(JJ+1)
      DO 130 IABC=1,N3N
      DO 129 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 129
      VAL2=VALU+VALU
      VAL3=VAL2+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(JJLL,IABC)*VAL3
      DM(JJLL,KABC)=DM(JJLL,KABC)+DA(IIJJ,IABC)*VAL3
      DM(IILL,KABC)=DM(IILL,KABC)-DA(JJJJ,IABC)*VALU
      DM(JJJJ,KABC)=DM(JJJJ,KABC)-DA(IILL,IABC)*VAL2
  129 CONTINUE
  130 CONTINUE
      GO TO 160
C
C  (IJ/KJ)  11=(31/21)
C
  211 IIJJ=IOFF(II)+JJ
      JJKK=JJ+IOFF(KK)
      IIKK=IOFF(II)+KK
      JJJJ=IOFF(JJ+1)
      DO 132 IABC=1,N3N
      DO 131 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 131
      VAL2=VALU+VALU
      VAL3=VAL2+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(JJKK,IABC)*VAL3
      DM(JJKK,KABC)=DM(JJKK,KABC)+DA(IIJJ,IABC)*VAL3
      DM(IIKK,KABC)=DM(IIKK,KABC)-DA(JJJJ,IABC)*VALU
      DM(JJJJ,KABC)=DM(JJJJ,KABC)-DA(IIKK,IABC)*VAL2
  131 CONTINUE
  132 CONTINUE
      GO TO 160
C
C  (IJ/KL)  12=(43/21)
C  (IJ/KL)  13=(42/31)
C  (IJ/KL)  14=(41/32)
C
  212 CONTINUE
      IIJJ=IOFF(II)+JJ
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      JJLL=IOFF(MAX0(JJ,LL))+MIN0(JJ,LL)
      JJKK=IOFF(MAX0(JJ,KK))+MIN0(JJ,KK)
      DO 134 IABC=1,N3N
      DO 133 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 133
      VAL2=VALU+VALU
      VAL4=VAL2+VAL2
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC)=DM(IIJJ,KABC)+DA(KKLL,IABC)*VAL4
      DM(KKLL,KABC)=DM(KKLL,KABC)+DA(IIJJ,IABC)*VAL4
      DM(IIKK,KABC)=DM(IIKK,KABC)-DA(JJLL,IABC)*VALU
      DM(JJLL,KABC)=DM(JJLL,KABC)-DA(IIKK,IABC)*VALU
      DM(IILL,KABC)=DM(IILL,KABC)-DA(JJKK,IABC)*VALU
      DM(JJKK,KABC)=DM(JJKK,KABC)-DA(IILL,IABC)*VALU
  133 CONTINUE
  134 CONTINUE
C
  160 CONTINUE
  170 CONTINUE
  180 CONTINUE
C
      CALL SREW(ITAP38)
C
      IF(IPRNT.LE.3) GO TO 305
      WRITE(6,2)
      CALL MATOUT(DA,NTRI,N3N,NTRI,N3N,6)
      WRITE(6,3)
      CALL MATOUT(DM,NTRI,NATRI,NTRI,NATRI,6)
C
  305 CONTINUE
      RETURN
      END
      SUBROUTINE MOCONS(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBASQ
      COMMON/SIGNS/IOFF(466),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBASIS
      DO 101 JJ=1,NBASIS
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      T(II,JJ)=SA(IIJJ)
  101 CONTINUE
      DO 103 II=1,NBASIS
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBASIS
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBASIS
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
