&TRACE ERR
*GLOBAL TXTLIB VEMLLIB VFORTLIB CMSLIB UTILITY
*GLOBAL LOADLIB VFLODLIB
EXEC HFSINIT
*  THIS EXEC RUNS CIGRPR$ IN A LOOP, UP TO "&ITERATE" NUMBER OF TIMES,
*  OVER GEOMETRIES WHICH ARE IN FILES OF FILETYPE "INFIL1", "INFIL2",
*  ... "INFIL&ITERATE".  THIS EXEC IS SPECIFICALLY DESIGNED TO GENERATE
*  A "FILE11" FULL OF GRADIENTS FOR FINITE-DISPLACEMENT APPLICATIONS.
*  IN ORDER TO REDUCE SCF ITERATIONS, THE UNPERTURBED OR STARTING
*  VECTOR SHOULD BE IN A FILE OF FILETYPE "OPTVEC30".  ALL OF THE
*  OUTPUTS ARE COLLECTED INTO A FILE WITH FILETYPE "TOTALOUT &TMP1".
*  IT IS BEST TO COPY AND MODIFY THIS EXEC TO SUIT YOUR OWN NEEDS.
&A = CIFREQ
&B = ******************
&C = &STRING OF &B  &A  **&B  &1  &B
&TYPE &C
&IF &N < 2  &GOTO -JOBSTART
&TMP1 = &CONCAT OF &2 1
&TMP2 = &CONCAT OF &2 2
&TMP4 = &CONCAT OF &2 4
-JOBSTART
CP QUERY TIME
&ITERATE = 7
&ITERNUM = 1
&LOOP -ENDITER  &ITERATE
&INFILE = &CONCAT OF INFIL &ITERNUM
&ITERNUM = &ITERNUM + 1
&FILENAM = &STRING OF   &1  &INFILE  A
&TYPE &FILENAM
COPYFILE &1 &INFILE  A1 = INFIL =  (REPLACE
ERASE  &1 FILE30 A4
COPYFILE &1 OPTVEC30 A4 = FILE30 =
*
EXEC  GEOM$ &1
EXEC  CIGRPR$ &1
STATE  &1 TOTALOUT V1
&IF &RETCODE = 0  &GOTO -NEXTCOPY
COPYFILE   &1 OUTPUT A   = TOTALOUT V1
&GOTO  -ENDITER
-NEXTCOPY
COPYFILE &1 OUTPUT A = TOTALOUT V1   (APPEND
-ENDITER
&EXIT
