      SUBROUTINE FENTRY(CC,IA,MAXCOR)
C            (CPTCAOS)
C   COUPLED PERTURBED HARTREE-FOCK PROGRAM
C   FOR TWO CONFIGURATION CLOSED-SHELL SYSTEMS IN AO BASIS
C   USING THE SUPER MATRIX (P-K FILE) FORMULATION
C**********************************************************************
C*   NOTICE OF PROGRAM MODIFICATION                                   *
C**********************************************************************
C   MODIFICATION FOR FENTRY                                           *
C*  BY: YUKIO YAMAGUCHI                                               *
C*  DATE: FEBRUARY 16, 1989                                           *
C**********************************************************************
C   MODIFICATION FOR IMS                                              *
C*  BY: YUKIO YAMAGUCHI                                               *
C*  DATE: FEBRUARY 01, 1989                                           *
C**********************************************************************
C*  BY: YUKIO YAMAGUCHI                                               *
C*  DATE: APRIL 28, 1988                                              *
C*  REASON: Initial implementation of Super Matrix method             *
C**********************************************************************
C*  BY:  RICHARD REMINGTON                         search:  c3-24-88  *
C*  DATE:  MARCH  24,  1988                                           *
C*  REASON: DECREASE CORE TO RUN IN 7MB ON 9370                       *
C**********************************************************************
C*  LAST UPDATED ON FEB 13, 1987 BY RICHARD REMINGTON                 *
C*  CHANGE THE LARGE CASE "CPGRAOB" VERSION TO THE STD. "CPGRAO" AND  *
C*  INCREASE MAXVEC,MAXVX FROM 500,12 TO 600,20 AND                   *
C*  INCREASE CC(1200000) TO CC(1750000) AND MAXCOR TO 1750000         *
C*  AND ZERO OUT CC USING VXINIT                                      *
C**********************************************************************
C*  LAST UPDATED ON OCT 24, 1985 BY RICHARD REMINGTON                 *
C*  INCREASE MAXVEC,MAXVX FROM 200,8 TO 500,12                        *
C**********************************************************************
C*  LAST UPDATED ON OCTOBER 08, 1985 BY YUKIO YAMAGUCHI               *
C**********************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
C3-24-88  DIMENSION CC(1600000),IA(1)
C2-16-89  DIMENSION CC(600000),IA(1)
      DIMENSION CC(MAXCOR),IA(1)
      CHARACTER*80 ALABEL
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/CIBAF/BAF(45,2)
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/ALPA(10,10),BETA(10,10)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/MFSEC/MASTER,NSECT
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/SIZES/MAXVEC,MAXVX
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      COMMON/CI105/A22(2,2)
C2-16-89 EQUIVALENCE (CC,IA)
      DATA ALABEL / ' SCF FIRST DERIVATIVE BROUGHT YOU BY Y & Y CO. EST.
     1 IN MARCH , 1982             ' /
      DATA TEN / 10.0D+00 /
      DATA A00,QURT,HALF,ONE / 0.0D+00 , 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' COUPLED PERTURBED HARTREE-FOCK PROGRAM FOR TCSCF IN
     1 AO BASIS (VERSION OF 2/01/1989)'/)
    2 FORMAT(7I5)
    3 FORMAT(2X,10I5)
    4 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' IPOL   = ',I8/
     * 2X,' ICONV  = ',I8/
     * 2X,' IPRNT  = ',I8/
     * 2X,' NBASIS = ',I8/
     * 2X,' NTRI   = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NBATRI = ',I8/
     * 2X,' NATOMS = ',I8)
    5 FORMAT(2X,' N3N    = ',I8/
     * 2X,' N3NP3  = ',I8/
     * 2X,' N3NXX  = ',I8/
     * 2X,' IOCC   = ',I8/
     * 2X,' JOCC   = ',I8/
     * 2X,' KOCC   = ',I8/
     * 2X,' NTYPES = ',I8/
     * 2X,' NTYPEP = ',I8/
     * 2X,' ITOLNR = ',I8/
     * 2X,' ITOLDS = ',I8/
     * 2X,' ITOLCP = ',I8/)
    6 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
    7 FORMAT(//,2X,' ALPA MATRIX'/)
    8 FORMAT(//,2X,' BETA MATRIX'/)
    9 FORMAT(//,2X,' SCF EIGENVECTOR (SO BASIS) MATRIX'/)
   10 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   11 FORMAT(//,2X,' REQUIRED MEMORY EXCEEDS MAXCOR'/
     1          2X,' ICMAX = ',I7,5X,' MAXCOR = ',I7/)
C
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C   REFERENCES FOR TCSCF AND GRSCF :                       :
C   Y.YAMAGUCHI, Y.OSAMURA, AND H.F.SCHAEFER III,          :
C   J.AM.CHEM.SOC. 105,7506(1983)                          :
C                     &                                    :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, D.J.FOX, M.A.VINCENT,  :
C   AND H.F.SCHAEFER III, J.MOL.STRUC. 103,183 (1983)      :
C                     &                                    :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, M.A.VINCENT, J.F.GAW,  :
C   AND H.F.SCHAEFER III, CHEM.PHYS. 72,131 (1982)         :
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
C   ITAP11 = GEOMETRY AND GRADIENTS
C   ITAP15 = SCF SECOND DERIVATIVES
C   ITAP17 = DIPOLE MOMENT DERIVATIVES
C   ITAP37 = TWO ELECTRON SO INTEGRALS (P-K FILE)
C   ITAP42 = FIRST AND SECOND DERIVATIVE INFORMATION
C   ITAP43 = DERIVATIVE DIPOLE MOMENTS
C   ITAP44 = FIRST DERIVATIVE
C   JTAPE1 = WORKING TAPE FOR CPHF
C   JTAPE2 = WORKING TAPE FOR E MATRICES
C
      CALL TSTART(6)
      CALL NOUNFL
      CALL INITMF(1)
C
C::::::::::::::::::::::::::::::::::::::::
C:::DEFINE MACHINE-DEPENDENT CONSTANTS:::
C::::::::::::::::::::::::::::::::::::::::
C3-24-88  MAXCOR= 1600000
C2-16-89  MAXCOR= 600000
      MAXVEC=    600
      MAXVX =     20
C
C   DEFINE BUFFER SIZES
      MAXBUF=4096
      MAXBF3=MAXBUF*3
      MAXBF6=MAXBUF*6
C
C     ZERO OUT CORE WITH AN INITIALIZATION OF CC TO "0"
C
CCC   MAXBYT = MAXCOR * 8
CCC   CALL VXINIT(CC,0,MAXBYT)
C
      ITAPE3=3
      INPUT=5
      ITAPE6=6
      ITAP11=11
      ITAP15=15
      ITAP17=17
      ITAP37=37
      ITAP42=42
      ITAP43=43
      ITAP44=44
      JTAPE1=91
      JTAPE2=92
C
C   LOCATION OF MATRICES IN ITAP44
C   (1) SA (N3NXX*NTRIL)
C   (2) HA (N3NXX*NTRIL)
C   (3) TA AND FA (N3NXX*NTRIL)
C   (4) EA (N3NXX*NBASQL)
C   (5) BA (N3NXX*NTRIL)
C   (6) UA (N3NXX*NBASQL)
C
      IOFF(1)=0
      DO 101 I=1,255
  101 IOFF(I+1)=IOFF(I)+I
C
      CALL LOCATE(INPUT,'# CPHFAO #',IERR)
      WRITE(6,1)
      WRITE(3,1)
C
C   READ IN PARAMETERS
      READ(5,2) IPOL,ICONV,IPRNT
C
C   SET UP TOLERANCES
      ITOLNR=ICONV/10000
      IF(ITOLNR.EQ.0) ITOLNR=8
      ITEMP=MOD(ICONV,10000)
      ITOLDS=ITEMP/100
      IF(ITOLDS.EQ.0) ITOLDS=10
      ITOLCP=MOD(ITEMP,100)
      IF(ITOLCP.EQ.0) ITOLCP=12
      TOLNR=ONE/(TEN**ITOLNR)
      TOLDS=ONE/(TEN**ITOLDS)
      TOLCP=ONE/(TEN**ITOLCP)
C
C   READ IN SCF INFORMATION
      NBASIS=LPARA(3)
      NBFAO=LPARA(11)
      NTRI=LPARA(4)
      NTRI2=NTRI*2
      NBATRI=IOFF(NBFAO+1)
      NBATR2=NBATRI*2
      IOCC=LPARA(7)
      KOCC=LPARA(8)
      JOCC=LPARA(9)
      NATOMS=LPARA(10)
      N3N=LPARA(17)
      NATRI=IOFF(N3N+1)
      N3NP3=N3N+3
      N3NXX=N3NP3
      IF(IPOL.NE.0) N3NXX=N3N
      NTRIL=((NTRI2-1)/NSECT+1)
      NBASQ=NBASIS*NBASIS*2
      NBASQL=((NBASQ-1)/NSECT+1)
      N3TRL=N3NXX*NTRIL
      N3QRL=N3NXX*NBASQL
      NTYPES=LPARA(18)
      NTYPEP=NTYPES+1
      DO 102 I=1,N3NXX
      ISA(I)=(I-1)*NTRIL+1
      IHA(I)=(I-1)*NTRIL+N3TRL+1
      IFA(I)=(I-1)*NTRIL+N3TRL+N3TRL+1
      IEA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+1
      IBA(I)=(I-1)*NTRIL+N3TRL+N3TRL+N3TRL+N3QRL+1
      IUA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+N3QRL+N3TRL+1
      WRITE(3,3) I,ISA(I),IHA(I),IFA(I),IEA(I),IBA(I),IUA(I)
  102 CONTINUE
      ENUC=APARA(1)
      ESCF=APARA(2)
      NSECMX=IUA(N3NXX)
      DO 103 I=1,10
      NSORB(I)=LPARA(I+40)
      FOCC(I)=APARA(I+40)
  103 CONTINUE
      WRITE(6,4) IPOL,ICONV,IPRNT,NBASIS,NTRI,NBFAO,NBATRI,NATOMS
      WRITE(6,5) N3N,N3NP3,N3NXX,IOCC,JOCC,KOCC,NTYPES,NTYPEP,
     1           ITOLNR,ITOLDS,ITOLCP
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C   DYNAMIC ALLOCATION
C     IC1  = EIG(NBASIS)
C     IC2  = OCC(NBASIS)
C     IC3  = EAO OR ESO(NBFAO,NBASIS)
C     IC4  = NIJ(NTRI,2)
C     IC5  = KIJ(NTRI,2)
C     IC6  = E1A(N3N)
C     IC7  = E2A(N3N,N3N)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
      IC4=IC3+NBFAO*NBFAO
      IA4=IC4+IC4-1
      IC5=IC4+NTRI
      IA5=IC5+IC5-1
      IC6=IC5+NTRI
      IC7=IC6+N3N
      ICSAVE=IC7+N3N*N3N
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C   READ IN AO-MO EIGENVECTORS
      CALL MREAD(CC(IC1),17)
      CALL MREAD(CC(IC2),18)
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,6)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C   CALCULATE ALPA AND BETA MATRICES
  301 CONTINUE
      CALL ZERO(ALPA,NTYPEP*NTYPEP)
      CALL ZERO(BETA,NTYPEP*NTYPEP)
      IJ=0
      DO 105 I=1,NTYPES
      DO 105 J=1,I
      IJ=IJ+1
      ALPA(I,J)= (ONE-APARA(10+IJ))*FOCC(I)*FOCC(J)*HALF
      BETA(I,J)=-(ONE-APARA(25+IJ))*FOCC(I)*FOCC(J)*QURT
      IF(I.EQ.J) GO TO 105
      ALPA(J,I)=ALPA(I,J)
      BETA(J,I)=BETA(I,J)
  105 CONTINUE
      IF(IPRNT.LE.2) GO TO 302
      WRITE(6,7)
      CALL MATOUT(ALPA,10,10,NTYPEP,NTYPEP,6)
      WRITE(6,8)
      CALL MATOUT(BETA,10,10,NTYPEP,NTYPEP,6)
C
C**************************************
C***PREPARATION FOR FIRST ORDER CPHF***
C**************************************
C
C   FORM REGISTER MATRICES
  302 CONTINUE
      WRITE(3,20)
   20 FORMAT(//,2X,' NOW YOU ARE IN REGIST'/)
C.................NIJ     KIJ.....
      CALL REGIST(IA(IA4),IA(IA5))
C
C   CALCULATE REQUIRED CORE MEMORY FOR THE CALCULATION
      CALL CORSIZ(MAXCOR,N3NXX,NADD,MAXVC,IPOL)
C
C   READ IN FIRST AND SECOND DERIVATIVE INFORMATION
C   AND FORM SM, HM AND FM MATRICES
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
      IC8=ICSAVE
      IC9=IC8+NBATRI
      IC10=IC9+NBFAO*NBFAO
      ICMAX=IC10+NBFAO*NBFAO
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     E1A     E2A     SS      U       T........
      CALL DERMAT(CC(IC3),CC(IC6),CC(IC7),CC(IC8),CC(IC9),CC(IC10),
C........................
     1            NSECMX)
C
C   READ IN SO-MO EIGENVECTORS
      CALL MREAD(CC(IC3),19)
      IF(IPRNT.LE.2) GO TO 305
      WRITE(6,9)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C:::::::::::::::::::::::::::::::::::
C:::TEST ROUTINES FOR INPUT CHECK:::
C:::::::::::::::::::::::::::::::::::
  305 CONTINUE
C
C   CALCULATE SCF ENERGY FOR A TEST
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN E0CAL'/)
      IC8=ICSAVE
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NTRI
      IC12=IC11+NBFAO*NBFAO
      IC13=IC12+NBFAO*NBFAO
      IC14=IC13+NTRI*NTYPES
      IC15=IC14+NTRI*NTYPES
      IC16=IC15+NTRI*NTYPES
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ESO     HSO     HMO     TM       U........
      CALL E0CAL(CC(IC2),CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................T        DP       DQ       DM       LBLI     BUFI.....
     1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),IA(IA16),CC(IC16))
C
C   CALCULATE SCF FIRST ENERGY DERIVATIVES
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN EADER'/)
      IC8=ICSAVE
      IC9=IC8+N3N
      IC10=IC9+N3N
      IC11=IC10+N3N
      IC12=IC11+N3N
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      ICMAX=IC15+NTRI*N3N*NTREAD
      WRITE(3,10) ICMAX,MAXCOR
C................OCC     D1O     D1T     D1W      E1T      SM.......
      CALL EADER(CC(IC2),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................HM       EPS      TM.......
     1           CC(IC13),CC(IC14),CC(IC15))
C
C   FORM HA MATRICES
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN HAMAT'/)
      IC8=ICSAVE
      IC9=IC8+N3N
      IC10=IC9+N3N
      IC11=IC10+N3N
      IC12=IC11+NTRI*N3N
      ICMAX=IC12+NTRI*N3N*NTREAD
      WRITE(3,10) ICMAX,MAXCOR
C................D1E     D1T     D1W      HM       TM.......
      CALL HAMAT(CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12))
C
C   FORM DERIVATIVE LAGRANGIAN MATRICES
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN FAMAT'/)
      IC8=ICSAVE
      IC9=IC8+NTRI
      IC10=IC9+NBASIS*NBASIS
      IC11=IC10+NTRI*N3N
      ICMAX=IC11+NTRI*N3N*NTREAD
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................FF      EPA     HM       TM.......
      CALL FAMAT(CC(IC8),CC(IC9),CC(IC10),CC(IC11))
C
C   CALCULATE BA MATRICES
C   FOR INDEPENDENT PAIRS
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN BAIND'/)
      IC8=ICSAVE
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI*NTYPEP
      IC12=IC11+NTRI
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................ESO     NIJ     U       T       ZETA     B0.......
      CALL BAIND(CC(IC3),IA(IA4),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................EPA      DP       DQ       DM       SM.......
     1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC15),
C................LBLI     BUFI.....
     2           IA(IA16),CC(IC16))
      IF(IPRNT.LE.2) GO TO 308
      CALL WRBMAT(CC(IC16),N3N)
C
C   FOR DEPENDENT PAIRS
  308 CONTINUE
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN BADEP'/)
      IC8=ICSAVE
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI
      IC13=IC12+NTRI*N3N
      IC14=IC13+NTRI*N3N
      IC15=IC14+NTRI*N3N
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     OCC     ESO     KIJ     U       T.......
      CALL BADEP(CC(IC1),CC(IC2),CC(IC3),IA(IA5),CC(IC8),CC(IC9),
C................B0       FF       DP       DQ       DM.......
     1           CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI.....
     2           IA(IA15),CC(IC15))
      IF(IPRNT.LE.2) GO TO 309
      CALL WRBMAT(CC(IC15),N3N)
C
C   CALCULATE H AND E MATRICES
  309 CONTINUE
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN HEMAT'/)
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NBFAO*NBFAO
      IC12=IC11+NTRI
      IC13=IC12+NTRI
      IC14=IC13+NTRI*5
      IC15=IC14+NTRI*5
      IC16=IC15+NTRI*5
      IC17=IC16+NBASIS*NBASIS
      IC18=IC17+NBASIS*NBASIS
      IC19=IC18+NBASIS*NBASIS
      IA19=IC19+IC19-1
      ICMAX=IC19+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................ESO     NIJ     A12     U       T        HSO......
      CALL HEMAT(CC(IC3),IA(IA4),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................HMO      DP       DQ       DM       E11      E22......
     1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC16),CC(IC17),
C................E12      LBLI     BUFI.....
     2           CC(IC18),IA(IA19),CC(IC19))
C
C   FORM BF MATRICES
      IF(IPOL.NE.0) GO TO 310
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN BFMAT'/)
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NTRI
      IC11=IC10+NTRI*NTYPES*3
      IC12=IC11+NBASIS*NBASIS
      ICMAX=IC12+NTRI
      WRITE(3,10) ICMAX,MAXCOR
C................NIJ     KIJ     HF      HM       EPF      BF.......
      CALL BFMAT(IA(IA4),IA(IA5),CC(IC9),CC(IC10),CC(IC11),CC(IC12))
      IF(IPRNT.LE.2) GO TO 310
      CALL WRBMAT(CC(IC12),N3NXX)
C
C   FORM BAF MATRICES
  310 CONTINUE
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN BAFMAT'/)
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NTRI*N3N
      IC11=IC10+NBASIS*NBASIS
      IC12=IC11+NBASIS*NBASIS
      ICMAX=IC12+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................SA      E11      E22      E12......
      CALL BAFMAT(CC(IC9),CC(IC10),CC(IC11),CC(IC12))
C
C********************************
C***FIRST ORDER CPHF PROCEDURE***
C********************************
C
C   SOLVE SIMULTANEOUS EQUATIONS ITERATIVELY
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN CPHF'/)
      NINDP2=NIND+2
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NBFAO*NBFAO
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      IC16=IC15+NTYPEP*(NTRI*NADD)
      IC17=IC16+NTYPEP*(NTRI*NADD)
      IC18=IC17+NTYPEP*(NTRI*NADD)
      IC19=IC18+MAXVC*(MAXVC+NADD)
      IC20=IC19+NINDP2*MAXVC*2
      IA20=IC20+IC20-1
      ICMAX=IC20+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C...............ESO     NIJ     KIJ     A12     U       A.......
      CALL CPHF(CC(IC3),IA(IA4),IA(IA5),CC(IC8),CC(IC9),CC(IC9),
C...............T        ZETA     B0       AA       TT       DP.......
     1          CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),CC(IC15),
C...............DQ       DM       W        BB       LBLI     BUFI.....
     2          CC(IC16),CC(IC17),CC(IC18),CC(IC19),IA(IA20),CC(IC20),
C.......................................
     3          NINDP2,MAXVC,NADD,N3NXX)
      IF(IPRNT.LE.4) GO TO 311
      CALL WRBMAT(CC(IC19),N3NXX)
C
C   CALCULATE DEPENDENT PAIRS OF U MATRICES
  311 CONTINUE
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN UCMAT'/)
      IC8=ICSAVE
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI*N3NXX
      IC13=IC12+NTRI*N3NXX
      IC14=IC13+NTRI*N3NXX
      IC15=IC14+NTRI
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     OCC     ESO     NIJ     KIJ     U.......
      CALL UCMAT(CC(IC1),CC(IC2),CC(IC3),IA(IA4),IA(IA5),CC(IC8),
C................T       B        DP       DQ       DM       DEL......
     1           CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI...........
     2           IA(IA15),CC(IC15),N3NXX)
      IF(IPRNT.LE.2) GO TO 312
      CALL WRBMAT(CC(IC14),N3NXX)
C
C   COMPLETE U MATRICES
  312 CONTINUE
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN UXMAT'/)
      IC8=ICSAVE
      IC9=IC8+NTRI*N3NXX
      IC10=IC9+NTRI*N3NXX
      ICMAX=IC10+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
C................B       SM      U..............
      CALL UXMAT(CC(IC8),CC(IC9),CC(IC10),N3NXX)
C
C   CALCULATE WA MATRICES
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN WAMAT'/)
      IC8=ICSAVE
      IC9=IC8+NBASIS*NBASIS
      IC10=IC9+NBASIS*NBASIS
      IC11=IC10+NBASIS*NBASIS
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF3
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................ESO     U       T       EPA      ZETA     WA.......
      CALL WAMAT(CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................SA       DP       DQ       DM       LBLI     BUFI.....
     1           CC(IC13),CC(IC14),CC(IC15),CC(IC13),IA(IA16),CC(IC16))
C
C   CALCULATE SCF SECOND DERIVATIVES
      WRITE(3,35)
   35 FORMAT(//,2X,' NOW YOU ARE IN SCF2ND'/)
      IC8=ICSAVE
      IC9=IC8+N3N*N3N
      IC10=IC9+N3N*N3N
      IC11=IC10+N3N*N3N
      IC12=IC11+NTRI*N3N
      IC13=IC12+NBASIS*NBASIS*N3N
      IC14=IC13+NBASIS*NBASIS
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NBASIS*NBASIS
      IC17=IC16+NBASIS*NBASIS
      ICMAX=IC17+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................E2A     E2M     E2C     E2P      SA       WA.......
      CALL SCF2ND(CC(IC7),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................EPA      UA       E11      E22      E12......
     1            CC(IC13),CC(IC14),CC(IC15),CC(IC16),CC(IC17))
C
C   SAVE C1A AND C2A VECTORS ON TAPE44
      IPOS=N3TRL*4+N3QRL*2
      CALL RWRIT(ITAP44,C1A,N3N*2,IPOS+1)
      CALL RWRIT(ITAP44,C2A,N3N*2,IPOS+2)
C
C   CALCULATE DIPOLE MOMENT DERIVATIVES
      IF(IPOL.NE.0) GO TO 320
      WRITE(3,36)
   36 FORMAT(//,2X,' NOW YOU ARE IN DIPMO'/)
      IC8=ICSAVE
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS*3
      WRITE(3,10) ICMAX,MAXCOR
C................OCC     DIP     UA......
      CALL DIPMO(CC(IC2),CC(IC8),CC(IC9))
C
C   CALCULATE ELECTRIC POLARIZABILITIES
      WRITE(3,37)
   37 FORMAT(//,2X,' NOW YOU ARE IN POLAR'/)
      IC8=ICSAVE
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
C................OCC     HF      UF......
      CALL POLAR(CC(IC2),CC(IC8),CC(IC9))
  320 CONTINUE
      GO TO 400
C
  399 WRITE(6,11) ICMAX,MAXCOR
      WRITE(3,11) ICMAX,MAXCOR
  400 CONTINUE
C
      CALL RCLOSE(ITAP42,3)
      CALL RCLOSE(ITAP43,3)
      CALL RCLOSE(ITAP44,3)
      CALL RCLOSE(MASTER,3)
      CALL RCLOSE(JTAPE1,3)
      CALL RCLOSE(JTAPE2,3)
      CALL TSTOP(6)
C
      RETURN
C2-16-89 STOP
      END
      SUBROUTINE REGIST(NIJ,KIJ)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' REGISTERED NUMBERS'/
     1 2X,' KVIR = ',I5/
     2 2X,' NIND = ',I5/
     3 2X,' NDEP = ',I5/
     4 2X,' NDIF = ',I5)
    2 FORMAT(//,2X,' NIJ MATRIX'/2X,' NO.',5X,' NIJ(1)',5X,' NIJ(2)'/)
    3 FORMAT(2X,I4,4X,I5,7X,I5)
    4 FORMAT(//,2X,' KIJ MATRIX'/2X,' NO.',5X,' KIJ(1)',5X,' KIJ(2)'/)
    5 FORMAT(//,2X,' NO.',8X,' FOCC',6X,' NSORB',7X,' NSTR',7X,' NEND'/)
    6 FORMAT(2X,I4,3X,F10.5,7X,I5,7X,I5,7X,I5)
C
      KVIR=NBASIS-JOCC
C
      NSTR(1)=1
      NEND(1)=NSORB(1)
      DO 101 ITYP=2,NTYPEP
      NSTR(ITYP)=NEND(ITYP-1)+1
      NEND(ITYP)=NSTR(ITYP)+NSORB(ITYP)-1
  101 CONTINUE
      DO 103 ITYP=1,NTYPEP
      DO 102 I=NSTR(ITYP),NEND(ITYP)
  102 MOTYP(I)=ITYP
  103 CONTINUE
C
      II=0
      DO 105 ITYP=2,NTYPEP
      DO 105 JTYP=1,ITYP-1
      DO 104 I=NSTR(ITYP),NEND(ITYP)
      DO 104 J=NSTR(JTYP),NEND(JTYP)
      II=II+1
      NIJ(II,1)=I
      NIJ(II,2)=J
  104 CONTINUE
  105 CONTINUE
      NIND=II
C
      II=0
      DO 107 ITYP=1,NTYPEP
C*C   IF(NSORB(ITYP).EQ.1) GO TO 107
C*C   DO 106 I=NSTR(ITYP)+1,NEND(ITYP)
C*C   DO 106 J=NSTR(ITYP),I-1
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=NSTR(ITYP),I
      II=II+1
      KIJ(II,1)=I
      KIJ(II,2)=J
  106 CONTINUE
  107 CONTINUE
      NDEP=II
C
      NDIF=NTRI-NIND-NDEP
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1) KVIR,NIND,NDEP,NDIF
      WRITE(6,2)
      DO 108 I=1,NIND
  108 WRITE(6,3) I,NIJ(I,1),NIJ(I,2)
      WRITE(6,4)
      DO 109 I=1,NDEP
  109 WRITE(6,3) I,KIJ(I,1),KIJ(I,2)
      WRITE(6,5)
      DO 110 I=1,NTYPEP
  110 WRITE(6,6) I,FOCC(I),NSORB(I),NSTR(I),NEND(I)
C
  205 RETURN
      END
      SUBROUTINE CORSIZ(MAXCOR,N3NXX,NADD,MAXVC,IPOL)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIZES/MAXVEC,MAXVX
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%'/
     1          2X,' %%%NOW YOU ARE IN CORSIZ%%%'/
     2          2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%'/)
    2 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
    3 FORMAT(//,2X,' *************************************************'/
     1          2X,' ***REQUIRED MEMORY EXCEEDS MAXCOR-----STOP !!!***'/
     2          2X,' *************************************************'/
     3          2X,' IGMAX = ',I10,5X,' MAXCOR = ',I10/)
    4 FORMAT(//,2X,' ::::::::::::::::::::::::::::::::::'/
     1          2X,' :::REQUIRED MEMORY FOR THIS RUN:::'/
     2          2X,' ::::::::::::::::::::::::::::::::::'/
     3          2X,' NADD  = ',I10,5X,' MAXVC  = ',I10/
     4          2X,' IGMAX = ',I10,5X,' MAXCOR = ',I10/)
    5 FORMAT(//,2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'/
     1          2X,' %%%NOW YOU ARE LEAVING CORSIZ%%%'/
     2          2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'/)
C
C   THIS SUBROUTINE WILL CHECK THE MAXIMUM CORE MEMORY REQUIRED
C   FOR THE CALCULATION
C
      WRITE(3,1)
      NADD=N3NXX*2
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C   DYNAMIC ALLOCATION
C     IC1  = EIG(NBASIS)
C     IC2  = OCC(NBASIS)
C     IC3  = EAO OR ESO(NBFAO,NBASIS)
C     IC4  = NIJ(NTRI,2)
C     IC5  = KIJ(NTRI,2)
C     IC6  = E1A(N3N)
C     IC7  = E2A(N3N,N3N)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
      IC4=IC3+NBFAO*NBASIS
      IA4=IC4+IC4-1
      IC5=IC4+NTRI
      IA5=IC5+IC5-1
      IC6=IC5+NTRI
      IC7=IC6+N3N
      ICSAVE=IC7+N3N*N3N
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C   READ IN AO-MO EIGENVECTORS
C
C   FORM REGISTER MATRICES
      WRITE(3,20)
   20 FORMAT(//,2X,' NOW YOU ARE IN REGIST'/)
C.................NIJ     KIJ.....
C***  CALL REGIST(IA(IA4),IA(IA5))
C
C   READ IN FIRST AND SECOND DERIVATIVE INFORMATION
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
      IC8=ICSAVE
      IC9=IC8+NBATRI
      IC10=IC9+NBFAO*NBFAO
      ICMAX=IC10+NBFAO*NBFAO
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=ICMAX
C.................EAO     E1A     E2A     SS      U       T........
C***  CALL DERMAT(CC(IC3),CC(IC6),CC(IC7),CC(IC8),CC(IC9),CC(IC10))
C
C   CALCULATE SCF ENERGY FOR A TEST
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN E0CAL'/)
      IC8=ICSAVE
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NTRI
      IC12=IC11+NBFAO*NBFAO
      IC13=IC12+NBFAO*NBFAO
      IC14=IC13+NTRI*NTYPES
      IC15=IC14+NTRI*NTYPES
      IC16=IC15+NTRI*NTYPES
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     ESO     HSO     HMO     TM       U........
C***  CALL E0CAL(CC(IC2),CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................T        DP       DQ       DM       LBLI     BUFI.....
C*** 1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),IA(IA16),CC(IC16))
C
C   CALCULATE SCF FIRST ENERGY DERIVATIVES
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN EADER'/)
      IC8=ICSAVE
      IC9=IC8+N3N
      IC10=IC9+N3N
      IC11=IC10+N3N
      IC12=IC11+N3N
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      ICMAX=IC15+NTRI*N3N*NTREAD
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     D1O     D1T     D1W      E1T      SM.......
C***  CALL EADER(CC(IC2),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................HM       EPS      TM.......
C*** 1           CC(IC13),CC(IC14),CC(IC15))
C
C   FORM HA MATRICES
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN HAMAT'/)
      IC8=ICSAVE
      IC9=IC8+N3N
      IC10=IC9+N3N
      IC11=IC10+N3N
      IC12=IC11+NTRI*N3N
      ICMAX=IC12+NTRI*N3N*NTREAD
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................D1E     D1T     D1W      HM       TM.......
C***  CALL HAMAT(CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12))
C
C   FORM DERIVATIVE LAGRANGIAN MATRICES
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN FAMAT'/)
      IC8=ICSAVE
      IC9=IC8+NTRI
      IC10=IC9+NBASIS*NBASIS
      IC11=IC10+NTRI*N3N
      ICMAX=IC11+NTRI*N3N*NTREAD
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................FF      EPA     HM       TM.......
C***  CALL FAMAT(CC(IC8),CC(IC9),CC(IC10),CC(IC11))
C
C   CALCULATE BA MATRICES
C   FOR INDEPENDENT PAIRS
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN BAIND'/)
      IC8=ICSAVE
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI*NTYPEP
      IC12=IC11+NTRI
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................ESO     NIJ     U       T       ZETA     B0.......
C***  CALL BAIND(CC(IC3),IA(IA4),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................EPA      DP       DQ       DM       SM.......
C*** 1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC15),
C................LBLI     BUFI.....
C*** 2           IA(IA16),CC(IC16))
C
C   FOR DEPENDENT PAIRS
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN BADEP'/)
      IC8=ICSAVE
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI
      IC13=IC12+NTRI*N3N
      IC14=IC13+NTRI*N3N
      IC15=IC14+NTRI*N3N
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................EIG     OCC     ESO     KIJ     U       T.......
C***  CALL BADEP(CC(IC1),CC(IC2),CC(IC3),IA(IA5),CC(IC8),CC(IC9),
C................B0       FF       DP       DQ       DM.......
C*** 1           CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI.....
C*** 2           IA(IA15),CC(IC15))
C
C   CALCULATE H AND E MATRICES
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN HEMAT'/)
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NBFAO*NBFAO
      IC12=IC11+NTRI
      IC13=IC12+NTRI
      IC14=IC13+NTRI*5
      IC15=IC14+NTRI*5
      IC16=IC15+NTRI*5
      IC17=IC16+NBASIS*NBASIS
      IC18=IC17+NBASIS*NBASIS
      IC19=IC18+NBASIS*NBASIS
      IA19=IC19+IC19-1
      ICMAX=IC19+MAXBF3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................ESO     NIJ     A12     U       T        HSO......
C***  CALL HEMAT(CC(IC3),IA(IA4),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................HMO      DP       DQ       DM       E11      E22......
C*** 1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC16),CC(IC17),
C................E12      LBLI     BUFI.....
C*** 2           CC(IC18),IA(IA19),CC(IC19))
C
C   FORM BF MATRICES
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN BFMAT'/)
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NTRI
      IC11=IC10+NTRI*NTYPES*3
      IC12=IC11+NBASIS*NBASIS
      ICMAX=IC12+NTRI
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................NIJ     KIJ     HF      HM       EPF      BF.......
C***  CALL BFMAT(IA(IA4),IA(IA5),CC(IC9),CC(IC10),CC(IC11),CC(IC12))
C
C   FORM BAF MATRICES
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN BAFMAT'/)
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NTRI*N3N
      IC11=IC10+NBASIS*NBASIS
      IC12=IC11+NBASIS*NBASIS
      ICMAX=IC12+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C.................SA      E11      E22      E12............
C***  CALL BAFMAT(CC(IC9),CC(IC10),CC(IC11),CC(IC12),N3NXX)
C
C   SOLVE SIMULTANEOUS EQUATIONS ITERATIVELY
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN CPHF'/)
  200 CONTINUE
      NADD=NADD/2
      MAXVC=MIN0(MAXVEC,NADD*MAXVX)
      NINDP2=NIND+2
      IC8=ICSAVE
      IC9=IC8+NIND*2
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NBFAO*NBFAO
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      IC16=IC15+NTYPEP*(NTRI*NADD)
      IC17=IC16+NTYPEP*(NTRI*NADD)
      IC18=IC17+NTYPEP*(NTRI*NADD)
      IC19=IC18+MAXVC*(MAXVC+NADD)
      IC20=IC19+NINDP2*MAXVC*2
      IA20=IC20+IC20-1
      ICMAX=IC20+MAXBF3
      WRITE(3,32) NIND,N3NXX,NADD,MAXVC,ICMAX
   32 FORMAT(2X,' NIND  = ',I6,3X,' N3NXX = ',I6,3X,' NADD = ',I6/
     1       2X,' MAXVC = ',I6,3X,' ICMAX = ',I6/)
      IF(NADD.EQ.1) GO TO 201
      IF(ICMAX.LT.MAXCOR) GO TO 201
      GO TO 200
  201 CONTINUE
      IGMAX=MAX0(ICMAX,IGMAX)
C...............ESO     NIJ     KIJ     A12     U       A.......
C***  CALL CPHF(CC(IC3),IA(IA4),IA(IA5),CC(IC8),CC(IC9),CC(IC9),
C...............T        ZETA     B0       AA       TT       DP.......
C*** 1          CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),CC(IC15),
C...............DQ       DM       W        BB       LBLI     BUFI.....
C*** 2          CC(IC16),CC(IC17),CC(IC18),CC(IC19),IA(IA20),CC(IC20),
C..................................
C*** 3          NINDP2,MAXVC,N3NXX)
C
C   CALCULATE DEPENDENT PAIRS OF U MATRICES
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN UCMAT'/)
      IC8=ICSAVE
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI*N3NXX
      IC13=IC12+NTRI*N3NXX
      IC14=IC13+NTRI*N3NXX
      IC15=IC14+NTRI
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................EIG     OCC     ESO     NIJ     KIJ     U.......
C***  CALL UCMAT(CC(IC1),CC(IC2),CC(IC3),IA(IA4),IA(IA5),CC(IC8),
C................T       B        DP       DQ       DM       DEL......
C*** 1           CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI...........
C*** 2           IA(IA15),CC(IC15),N3NXX)
C
C   COMPLETE U MATRICES
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN UXMAT'/)
      IC8=ICSAVE
      IC9=IC8+NTRI*N3NXX
      IC10=IC9+NTRI*N3NXX
      ICMAX=IC10+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................B       SM      U..............
C***  CALL UXMAT(CC(IC8),CC(IC9),CC(IC10),N3NXX)
C
C   CALCULATE WA MATRICES
      WRITE(3,35)
   35 FORMAT(//,2X,' NOW YOU ARE IN WAMAT'/)
      IC8=ICSAVE
      IC9=IC8+NBASIS*NBASIS
      IC10=IC9+NBASIS*NBASIS
      IC11=IC10+NBASIS*NBASIS
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................ESO     U       T       EPA      ZETA     WA.......
C***  CALL WAMAT(CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................SA       DP       DQ       DM       LBLI     BUFI.....
C*** 1           CC(IC13),CC(IC14),CC(IC15),CC(IC13),IA(IA16),CC(IC16))
C
C   CALCULATE SCF SECOND DERIVATIVES
      WRITE(3,36)
   36 FORMAT(//,2X,' NOW YOU ARE IN SCF2ND'/)
      IC8=ICSAVE
      IC9=IC8+N3N*N3N
      IC10=IC9+N3N*N3N
      IC11=IC10+N3N*N3N
      IC12=IC11+NTRI*N3N
      IC13=IC12+NBASIS*NBASIS*N3N
      IC14=IC13+NBASIS*NBASIS
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NBASIS*NBASIS
      IC17=IC16+NBASIS*NBASIS
      ICMAX=IC17+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C.................E2A     E2M     E2C     E2P      SA       WA.......
C***  CALL SCF2ND(CC(IC7),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................EPA      UA       E11      E22      E12......
C*** 1            CC(IC13),CC(IC14),CC(IC15),CC(IC16),CC(IC17))
C
C   CALCULATE DIPOLE MOMENT DERIVATIVES
      IF(IPOL.NE.0) GO TO 320
      WRITE(3,37)
   37 FORMAT(//,2X,' NOW YOU ARE IN DIPMO'/)
      IC8=ICSAVE
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS*3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     DIP     UA......
C***  CALL DIPMO(CC(IC2),CC(IC8),CC(IC9))
C
C   CALCULATE ELECTRIC POLARIZABILITIES
      WRITE(3,38)
   38 FORMAT(//,2X,' NOW YOU ARE IN POLAR'/)
      IC8=ICSAVE
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     HF      UF......
C***  CALL POLAR(CC(IC2),CC(IC8),CC(IC9))
  320 CONTINUE
C
      IF(IGMAX.LT.MAXCOR) GO TO 400
C
C   REQUIRED MEMORY EXCEEDS MAXCOR-----STOP !!!
      WRITE(6,3) ICMAX,MAXCOR
      WRITE(3,3) ICMAX,MAXCOR
C
  400 CONTINUE
      WRITE(3,4) NADD,MAXVC,IGMAX,MAXCOR
      WRITE(6,4) NADD,MAXVC,IGMAX,MAXCOR
C
      RETURN
      END
      SUBROUTINE DERMAT(EAO,E1A,E2A,SS,U,T,NSECMX)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E2A(N3N,N3N),SS(NBATRI)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
    1 FORMAT(//,2X,' SCF FIRST DERIVATIVE MATRIX'/)
    2 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SA (AO BASIS) MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' SA (MO BASIS) MATRIX, IABC = ',I5/)
    5 FORMAT(//,2X,' HA (AO BASIS) MATRIX, IABC = ',I5/)
    6 FORMAT(//,2X,' HA (MO BASIS) MATRIX, IABC = ',I5/)
    7 FORMAT(//,2X,' TA (AO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
    8 FORMAT(//,2X,' TA (MO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
C
      CALL RFILE(ITAP42)
      CALL RFILE(ITAP44)
      CALL RFILE(JTAPE1)
      NBSET=6
      NTREAD=NBSET
      CALL ZFILE(ITAP44,NSECMX)
C
C   READ IN SCF FIRST DERIVATIVES
      CALL SREAD(ITAP42,E1A,N3N*2)
      WRITE(6,1)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
C
C   READ IN SCF SECOND DERIVATIVES
      CALL SREAD(ITAP42,E2A,N3N*N3N*2)
      WRITE(6,2)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
C
C   READ IN S FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,3) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  201 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IS)
      IF(IPRNT.LE.4) GO TO 101
      WRITE(6,4) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  101 CONTINUE
C
C   READ IN ONE ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 102 IABC=1,N3N
      IH=IHA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,5) IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  202 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IH)
      IF(IPRNT.LE.4) GO TO 102
      WRITE(6,6) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  102 CONTINUE
C
C   READ IN TWO ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 104 ITYP=1,NBSET
      DO 103 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 203
      WRITE(6,7) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  203 CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL SWRIT(JTAPE1,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,8) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  103 CONTINUE
  104 CONTINUE
C
      CALL SREW(ITAP42)
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE MOCONV(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBFAO
      DO 101 JJ=1,NBFAO
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      T(II,JJ)=SA(IIJJ)
  101 CONTINUE
      DO 103 II=1,NBFAO
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBFAO
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBFAO
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE MOCONS(SA,NNA,SM,NNM,ESO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM SO TO MO BASIS
      DO 101 II=1,NBASIS
      DO 101 JJ=1,II
      IIJJ=IOFF(II)+JJ
      T(II,JJ)=SA(IIJJ)
      IF(II.EQ.JJ) GO TO 101
      T(JJ,II)=SA(IIJJ)
  101 CONTINUE
      DO 103 II=1,NBASIS
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBASIS
      SUM=SUM+ESO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBASIS
      SUM=SUM+ESO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE E0CAL(OCC,ESO,HSO,HMO,TM,U,T,DP,DQ,DM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),HSO(NTRI),HMO(NTRI),TM(NTRI)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION DP(NTRI,NTYPES),DQ(NTRI,NTYPES),DM(NTRI,NTYPES)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' H (SO BASIS) MATRIX'/)
    2 FORMAT(//,2X,' H (MO BASIS) MATRIX'/)
    3 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
C
C   CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(4)
C
C   READ IN ONE ELECTRON INTEGRALS (SO BASIS)
      CALL MREAD(HSO,15)
      CALL MOCONS(HSO,NTRI,HMO,NTRI,ESO,U,T)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL PRINT(HSO,NTRI,NBASIS,6)
      WRITE(6,2)
      CALL PRINT(HMO,NTRI,NBASIS,6)
C
C   CALCULATE ONE-ELCTRON ENERGY
  201 CONTINUE
      ELEC1=A00
      DO 101 I=1,JOCC
      II=IOFF(I+1)
      ELEC1=ELEC1+HMO(II)*OCC(I)
  101 CONTINUE
C
C   FORM DENSITY MATRIX IN SO BASIS
      CALL PQAMAT(DP,DQ,ESO,NTYPES)
C
C   FORM HALF-TRANSFORMED TWO ELECTRON MATRIX
      CALL PQMAT(DP,DQ,DM,NTYPES,LBLI,BUFI)
C
C   CALCULATE TWO-ELECTRON ENERGY
      ELEC2=A00
      DO 103 ITYP=1,NTYPES
      CALL MOCONS(DM(1,ITYP),NTRI,TM,NTRI,ESO,U,T)
      DO 102 I=NSTR(ITYP),NEND(ITYP)
      II=IOFF(I+1)
      ELEC2=ELEC2+TM(II)
  102 CONTINUE
  103 CONTINUE
C
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,3) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
      RETURN
      END
      SUBROUTINE PQAMAT(DP,DQ,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C***********************************************************************
C*  FOLLOWINGS ARE OLD CODES.....THEY ALSO WORK FINE
C***********************************************************************
C*    IJ=0
C*    DO 105 I=1,NBASIS
C*    DO 105 J=1,I
C*    IJ=IJ+1
C*    DO 104 ITYP=1,NTYPES
C*    SUMP=A00
C*    SUMQ=A00
C*    DO 103 JTYP=1,NTYPES
C*    DO 102 K=NSTR(JTYP),NEND(JTYP)
C*    FAC=ESO(I,K)*ESO(J,K)
C*    IF(DABS(FAC).LT.TOLDS) GO TO 102
C*    SUMP=SUMP+A(ITYP,JTYP)*FAC
C*    SUMQ=SUMQ+B(ITYP,JTYP)*FAC
C*102 CONTINUE
C*103 CONTINUE
C*    DP(IJ,ITYP)=SUMP
C*    DQ(IJ,ITYP)=SUMQ
C*104 CONTINUE
C*105 CONTINUE
C***********************************************************************
C
      CALL ZERO(DP,NTRI*NABC)
      CALL ZERO(DQ,NTRI*NABC)
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 103 K=1,JOCC
      KTYP=MOTYP(K)
      FAC=ESO(I,K)*ESO(J,K)
      IF(DABS(FAC).LT.TOLDS) GO TO 103
      DO 102 ITYP=1,NTYPES
      AVAL=A(ITYP,KTYP)*FAC
      BVAL=B(ITYP,KTYP)*FAC
      DP(IJ,ITYP)=DP(IJ,ITYP)+AVAL
      DQ(IJ,ITYP)=DQ(IJ,ITYP)+BVAL
  102 CONTINUE
  103 CONTINUE
  105 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES,NTRI,NTYPES,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES,NTRI,NTYPES,6)
C
  201 CONTINUE
      DO 107 I=1,NBASIS
      II=IOFF(I+1)
      DO 106 ITYP=1,NTYPES
      DP(II,ITYP)=DP(II,ITYP)/TWO
      DQ(II,ITYP)=DQ(II,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
      CALL SCLMPY(DP,NTYPES*NTRI,TWO)
      CALL SCLMPY(DQ,NTYPES*NTRI,TWO)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES,NTRI,NTYPES,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES,NTRI,NTYPES,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE PQMAT(DP,DQ,DM,NABC,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),DM(NTRI,NABC)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
    1 FORMAT(2X,3I5,4F15.8)
    2 FORMAT(//,2X,' DM MATRIX'/)
C
C   DP :  COULOMB DENSITY TYPE SO MATRIX
C   DQ :  EXCHANGE DENSITY TYPE SO MATRIX
C   DM :  HALF TRANSFORMED SO MATRIX
C
C   FORM DM MATRICES
      CALL ZERO(DM,NTRI*NABC)
C
      CALL RFILE(ITAP37)
  200 CONTINUE
      CALL SREAD(ITAP37,LBLI,MAXBF6)
      NBUF=MOD(LBLI(1),MAXBUF+1)
      ILSTI=MOD(LBLI(2),MAXBUF+1)
      LBLI(1)=LBLI(1)/(MAXBUF+1)
      LBLI(2)=LBLI(2)/(MAXBUF+1)
      DO 102 I=1,NBUF
      IJ=LBLI(I+I-1)
      KL=LBLI(I+I)
      P=BUFI(I+MAXBUF)
      Q=BUFI(I+MAXBUF+MAXBUF)
      CJ=P+Q
      EK=Q+Q
C     IF(IPRNT.LE.9) GO TO 201
C     WRITE(6,1) I,IJ,KL,P,Q,CJ,EK
  201 CONTINUE
      DO 101 IABC=1,NABC
      DM(IJ,IABC)=DM(IJ,IABC)+DP(KL,IABC)*CJ+DQ(KL,IABC)*EK
      DM(KL,IABC)=DM(KL,IABC)+DP(IJ,IABC)*CJ+DQ(IJ,IABC)*EK
  101 CONTINUE
  102 CONTINUE
      IF(ILSTI.EQ.0) GO TO 200
C
      CALL RCLOSE(ITAP37,3)
C
      IF (IPRNT.LE.4) GO TO 202
      WRITE(6,2)
      CALL MATOUT(DM,NTRI,NABC,NTRI,NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE EADER(OCC,D1O,D1T,D1W,E1T,SM,HM,EPS,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),SM(NTRI),HM(NTRI),EPS(NTRI)
      DIMENSION TM(NTRI,N3N,NTREAD)
      DIMENSION D1O(N3N),D1T(N3N),D1W(N3N),E1T(N3N)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/ALPA(10,10),BETA(10,10)
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' LAGRANGIAN MATRIX'/)
    2 FORMAT(//,2X,' NUCLEAR REPULSION FIRST DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SCF ONE-ELECTRON FIRST DERIVATIVE MATRIX'/)
    4 FORMAT(//,2X,' SCF TWO-ELECTRON FIRST DERIVATIVE MATRIX'/)
    5 FORMAT(//,2X,' SCF OVERLAP CONTRIBUTION MATRIX'/)
    6 FORMAT(//,2X,' TOTAL SCF FIRST DERIVATIVE MATRIX'/)
C
C   CALCULATE SCF FIRST DERIVATIVES FOR A TEST
C   SEE EQ.(9)
C
      CALL MREAD(EPS,25)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1)
      CALL PRINT(EPS,NTRI,NBASIS,6)
C
C   CALL BACK DERIVATIVE TM MATRICES
  201 CONTINUE
      CALL SREW(JTAPE1)
      DO 102 ITYP=1,NTREAD
      DO 101 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  101 CONTINUE
  102 CONTINUE
C
      CALL SREW(ITAP44)
      DO 110 IABC=1,N3N
      IS=ISA(IABC)
      IH=IHA(IABC)
      CALL RREAD(ITAP44,SM,NTRI2,IS)
      CALL RREAD(ITAP44,HM,NTRI2,IH)
C  ONE-ELECTRON CONTRIBUTION
      VALU=A00
      DO 106 I=1,JOCC
      II=IOFF(I+1)
      VALU=VALU+HM(II)*OCC(I)
  106 CONTINUE
      D1O(IABC)=VALU
C  TWO-ELECTRON CONTRIBUTION
      VALU=A00
      DO 108 ITYP=1,NTYPES
      DO 108 JTYP=1,NTYPES
      DO 107 J=NSTR(JTYP),NEND(JTYP)
      JJ=IOFF(J+1)
      VALU=VALU+ALPA(ITYP,JTYP)*TM(JJ,IABC,ITYP*2-1)
     1         +BETA(ITYP,JTYP)*TM(JJ,IABC,ITYP*2)
  107 CONTINUE
  108 CONTINUE
      D1T(IABC)=VALU
C   OVERLAP CONTRIBUTION
      VALU=A00
      DO 109 I=1,JOCC
      DO 109 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU=VALU-SM(IJ)*EPS(IJ)
  109 CONTINUE
      D1W(IABC)=VALU*TWO
C
C   SUM UP ALL CONTRIBUTIONS
      E1T(IABC)=D1O(IABC)+D1T(IABC)+D1W(IABC)
C
  110 CONTINUE
C
C     WRITE(6,2)
C     CALL MATOUT(E1N,3,NATOMS,3,NATOMS,6)
      WRITE(6,3)
      CALL MATOUT(D1O,3,NATOMS,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(D1T,3,NATOMS,3,NATOMS,6)
      WRITE(6,5)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
      WRITE(6,6)
      CALL MATOUT(E1T,3,NATOMS,3,NATOMS,6)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE HAMAT(D1E,D1T,D1W,HM,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION D1E(N3N),D1T(N3N),D1W(N3N)
      DIMENSION HM(NTRI,N3N),TM(NTRI,N3N,NTREAD)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/ALPA(10,10),BETA(10,10)
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA A00,HALF,TWO,FOUR / 0.0D+00 , 0.5D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' HA MATRICES'/)
    2 FORMAT(//,2X,I5,5F15.8)
    3 FORMAT(//,2X,' H11A MATRIX'/)
    4 FORMAT(//,2X,' H22A MATRIX'/)
    5 FORMAT(//,2X,' H12A MATRIX'/)
    6 FORMAT(//,2X,' D1N MATRIX'/)
    7 FORMAT(//,2X,' D1E MATRIX'/)
    8 FORMAT(//,2X,' D1W MATRIX'/)
    9 FORMAT(//,2X,' EGRAD MATRIX'/)
C
C   CALL BACK HM AND TM MATRICES AND FORM HA MATRICES
C   SEE EQS.(33)-(35)
C
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HM(1,IABC),NTRI2,IH)
  102 CONTINUE
C
      CALL SREW(JTAPE1)
      DO 106 ITYP=1,NTREAD
      DO 105 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  105 CONTINUE
  106 CONTINUE
C
C*    WRITE(6,1)
      DO 115 IABC=1,N3N
      ELEC1=A00
      DO 110 I=1,IOCC
      II=IOFF(I+1)
      ELEC1=ELEC1+HM(II,IABC)*TWO
  110 CONTINUE
      ELEC2=A00
      DO 111 I=1,IOCC
      II=IOFF(I+1)
      ELEC2=ELEC2+TM(II,IABC,1)*TWO-TM(II,IABC,2)
  111 CONTINUE
C
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      H11=HM(MM,IABC)*TWO+TM(MM,IABC,3)
      H22=HM(NN,IABC)*TWO+TM(NN,IABC,5)
      H12=TM(MM,IABC,6)
      DO 112 I=1,IOCC
      II=IOFF(I+1)
      H11=H11+TM(II,IABC,3)*FOUR-TM(II,IABC,4)*TWO
      H22=H22+TM(II,IABC,5)*FOUR-TM(II,IABC,6)*TWO
  112 CONTINUE
      H11A(IABC)=ELEC1+ELEC2+H11
      H22A(IABC)=ELEC1+ELEC2+H22
      H12A(IABC)=H12
      IF(IPRNT.LE.3) GO TO 115
      WRITE(6,2) IABC,ELEC1,ELEC2,H11,H22,H12
  115 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,3)
      CALL MATOUT(H11A,3,15,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(H22A,3,15,3,NATOMS,6)
      WRITE(6,5)
      CALL MATOUT(H12A,3,15,3,NATOMS,6)
C
C   CALCULATE SCF DERIVATIVES FOR A TEST
  201 C1SQ=FOCC(2)*HALF
      C2SQ=FOCC(3)*HALF
      C12P=BETA(2,3)*TWO
      C1=DSQRT(C1SQ)
      C2=DSQRT(C2SQ)
      IF(BETA(2,3).LT.A00) C2=-C2
      DO 120 IABC=1,N3N
      D1E(IABC)=H11A(IABC)*C1SQ+H22A(IABC)*C2SQ+H12A(IABC)*C12P
      D1T(IABC)=D1E(IABC)+D1W(IABC)
      E1T(IABC)=D1T(IABC)
  120 CONTINUE
      IF(IPRNT.LE.2) GO TO 202
C***  WRITE(6,6)
C***  CALL MATOUT(D1N,3,15,3,NATOMS,6)
      WRITE(6,7)
      CALL MATOUT(D1E,3,NATOMS,3,NATOMS,6)
      WRITE(6,8)
      CALL MATOUT(D1W,3,15,3,NATOMS,6)
      WRITE(6,9)
      CALL MATOUT(D1T,3,15,3,NATOMS,6)
C
  202 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE FAMAT(FF,EPA,HM,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION FF(NTRI),HM(NTRI,N3N),TM(NTRI,N3N,NTREAD)
      DIMENSION EPA(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/ALPA(10,10),BETA(10,10)
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA HALF / 0.5D+00 /
    1 FORMAT(//,2X,' EPA MATRIX, IABC = ',I5/)
C
C   CALL BACK HM AND TM MATRICES AND FORM FA MATRICES
C   SEE EQS.(15) AND (32)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
C
      DO 101 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HM(1,IABC),NTRI2,IH)
  101 CONTINUE
C
      DO 103 ITYP=1,NTREAD
      DO 102 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  102 CONTINUE
  103 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 110 IABC=1,N3N
      IE=IEA(IABC)
      DO 105 I=1,NBASIS
      ITYP=MOTYP(I)
      FAC=FOCC(ITYP)*HALF
      DO 105 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU=HM(IJ,IABC)*FAC
      DO 104 KTYP=1,NTYPES
      VALU=VALU+ALPA(ITYP,KTYP)*TM(IJ,IABC,KTYP*2-1)
     1         +BETA(ITYP,KTYP)*TM(IJ,IABC,KTYP*2)
  104 CONTINUE
      EPA(I,J)=VALU
  105 CONTINUE
C
      CALL RWRIT(ITAP44,EPA,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,1) IABC
      CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
C
C   STORE DERIVATIVE AVERAGED FOCKS
      DO 115 IABC=1,N3N
      IT=IFA(IABC)
      DO 114 I=1,NTRI
C 114 FF(I)=HM(I,IABC)+TM(I,IABC,NTREAD)
C 114 FF(I)=HM(I,IABC)+TM(I,IABC,NTREAD)
  114 FF(I)=HM(I,IABC)+TM(I,IABC,1)
      CALL RWRIT(ITAP44,FF,NTRI2,IT)
  115 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE BAIND(ESO,NIJ,U,T,ZETA,B0,EPA,DP,DQ,DM,SM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION NIJ(NTRI,2),ZETA(NTRI,NTYPEP)
      DIMENSION B0(NTRI),EPA(NBASIS,NBASIS)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,N3N,NTYPEP),SM(NTRI,N3N*NTYPEP)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' ZETA MATRIX, ITYP = ',I5/)
    2 FORMAT(//,2X,' FM MATRIX, IABC = ',I5/)
    3 FORMAT(2X,3I5,4F15.8)
    4 FORMAT(//,2X,' B0 MATRIX IN BAIND'/)
    5 FORMAT(//,2X,' DP MATRIX IN BAIND'/)
C
C   FORM B0 MATRICES
C   SEE EQ.(26)
C
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
      IF(IPRNT.LE.2) GO TO 101
      WRITE(6,1) ITYP
      CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
      CALL SREW(ITAP44)
      DO 120 IABC=1,N3N
      IS=ISA(IABC)
      IE=IEA(IABC)
      IB=IBA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      CALL ZERO(B0,NTRI)
C
C   ELEMENTS FOR INDEPENDENT PAIRS
C     IF(IPRNT.LE.4) GO TO 301
C     WRITE(6,2) IABC
C     CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  301 DO 110 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      VALU1=EPA(I,J)-EPA(J,I)
      VALU2=A00
C
      DO 107 K=2,NBASIS
      LIM=MIN0(JOCC,K-1)
      DO 107 L=1,LIM
      KL=IOFF(K)+L
      FAC2=A00
      IF(K.NE.J) GO TO 205
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      FAC2=FAC2+ZETA(IL,ITYP)-ZETA(IL,JTYP)
  205 IF(K.NE.I) GO TO 206
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      FAC2=FAC2-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  206 VALU2=VALU2-SM(KL,IABC)*FAC2
  107 CONTINUE
      VALUT=VALU1+VALU2
      B0(IJ)=VALUT
C     IF(IPRNT.LE.4) GO TO 110
C     WRITE(6,3) II,I,J,VALU1,VALU2,VALUT
  110 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      IF(IPRNT.LE.4) GO TO 120
      WRITE(6,4)
      CALL PRINT(B0,NTRI,NBASIS,6)
  120 CONTINUE
C
C   FORM DENSITY LIKE MATRIX IN SO BASIS
  302 CONTINUE
      CALL PQBMAT(DP,DQ,SM,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY MATRIX
      CALL PQMAT(DP,DQ,DM,N3N*NTYPES,LBLI,BUFI)
C
C   FORM DENSITY LIKE MATRIX IN MO BASIS
      DO 125 IABC=1,N3N
      DO 125 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  125 CONTINUE
      IF(IPRNT.LE.4) GO TO 303
      WRITE(6,5)
      CALL MATOUT(DP,NTRI,NTYPEP*N3N,NTRI,N3N*NTYPEP,6)
C
C   COMPLETE B0 MATRICES
  303 CONTINUE
      DO 130 IABC=1,N3N
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 129 II=1,NIND
      I=NIJ(II,1)
      ITYP=MOTYP(I)
      J=NIJ(II,2)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      B0(IJ)=B0(IJ)+DP(IJ,IABC,ITYP)-DP(IJ,IABC,JTYP)
  129 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
  130 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE PQBMAT(DP,DQ,SM,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION SM(NTRI,NABC*NTYPEP)
      DIMENSION ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA TWO / 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   FORM DENSITY LIKE MATRIX
C   SEE EQ.(A-2)
C
      CALL ZERO(DP,NTRI*NTYPEP*NABC)
      CALL ZERO(DQ,NTRI*NTYPEP*NABC)
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 103 K=1,JOCC
      KTYP=MOTYP(K)
      DO 103 L=1,K
      KL=IOFF(K)+L
      FAC=ESO(I,K)*ESO(J,L)+ESO(J,K)*ESO(I,L)
      IF(K.EQ.L) FAC=ESO(I,K)*ESO(J,K)
      IF(DABS(FAC).LT.TOLDS) GO TO 103
      DO 102 ITYP=1,NTYPES
      AVAL=A(ITYP,KTYP)
      BVAL=B(ITYP,KTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 101 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)-SM(KL,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)-SM(KL,IABC)*FACB
  101 CONTINUE
  102 CONTINUE
  103 CONTINUE
  105 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  201 CONTINUE
      DO 108 I=1,NBASIS
      II=IOFF(I+1)
      DO 107 ITYP=1,NTYPES
      DO 106 IABC=1,NABC
      DP(II,IABC,ITYP)=DP(II,IABC,ITYP)/TWO
      DQ(II,IABC,ITYP)=DQ(II,IABC,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
  108 CONTINUE
      CALL SCLMPY(DP,NTRI*NTYPES*NABC,TWO)
      CALL SCLMPY(DQ,NTRI*NTYPES*NABC,TWO)
C
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE BADEP(EIG,OCC,ESO,KIJ,U,T,B0,FF,DP,DQ,DM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),OCC(NBASIS),KIJ(NTRI,2)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION B0(NTRI),FF(NTRI)
      DIMENSION DP(NTRI,N3N),DQ(NTRI,N3N),DM(NTRI,N3N)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' B0 MATRIX IN BADEP'/)
    2 FORMAT(//,2X,' DM MATRIX IN BADEP'/)
C
      CALL SREW(ITAP44)
C   DM ARRAY STORES DERIVATIVE OVERLAP MATRIX
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      IT=IFA(IABC)
      IB=IBA(IABC)
      CALL RREAD(ITAP44,DM(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,FF,NTRI2,IT)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      IJ=0
      DO 101 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      B0(IJ)=FF(IJ)-EIG(J)*DM(IJ,IABC)
  101 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      IF(IPRNT.LE.4) GO TO 102
      WRITE(6,1)
      CALL PRINT(B0,NTRI,NBASIS,6)
  102 CONTINUE
C
C   FORM DENSITY LIKE MATRIX
C   DM ARRAY CONTAINES DERIVATIVE OVERLAP MATRIX
C   DP ARRAY STORES DENSITY LIKE MATRIX IN SO BASIS
      CALL PQCMAT(DP,DQ,DM,OCC,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   AND TRANSFORM THEM INTO MO BASIS
C   DP ARRAY CONTAINS DENSITY LIKE MATRIX IN SO BASIS
C   DM ARRAY STORES HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,N3N,LBLI,BUFI)
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,2)
      CALL MATOUT(DM,NTRI,N3N,NTRI,N3N,6)
C
C   COMPLETE B0 MATRICES
  302 CONTINUE
      CALL SREW(ITAP44)
      DO 105 IABC=1,N3N
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      CALL MOCONS(DM(1,IABC),NTRI,DP(1,IABC),NTRI,ESO,U,T)
      DO 104 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      B0(IJ)=B0(IJ)+DP(IJ,IABC)
  104 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
  105 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE PQCMAT(DP,DQ,SM,OCC,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),SM(NTRI,NABC)
      DIMENSION OCC(NBASIS),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA HALF,TWO / 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX IN PQCMAT'/)
    2 FORMAT(//,2X,' DQ MATRIX IN PQCMAT'/)
C
C   FORM DENSITY LIKE MATRICES
C
      CALL ZERO(DP,NTRI*NABC)
      CALL ZERO(DQ,NTRI*NABC)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 103 K=1,JOCC
      FACO=OCC(K)*HALF
      DO 103 L=1,K
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      FACV=ESO(I,K)*ESO(J,L)+ESO(J,K)*ESO(I,L)
      IF(K.EQ.L) FACV=ESO(I,K)*ESO(J,K)
      FAC=FACO*FACV
      IF(DABS(FAC).LT.TOLDS) GO TO 103
      DO 102 IABC=1,NABC
      FACT=-SM(KL,IABC)*FAC
      FAC2=FACT+FACT
      DP(IJ,IABC)=DP(IJ,IABC)+FAC2
      DQ(IJ,IABC)=DQ(IJ,IABC)-FACT
  102 CONTINUE
  103 CONTINUE
  105 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC,NTRI,NABC,6)
C
  201 CONTINUE
      DO 107 I=1,NBASIS
      II=IOFF(I+1)
      DO 106 ITYP=1,NABC
      DP(II,ITYP)=DP(II,ITYP)/TWO
      DQ(II,ITYP)=DQ(II,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
      CALL SCLMPY(DP,NABC*NTRI,TWO)
      CALL SCLMPY(DQ,NABC*NTRI,TWO)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC,NTRI,NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE HEMAT(ESO,NIJ,A12,U,T,HSO,HMO,DP,DQ,DM,E11,E22,E12,
     1                 LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION NIJ(NTRI,2),A12(NIND,2),HSO(NTRI),HMO(NTRI)
      DIMENSION DP(NTRI,5),DQ(NTRI,5),DM(NTRI,5)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      DATA A00,HALF,TWO,FOUR / 0.0D+00 , 0.5D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' H (SO BASIS) MATRIX'/)
    2 FORMAT(//,2X,' H (MO BASIS) MATRIX'/)
    3 FORMAT(//,2X,' DM MATRIX'/)
    4 FORMAT(//,2X,' DP MATRIX'/)
    5 FORMAT(//,2X,' HMAT ELEMENTS'/
     * 2X,' ELEC1C     = ',F20.10/
     * 2X,' ELEC1M     = ',F20.10/
     * 2X,' ELEC1N     = ',F20.10/
     * 2X,' ELEC2C     = ',F20.10/
     * 2X,' ELEC2M     = ',F20.10/
     * 2X,' ELEC2N     = ',F20.10/
     * 2X,' H11        = ',F20.10/
     * 2X,' H22        = ',F20.10/
     * 2X,' H12        = ',F20.10/
     * 2X,' H21        = ',F20.10/)
    6 FORMAT(//,2X,' TCSCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' C1         = ',F20.10/
     * 2X,' C2         = ',F20.10/
     * 2X,' E1         = ',F20.10/
     * 2X,' E2         = ',F20.10/
     * 2X,' C1SQ       = ',F20.10/
     * 2X,' C2SQ       = ',F20.10/
     * 2X,' C12P       = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10/)
    7 FORMAT(//,2X,' E11 MATRIX'/)
    8 FORMAT(//,2X,' E22 MATRIX'/)
    9 FORMAT(//,2X,' E12 MATRIX'/)
   10 FORMAT(//,2X,' EE MATRIX'/)
C
C   CALCULATE H MATRIX
C   SEE EQS.(5)-(7)
C
C   READ IN ONE ELECTRON INTEGRALS (SO BASIS)
      CALL MREAD(HSO,15)
      CALL MOCONS(HSO,NTRI,HMO,NTRI,ESO,U,T)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL PRINT(HSO,NTRI,NBASIS,6)
      WRITE(6,2)
      CALL PRINT(HMO,NTRI,NBASIS,6)
C
C   CALCULATE ONE-ELECTRON ENERGY
  201 CONTINUE
      ELEC1C=A00
      DO 101 I=1,IOCC
      II=IOFF(I+1)
      ELEC1C=ELEC1C+HMO(II)*TWO
  101 CONTINUE
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      ELEC1M=HMO(MM)*TWO
      ELEC1N=HMO(NN)*TWO
C
C   FORM DENSITY MATRIX IN SO BASIS
      CALL PQDMAT(DP,DQ,ESO,5)
C
C   FORM HALF-TRANSFORMED TWO ELECTRON MATRIX
      CALL PQMAT(DP,DQ,DM,5,LBLI,BUFI)
C
C   CALCULATE TWO-ELECTRON ENERGY
      DO 102 ITYP=1,5
      CALL MOCONS(DM(1,ITYP),NTRI,DP(1,ITYP),NTRI,ESO,U,T)
  102 CONTINUE
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,3)
      CALL MATOUT(DM,NTRI,5,NTRI,5,6)
      WRITE(6,4)
      CALL MATOUT(DP,NTRI,5,NTRI,5,6)
C
  202 CONTINUE
      ELEC2C=A00
      ELEC2M=A00
      ELEC2N=A00
      DO 103 I=1,IOCC
      II=IOFF(I+1)
      ELEC2C=ELEC2C+DP(II,1)
      ELEC2M=ELEC2M+DP(II,2)*TWO
      ELEC2N=ELEC2N+DP(II,3)*TWO
  103 CONTINUE
C
      ELEC2M=ELEC2M+DP(MM,4)
      ELEC2N=ELEC2N+DP(NN,5)
      H11=ELEC1C+ELEC1M+ELEC2C+ELEC2M
      H22=ELEC1C+ELEC1N+ELEC2C+ELEC2N
      H12=DP(NN,4)
      H21=DP(MM,5)
      WRITE(6,5) ELEC1C,ELEC1M,ELEC1N,ELEC2C,ELEC2M,ELEC2N,H11,H22,
     1           H12,H21
C
C   CALCULATE SCF ENERGY FOR A TEST
      B=H11+H22
      C=H11*H22-H12*H12
      SQBC=DSQRT(B*B-C*FOUR)
      E1=(B-SQBC)*HALF
      E2=(B+SQBC)*HALF
      ELEC=H11*C1SQ+H22*C2SQ+H12*C12P
      ETOT=ENUC+ELEC
      WRITE(6,6) ESCF,ENUC,C1,C2,E1,E2,C1SQ,C2SQ,C12P,ELEC,ETOT
C
C   FORM E MATRICES
C   SEE EQS.(18)-(21)
C
      CALL ZERO(E11,NBASIS*NBASIS)
      CALL ZERO(E22,NBASIS*NBASIS)
      CALL ZERO(E12,NBASIS*NBASIS)
      MN=M+IOFF(N)
      DO 105 I=1,NBASIS
      IM=IOFF(MAX0(I,M))+MIN0(I,M)
      IN=IOFF(MAX0(I,N))+MIN0(I,N)
      E12(I,M)=DP(IM,5)*HALF
      E12(I,N)=DP(IN,4)*HALF
      DO 105 K=1,JOCC
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      IF(K.EQ.N) GO TO 301
      E11(I,K)=HMO(IK)+DP(IK,1)+DP(IK,2)
  301 CONTINUE
      IF(K.EQ.M) GO TO 105
      E22(I,K)=HMO(IK)+DP(IK,1)+DP(IK,3)
  105 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 203
      WRITE(6,7)
      CALL MATOUT(E11,NBASIS,NBASIS,NBASIS,NBASIS,6)
      WRITE(6,8)
      CALL MATOUT(E22,NBASIS,NBASIS,NBASIS,NBASIS,6)
      WRITE(6,9)
      CALL MATOUT(E12,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  203 CONTINUE
      DO 106 I=1,NBASIS
      DO 106 J=1,NBASIS
      T(I,J)=E11(I,J)*C1SQ+E22(I,J)*C2SQ+E12(I,J)*C12P
  106 CONTINUE
      IF(IPRNT.LE.2) GO TO 204
      WRITE(6,10)
      CALL MATOUT(T,NBFAO,NBFAO,NBASIS,NBASIS,6)
C
C   CALCULATE A12 MATRIX ELEMENTS
  204 CONTINUE
      DO 107 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      A1=(E11(I,J)-E11(J,I))*C1+(E12(I,J)-E12(J,I))*C2
      A2=(E12(I,J)-E12(J,I))*C1+(E22(I,J)-E22(J,I))*C2
      A12(II,1)=A1*TWO
      A12(II,2)=A2*TWO
  107 CONTINUE
C
C   STORE E MATRICES IN JTAPE2
      CALL RFILE(JTAPE2)
      CALL SWRIT(JTAPE2,E11,NBASQ)
      CALL SWRIT(JTAPE2,E22,NBASQ)
      CALL SWRIT(JTAPE2,E12,NBASQ)
      CALL SREW(JTAPE2)
C
      RETURN
      END
      SUBROUTINE PQDMAT(DP,DQ,ESO,NCALS)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,5),DQ(NTRI,5),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   (1)   SUM(K)<2(IJ/KK)-(IK/JK)>
C   (2)   2(IJ/MM)-(IM/JM)
C   (3)   2(IJ/NN)-(IN/JN)
C   (4)    (IM/JM)
C   (5)    (IN/JN)
C
      M=IOCC+1
      N=IOCC+2
      IJ=0
      DO 102 I=1,NBASIS
      DO 102 J=1,I
      IJ=IJ+1
      SUMC=A00
      DO 101 K=1,IOCC
      SUMC=SUMC+ESO(I,K)*ESO(J,K)
  101 CONTINUE
      DP(IJ,1)=SUMC*TWO
      DQ(IJ,1)=-SUMC
      DENM=ESO(I,M)*ESO(J,M)
      DENN=ESO(I,N)*ESO(J,N)
      DP(IJ,2)=DENM*TWO
      DQ(IJ,2)=-DENM
      DP(IJ,3)=DENN*TWO
      DQ(IJ,3)=-DENN
      DP(IJ,4)=A00
      DQ(IJ,4)=DENM
      DP(IJ,5)=A00
      DQ(IJ,5)=DENN
  102 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,5,NTRI,5,6)
      WRITE(6,2)
      CALL MATOUT(DP,NTRI,5,NTRI,5,6)
 
  201 CONTINUE
      DO 105 I=1,NBASIS
      II=IOFF(I+1)
      DO 104 ITYP=1,5
      DP(II,ITYP)=DP(II,ITYP)/TWO
      DQ(II,ITYP)=DQ(II,ITYP)/TWO
  104 CONTINUE
  105 CONTINUE
      CALL SCLMPY(DP,NTRI*5,TWO)
      CALL SCLMPY(DQ,NTRI*5,TWO)
C
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,5,NTRI,5,6)
      WRITE(6,2)
      CALL MATOUT(DP,NTRI,5,NTRI,5,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE BFMAT(NIJ,KIJ,HF,HM,EPF,BF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION HF(NTRI),HM(NTRI,3,NTYPES),EPF(NBASIS,NBASIS),BF(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/DIPOL/DIPDA(135),DIPDM(135),DIPDN(135),DIPDT(135)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA A00,HALF,TWO / 0.0D+00 , 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DIPDA MATRIX'/)
    2 FORMAT(//,2X,' DIPDN MATRIX'/)
    3 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    4 FORMAT(//,2X,' EPF MATRIX, IXYZ = ',I5/)
C
      CALL RFILE(ITAP43)
      CALL SREAD(ITAP43,DIPDA,270)
      CALL SREAD(ITAP43,DIPDN,270)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,2)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      DO 104 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      CALL SREAD(ITAP43,HF,NTRI2)
      CALL RWRIT(ITAP44,HF,NTRI2,IH)
      DO 102 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 101 I=1,NTRI
  101 HM(I,IXYZ,ITYP)=HF(I)*FAC
  102 CONTINUE
      DPM(IXYZ)=HF(MM)
      DPN(IXYZ)=HF(NN)
      VALC=A00
      DO 103 I=1,IOCC
      II=IOFF(I+1)
      VALC=VALC+HF(II)
  103 CONTINUE
      H11F(IXYZ)=(VALC+HF(MM))*TWO
      H22F(IXYZ)=(VALC+HF(NN))*TWO
      IF(IPRNT.LE.4) GO TO 104
      WRITE(6,3) IXYZ
      CALL PRINT(HF,NTRI,NBASIS,6)
  104 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 110 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      IE=IEA(IXYZ+N3N)
      IB=IBA(IXYZ+N3N)
      CALL RREAD(ITAP44,HF,NTRI2,IH)
      CALL ZERO(EPF,NBASIS*NBASIS)
C
      DO 107 ITYP=1,NTYPES
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EPF(I,J)=HM(IJ,IXYZ,ITYP)
  106 CONTINUE
  107 CONTINUE
C
C   ELEMENTS FOR INDEPENDENT PAIRS
      DO 108 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=EPF(I,J)-EPF(J,I)
  108 CONTINUE
C
C   ELEMENTS FOR DEPENDENT PAIRS
      DO 109 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=HF(IJ)
  109 CONTINUE
C
      CALL RWRIT(ITAP44,BF,NTRI2,IB)
      CALL RWRIT(ITAP44,EPF,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,4) IXYZ
      CALL MATOUT(EPF,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
      CALL SREW(ITAP43)
      CALL SREW(ITAP44)
C
      RETURN
      END
      SUBROUTINE BAFMAT(SA,E11,E22,E12)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NTRI,N3N)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIBAF/BAF(45,2)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      COMMON/CI105/A22(2,2)
      DATA HALF,TWO,FOUR /  0.5D+00 , 2.0D+00 , 4.0D+00 /
C
C   CALCUATE BAF MATRICES
C   CALL BACK E MATRICES
      CALL SREW(JTAPE2)
      CALL SREAD(JTAPE2,E11,NBASQ)
      CALL SREAD(JTAPE2,E22,NBASQ)
      CALL SREAD(JTAPE2,E12,NBASQ)
      CALL SREW(JTAPE2)
C
C   CALL BACK SA MATRICES
      CALL SREW(ITAP44)
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
  101 CONTINUE
C
      FAC1=-C12P*C2
      FAC2= C12P*C1
      DO 105 IABC=1,N3NP3
      IF(IABC.GT.N3N) GO TO 201
      B11=-H11A(IABC)+E1T(IABC)
      B12=-H12A(IABC)
      B21=-H12A(IABC)
      B22=-H22A(IABC)+E1T(IABC)
      DO 103 I=1,JOCC
      DO 103 J=1,I
      IJ=IOFF(I)+J
      FAC4=FOUR
      IF(I.EQ.J) FAC4=TWO
      B11=B11+SA(IJ,IABC)*E11(J,I)*FAC4
      B22=B22+SA(IJ,IABC)*E22(J,I)*FAC4
      B12=B12+SA(IJ,IABC)*E12(J,I)*FAC4
      B21=B21+SA(IJ,IABC)*E12(J,I)*FAC4
  103 CONTINUE
      BA1=(B11*C1+B12*C2)*HALF
      BA2=(B21*C1+B22*C2)*HALF
      BAF(IABC,1)=BA1
      BAF(IABC,2)=BA2
      GO TO 105
  201 CONTINUE
      IXYZ=IABC-N3N
      BAF(IABC,1)=(DPM(IXYZ)-DPN(IXYZ))*FAC1
      BAF(IABC,2)=(DPM(IXYZ)-DPN(IXYZ))*FAC2
  105 CONTINUE
C
C   CALCULATE A22 MATRIX ELEMENTS
      A22(1,1)=(H11-ELEC+C1*C1)*HALF
      A22(2,2)=(H22-ELEC+C2*C2)*HALF
      A22(1,2)=(H12+C1*C2)*HALF
      A22(2,1)=A22(1,2)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE CPHF(ESO,NIJ,KIJ,A12,U,A,T,ZETA,B0,AA,TT,DP,DQ,DM,W,BB,
     1                LBLI,BUFI,NINDP2,NMAX,NADD,N3NXX)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*16 DLIM,DETERM
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION A(NBFAO,NBFAO),ZETA(NTRI,NTYPEP)
      DIMENSION BB(NINDP2,NMAX*2),W(NMAX,NMAX+NADD)
      DIMENSION DP(NTRI,NADD,NTYPEP),DQ(NTRI,NADD,NTYPEP)
      DIMENSION DM(NTRI,NADD,NTYPEP),A12(NIND,2)
      DIMENSION AA(NTRI),TT(NTRI),B0(NTRI),NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),BNORM(1024)
      COMMON/CIBAF/BAF(45,2)
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      COMMON/CI105/A22(2,2)
      DATA DLIM / 1.0Q-76 /
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' A MATRIX'/)
    2 FORMAT(//,2X,' IGROUP = ',I5,5X,' IFIRST = ',I5,5X,
     1             ' NABC   = ',I5/)
    3 FORMAT(//,2X,' BB MATRIX'/)
    4 FORMAT(//,2X,' SCALED BB MATRIX'/)
    5 FORMAT(//,2X,' ORTHONORMALIZED BB MATRIX'/)
    6 FORMAT(//,2X,' ORTHONORMALITY OF BB MATRIX'/)
    7 FORMAT(//,2X,' ITERATION NO. = ',I5/)
    8 FORMAT(//,2X,' (1-A)*B MATRIX'/)
    9 FORMAT(//,2X,' KVEC = ',I5,' K = ',I5,' TT = ',(10F10.5))
   10 FORMAT(/,2X,' NITER  = ',I5,5X,' NVEC   = ',I5,5X,' NADDV  = ',I5/
     1)
   11 FORMAT(//,2X,' NORM OF ADDED VECTOR',(5(I5,F15.8)))
   12 FORMAT(//,2X,' BB MATRIX'/)
   13 FORMAT(/,2X,' END OF ITERATION    NVTOT  = ',I5/)
   14 FORMAT(//,2X,'***NVTOT EXCEEDS NMAX***',5X,' NMAX = ',I5/)
   15 FORMAT(//,2X,' EXPANDED VECTORS BB AND A*BB MATRICES'/)
   16 FORMAT(//,2X,' W MATRIX'/)
   17 FORMAT(//,2X,' SCALED W MATRIX'/)
   18 FORMAT(//,2X,'***SIMULTANEOUS EQUATION CANNOT BE SOLVED***',
     1 5X,' DETERM = ',Q20.10/)
   19 FORMAT(/,2X,' LINEAR EQUATION HAS BEEN SOLVED',
     1 5X,' DETERM = ',Q20.10/)
   20 FORMAT(//,2X,' SOLUTION MATRIX OF SCALED W MATRIX'/)
   21 FORMAT(//,2X,' SOLUTION MATRIX'/)
   22 FORMAT(2X,I4,3X,F15.8,3X,F15.8)
C
      NIND2=NIND*2
      NINDP2=NIND+2
      NINP2=NINDP2*2
C
C   CALL BACK LAGRANGIAN AND ZETA MATRICES
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
C     CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
C   FORM A MATRIX
C   SEE EQ.(A-1)
C
      CALL ZERO(A,NBASIS*NBASIS)
      DO 104 I=1,NBASIS
      DO 104 J=1,NBASIS
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      II=IOFF(I+1)
      JJ=IOFF(J+1)
      A(I,J)=A(I,J)-ZETA(II,ITYP)+ZETA(II,JTYP)
     1             -ZETA(JJ,JTYP)+ZETA(JJ,ITYP)
  104 CONTINUE
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,1)
      CALL MATOUT(A,NBFAO,NBFAO,NBASIS,NBASIS,6)
C
C   FORM SCALING VECTOR
C   SEE EQS.(24) AND (25)
C
  301 CONTINUE
C
C:::ORBITAL SPACE:::
      DO 105 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      AA(II)=ONE/DSQRT(DABS(A(I,J)))
  105 CONTINUE
C
C:::CISPACE:::
      AA(NIND+1)=ONE/DSQRT(DABS(A22(1,1)))
      AA(NIND+2)=ONE/DSQRT(DABS(A22(2,2)))
C
C   START GROUPING THE B VECTORS
      IGROUP=0
      ILAST=0
  500 CONTINUE
      IGROUP=IGROUP+1
      IFIRST=ILAST+1
      ILAST=IFIRST+NADD-1
      IF(ILAST.GT.N3NXX) ILAST=N3NXX
      NABC=ILAST-IFIRST+1
      WRITE(6,2) IGROUP,IFIRST,NABC
      CALL SREW(JTAPE1)
C
C   CALL BACK B0 MATRICES
      CALL SREW(ITAP44)
      DO 107 IABC=1,NABC
      III=IABC+IFIRST-1
C
C:::ORBITAL SPACE:::
      IB=IBA(III)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 106 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BB(II,IABC)=B0(IJ)
  106 CONTINUE
C
C:::CI SPACE:::
      BB(NIND+1,IABC)=BAF(III,1)
      BB(NIND+2,IABC)=BAF(III,2)
  107 CONTINUE
      IF(IPRNT.LE.2) GO TO 302
      WRITE(6,3)
      CALL MATOUT(BB,NINDP2,NABC,NINDP2,NABC,6)
  302 CONTINUE
C
      DO 109 II=1,NINDP2
      DO 108 IABC=1,NABC
  108 BB(II,IABC)=BB(II,IABC)*AA(II)
  109 CONTINUE
      IF(IPRNT.LE.2) GO TO 303
      WRITE(6,4)
      CALL MATOUT(BB,NINDP2,NABC,NINDP2,NABC,6)
C
C   ORTHONORMALIZE B0 MATRIX
C
  303 CONTINUE
      CALL ORTHOG(BB,BB(1,NABC+1),NINDP2,NABC,NINDP2,NABC,NVEC)
      IF(IPRNT.LE.2) GO TO 304
      WRITE(6,5)
      CALL MATOUT(BB,NINDP2,NVEC,NINDP2,NVEC,6)
  304 CONTINUE
      DO 110 IABC=1,NVEC
  110 BNORM(IABC)=ONE
C
C   CHECK ORTHONORMALITY
      IF(IPRNT.LE.2) GO TO 305
      DO 112 I=1,NVEC
      DO 112 J=1,NVEC
      VALU=A00
      DO 111 K=1,NINDP2
  111 VALU=VALU+BB(K,I)*BB(K,J)
      W(I,J)=VALU
  112 CONTINUE
      WRITE(6,6)
      CALL MATOUT(W,NMAX,NMAX+NABC,NVEC,NVEC,6)
C
  305 NPVEC=0
      NITER=0
C:::::::::::::::::::::
C:::START ITERATION:::
C:::::::::::::::::::::
  200 NITER=NITER+1
      IF(IPRNT.GT.2)
     *WRITE(6,7) NITER
C
      DO 114 II=1,NINDP2
      DO 113 IABC=1,NVEC
  113 BB(II,NPVEC+IABC)=BB(II,NPVEC+IABC)*AA(II)
  114 CONTINUE
C
C   FORM A*B MATRIX
      CALL MAKEAB(BB(1,NPVEC+1),DP,DQ,DM,BB(1,NPVEC+NVEC+1),ZETA,
     1            NIJ,A12,ESO,U,T,LBLI,BUFI,NINDP2,NVEC)
C
      DO 116 II=1,NINDP2
      DO 115 IABC=1,NVEC
      BB(II,NPVEC+IABC)=BB(II,NPVEC+IABC)/AA(II)
      BB(II,NPVEC+NVEC+IABC)=BB(II,NPVEC+NVEC+IABC)*AA(II)
  115 CONTINUE
  116 CONTINUE
C
C   STORE A*B MATRIX
      DO 117 IABC=NPVEC+NVEC+1,NPVEC+NVEC*2
      CALL SWRIT(JTAPE1,BB(1,IABC),NINP2)
  117 CONTINUE
C
C   FORM (1-A)*B
      DO 120 IABC=NPVEC+1,NPVEC+NVEC
      DO 119 II=1,NINDP2
C*119 BB(II,NVEC+IABC)=-BB(II,IABC)-BB(II,NVEC+IABC)
  119 BB(II,NVEC+IABC)=BB(II,IABC)-BB(II,NVEC+IABC)
  120 CONTINUE
      IF(IPRNT.LE.2) GO TO 306
      WRITE(6,8)
      CALL MATOUT(BB(1,NPVEC+NVEC+1),NINDP2,NVEC,NINDP2,NVEC,6)
C
C   FORM ADDITIONAL EXPANSION VECTORS
  306 NADDV=0
      K=NPVEC+NVEC
      DO 130 IABC=NPVEC+NVEC+1,NPVEC+NVEC*2
C
      DO 121 II=1,NINDP2
  121 TT(II)=BB(II,IABC)
      DO 124 L=1,K
      CALL NORM(XBB,BB(1,L),BB(1,IABC),NINDP2)
      FAC=XBB/BNORM(L)
      DO 123 II=1,NINDP2
  123 TT(II)=TT(II)-BB(II,L)*FAC
  124 CONTINUE
      IF(IPRNT.LE.2) GO TO 307
      WRITE(6,9) IABC,K,(TT(I),I=1,NINDP2)
  307 CONTINUE
      CALL NORM(XTT,TT,TT,NINDP2)
      IF(DABS(XTT).LT.TOLCP) GO TO 130
      NADDV=NADDV+1
      K=K+1
      BNORM(K)=XTT
      DO 126 II=1,NINDP2
C*126 BB(II,K)=-TT(II)
  126 BB(II,K)=TT(II)
C
  130 CONTINUE
C
      WRITE(6,10) NITER,NVEC,NADDV
      WRITE(3,10) NITER,NVEC,NADDV
      NPVEC=NPVEC+NVEC
      NVEC=NADDV
      IF(NADDV.EQ.0) GO TO 310
      IF(IPRNT.LE.2) GO TO 308
      IST=NPVEC+1
      IED=NPVEC+NVEC
      WRITE(6,11) (I,BNORM(I),I=IST,IED)
      WRITE(6,12)
      CALL MATOUT(BB(1,NPVEC+1),NINDP2,NVEC,NINDP2,NVEC,6)
  308 GO TO 200
C
  310 NVTOT=NPVEC
      WRITE(6,13) NVTOT
      WRITE(3,13) NVTOT
      IF(NVTOT.LE.NMAX) GO TO 400
      WRITE(6,14) NMAX
      STOP
C
C   CALL BACK A*B MATRICES
  400 CONTINUE
      CALL SREW(JTAPE1)
      DO 131 IABC=NVTOT+1,NVTOT*2
  131 CALL SREAD(JTAPE1,BB(1,IABC),NINP2)
      IF(IPRNT.LE.2) GO TO 311
      WRITE(6,15)
      CALL MATOUT(BB,NINDP2,NVTOT*2,NINDP2,NVTOT*2,6)
C
  311 CONTINUE
      DO 133 I=1,NVTOT
      DO 133 J=1,NVTOT
      SUM=A00
      DO 132 II=1,NINDP2
C*132 SUM=SUM-BB(II,I)*BB(II,NVTOT+J)
  132 SUM=SUM+BB(II,I)*BB(II,NVTOT+J)
      W(I,J)=SUM
  133 CONTINUE
C
C   READ BACK ORIGINAL B0 MATRICES
      CALL SREW(ITAP44)
      DO 135 IABC=1,NABC
      III=IABC+IFIRST-1
      IB=IBA(III)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 134 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BB(II,NVTOT+IABC)=B0(IJ)*AA(II)
  134 CONTINUE
      BB(NIND+1,NVTOT+IABC)=BAF(III,1)*AA(NIND+1)
      BB(NIND+2,NVTOT+IABC)=BAF(III,2)*AA(NIND+2)
  135 CONTINUE
      IF(IPRNT.LE.2) GO TO 312
      WRITE(6,4)
      CALL MATOUT(BB(1,NVTOT+1),NINDP2,NABC,NINDP2,NABC,6)
C
C   SET B0*B ON W
  312 CONTINUE
      DO 137 I=1,NVTOT
      DO 137 J=1,NABC
      SUM=A00
      DO 136 II=1,NINDP2
  136 SUM=SUM+BB(II,I)*BB(II,NVTOT+J)
      W(I,NVTOT+J)=SUM
  137 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 313
      WRITE(6,16)
      CALL MATOUT(W,NMAX,NMAX+NABC,NVTOT,NVTOT+NABC,6)
  313 CONTINUE
      DO 138 I=1,NVTOT
      AVAL=ONE/DSQRT(BNORM(I))
      DO 138 J=1,NVTOT
      BVAL=ONE/DSQRT(BNORM(J))
      W(I,J)=W(I,J)*AVAL*BVAL
  138 CONTINUE
      DO 139 I=1,NVTOT
      AVAL=ONE/DSQRT(BNORM(I))
      DO 139 J=1,NABC
      W(I,NVTOT+J)=W(I,NVTOT+J)*AVAL
  139 CONTINUE
      IF(IPRNT.LE.3) GO TO 315
      WRITE(6,17)
      CALL MATOUT(W,NMAX,NMAX+NABC,NVTOT,NVTOT+NABC,6)
C
C   SOLVE SMALL SIMULTANEOUS EQUATION
  315 CONTINUE
      CALL FLINQ(W,NMAX,NVTOT,NABC,DETERM)
      IF(QABS(DETERM).GT.DLIM) GO TO 320
C
C   SIMULTANEOUS EQUATION CANNOT BE SOLVED---STOP!!!
      WRITE(6,18) DETERM
      STOP
C
C::::::::::::::::::::::::::::::::
C:::LINEAR EQUATION WAS SOLVED:::
C::::::::::::::::::::::::::::::::
  320 WRITE(6,19) DETERM
      IF(IPRNT.LE.2) GO TO 322
      WRITE(6,20)
      CALL MATOUT(W(1,NVTOT+1),NMAX,NMAX+NABC,NVTOT,NABC,6)
  322 CONTINUE
      DO 151 I=1,NVTOT
      AVAL=ONE/DSQRT(BNORM(I))
      DO 151 J=1,NABC
      W(I,NVTOT+J)=W(I,NVTOT+J)*AVAL
  151 CONTINUE
      IF(IPRNT.LE.4) GO TO 323
      WRITE(6,21)
      CALL MATOUT(W(1,NVTOT+1),NMAX,NMAX+NABC,NVTOT,NABC,6)
C
C   OBTAIN INDEPENDENT ELEMENTS OF UA MATRICES
  323 CONTINUE
      DO 153 II=1,NINDP2
      DO 153 IABC=1,NABC
      SUM=A00
      DO 152 I=1,NVTOT
C*152 SUM=SUM-BB(II,I)*W(I,NVTOT+IABC)
  152 SUM=SUM+BB(II,I)*W(I,NVTOT+IABC)
      BB(II,NVTOT+IABC)=SUM*AA(II)
  153 CONTINUE
C
      CALL SREW(ITAP44)
      DO 155 IABC=1,NABC
      III=IABC+IFIRST-1
C
C:::ORBITAL SPACE:::
      IB=IBA(III)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 154 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      B0(IJ)=BB(II,NVTOT+IABC)
  154 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
C
C:::CI SPACE:::
      C1A(III)=BB(NIND+1,NVTOT+IABC)
      C2A(III)=BB(NIND+2,NVTOT+IABC)
      IF(IPRNT.LE.2) GO TO 155
      WRITE(6,22) III,C1A(III),C2A(III)
  155 CONTINUE
C
      IF(ILAST.GE.N3NXX) GO TO 501
      GO TO 500
C
  501 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE MAKEAB(B0,DP,DQ,DM,B1,ZETA,NIJ,A12,ESO,U,T,LBLI,BUFI,
     1                  NINDP2,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION B0(NINDP2,NABC),B1(NINDP2,NABC)
      DIMENSION ZETA(NTRI,NTYPEP),NIJ(NTRI,2),A12(NIND,2)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION DM(NTRI,NABC,NTYPEP)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI102/NIND,NDEP
      COMMON/CI105/A22(2,2)
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' B1 MATRIX'/)
    2 FORMAT(//,2X,' FINAL B1 MATRIX'/)
C
C   SEE EQ.(21)
C   FORM DENSITY LIKE MATRIX IN SO BASIS
C
      CALL PQEMAT(DP,DQ,B0,NIJ,ESO,NINDP2,NABC)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,NTYPES*NABC,LBLI,BUFI)
C
C   COMPLETE TRANSFORMATIONTION
      DO 101 IABC=1,NABC
      DO 101 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  101 CONTINUE
C
      CALL ZERO(B1,NINDP2*NABC)
C
      DO 104 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      DO 103 IABC=1,NABC
  103 B1(II,IABC)=DP(IJ,IABC,ITYP)-DP(IJ,IABC,JTYP)
  104 CONTINUE
      IF(IPRNT.LE.3) GO TO 301
      WRITE(6,1)
      CALL MATOUT(B1,NINDP2,NABC,NINDP2,NABC,6)
C
C   SEE EQ.(12)
C   REST OF EQ.(11)
C
  301 CONTINUE
      DO 110 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      DO 106 JJ=1,NIND
      K=NIJ(JJ,1)
      L=NIJ(JJ,2)
      KTYP=MOTYP(K)
      LTYP=MOTYP(L)
      ZVAL1=A00
      ZVAL2=A00
      ZVAL3=A00
      ZVAL4=A00
      IF(J.NE.K) GO TO 201
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      ZVAL1=ZETA(IL,ITYP)-ZETA(IL,JTYP)
  201 IF(I.NE.K) GO TO 202
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      ZVAL2=-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  202 IF(J.NE.L) GO TO 203
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      ZVAL3=-ZETA(IK,ITYP)+ZETA(IK,JTYP)
  203 IF(I.NE.L) GO TO 204
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      ZVAL4=ZETA(JK,JTYP)-ZETA(JK,ITYP)
  204 ZVALT=ZVAL1+ZVAL2+ZVAL3+ZVAL4
C
      DO 105 IABC=1,NABC
      B1(II,IABC)=B1(II,IABC)+B0(JJ,IABC)*ZVALT
  105 CONTINUE
  106 CONTINUE
C
      DO 108 JJ=1,2
      DO 107 IABC=1,NABC
      B1(II,IABC)=B1(II,IABC)+B0(JJ+NIND,IABC)*A12(II,JJ)
  107 CONTINUE
  108 CONTINUE
C
  110 CONTINUE
C
      DO 120 II=1,2
      DO 115 JJ=1,NIND
      DO 114 IABC=1,NABC
      B1(II+NIND,IABC)=B1(II+NIND,IABC)+B0(JJ,IABC)*A12(JJ,II)
  114 CONTINUE
  115 CONTINUE
C
      DO 117 JJ=1,2
      DO 116 IABC=1,NABC
      B1(II+NIND,IABC)=B1(II+NIND,IABC)+B0(JJ+NIND,IABC)*A22(II,JJ)
  116 CONTINUE
  117 CONTINUE
C
  120 CONTINUE
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,2)
      CALL MATOUT(B1,NINDP2,NABC,NINDP2,NABC,6)
C
  302 CONTINUE
      RETURN
      END
      SUBROUTINE PQEMAT(DP,DQ,B0,NIJ,ESO,NINDP2,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION B0(NINDP2,NABC),NIJ(NTRI,2),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/CI102/NIND,NDEP
      DATA TWO / 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX IN PQDMAT'/)
    2 FORMAT(//,2X,' DQ MATRIX IN PQDMAT'/)
C
C   SEE EQS.(22) AND (23)
C
      CALL ZERO(DP,NTRI*NTYPEP*NABC)
      CALL ZERO(DQ,NTRI*NTYPEP*NABC)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 103 II=1,NIND
      K=NIJ(II,1)
      L=NIJ(II,2)
      KTYP=MOTYP(K)
      LTYP=MOTYP(L)
      FAC=ESO(I,K)*ESO(J,L)+ESO(I,L)*ESO(J,K)
      IF(DABS(FAC).LT.TOLDS) GO TO 103
      DO 102 ITYP=1,NTYPES
      AVAL=A(ITYP,KTYP)-A(ITYP,LTYP)
      BVAL=B(ITYP,KTYP)-B(ITYP,LTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 101 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)+B0(II,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)+B0(II,IABC)*FACB
  101 CONTINUE
  102 CONTINUE
  103 CONTINUE
  105 CONTINUE
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  201 CONTINUE
      DO 108 I=1,NBASIS
      II=IOFF(I+1)
      DO 107 ITYP=1,NTYPES
      DO 106 IABC=1,NABC
      DP(II,IABC,ITYP)=DP(II,IABC,ITYP)/TWO
      DQ(II,IABC,ITYP)=DQ(II,IABC,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
  108 CONTINUE
      CALL SCLMPY(DP,NTRI*NTYPES*NABC,TWO)
      CALL SCLMPY(DQ,NTRI*NTYPES*NABC,TWO)
C
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE UCMAT(EIG,OCC,ESO,NIJ,KIJ,U,T,B,DP,DQ,DM,DEL,LBLI,BUFI,
     1                 NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),OCC(NBASIS)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION B(NTRI),DP(NTRI,NABC),DQ(NTRI,NABC),DM(NTRI,NABC)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3),DEL(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/CI102/NIND,NDEP
      DATA DLIMIT / 1.0D-8 /
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' B0 MATRIX'/)
    2 FORMAT(//,2X,' DP MATRIX'/)
    3 FORMAT(//,2X,' DM MATRIX'/)
C
C   CALL BACK B0 MATRICES
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,DM(1,IABC),NTRI2,IB)
  101 CONTINUE
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1)
      CALL MATOUT(DM,NTRI,NABC,NTRI,NABC,6)
C
C   FORM DENSITY LIKE MATRIX IN SO BASIS
  301 CONTINUE
      CALL PQFMAT(DP,DQ,DM,NIJ,OCC,ESO,NABC)
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,2)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   AND TRANSFORM THEM INTO MO BASIS
  302 CONTINUE
      CALL PQMAT(DP,DQ,DM,NABC,LBLI,BUFI)
      DO 102 IABC=1,NABC
      CALL MOCONS(DM(1,IABC),NTRI,DP(1,IABC),NTRI,ESO,U,T)
  102 CONTINUE
      IF(IPRNT.LE.4) GO TO 303
      WRITE(6,3)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
C
  303 CONTINUE
      DO 103 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      DEL(II)=A00
      DELJI=EIG(J)-EIG(I)
      IF(I.EQ.J) DELJI=ONE
      IF(DABS(DELJI).LE.DLIMIT) GO TO 103
      DEL(II)=ONE/DELJI
  103 CONTINUE
C
      DO 105 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B,NTRI2,IB)
      DO 104 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      TDEL=DEL(II)
      B(IJ)=(B(IJ)+DP(IJ,IABC))*TDEL
  104 CONTINUE
      CALL RWRIT(ITAP44,B,NTRI2,IB)
  105 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE PQFMAT(DP,DQ,B,NIJ,OCC,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),B(NTRI,NABC)
      DIMENSION OCC(NBASIS),ESO(NBFAO,NBASIS),NIJ(NTRI,2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/CI102/NIND,NDEP
      DATA HALF,TWO / 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   FORM DENSITY MATRICES
C
C   FORM DP MATRIX
      CALL ZERO(DP,NTRI*NABC)
      CALL ZERO(DQ,NTRI*NABC)
C
      IJ=0
      DO 103 I=1,NBASIS
      DO 103 J=1,I
      IJ=IJ+1
      DO 102 II=1,NIND
      K=NIJ(II,1)
      L=NIJ(II,2)
      KL=IOFF(K)+L
      FAC=(ESO(I,K)*ESO(J,L)+ESO(I,L)*ESO(J,K))*(OCC(L)-OCC(K))*HALF
      IF(DABS(FAC).LT.TOLDS) GO TO 102
      DO 101 IABC=1,NABC
      FACT=B(KL,IABC)*FAC
      FAC2=FACT+FACT
      DP(IJ,IABC)=DP(IJ,IABC)+FAC2
      DQ(IJ,IABC)=DQ(IJ,IABC)-FACT
  101 CONTINUE
  102 CONTINUE
  103 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC,NTRI,NABC,6)
C
  201 CONTINUE
      DO 107 I=1,NBASIS
      II=IOFF(I+1)
      DO 106 ITYP=1,NABC
      DP(II,ITYP)=DP(II,ITYP)/TWO
      DQ(II,ITYP)=DQ(II,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
      CALL SCLMPY(DP,NABC*NTRI,TWO)
      CALL SCLMPY(DQ,NABC*NTRI,TWO)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC,NTRI,NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE ORTHOG(U,W,NDIM,MDIM,N,NTEMP,NVEC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NDUM(360)
      DIMENSION U(NDIM,MDIM),W(NDIM)
      DATA A00 / 0.0D+00 /
C
      DO 101 I=1,NTEMP
  101 NDUM(I)=0
C   N=1
      DO 102 I=1,N
  102 W(I)=U(I,1)
      CALL NORMLZ(W,NDIM,N,IFLAG)
      DO 103 I=1,N
  103 U(I,1)=W(I)
C
C   N=>2
C
      NVEC=1
      IF(NTEMP.EQ.1) RETURN
      DO 110 II=2,NTEMP
      DO 104 I=1,N
  104 W(I)=U(I,II)
      DO 107 K=1,II-1
      IF(NDUM(K).NE.0) GO TO 107
      EU=A00
      DO 105 I=1,N
  105 EU=EU+U(I,K)*U(I,II)
      DO 106 I=1,N
  106 W(I)=W(I)-EU*U(I,K)
  107 CONTINUE
      CALL NORMLZ(W,NDIM,N,IFLAG)
      IF(IFLAG.NE.0) NDUM(II)=1
      IF(IFLAG.NE.0) GO TO 110
      NVEC=NVEC+1
      DO 108 I=1,N
  108 U(I,II)=W(I)
  110 CONTINUE
C
      IX=0
      DO 120 II=1,NTEMP
      IF(NDUM(II).NE.0) GO TO 120
      IX=IX+1
      DO 115 I=1,N
  115 W(I)=U(I,II)
      DO 116 I=1,N
  116 U(I,IX)=W(I)
  120 CONTINUE
C
      RETURN
      END
      SUBROUTINE NORMLZ(V,NDIM,N,IFLAG)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION V(NDIM)
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
    1 FORMAT(//,2X,'***ERROR IN NORMALIZATION***'/)
      DATA RLIM / 1.0D-38 /
      DATA A00 / 0.0D+00 /
C
      IFLAG=0
      SQL=A00
      DO 101 I=1,N
  101 SQL=SQL+V(I)*V(I)
      R=DSQRT(SQL)
      IF(R.LE.RLIM) GO TO 202
      IF(R.LE.TOLNR) GO TO 204
C
  201 DO 102 I=1,N
  102 V(I)=V(I)/R
      GO TO 205
  202 WRITE(6,1)
      STOP
C
  204 IFLAG=1
  205 RETURN
      END
      SUBROUTINE NORM(XAB,A,B,N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(1),B(1)
      DATA A00 / 0.0D+00 /
C
      XAB=A00
      DO 101 I=1,N
  101 XAB=XAB+A(I)*B(I)
      RETURN
      END
      SUBROUTINE UXMAT(B,SM,U,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION B(NTRI,NABC),SM(NTRI,NABC),U(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      DATA A00,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' U MATRIX, IABC= ',I5/)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B(1,IABC),NTRI2,IB)
      IF(IABC.GT.N3N) GO TO 101
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
  101 CONTINUE
C
C   FILL UP THE REST OF U MATRIX
      DO 105 IABC=1,NABC
      IU=IUA(IABC)
      IF(IABC.GT.N3N) GO TO 301
C
      IJ=0
      DO 102 I=1,NBASIS
      DO 102 J=1,I
      IJ=IJ+1
      IF(I.EQ.J) GO TO 201
      U(I,J)=B(IJ,IABC)
      U(J,I)=-U(I,J)-SM(IJ,IABC)
      GO TO 102
  201 U(I,I)=-SM(IJ,IABC)*HALF
  102 CONTINUE
      GO TO 302
C
  301 CONTINUE
      IJ=0
      DO 103 I=1,NBASIS
      DO 103 J=1,I
      IJ=IJ+1
      IF(I.EQ.J) GO TO 202
      U(I,J)=B(IJ,IABC)
      U(J,I)=-U(I,J)
      GO TO 103
  202 U(I,I)=A00
  103 CONTINUE
C
  302 CONTINUE
      CALL RWRIT(ITAP44,U,NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 105
      WRITE(6,1) IABC
      CALL MATOUT(U,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  105 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE WAMAT(ESO,U,T,EPA,ZETA,WA,SA,DP,DQ,DM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ESO(NBFAO,NBASIS),U(NBASIS,NBASIS),T(NBASIS,NBASIS)
      DIMENSION EPA(NBASIS,NBASIS)
      DIMENSION ZETA(NTRI,NTYPEP),WA(NBASIS,NBASIS),SA(NTRI,N3N*NTYPEP)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,N3N,NTYPEP)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' WA MATRIX, IABC = ',I5/)
C
C   FORM WA MATRICES
C   SEE EQ.(13)
C
C   CALL BACK LAGRANGIAN MATRICES
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
C   CALL BACK DERIVATIVE OVERLAP INTEGRALS
      CALL SREW(ITAP44)
      DO 103 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
  103 CONTINUE
C
C   FORM DENSITY LIKE MATRIX
      CALL PQGMAT(DP,DQ,SA,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,NTYPES*N3N,LBLI,BUFI)
C
C   COMPLETE TRANSFORMATION
      DO 105 IABC=1,N3N
      DO 105 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  105 CONTINUE
C
      CALL SREW(JTAPE1)
      DO 110 IABC=1,N3N
      IS=ISA(IABC)
      IE=IEA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      DO 107 I=1,NBASIS
      ITYP=MOTYP(I)
      DO 107 J=1,NBASIS
      JTYP=MOTYP(J)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU1=EPA(J,I)+EPA(J,I)
      VALU2=DP(IJ,IABC,JTYP)
      VALU3=A00
      DO 106 K=1,JOCC
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      FAC1=ZETA(IK,ITYP)+ZETA(IK,JTYP)
      FAC2=ZETA(JK,JTYP)+ZETA(JK,JTYP)
      VALU3=VALU3+SA(JK,IABC)*FAC1+SA(IK,IABC)*FAC2
  106 CONTINUE
      WA(I,J)=VALU1-VALU2-VALU3
  107 CONTINUE
      CALL SWRIT(JTAPE1,WA,NBASQ)
      IF(IPRNT.LE.3) GO TO 110
      WRITE(6,1) IABC
      CALL MATOUT(WA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE PQGMAT(DP,DQ,SA,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION ESO(NBFAO,NBASIS),SA(NTRI,NABC*NTYPEP)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TOLER/TOLNR,TOLDS,TOLCP
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA TWO / 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX IN PQGMAT'/)
    2 FORMAT(//,2X,' DQ MATRIX IN PQGMAT'/)
C
      CALL ZERO(DP,NTRI*NTYPEP*NABC)
      CALL ZERO(DQ,NTRI*NTYPEP*NABC)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 104 K=1,JOCC
      KTYP=MOTYP(K)
      DO 104 L=1,JOCC
      LTYP=MOTYP(L)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      FAC=ESO(I,K)*ESO(J,L)+ESO(J,K)*ESO(I,L)
      IF(DABS(FAC).LT.TOLDS) GO TO 104
      DO 103 ITYP=1,NTYPES
      AVAL=A(ITYP,LTYP)
      BVAL=B(ITYP,LTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 102 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)+SA(KL,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)+SA(KL,IABC)*FACB
  102 CONTINUE
  103 CONTINUE
  104 CONTINUE
  105 CONTINUE
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  201 CONTINUE
      DO 108 I=1,NBASIS
      II=IOFF(I+1)
      DO 107 ITYP=1,NTYPES
      DO 106 IABC=1,NABC
      DP(II,IABC,ITYP)=DP(II,IABC,ITYP)/TWO
      DQ(II,IABC,ITYP)=DQ(II,IABC,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
  108 CONTINUE
      CALL SCLMPY(DP,NTRI*NTYPES*NABC,TWO)
      CALL SCLMPY(DQ,NTRI*NTYPES*NABC,TWO)
C
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE SCF2ND(E2A,E2M,E2C,E2P,SA,WA,EPA,UA,E11,E22,E12)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E2A(N3N,N3N),E2M(N3N,N3N),E2C(N3N,N3N),E2P(N3N,N3N)
      DIMENSION SA(NTRI,N3N),WA(NBASIS,NBASIS,N3N)
      DIMENSION EPA(NBASIS,NBASIS),UA(NBASIS,NBASIS)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' WA MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' E2P MATRIX'/)
    3 FORMAT(//,2X,' E2M MATRIX'/)
    4 FORMAT(///
     1 2X,':::::::::::::::::::::::::::::::::::::::::::::'/
     2 2X,':::SUMMARY OF FIRST ORDER CPHF CALCULATION:::'/
     3 2X,':::::::::::::::::::::::::::::::::::::::::::::'/)
    5 FORMAT(//,2X,' E2A MATRIX'/)
    6 FORMAT(//,2X,' E2M MATRIX'/)
    7 FORMAT(//,2X,' E2C MATRIX'/)
    8 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    9 FORMAT(2I5)
   10 FORMAT(3F20.10)
C
C   CALCULATE SCF SECOND DERIVATIVES
C   SEE EQ.(12)
C
C   CALL BACK SA MATRICES
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
      CALL SREAD(JTAPE1,WA(1,1,IABC),NBASQ)
CCC   WRITE(6,1) IABC
CCC   CALL MATOUT(WA(1,1,IABC),NBASIS,NBASIS,NBASIS,NBASIS,6)
  101 CONTINUE
C
C   (B COORDINATE)
      DO 110 IABC=1,N3N
      IE=IEA(IABC)
      IU=IUA(IABC)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      CALL RREAD(ITAP44,UA,NBASQ,IU)
C
C   (A COORDINATE)
      DO 105 JABC=1,N3N
      VALUP=A00
      VALUM=A00
C
      DO 104 I=1,JOCC
      DO 102 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-SA(IJ,JABC)*EPA(I,J)
  102 CONTINUE
      DO 103 J=1,NBASIS
      VALUM=VALUM+WA(J,I,JABC)*UA(J,I)
  103 CONTINUE
  104 CONTINUE
      E2M(IABC,JABC)=VALUM*TWO
      E2P(IABC,JABC)=VALUP*TWO
  105 CONTINUE
  110 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,2)
      CALL MATOUT(E2P,N3N,N3N,N3N,N3N,6)
      WRITE(6,3)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
C*******************************************
C***SUM UP CONTRIBUTIONS DUE TO MO CHANGE***
C*******************************************
  201 CONTINUE
      DO 115 I=1,N3N
      DO 115 J=1,N3N
  115 E2M(I,J)=E2M(I,J)+E2P(I,J)
C
C***********************************************
C***CONTRIBUTION DUE TO CI COEFFICIENT CHANGE***
C***********************************************
      CALL SREW(JTAPE2)
      CALL SREAD(JTAPE2,E11,NBASQ)
      CALL SREAD(JTAPE2,E22,NBASQ)
      CALL SREAD(JTAPE2,E12,NBASQ)
      DO 120 IABC=1,N3N
      DO 120 JABC=1,N3N
      VALU11=H11A(JABC)
      VALU22=H22A(JABC)
      VALU12=H12A(JABC)
      DO 119 I=1,JOCC
      DO 119 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU11=VALU11-SA(IJ,JABC)*E11(I,J)*TWO
      VALU22=VALU22-SA(IJ,JABC)*E22(I,J)*TWO
      VALU12=VALU12-SA(IJ,JABC)*E12(I,J)*TWO
  119 CONTINUE
      E2C(IABC,JABC)=(C1A(IABC)*C1*VALU11+C1A(IABC)*C2*VALU12
     1               +C2A(IABC)*C1*VALU12+C2A(IABC)*C2*VALU22)*TWO
  120 CONTINUE
CCC   DO 120 IABC=1,N3N
CCC   DO 120 JABC=1,N3N
CCC   E2C(I,J)=-(C1A(IABC)*C1A(JABC))*(H11-ELEC)*TWO
CCC  1         -(C2A(IABC)*C2A(JABC))*(H22-ELEC)*TWO
CCC  2         -(C1A(IABC)*C2A(JABC)+C1A(JABC)*C2A(IABC))*H12*TWO
CC120 CONTINUE
C
      WRITE(6,4)
      WRITE(6,5)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
      WRITE(6,6)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
      WRITE(6,7)
      CALL MATOUT(E2C,N3N,N3N,N3N,N3N,6)
      DO 121 I=1,N3N
      DO 121 J=1,N3N
  121 E2M(I,J)=E2A(I,J)+E2M(I,J)+E2C(I,J)
      WRITE(6,8)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
      ITAP15=15
      REWIND ITAP15
      N6N=N3N*2
      WRITE(ITAP15,9) NATOMS,N6N
      WRITE(ITAP15,10) ((E2M(I,J),J=1,N3N),I=1,N3N)
      REWIND ITAP15
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      CALL SREW(JTAPE2)
      RETURN
      END
      SUBROUTINE DIPMO(OCC,DIP,UA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),DIP(NTRI,3),UA(NBASIS,NBASIS,3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),DIPDE(3,45),DIPDC(3,45),DUM(754)
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA HALF,ONE,TWO,FOUR / 0.5D+00 , 1.0D+00 , 2.0D+00 , 4.0D+00 /
      DATA DEBYE,BOHR / 2.541765480D+00 , 0.52917706D+00 /
    1 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS'/)
    2 FORMAT(//,2X,' UA MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    3 FORMAT(//,2X,' DIPDA MATRIX'/)
    4 FORMAT(//,2X,' DIPDM MATRIX'/)
    5 FORMAT(//,2X,' DIPDC MATRIX'/)
    6 FORMAT(//,2X,' DIPDE MATRIX'/)
    7 FORMAT(//,2X,' DIPDN MATRIX'/)
    8 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/BOHR)
     *'/)
    9 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/A)'/)
   10 FORMAT(2I5)
   11 FORMAT(3F20.10)
C
      CALL SREW(ITAP44)
      DEB4=DEBYE*FOUR
      DEB2=DEBYE*TWO
      RBOHR=ONE/BOHR
C     WRITE(6,1)
C
      CALL ZERO(DIPDM,N3N*3)
      CALL ZERO(DIPDC,N3N*3)
C
      DO 102 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,DIP(1,IXYZ),NTRI2,IH)
  102 CONTINUE
C
      KABC=0
      DO 105 IABC=1,NATOMS
      I00=(IABC-1)*3
      IXX=I00+1
      IYY=I00+2
      IZZ=I00+3
      DO 103 IXYZ=1,3
      KABC=KABC+1
      IU=IUA(KABC)
      CALL RREAD(ITAP44,UA(1,1,IXYZ),NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 103
      WRITE(6,2) IABC,IXYZ
      CALL MATOUT(UA(1,1,IXYZ),NBASIS,NBASIS,NBASIS,NBASIS,6)
  103 CONTINUE
C
      DO 104 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 104 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      DIPDM(1,IXX)=DIPDM(1,IXX)+UA(J,I,1)*DIP(IJ,1)*FAC
      DIPDM(1,IYY)=DIPDM(1,IYY)+UA(J,I,2)*DIP(IJ,1)*FAC
      DIPDM(1,IZZ)=DIPDM(1,IZZ)+UA(J,I,3)*DIP(IJ,1)*FAC
      DIPDM(2,IXX)=DIPDM(2,IXX)+UA(J,I,1)*DIP(IJ,2)*FAC
      DIPDM(2,IYY)=DIPDM(2,IYY)+UA(J,I,2)*DIP(IJ,2)*FAC
      DIPDM(2,IZZ)=DIPDM(2,IZZ)+UA(J,I,3)*DIP(IJ,2)*FAC
      DIPDM(3,IXX)=DIPDM(3,IXX)+UA(J,I,1)*DIP(IJ,3)*FAC
      DIPDM(3,IYY)=DIPDM(3,IYY)+UA(J,I,2)*DIP(IJ,3)*FAC
      DIPDM(3,IZZ)=DIPDM(3,IZZ)+UA(J,I,3)*DIP(IJ,3)*FAC
  104 CONTINUE
      DIPDC(1,IXX)=C1A(IXX)*H11F(1)*C1+C2A(IXX)*H22F(1)*C2
      DIPDC(1,IYY)=C1A(IYY)*H11F(1)*C1+C2A(IYY)*H22F(1)*C2
      DIPDC(1,IZZ)=C1A(IZZ)*H11F(1)*C1+C2A(IZZ)*H22F(1)*C2
      DIPDC(2,IXX)=C1A(IXX)*H11F(2)*C1+C2A(IXX)*H22F(2)*C2
      DIPDC(2,IYY)=C1A(IYY)*H11F(2)*C1+C2A(IYY)*H22F(2)*C2
      DIPDC(2,IZZ)=C1A(IZZ)*H11F(2)*C1+C2A(IZZ)*H22F(2)*C2
      DIPDC(3,IXX)=C1A(IXX)*H11F(3)*C1+C2A(IXX)*H22F(3)*C2
      DIPDC(3,IYY)=C1A(IYY)*H11F(3)*C1+C2A(IYY)*H22F(3)*C2
      DIPDC(3,IZZ)=C1A(IZZ)*H11F(3)*C1+C2A(IZZ)*H22F(3)*C2
  105 CONTINUE
C
      DO 106 I=1,3
      DO 106 J=1,N3N
      DIPDM(I,J)=-DIPDM(I,J)*DEB4
      DIPDC(I,J)=-DIPDC(I,J)*DEB2
  106 CONTINUE
C
C   SUM UP CONTRIBUTIONS FROM MO CHANGE
      DO 107 I=1,3
      DO 107 J=1,N3N
      DIPDE(I,J)=DIPDA(I,J)+DIPDM(I,J)+DIPDC(I,J)
  107 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,3)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,4)
      CALL MATOUT(DIPDM,3,45,3,N3N,6)
      WRITE(6,5)
      CALL MATOUT(DIPDC,3,45,3,N3N,6)
C
C   SUM UP ALL CONTRIBUTIONS
  201 CONTINUE
      DO 108 I=1,3
      DO 108 J=1,N3N
      DIPDT(I,J)=DIPDE(I,J)+DIPDN(I,J)
  108 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,6)
      CALL MATOUT(DIPDE,3,45,3,N3N,6)
      WRITE(6,7)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
  202 CONTINUE
      WRITE(6,8)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      DO 109 I=1,3
      DO 109 J=1,N3N
      DIPDT(I,J)=DIPDT(I,J)*RBOHR
  109 CONTINUE
      WRITE(6,9)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      ITAP17=17
      REWIND ITAP17
      WRITE(ITAP17,10) NATOMS,N3N
      WRITE(ITAP17,11) ((DIPDT(I,J),J=1,N3N),I=1,3)
      REWIND ITAP17
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE POLAR(OCC,HF,UF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),HF(NTRI,3),UF(NBASIS,NBASIS)
      DIMENSION POM(3,3),POC(3,3)
      DIMENSION POL(3,3),POLC(3,3),EIG(3),EIV(3,3),A(6),FV1(3),FV2(3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA A00,HALF,TWO,FOUR / 0.0D+00 , 0.5D+00 , 2.0D+00 , 4.0D+00 /
      DATA BOHR / 0.52917706D+00 /
    1 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    2 FORMAT(//,2X,' UF MATRIX, IXYZ = ',I5/)
    3 FORMAT(//,2X,' POM MATRIX'/)
    4 FORMAT(//,2X,' POC MATRIX'/)
    5 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    6 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
    7 FORMAT(//,2X,' A MATRIX'/)
    8 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    9 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
   10 FORMAT(//,2X,' EIGENVECTOR MATRIX'/)
C
      CALL SREW(ITAP44)
      A33=BOHR*BOHR*BOHR
C
C   CALL BACK HF MATRICES
      DO 101 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,HF(1,IXYZ),NTRI2,IH)
      IF(IPRNT.LE.3) GO TO 101
      WRITE(6,1) IXYZ
      CALL PRINT(HF(1,IXYZ),NTRI,NBASIS,6)
  101 CONTINUE
C
C:::B (G) COORDINATE:::
      DO 105 IXYZ=1,3
      C1G=C1A(N3N+IXYZ)
      C2G=C2A(N3N+IXYZ)
      IU=IUA(N3N+IXYZ)
      CALL RREAD(ITAP44,UF,NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 301
      WRITE(6,2) IXYZ
      CALL MATOUT(UF,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
C:::A (F) COORDINATES:::
  301 CONTINUE
      DO 103 JXYZ=1,3
      VALUP=A00
      DO 102 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 102 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-UF(J,I)*HF(IJ,JXYZ)*FAC
  102 CONTINUE
      POM(IXYZ,JXYZ)=VALUP*FOUR
      POC(IXYZ,JXYZ)=-(C1G*C1*H11F(JXYZ)+C2G*C2*H22F(JXYZ))*TWO
  103 CONTINUE
  105 CONTINUE
C
      DO 106 IXYZ=1,3
      DO 106 JXYZ=1,3
      POL(IXYZ,JXYZ)=POM(IXYZ,JXYZ)+POC(IXYZ,JXYZ)
  106 CONTINUE
      WRITE(6,3)
      CALL MATOUT(POM,3,3,3,3,6)
      WRITE(6,4)
      CALL MATOUT(POC,3,3,3,3,6)
      WRITE(6,5)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 107 I=1,3
      DO 107 J=1,3
      POLC(I,J)=POL(I,J)*A33
  107 CONTINUE
      WRITE(6,6)
      CALL MATOUT(POLC,3,3,3,3,6)
C
      IJ=0
      DO 108 I=1,3
      DO 108 J=1,I
      IJ=IJ+1
      A(IJ)=POL(I,J)
  108 CONTINUE
      WRITE(6,7)
      CALL PRINT(A,6,3,6)
      CALL RSP(3,3,6,A,EIG,1,EIV,FV1,FV2)
C
      CALL ZERO(POL,9)
      DO 110 I=1,3
  110 POL(I,I)=EIG(I)
      WRITE(6,8)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 111 I=1,3
      DO 111 J=1,3
      POLC(I,J)=POL(I,J)*A33
  111 CONTINUE
      WRITE(6,9)
      CALL MATOUT(POLC,3,3,3,3,6)
      WRITE(6,10)
      CALL MATOUT(EIV,3,3,3,3,6)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE SCLMPY(A,NDIM,SCALE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(1)
C
      DO 101 I=1,NDIM
      A(I)=A(I)*SCALE
  101 CONTINUE
C
      RETURN
      END
      SUBROUTINE WRBMAT(BB,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BB(NTRI,NABC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/TAPES/ITAP37,ITAP42,ITAP43,ITAP44
    1 FORMAT(//,2X,' B0 MATRIX'/)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,BB(1,IABC),NTRI2,IB)
  101 CONTINUE
      WRITE(6,1)
      CALL MATOUT(BB,NTRI,NABC,NTRI,NABC,6)
      CALL SREW(ITAP44)
C
      RETURN
      END
