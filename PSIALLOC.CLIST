PROC 4 MOL UNIT TYPE SIZE PASS(NOPASS) STATUS(NOSTATUS) +
       FN(UNDEFINED) DA(Y) ATTR(NOATTR)
CONTROL MSG LIST NOF
 
/* THE MASTER ALLOCATION PROCEDURE FOR PSI
/* NOTE: THE SIZE IS ALWAYS GIVEN IN TERMS OF TRACKS
 
/* SET UP ALL DESIRED ATTRIBUTES
  CONTROL NOMSG NOLIST NOF
  ATTR NULL  REUSE
  ATTR FB80  REUSE RECFM(F,B) LRECL(80) BLK(3120)
  ATTR UA133 REUSE RECFM(U,A) BLK(133)
  CONTROL MSG LIST NOF
 
  SET &FSIZE = &SIZE
 
  IF &FN EQ UNDEFINED THEN SET &FN = FILE&UNIT
 
  SET &CPUID = &$CPUIDIMS
  IF &CPUID ^= S820 && &TYPE = ES THEN SET &TYPE = WORK
 
/* SET UP PROPER ATTRIBUTE IF NONE IS SPECIFIED
  IF &ATTR EQ NOATTR && &DA EQ Y THEN SET &ATTR = NULL
  IF &ATTR EQ NOATTR && &DA EQ N THEN DO
    SET &ATTR = NULL
/*  IF &UNIT EQ  5 THEN SET &ATTR = FB80
    IF &UNIT GE 11 && &UNIT LE 29 THEN SET &ATTR = FB80
    IF &UNIT GE 31 && &UNIT LE 33 THEN SET &ATTR = UA133
    END
 
  IF &TYPE EQ WORK    && &PASS EQ NOPASS THEN SET &PASS = PASS
  IF &TYPE EQ PRLWORK && &PASS EQ NOPASS THEN SET &PASS = PASS
  IF &TYPE EQ ES      && &PASS EQ NOPASS THEN SET &PASS = PASS
  IF &PASS EQ NOPASS THEN SET &PASS = KEEP
 
  IF &UNIT LT 10 THEN SET &DDNAME = FT0&UNIT.F001
  ELSE SET &DDNAME = FT&UNIT.F001
 
  IF &STATUS EQ UNK THEN SET &STATUS = UNKNOWN
  IF &STATUS EQ UNKNOWN THEN DO
    IF &TYPE EQ SAVE    THEN SET &DSNKEY = DS(&MOL..&FN)
    IF &TYPE EQ WORK    THEN SET &DSNKEY = TEMP(&FN)
    IF &TYPE EQ PRLWORK THEN SET &DSNKEY = TEMP(&FN)
    IF &TYPE EQ SHRT    THEN SET &DSNKEY = DS(@.&MOL..&FN)
    IF &TYPE EQ PRLSHRT THEN SET &DSNKEY = DS(@.&MOL..&FN)
    IF &TYPE EQ ES      THEN SET &DSNKEY = TEMP(&FN)
    IF &SUBSTR(1:1,&TYPE) EQ &STR(T) THEN SET &ALLOPT = RECEIVE
    ELSE SET &ALLOPT = OLD
    CONTROL NOMSG NOLIST NOF
    ALLOC &DSNKEY &ALLOPT
    IF &LASTCC EQ 0 THEN DO
      SET &STATUS = &ALLOPT
      SET &SIZE = 0
      END
    ELSE DO
      IF &SIZE EQ 0 THEN EXIT CODE(0)
      SET &STATUS = NEW
      END
    CONTROL MSG LIST NOF
    END
 
  IF &SIZE EQ 0 THEN DO
    IF &STATUS EQ NOSTATUS THEN SET &STATUS = OLD
    SET &SPSTR = &STR()
    END
  ELSE DO
    SET &SPSTR = &STR(TR SP(&SIZE,&SIZE))
    END
 
  IF &TYPE EQ WORK    && &STATUS EQ OLD THEN SET &STATUS = RECEIVE
  IF &TYPE EQ PRLWORK && &STATUS EQ OLD THEN SET &STATUS = RECEIVE
  IF &TYPE EQ ES      && &STATUS EQ OLD THEN SET &STATUS = RECEIVE
 
/* THIS LETS FORTRAN KNOW HOW BIG THE FILE IS
/* BUT IF FILE IS NOT DIRECT ACCESS THERE IS NO NEED TO DO THIS
  IF &DA EQ Y THEN DO
/*  SET &MAXREC = &SIZE * 10
    SET &MAXREC = &FSIZE * 2
    OPENFILE MAXRECDD,OUTPUT
    SET &MAXRECDD = &STR( &UNIT &MAXREC)
    PUTFILE MAXRECDD
    CLOSFILE MAXRECDD
    CONTROL NOMSG NOLIST NOF
    CO @.&MOL..MAXRECT @.&MOL..MAXREC ADD
    CONTROL MSG LIST NOF
    END
 
  IF &STATUS EQ NOSTATUS THEN SET &STATUS = RENEW
 
/* RENEWING A PASSED FILE GIVES AN ERROR.
  IF &TYPE EQ ES | &TYPE EQ WORK | &TYPE EQ PRLWORK THEN DO
    IF &STATUS EQ RENEW THEN DO
      CONTROL NOMSG NOLIST NOF
      ALLOC TEMP(&FN) RECEIVE DELETE
      CONTROL MSG LIST NOF
      END
    END
 
  IF &TYPE EQ ES THEN DO
    IF &PASS EQ KEEP THEN SET &PASS = PASS
/* STATUS = RECIEVE WITH PASS = PASS DIED
/* STATUS = RECEIVE WITH PASS = &STR() DIED
    IF &STATUS EQ RECEIVE && &PASS EQ PASS THEN SET &PASS = OLD
/* STATUS = RENEW WITH PASS = (DELETE OR PASS) ALSO DIES
    IF &STATUS EQ RENEW THEN SET &STATUS = &STR()
    ALLOC DD(&DDNAME) TEMP(&FN) UNIT(ES) SPACE(1,1) MB &PASS +
          REUSE &STATUS US(&ATTR)
    EXIT CODE(0)
    END
  IF &TYPE EQ PRLWORK THEN DO
    IF &PASS EQ KEEP THEN SET &PASS = PASS
    ALLOC DD(&DDNAME) TEMP(&FN) &SPSTR +
          &PASS PRL(*) REUSE &STATUS US(&ATTR)
    EXIT CODE(0)
    END
  IF &TYPE EQ PRLSHRT THEN DO
    ALLOC DD(&DDNAME) DS(@.&MOL..&FN) &SPSTR +
          &PASS PRL(*) REUSE &STATUS US(&ATTR)
    EXIT CODE(0)
    END
  IF &TYPE EQ WORK THEN DO
    IF &PASS EQ KEEP THEN SET &PASS = PASS
    ALLOC DD(&DDNAME) TEMP(&FN) &SPSTR +
          &PASS REUSE &STATUS UNIT(WORK) US(&ATTR)
    EXIT CODE(0)
    END
  IF &TYPE EQ SHRT THEN DO
    ALLOC DD(&DDNAME) DS(@.&MOL..&FN) &SPSTR +
          &PASS REUSE &STATUS UNIT(SHRT) US(&ATTR)
    EXIT CODE(0)
    END
  IF &TYPE EQ SAVE THEN DO
    ALLOC DD(&DDNAME) DS(&MOL..&FN) &SPSTR +
          KEEP REUSE &STATUS UNIT(SAVE) US(&ATTR)
    EXIT CODE(0)
    END
 
  WRITE PSIALLOC: UNKNOWN &&TYPE=&TYPE FOR &DDNAME:  FILE NOT ALLOCATED
  EXIT CODE(877)
