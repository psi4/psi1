      FUNCTION ABIJ(I,J,K,L,ALPA,BETA,CC,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI),CC(NNC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA TWO / 2.0D+00 /
C
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      IJKL=IOFF(MAX0(IJ,KL))+MIN0(IJ,KL)
      ILJK=IOFF(MAX0(IL,JK))+MIN0(IL,JK)
      IKJL=IOFF(MAX0(IK,JL))+MIN0(IK,JL)
      ABIJ=ALPA(JL)*CC(IJKL)*TWO+BETA(JL)*(CC(IKJL)+CC(ILJK))
      RETURN
      END
      SUBROUTINE ABMAT(ALPA,BETA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/COUPL/ALPC(15),BETC(15)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA ZERO,QUAR,HALF,ONE / 0.0D+00 , 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' ALPA MATRIX'/)
    2 FORMAT(//,2X,' BETA MATRIX'/)
C
C   SET UP ALPHA AND BETA MATRICES
      DO 101 I=1,NTRI
      ALPA(I)=ZERO
      BETA(I)=alpa(i)
  101 CONTINUE
C
      NSTRI=0
      NENDI=0
      DO 105 ITYP=1,NTYPES
      MM=NSORB(ITYP)
      IF(MM.EQ.0) GO TO 105
      FI=FOCC(ITYP)
      NSTRI=NENDI+1
      NENDI=NSTRI+MM-1
      NSTRJ=0
      NENDJ=0
      DO 104 JTYP=1,ITYP
      NN=NSORB(JTYP)
      IF(NN.EQ.0) GO TO 104
      FJ=FOCC(JTYP)
      NSTRJ=NENDJ+1
      NENDJ=NSTRJ+NN-1
      DO 102 I=NSTRI,NENDI
      DO 102 J=NSTRJ,NENDJ
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      IIJJ=IOFF(MAX0(ITYP,JTYP))+MIN0(ITYP,JTYP)
      ALPA(IJ)=(ONE-ALPC(IIJJ))*FI*FJ*HALF
      BETA(IJ)=-(ONE-BETC(IIJJ))*FI*FJ*QUAR
  102 CONTINUE
  104 CONTINUE
  105 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1)
      CALL PRINT(ALPA,NTRI,NBASIS,6)
      WRITE(6,2)
      CALL PRINT(BETA,NTRI,NBASIS,6)
C
  205 RETURN
      END
      SUBROUTINE AMAT(OCC,NIJ,KIJ,ALPA,BETA,ZETA,CC,LBLO,BUFO,
     1                NNC,ICORE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION OCC(NBASIS),ALPA(NTRI),BETA(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION CC(NNC),LBLO(MAXBF4),BUFO(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' A MATRIX'/)
    2 FORMAT(2X,6I5,3F15.8)
    3 FORMAT(2X,6I5,F15.8)
C
C   FORM A MATRIX
C   SEE EQ.(12)
C
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLO,BUFO,IPRNT)
      CALL SREW(JTAPE1)
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1)
C
C   ELEMENTS FOR INDEPENDENT PAIRS
  301 IBL=0
      DO 102 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
C
      DO 101 JJ=1,II
      K=NIJ(JJ,1)
      L=NIJ(JJ,2)
      KL=IOFF(K)+L
      VALU1=ZERO
      VALU2=valu1
      ZVAL1=valu1
      ZVAL2=valu1
      ZVAL3=valu1
      ZVAL4=valu1
      VALU1=VALU1+GSAI(I,J,K,L,ALPA,BETA,CC,NNC)
      IF(L.GT.JOCC) GO TO 201
      VALU1=VALU1-GSAI(I,J,L,K,ALPA,BETA,CC,NNC)
  201 IF(J.NE.K) GO TO 202
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      ZVAL1=ZETA(IL,ITYP)-ZETA(IL,JTYP)
  202 IF(I.NE.K) GO TO 203
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      ZVAL2=-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  203 IF(J.NE.L) GO TO 204
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      ZVAL3=-ZETA(IK,ITYP)+ZETA(IK,JTYP)
  204 IF(I.NE.L) GO TO 205
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      ZVAL4=ZETA(JK,JTYP)-ZETA(JK,ITYP)
  205 VALU2=ZVAL1+ZVAL2+ZVAL3+ZVAL4
      VALUT=VALU1+VALU2
C     IF(IPRNT.LE.4) GO TO 206
C     WRITE(6,2) II,JJ,I,J,K,L,VALU1,VALU2,VALUT
  206 IBL=IBL+1
      LBLO(IBL+IBL-1)=II
      LBLO(IBL+IBL)=JJ
      BUFO(MAXBUF+IBL)=VALUT
      IF(IBL.LT.MAXBUF) GO TO 101
      IBL=0
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
  101 CONTINUE
  102 CONTINUE
C
C   STORE THE LAST BUFFER
      IBL=IBL+1
      LBLO(IBL+IBL-1)=0
      LBLO(IBL+IBL)=0
      BUFO(MAXBUF+IBL)=ZERO
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
C
C   ELEMENTS FOR DEPENDENT PAIRS
      IF(ICORE.EQ.0) GO TO 210
      IBL=0
      DO 105 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      DO 104 JJ=1,NIND
      K=NIJ(JJ,1)
      L=NIJ(JJ,2)
      A=ZIJ(I,J,K,L,CC,NNC)*(OCC(L)-OCC(K))*HALF
      IBL=IBL+1
      LBLO(IBL+IBL-1)=256*I+J
      LBLO(IBL+IBL)=256*K+L
      BUFO(MAXBUF+IBL)=A
C     IF(IPRNT.LE.4) GO TO 207
C     WRITE(6,3) II,JJ,I,J,K,L,A
  207 CONTINUE
      IF(IBL.LT.MAXBUF) GO TO 104
      IBL=0
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
  104 CONTINUE
  105 CONTINUE
C
C   STORE THE LAST BUFFER
      IBL=IBL+1
      LBLO(IBL+IBL-1)=0
      LBLO(IBL+IBL)=0
      BUFO(MAXBUF+IBL)=ZERO
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
C
  210 CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE BAMAT(EIG,OCC,NIJ,KIJ,ALPA,BETA,ZETA,SM,B0,EPA,FA,
     1                 CC,LBLI,BUFI,NNC,ICORE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),OCC(NBASIS),NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION ALPA(NTRI),BETA(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION SM(NTRI,N3N),B0(NTRI,N3N),EPA(NBASIS,NBASIS),FA(NTRI)
      DIMENSION CC(NNC),LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' FM MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' B0 MATRIX, IABC = ',I5/)
    3 FORMAT(2X,3I5,4F15.8)
    4 FORMAT(//,2X,' B0 MATRIX'/)
C
C   FORM B0 MATRIX
C   SEE EQ.(13)
C
C   READ IN TWO ELECTRON MO INTEGRALS AGAIN
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   CALL BACK SM MATRICES
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
      DO 101 I=1,NTRI
      B0(I,IABC)=ZERO
  101 CONTINUE
  102 CONTINUE
C
C   ELEMENTS FOR INDEPENDENT PAIRS
      DO 120 IABC=1,N3N
      IT=IFA(IABC)
      IE=IEA(IABC)
      IB=IBA(IABC)
      CALL RREAD(ITAP44,FA,NTRI2,IT)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1) IABC
      CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
      WRITE(6,2) IABC
  301 CONTINUE
      DO 110 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      VALU1=EPA(I,J)-EPA(J,I)
      VALU2=ZERO
      VALU3=valu2
C
      DO 107 K=1,NBASIS
      LIM=MIN0(JOCC,K)
      DO 107 L=1,LIM
      KL=IOFF(K)+L
      FAC2=GSAI(I,J,K,L,ALPA,BETA,CC,NNC)
      IF(K.EQ.L) FAC2=FAC2*HALF
      VALU2=VALU2-SM(KL,IABC)*FAC2
      FAC3=ZERO
      IF(K.NE.J) GO TO 205
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      FAC3=FAC3+ZETA(IL,ITYP)-ZETA(IL,JTYP)
  205 IF(K.NE.I) GO TO 206
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      FAC3=FAC3-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  206 VALU3=VALU3-SM(KL,IABC)*FAC3
  107 CONTINUE
      VALUT=VALU1+VALU2+VALU3
      B0(IJ,IABC)=VALUT
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,3) II,I,J,VALU1,VALU2,VALU3,VALUT
  110 CONTINUE
C
C   ELEMENTS FOR DEPENDENT PAIRS
      IF(ICORE.EQ.0) GO TO 210
      DO 115 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      VALU1=FA(IJ)-EIG(J)*SM(IJ,IABC)
      VALU2=ZERO
      DO 114 K=1,JOCC
      DO 114 L=1,K
      KL=IOFF(K)+L
      FAC=ZIJ(I,J,K,L,CC,NNC)*OCC(K)*HALF
      IF(K.EQ.L) FAC=FAC*HALF
      VALU2=VALU2-SM(KL,IABC)*FAC
  114 CONTINUE
      B0(IJ,IABC)=VALU1+VALU2
  115 CONTINUE
  210 CONTINUE
      CALL RWRIT(ITAP44,B0(1,IABC),NTRI2,IB)
  120 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 302
      WRITE(6,4)
      CALL MATOUT(B0,NTRI,N3N,NTRI,N3N,6)
C
  302 CONTINUE
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE BFMAT(NIJ,KIJ,HF,HM,EPF,BF,ICORE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION HF(NTRI),HM(NTRI,3,NTYPES),EPF(NBASIS,NBASIS),BF(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/DIPOL/DIPDA(135),DIPDM(135),DIPDN(135),DIPDT(135)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI102/NIND,NDEP
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' DIPDA MATRIX'/)
    2 FORMAT(//,2X,' DIPDN MATRIX'/)
    3 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    4 FORMAT(//,2X,' EPF MATRIX, IXYZ = ',I5/)
C
      CALL RFILE(ITAP43)
      CALL SREAD(ITAP43,DIPDA,270)
      CALL SREAD(ITAP43,DIPDN,270)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,2)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      DO 103 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      CALL SREAD(ITAP43,HF,NTRI2)
      CALL RWRIT(ITAP44,HF,NTRI2,IH)
      DO 102 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 101 I=1,NTRI
  101 HM(I,IXYZ,ITYP)=HF(I)*FAC
  102 CONTINUE
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,3) IXYZ
      CALL PRINT(HF,NTRI,NBASIS,6)
  103 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 110 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      IE=IEA(IXYZ+N3N)
      IB=IBA(IXYZ+N3N)
      CALL RREAD(ITAP44,HF,NTRI2,IH)
      DO 104 I=1,NTRI
  104 BF(I)=ZERO
      DO 105 I=1,NBASIS
      DO 105 J=1,NBASIS
  105 EPF(I,J)=ZERO
C
      DO 107 ITYP=1,NTYPES
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EPF(I,J)=HM(IJ,IXYZ,ITYP)
  106 CONTINUE
  107 CONTINUE
C
C   ELEMENTS FOR INDEPENDENT PAIRS
      DO 108 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=EPF(I,J)-EPF(J,I)
  108 CONTINUE
C
C   ELEMENTS FOR DEPENDENT PAIRS
      IF(ICORE.EQ.0) GO TO 202
      DO 109 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=HF(IJ)
  109 CONTINUE
C
  202 CONTINUE
      CALL RWRIT(ITAP44,BF,NTRI2,IB)
      CALL RWRIT(ITAP44,EPF,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,4) IXYZ
      CALL MATOUT(EPF,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
      CALL SREW(ITAP43)
      CALL SREW(ITAP44)
C
      RETURN
      END
      SUBROUTINE CIGRAD(E1A,E1T,D1W,U,W)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E1T(N3N),D1W(N3N)
      DIMENSION U(NBASIS,NBASIS),W(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      DATA ZERO / 0.0D+00 /
    1 FORMAT(//,2X,' LAGRANGIAN MATRIX'/)
    2 FORMAT(//,2X,' U MATRIX, IABC = ',I5/)
    3 FORMAT(//,2X,' EA1 MATRIX'/)
    4 FORMAT(//,2X,' EA2 MATRIX'/)
    5 FORMAT(//,2X,' E1T MATRIX'/)
C
      CALL MREAD(W,32)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,1)
      CALL MATOUT(W,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  301 CONTINUE
      CALL SREW(ITAP44)
      DO 105 IABC=1,N3N
      IU=IUA(IABC)
      CALL RREAD(ITAP44,U,NBASQ,IU)
      IF(IPRNT.LE.2) GO TO 302
      WRITE(6,2) IABC
      CALL MATOUT(U,NBASIS,NBASIS,NBASIS,NBASIS,6)
  302 VALU=ZERO
      DO 103 I=1,NBASIS
      DO 103 J=1,NBASIS
  103 VALU=VALU+U(J,I)*W(I,J)
      D1W(IABC)=VALU
  105 CONTINUE
      WRITE(6,3)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
C
C   SUM UP ALL CONTRIBUTIONS
      DO 107 IABC=1,N3N
  107 E1T(IABC)=E1A(IABC)+D1W(IABC)
      WRITE(6,5)
      CALL MATOUT(E1T,3,NATOMS,3,NATOMS,6)
C
      CALL SREW(ITAP44)
      RETURN
      END
clj The following is called by a C program which handles dynamic
clj allocation of memory.
      subroutine fentry(cc,ia,maxcor)
cets  program cpgrmo
C   COUPLED PERTURBED HARTREE-FOCK PROGRAM
C   FOR THE GENERAL OPEN-SHELL SYSTEM IN MO BASIS
C*****************************************************
C***LAST UPDATED ON MAY 29, 1985 BY YUKIO YAMAGUCHI***
C*****************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
clj      DIMENSION CC(1200000),IA(1)
cets  integer corsz
cets  parameter (corsz=400000)
cets  DIMENSION CC(corsz),IA(1)
      dimension cc(maxcor),ia(intowp(maxcor))
      CHARACTER*80 ALABEL
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/COUPL/ALPC(15),BETC(15)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/MFSEC/MASTER,NSECT
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
clj Equivalence is insured by passing the same pointers for cc and ia.
clj   EQUIVALENCE (CC,IA)
      DATA ALABEL / ' SCF FIRST DERIVATIVE BROUGHT YOU BY Y & Y CO. EST.
     1 IN MARCH , 1982             ' /
    1 FORMAT(//,2X,' COUPLED PERTURBED HARTREE-FOCK PROGRAM FOR GENERAL
     1OPEN-SHELL SCF IN MO BASIS'/)
    2 FORMAT(5I5)
    3 FORMAT(2X,10I5)
    4 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' NBASIS = ',I8/
     * 2X,' NTRI   = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NBATRI = ',I8/
     * 2X,' NATOMS = ',I8/
     * 2X,' N3N    = ',I8/
     * 2X,' N3NP3  = ',I8/
     * 2X,' N3NX3  = ',I8/
     * 2X,' N3NXX  = ',I8/
     * 2X,' IOCC   = ',I8/
     * 2X,' JOCC   = ',I8/
     * 2X,' KOCC   = ',I8/
     * 2X,' NTYPES = ',I8/
     * 2X,' NTYPEP = ',I8/
     * 2X,' ITEST  = ',I8/
     * 2X,' IORB   = ',I8/
     * 2X,' IPOL   = ',I8/
     * 2X,' ICORE  = ',I8/
     * 2X,' IPRNT  = ',I8)
    5 FORMAT(//,2X,' ALPC MATRIX'/)
    6 FORMAT(//,2X,' BETC MATRIX'/)
    7 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
    8 FORMAT(//,2X,' SCF EIGENVECTOR (SO BASIS) MATRIX'/)
    9 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   10 FORMAT(//,2X,' REQUIRED MEMORY EXCEEDS MAXCOR'/
     1          2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   11 FORMAT(//,2X,' NBASIS IS TOO LARGE'/
     1          2X,' NBASIS = ',I5,5X,' MAXBAS = ',I5/)
C
C::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C   REFERENCES FOR GRSCF :                                :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, D.J.FOX, M.A.VINCENT, :
C   AND H.F.SCHAEFER III, J.MOL.STRUC. 103,183 (1983)     :
C                 &                                       :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, M.A.VINCENT, J.F.GAW, :
C   AND H.F.SCHAEFER III, CHEM.PHYS. 72,131(1982)         :
C::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
C   ITAP11 = GEOMETRY AND GRADIENT
C   ITAP15 = SCF SECOND DERIVATIVE
C   ITAP17 = DIPOLE DERIVATIVES
C   ITAP25 = FIRST DERIVATIVES OF ORBITAL ENERGIES
C   ITAP35 = TWO ELECTRON MO INTEGRALS
C   ITAP42 = FIRST AND SECOND DERIVATIVE INFORMATION
C   ITAP43 = FIRST DERIVATIVES OF DIPOLE MOMENTS
C   ITAP44 = FIRST DERIVATIVES AND U'S
C   JTAPE1 = WORKING TAPE FOR CPHF
C
cets  CALL TSTART(6)
clj      CALL NOUNFL
      call drum
      CALL INITMF(1)
cets
      open (unit=15,file='file15',status='unknown')
      open (unit=17,file='file17',status='unknown')
C
      ITAPE3=3
      INPUT=5
      ITAPE6=6
      ITAP11=11
      ITAP15=15
      ITAP17=17
      ITAP25=25
      ITAP35=35
      ITAP42=42
      ITAP43=43
      ITAP44=44
      JTAPE1=91
      call tstart(itape6)
cets  MAXCOR=corsz
      write(6,*) 'maxcor = ', maxcor
C
      CALL LOCATE(INPUT,'# CPHFMO #',IERR)
C
      IOFF(1)=0
      DO 101 I=1,1378
  101 IOFF(I+1)=IOFF(I)+I
C
      WRITE(6,1)
      WRITE(3,1)
C
C   READIN PARAMETERS
      READ(5,2) ITEST,IORB,IPOL,ICORE,IPRNT
C
C   READIN SCF INFORMATION
      NBASIS=LPARA(3)
      NBFAO=LPARA(11)
      NTRI=LPARA(4)
      NTRI2=NTRI*2
      NTOT=IOFF(NTRI+1)
      NBASQ=NBASIS*NBASIS*2
      NBATRI=IOFF(NBFAO+1)
      NBATR2=NBATRI*2
      IOCC=LPARA(7)
      KOCC=LPARA(8)
      JOCC=LPARA(9)
      NATOMS=LPARA(10)
      N3N=LPARA(17)
      NATRI=IOFF(N3N+1)
      N3NP3=N3N+3
      N3NX3=N3N*3
      N3NXX=N3NP3
      IF(IPOL.EQ.0) N3NXX=N3N
      NTYPES=LPARA(18)
      NTYPEP=NTYPES+1
      NTRIL=((NTRI2-1)/NSECT+1)
      NBASQL=((NBASQ-1)/NSECT+1)
      N3TRL=N3NXX*NTRIL
      N3QRL=N3NXX*NBASQL
      ENUC=APARA(1)
      ESCF=APARA(2)
      DO 102 I=1,10
      NSORB(I)=LPARA(I+40)
      FOCC(I)=APARA(I+40)
  102 CONTINUE
      DO 103 I=1,15
      ALPC(I)=APARA(I+10)
      BETC(I)=APARA(I+25)
  103 CONTINUE
      DO 104 I=1,N3NXX
      ISA(I)=(I-1)*NTRIL+1
      IHA(I)=(I-1)*NTRIL+N3TRL+1
      IFA(I)=(I-1)*NTRIL+N3TRL+N3TRL+1
      IEA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+1
      IBA(I)=(I-1)*NTRIL+N3TRL+N3TRL+N3TRL+N3QRL+1
      IUA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+N3QRL+N3TRL+1
      WRITE(3,3) I,ISA(I),IHA(I),IFA(I),IEA(I),IBA(I),IUA(I)
  104 CONTINUE
      WRITE(6,4) NBASIS,NTRI,NBFAO,NBATRI,NATOMS,N3N,N3NP3,N3NX3,N3NXX,
     1 IOCC,JOCC,KOCC,NTYPES,NTYPEP,ITEST,IORB,IPOL,ICORE,IPRNT
      IF(NBASIS.GT.MAXBAS) GO TO 499
      WRITE(6,5)
      CALL PRINT(ALPC,15,NTYPEP,6)
      WRITE(6,6)
      CALL PRINT(BETC,15,NTYPEP,6)
C
C   READ IN AO-MO EIGENVECTORS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      CALL MREAD(CC(IC1),17)
      CALL MREAD(CC(IC2),18)
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,7)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C**************************
C***PREPARATION FOR CPHF***
C**************************
C
C   FORM REGISTER MATRIX
  301 CONTINUE
      WRITE(3,20)
   20 FORMAT(//,2X,' NOW YOU ARE IN REGIST'/)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC4=IC3+NBFAO*NBASIS
      IA4=IC4+IC4-1
      IC5=IC4+NTRI
      IA5=IC5+IC5-1
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C.................NIJ     KIJ.....
      CALL REGIST(IA(IA4),IA(IA5))
C
C   FORM ALPA AND BETA MATRICES
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN ABMAT'/)
      IC6=IC5+NTRI
      IC7=IC6+NTRI
C................ALPA    BETA....
      CALL ABMAT(CC(IC6),CC(IC7))
C
C   READ IN FIRST AND SECOND DERIVATIVE INFORMATION
C   AND FORM SM, HM AND FM MATRICES
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC8=IC7+NTRI
      IC9=IC8+N3N
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC10=IC9+N3N*N3N
      IC11=IC10+NBATRI
      IC12=IC11+NBFAO*NBFAO
      ICMAX=IC12+NBFAO*NBFAO
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     E1A     E2A     SS       U        T........
      CALL DERMAT(CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12))
C
C   READ IN SO-MO EIGENVECTORS
      CALL MREAD(CC(IC3),19)
      IF(IPRNT.LE.2) GO TO 302
      WRITE(6,8)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C:::::::::::::::::::::::::::::::::::
C:::TEST ROUTINES FOR INPUT CHECK:::
C:::::::::::::::::::::::::::::::::::
  302 CONTINUE
      IF(ITEST.EQ.0) GO TO 303
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN E0CAL'/)
C   CALCULATE SCF ENERGY
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTOT
      IA12=IC12+IC12-1
      ICMAX=IC12+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ALPA    BETA    H        CC       LBLI.....
      CALL E0CAL(CC(IC2),CC(IC6),CC(IC7),CC(IC10),CC(IC11),IA(IA12),
C................BUFI..........
     1           CC(IC12),NTOT)
C
C   CALCULATE LAGRANGIAN MATRIX
  303 CONTINUE
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN E0LAG'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTOT
      IA14=IC14+IC14-1
      ICMAX=IC14+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ALPA    BETA    EPS      ZETA     H........
      CALL E0LAG(CC(IC2),CC(IC6),CC(IC7),CC(IC10),CC(IC11),CC(IC12),
C................CC       LBLI     BUFI..........
     1           CC(IC13),IA(IA14),CC(IC14),NTOT)
C
C   CALCULATE FIRST ENERGY GRADIENTS
      IF(ITEST.EQ.0) GO TO 304
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN MODER'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+N3N
      IC14=IC13+N3N
      IC15=IC14+N3N
      IC16=IC15+N3N
      IC17=IC16+NTRI*N3N
      IC18=IC17+NTRI*N3N
      ICMAX=IC18+NTRI*N3N*NTYPEP
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     EPS      D1O      D1T      D1W......
      CALL MODER(CC(IC2),CC(IC10),CC(IC12),CC(IC13),CC(IC14),
C................E1T      SM       HM       TM.......
     1           CC(IC15),CC(IC16),CC(IC17),CC(IC18))
C
C   FORM DERIVATIVE LAGRANGIAN MATRIX
  304 CONTINUE
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN FAMAT'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NBASIS*NBASIS
      ICMAX=IC15+NTRI*N3N*NTYPEP
      WRITE(3,9) ICMAX,MAXCOR
C................HH       TT       EPA      FM..................
      CALL FAMAT(CC(IC12),CC(IC13),CC(IC14),CC(IC15),IPOL,ICORE)
C
C   CALCULATE BA MATRIX
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN BAMAT'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI*N3N
      IC14=IC13+NTRI*N3N
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NTRI
      IC17=IC16+NTOT
      IA17=IC17+IC17-1
      ICMAX=IC17+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     OCC     NIJ     KIJ     ALPA    BETA....
      CALL BAMAT(CC(IC1),CC(IC2),IA(IA4),IA(IA5),CC(IC6),CC(IC7),
C................ZETA     SM       B0       EPA      FA.......
     1           CC(IC11),CC(IC12),CC(IC13),CC(IC14),CC(IC15),
C................CC       LBLI     BUFI................
     2           CC(IC16),IA(IA17),CC(IC17),NTOT,ICORE)
C     CALL WRBMAT(CC(IC16),N3NXX)
C
C   CALCULATE A MATRIX
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN AMAT'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTOT
      IA13=IC13+IC13-1
      ICMAX=IC13+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
C...............OCC     NIJ     KIJ     ALPA    BETA    ZETA.....
      CALL AMAT(CC(IC2),IA(IA4),IA(IA5),CC(IC6),CC(IC7),CC(IC11),
C...............CC       LBLI     BUFI................
     1          CC(IC12),IA(IA13),CC(IC13),NTOT,ICORE)
C
C   FORM BF MATRIX
      IF(IPOL.EQ.0) GO TO 305
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN BFMAT'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTRI*NTYPES*3
      IC15=IC14+NBASIS*NBASIS
      ICMAX=IC15+NTRI
      WRITE(3,9) ICMAX,MAXCOR
C................NIJ     KIJ     HF       HM       EPF      BF.......
      CALL BFMAT(IA(IA4),IA(IA5),CC(IC12),CC(IC13),CC(IC14),CC(IC15),
C......................
     1           ICORE)
C
C     CALL WRBMAT(CC(IC15),N3NXX)
C
C********************
C***CPHF PROCEDURE***
C********************
C
  305 CONTINUE
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN CPHF'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NBASIS*NBASIS
      IC15=IC14+NTRI*N3NXX
      IC16=IC15+NTRI*N3NXX
      IC17=IC16+NIND*(NIND+N3NXX)
      IA17=IC17+IC17-1
      ICMAX=IC17+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C...............EIG     NIJ     KIJ     B0       UA       SA.......
      CALL CPHF(CC(IC1),IA(IA4),IA(IA5),CC(IC12),CC(IC13),CC(IC14),
C...............BA       C        LBLI     BUFI......................
     1          CC(IC15),CC(IC16),IA(IA17),CC(IC17),NIND,N3NXX,ICORE)
C     CALL WRBMAT(CC(IC15),N3NXX)
C
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN SCF2ND'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+N3N*N3N
      IC14=IC13+N3N*N3N
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NBASIS*NBASIS
      IC17=IC16+NTRI*N3N
      IC18=IC17+NBASIS*NBASIS*N3N
      IC19=IC18+NTOT
      IA19=IC19+IC19-1
      ICMAX=IC19+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................ALPA    BETA    E2A     EPS      ZETA     E2M......
      CALL SCF2ND(CC(IC6),CC(IC7),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................E2P      EPA      UA       SA       WA.......
     1            CC(IC13),CC(IC14),CC(IC15),CC(IC16),CC(IC17),
C.................CC       LBLI     BUFI..........
     2            CC(IC18),IA(IA19),CC(IC19),NTOT)
C
      IF(IPOL.EQ.0) GO TO 306
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN DIPMO'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI*3
      ICMAX=IC13+NBASIS*NBASIS*3
      WRITE(3,9) ICMAX,MAXCOR
C................OCC     DIP      UA.......
      CALL DIPMO(CC(IC2),CC(IC12),CC(IC13))
C
  306 CONTINUE
      IF(ICORE.EQ.0) GO TO 307
      IF(IORB.EQ.0) GO TO 307
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN ORB1ST'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      ICMAX=IC13+NBASIS*N3N
      WRITE(3,9) ICMAX,MAXCOR
C.................B0       CC.............
      CALL ORB1ST(CC(IC12),CC(IC13),N3N,1)
      IF(IPOL.EQ.0) GO TO 307
C.................B0       CC...........
      CALL ORB1ST(CC(IC12),CC(IC13),3,2)
C
  307 CONTINUE
      IF(IPOL.EQ.0) GO TO 310
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN POLAR'/)
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI*3
      ICMAX=IC13+NBASIS*NBASIS
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     HF       UF.......
      CALL POLAR(CC(IC2),CC(IC12),CC(IC13))
C
  310 CONTINUE
      GO TO 500
  399 CONTINUE
      WRITE(6,10) ICMAX,MAXCOR
      WRITE(3,10) ICMAX,MAXCOR
      GO TO 500
  499 WRITE(6,11) NBASIS,MAXBAS
      WRITE(3,11) NBASIS,MAXBAS
  500 CONTINUE
C
      CALL RCLOSE(ITAP42,3)
      CALL RCLOSE(ITAP43,3)
      CALL RCLOSE(ITAP44,3)
      CALL RCLOSE(MASTER,3)
      CALL RCLOSE(JTAPE1,3)
c     CALL TSTOP(6)
      call tstop(itape6)
C
      STOP
      END
      SUBROUTINE CPHF(EIG,NIJ,KIJ,B0,UA,SA,BA,C,LBLI,BUFI,
     1                NNX,NABC,ICORE)
      IMPLICIT REAL*8 (A-H,O-Z)
clj Sun f77 doesn't like real*16
clj      REAL*16 DLIM,DETERM
      REAL*8 DLIM,DETERM
      DIMENSION EIG(NBASIS),UA(NBASIS,NBASIS)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2),B0(NTRI)
      DIMENSION SA(NTRI,NABC),BA(NTRI,NABC),C(NNX,NNX+NABC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA CRIT / 1.0D-6 /
clj      DATA DLIM / 1.0Q-76 /
      DATA DLIM / 1.0D-76 /
      DATA ZERO,HALF,ONE / 0.0D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' C MATRIX'/)
clj    2 FORMAT(//,2X,' DETERM = ',Q20.10/)
    2 FORMAT(//,2X,' DETERM = ',D20.10/)
    3 FORMAT(//,2X,'***THE VALUE OF DETERMINANT IS TOO SMALL***',
     1 5X,' DETERM = ',D20.10/)
clj     1 5X,' DETERM = ',Q20.10/)
    4 FORMAT(//,2X,' IABC = ',I5/)
    5 FORMAT(//,2X,' U MATRIX, IABC = ',I5/)
    6 FORMAT(//,2X,' CA MATRIX, IABC = ',I5/)
C
C**********************************
C***SET UP SIMULTANEOUS EQUATION***
C**********************************
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,BA(1,IABC),NTRI2,IB)
      IF(IABC.GT.N3N) GO TO 101
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
  101 CONTINUE
C
      CALL SREW(JTAPE1)
  500 CALL SREAD(JTAPE1,LBLI,MAXBF4)
      DO 102 M=1,MAXBUF
      II=LBLI(M+M-1)
      IF(II.EQ.0) GO TO 600
      JJ=LBLI(M+M)
      C(II,JJ)=BUFI(MAXBUF+M)
      C(JJ,II)=BUFI(MAXBUF+M)
  102 CONTINUE
      GO TO 500
  600 CONTINUE
C
      DO 105 IABC=1,NABC
      DO 103 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      C(II,NIND+IABC)=BA(IJ,IABC)
  103 CONTINUE
  105 CONTINUE
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,1)
      CALL MATOUT(C,NNX,NNX+NABC,NIND,NIND+NABC,6)
C
C   SOLVE THE SIMULTANEOUS EQUATION
  202 CALL FLINQ(C,NNX,NIND,NABC,DETERM)
      WRITE(6,2) DETERM
      IF(IPRNT.GT.4)
     * CALL MATOUT(C,NNX,NNX+NABC,NIND,NIND+NABC,6)
clj      IF(QABS(DETERM).LE.DLIM) GO TO 203
      IF(ABS(DETERM).LE.DLIM) GO TO 203
      GO TO 205
  203 WRITE(6,3) DETERM
      STOP
C
C   OBTAIN INDEPENDENT ELEMENTS OF U MATRIX
  205 DO 110 IABC=1,NABC
      DO 106 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BA(IJ,IABC)=C(II,NIND+IABC)
  106 CONTINUE
      IF(IPRNT.LE.5) GO TO 110
      CALL PRINT(BA(1,IABC),NTRI,NBASIS,6)
  110 CONTINUE
C
C   CALCULATE DEPENDENT ELEMENTS OF U MATRIX
      IF(ICORE.EQ.0) GO TO 800
  700 CALL SREAD(JTAPE1,LBLI,MAXBF4)
      DO 115 M=1,MAXBUF
      IB1=LBLI(M+M-1)
      IF(IB1.EQ.0) GO TO 800
      IB2=LBLI(M+M)
      J=MOD(IB1,256)
      I=IB1/256
      L=MOD(IB2,256)
      K=IB2/256
      IJ=IOFF(I)+J
      KL=IOFF(K)+L
      VALU=BUFI(MAXBUF+M)
      DO 114 IABC=1,NABC
      BA(IJ,IABC)=BA(IJ,IABC)+VALU*BA(KL,IABC)
  114 CONTINUE
  115 CONTINUE
      GO TO 700
  800 CALL SREW(JTAPE1)
C
      DO 125 IABC=1,NABC
      IB=IBA(IABC)
      IU=IUA(IABC)
      IF(ICORE.EQ.0) GO TO 900
C
      DO 116 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      DELJI=EIG(J)-EIG(I)
      IF(I.EQ.J) DELJI=ONE
      IF(DABS(DELJI).LT.CRIT) GO TO 206
      BA(IJ,IABC)=BA(IJ,IABC)/DELJI
      GO TO 116
  206 BA(IJ,IABC)=ZERO
  116 CONTINUE
C
  900 CONTINUE
      IF(IABC.GT.N3N) GO TO 208
      DO 118 I=1,NBASIS
      DO 118 J=1,I
      IJ=IOFF(I)+J
      B0(IJ)=BA(IJ,IABC)
      IF(I.EQ.J) GO TO 207
      UA(I,J)=BA(IJ,IABC)
      UA(J,I)=-UA(I,J)-SA(IJ,IABC)
      GO TO 118
  207 UA(I,I)=-SA(IJ,IABC)*HALF
  118 CONTINUE
      GO TO 210
C
  208 CONTINUE
      DO 119 I=1,NBASIS
      DO 119 J=1,I
      IJ=IOFF(I)+J
      B0(IJ)=BA(IJ,IABC)
      IF(I.EQ.J) GO TO 209
      UA(I,J)=BA(IJ,IABC)
      UA(J,I)=-UA(I,J)
      GO TO 119
  209 UA(I,I)=ZERO
  119 CONTINUE
C
  210 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      CALL RWRIT(ITAP44,UA,NBASQ,IU)
      IF(IPRNT.LE.2) GO TO 125
      WRITE(6,5) IABC
      CALL MATOUT(UA,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  125 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE DERMAT(EAO,E1A,E2A,SS,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E2A(N3N,N3N),SS(NBATRI)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
    1 FORMAT(//,2X,' SCF FIRST DERIVATIVE MATRIX'/)
    2 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SA (AO BASIS) MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' SA (MO BASIS) MATRIX, IABC = ',I5/)
    5 FORMAT(//,2X,' HA (AO BASIS) MATRIX, IABC = ',I5/)
    6 FORMAT(//,2X,' HA (MO BASIS) MATRIX, IABC = ',I5/)
    7 FORMAT(//,2X,' TA (AO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
    8 FORMAT(//,2X,' TA (MO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
C
      CALL RFILE(ITAP42)
      CALL RFILE(ITAP44)
      CALL RFILE(JTAPE1)
      NBSET=NTYPES
      NTREAD=NBSET
C
C   READ IN SCF FIRST DERIVATIVES
      CALL SREAD(ITAP42,E1A,N3N*2)
      WRITE(6,1)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
C
C   READ IN SCF SECOND DERIVATIVES
      CALL SREAD(ITAP42,E2A,N3N*N3N*2)
      WRITE(6,2)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
C
C   READ IN S FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,3) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  201 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IS)
      IF(IPRNT.LE.4) GO TO 101
      WRITE(6,4) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  101 CONTINUE
C
C   READ IN ONE ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 102 IABC=1,N3N
      IH=IHA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,5) IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  202 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IH)
      IF(IPRNT.LE.4) GO TO 102
      WRITE(6,6) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  102 CONTINUE
C
C   READ IN TWO ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 104 ITYP=1,NBSET
      DO 103 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 203
      WRITE(6,7) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  203 CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL SWRIT(JTAPE1,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,8) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  103 CONTINUE
  104 CONTINUE
C
      CALL SREW(ITAP42)
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE DIPMO(OCC,DIP,UA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),DIP(NTRI,3),UA(NBASIS,NBASIS,3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(384),DIPDE(3,45),DUM(889)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,HALF,ONE,FOUR / 0.0D+00 , 0.5D+00 , 1.0D+00 , 4.0D+00 /
      DATA DEBYE,BOHR / 2.541765480D+00 , 0.52917706D+00 /
    1 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS'/)
    2 FORMAT(//,2X,' UA MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    3 FORMAT(//,2X,' DIPDA MATRIX'/)
    4 FORMAT(//,2X,' DIPDM MATRIX'/)
    5 FORMAT(//,2X,' DIPDE MATRIX'/)
    6 FORMAT(//,2X,' DIPDN MATRIX'/)
    7 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/BOHR)
     *'/)
    8 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/A)'/)
    9 FORMAT(2I5)
   10 FORMAT(3F20.10)
C
      CALL SREW(ITAP44)
      DEB4=DEBYE*FOUR
      RBOHR=ONE/BOHR
C     WRITE(6,1)
C
      DO 101 I=1,3
      DO 101 J=1,N3N
      DIPDM(I,J)=ZERO
  101 CONTINUE
C
      DO 102 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,DIP(1,IXYZ),NTRI2,IH)
  102 CONTINUE
C
      KABC=0
      DO 105 IABC=1,NATOMS
      I00=(IABC-1)*3
      IXX=I00+1
      IYY=I00+2
      IZZ=I00+3
      DO 103 IXYZ=1,3
      KABC=KABC+1
      IU=IUA(KABC)
      CALL RREAD(ITAP44,UA(1,1,IXYZ),NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 103
      WRITE(6,2) IABC,IXYZ
      CALL MATOUT(UA(1,1,IXYZ),NBASIS,NBASIS,NBASIS,NBASIS,6)
  103 CONTINUE
C
      DO 104 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 104 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      DIPDM(1,IXX)=DIPDM(1,IXX)+UA(J,I,1)*DIP(IJ,1)*FAC
      DIPDM(1,IYY)=DIPDM(1,IYY)+UA(J,I,2)*DIP(IJ,1)*FAC
      DIPDM(1,IZZ)=DIPDM(1,IZZ)+UA(J,I,3)*DIP(IJ,1)*FAC
      DIPDM(2,IXX)=DIPDM(2,IXX)+UA(J,I,1)*DIP(IJ,2)*FAC
      DIPDM(2,IYY)=DIPDM(2,IYY)+UA(J,I,2)*DIP(IJ,2)*FAC
      DIPDM(2,IZZ)=DIPDM(2,IZZ)+UA(J,I,3)*DIP(IJ,2)*FAC
      DIPDM(3,IXX)=DIPDM(3,IXX)+UA(J,I,1)*DIP(IJ,3)*FAC
      DIPDM(3,IYY)=DIPDM(3,IYY)+UA(J,I,2)*DIP(IJ,3)*FAC
      DIPDM(3,IZZ)=DIPDM(3,IZZ)+UA(J,I,3)*DIP(IJ,3)*FAC
  104 CONTINUE
  105 CONTINUE
C
      DO 106 I=1,3
      DO 106 J=1,N3N
      DIPDM(I,J)=-DIPDM(I,J)*DEB4
  106 CONTINUE
C
C   SUM UP CONTRIBUTIONS FROM MO CHANGE
      DO 107 I=1,3
      DO 107 J=1,N3N
      DIPDE(I,J)=DIPDA(I,J)+DIPDM(I,J)
  107 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,3)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,4)
      CALL MATOUT(DIPDM,3,45,3,N3N,6)
C
C   SUM UP ALL CONTRIBUTIONS
  201 CONTINUE
      DO 108 I=1,3
      DO 108 J=1,N3N
      DIPDT(I,J)=DIPDE(I,J)+DIPDN(I,J)
  108 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,5)
      CALL MATOUT(DIPDE,3,45,3,N3N,6)
      WRITE(6,6)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
  202 CONTINUE
      WRITE(6,7)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      DO 109 I=1,3
      DO 109 J=1,N3N
      DIPDT(I,J)=DIPDT(I,J)*RBOHR
  109 CONTINUE
      WRITE(6,8)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      ITAP17=17
      REWIND ITAP17
      WRITE(ITAP17,9) NATOMS,N3N
      WRITE(ITAP17,10) ((DIPDT(I,J),J=1,N3N),I=1,3)
      REWIND ITAP17
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE E0CAL(OCC,ALPA,BETA,H,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),ALPA(NTRI),BETA(NTRI),H(NTRI),CC(NNC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,ONE,TWO / 0.0D+00 , 1.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' H MATRIX (MO BASIS)'/)
    2 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
C
C   CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(1)
C
      CALL MREAD(H,16)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL PRINT(H,NTRI,NBASIS,6)
C
C   READ IN TWO ELECTRON MO INTEGRALS
  201 CONTINUE
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   CALCULATE ONE-ELECTRON ENERGY
      ELEC1=ZERO
      DO 101 I=1,JOCC
      II=IOFF(I+1)
      ELEC1=ELEC1+OCC(I)*H(II)
  101 CONTINUE
C   CALCULATE TWO-ELECTRON ENRGY
      ELEC2=ZERO
      DO 102 I=1,JOCC
      DO 102 J=1,I
      IJ=IOFF(I)+J
      II=IOFF(I+1)
      JJ=IOFF(J+1)
      IIJJ=IOFF(II)+JJ
      IJIJ=IOFF(IJ+1)
      FAC=TWO
      IF(I.EQ.J) FAC=ONE
      ELEC2=ELEC2+(ALPA(IJ)*CC(IIJJ)+BETA(IJ)*CC(IJIJ))*FAC
  102 CONTINUE
C
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,2) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
      RETURN
      END
      SUBROUTINE E0LAG(OCC,ALPA,BETA,EPS,ZETA,H,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),ALPA(NTRI),BETA(NTRI),H(NTRI),CC(NNC)
      DIMENSION EPS(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,HALF,TWO / 0.0D+00 , 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' LAGRANGIAN MATRIX'/)
    2 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
    3 FORMAT(//,2X,' ZETA MATRIX  , ITYP = ',I5/)
C
C   FORM LAGRANGIAN MATRIX AND CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(3)
C
      CALL MREAD(H,16)
C   READ IN TWO ELECTRON MO INTEGRALS
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   ONE-ELECTRON TERMS
      DO 101 I=1,NBASIS
      FAC=OCC(I)*HALF
      DO 101 J=1,I
      IJ=IOFF(I)+J
      EPS(IJ)=H(IJ)*FAC
  101 CONTINUE
      ELEC1=ZERO
      DO 102 I=1,JOCC
      II=IOFF(I+1)
  102 ELEC1=ELEC1+EPS(II)*TWO
C
C   TWO-ELECTRON TERMS
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      VALU=ZERO
      DO 104 K=1,JOCC
      KK=IOFF(K+1)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IJKK=IOFF(MAX0(IJ,KK))+MIN0(IJ,KK)
      IKJK=IOFF(MAX0(IK,JK))+MIN0(IK,JK)
      VALU=VALU+ALPA(IK)*CC(IJKK)+BETA(IK)*CC(IKJK)
  104 CONTINUE
      EPS(IJ)=EPS(IJ)+VALU
  105 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1)
      CALL PRINT(EPS,NTRI,NBASIS,6)
  201 ELEC2=ZERO
      DO 106 I=1,JOCC
      II=IOFF(I+1)
  106 ELECT=ELECT+EPS(II)
C
      ELEC2=ELECT-ELEC1*HALF
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,2) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
C   CALCULATE ZETA MATRICES
C   SEE EQ.(8)
C
      DO 110 ITYP=1,NTYPES
      L=NSTR(ITYP)
      FAC=FOCC(ITYP)*HALF
      DO 109 I=1,NBASIS
      DO 109 J=1,I
      IJ=IOFF(I)+J
      VALU=H(IJ)*FAC
      DO 108 K=1,JOCC
      KK=IOFF(K+1)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IJKK=IOFF(MAX0(IJ,KK))+MIN0(IJ,KK)
      IKJK=IOFF(MAX0(IK,JK))+MIN0(IK,JK)
      LK=IOFF(MAX0(L,K))+MIN0(L,K)
      VALU=VALU+ALPA(LK)*CC(IJKK)+BETA(LK)*CC(IKJK)
  108 CONTINUE
      ZETA(IJ,ITYP)=VALU
  109 CONTINUE
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,3) ITYP
      CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  110 CONTINUE
C
C   FOR VIRTUAL SPACE
      DO 115 I=1,NTRI
  115 ZETA(I,NTYPEP)=ZERO
C
      RETURN
      END
      SUBROUTINE FAMAT(HH,TT,EPA,FM,IPOL,ICORE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION HH(NTRI),TT(NTRI),FM(NTRI,N3N,NTYPEP)
      DIMENSION EPA(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' EPA MATRIX, IABC = ',I5/)
C
C   CALL BACK HM AND TM MATRICES AND FORM FA MATRICES
C   SEE EQ.(7)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      DO 103 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HH,NTRI2,IH)
      DO 102 ITYP=1,NTREAD
      FAC=FOCC(ITYP)*HALF
      DO 101 I=1,NTRI
  101 FM(I,IABC,ITYP)=HH(I)*FAC
  102 CONTINUE
  103 CONTINUE
C
      DO 106 ITYP=1,NTREAD
      DO 105 IABC=1,N3N
      CALL SREAD(JTAPE1,TT,NTRI2)
      DO 104 I=1,NTRI
      FM(I,IABC,ITYP)=FM(I,IABC,ITYP)+TT(I)
  104 CONTINUE
  105 CONTINUE
  106 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 120 IABC=1,N3N
      IE=IEA(IABC)
      DO 110 I=1,NBASIS
      DO 110 J=1,NBASIS
  110 EPA(I,J)=ZERO
C
      DO 112 ITYP=1,NTYPES
      DO 111 I=NSTR(ITYP),NEND(ITYP)
      DO 111 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EPA(I,J)=FM(IJ,IABC,ITYP)
  111 CONTINUE
  112 CONTINUE
C
      CALL RWRIT(ITAP44,EPA,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 120
      WRITE(6,1) IABC
      CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  120 CONTINUE
C
C   STORE DERIVATIVE AVERAGED FOCK
      IF(ICORE.NE.0) GO TO 202
      DO 121 I=1,NTRI
  121 TT(I)=ZERO
      DO 122 IABC=1,N3N
      IT=IFA(IABC)
      CALL RWRIT(ITAP44,TT,NTRI2,IT)
  122 CONTINUE
      GO TO 205
  202 CONTINUE
      DO 125 IABC=1,N3N
      IT=IFA(IABC)
      CALL RWRIT(ITAP44,FM(1,IABC,NTREAD),NTRI2,IT)
  125 CONTINUE
  205 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      FUNCTION GSAI(I,J,K,L,ALPA,BETA,CC,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI),CC(NNC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA TWO / 2.0D+00 /
C
C FORM GSAI MATRIX
C
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IJKL=IOFF(MAX0(IJ,KL))+MIN0(IJ,KL)
      IKJL=IOFF(MAX0(IK,JL))+MIN0(IK,JL)
      ILJK=IOFF(MAX0(IL,JK))+MIN0(IL,JK)
      GSAI=(ALPA(IK)-ALPA(JK))*CC(IJKL)*TWO+(BETA(IK)-BETA(JK))
     1 *(CC(IKJL) + CC(ILJK))
C
      RETURN
      END
      SUBROUTINE MOCONV(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA ZERO / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBFAO
      DO 101 JJ=1,NBFAO
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      T(II,JJ)=SA(IIJJ)
  101 CONTINUE
      DO 103 II=1,NBFAO
      DO 103 J=1,NBASIS
      SUM=ZERO
      DO 102 JJ=1,NBFAO
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=ZERO
      DO 104 II=1,NBFAO
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE MODER(OCC,EPS,D1O,D1T,D1W,E1T,SM,HM,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),EPS(NTRI)
      DIMENSION D1O(N3N),D1T(N3N),D1W(N3N),E1T(N3N)
      DIMENSION SM(NTRI,N3N),HM(NTRI,N3N),TM(NTRI,N3N,NTYPEP)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' D1N MATRIX'/)
    2 FORMAT(//,2X,' D1O MATRIX'/)
    3 FORMAT(//,2X,' D1T MATRIX'/)
    4 FORMAT(//,2X,' D1W MATRIX'/)
    5 FORMAT(//,2X,' EGRAD MATRIX'/)
C
C   CALCULATE SCF FIRST DERIVATIVES FOR A TEST
C   SEE EQ.(2)
C
C   READ BACK DERIVATIVE SM AND HM MATRICES
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      IH=IHA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,HM(1,IABC),NTRI2,IH)
  102 CONTINUE
C
C   READ BACK DERIVATIVE TM MATRICES
      CALL SREW(JTAPE1)
      DO 105 ITYP=1,NTYPES
      DO 104 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  104 CONTINUE
  105 CONTINUE
C
      DO 110 IABC=1,N3N
C  ONE-ELECTRON CONTRIBUTION
      VALU=ZERO
      DO 106 I=1,JOCC
      II=IOFF(I+1)
      VALU=VALU+HM(II,IABC)*OCC(I)
  106 CONTINUE
      D1O(IABC)=VALU
C  TWO-ELECTRON CONTRIBUTION
      VALU=ZERO
      DO 108 ITYP=1,NTYPES
      DO 107 I=NSTR(ITYP),NEND(ITYP)
      II=IOFF(I+1)
      VALU=VALU+TM(II,IABC,ITYP)
  107 CONTINUE
      D1T(IABC)=VALU
  108 CONTINUE
C   OVERLAP CONTRIBUTION
      VALU=ZERO
      DO 109 I=1,JOCC
      DO 109 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU=VALU-SM(IJ,IABC)*EPS(IJ)
  109 CONTINUE
      D1W(IABC)=VALU*TWO
C
C   SUM UP ALL CONTRIBUTIONS
      E1T(IABC)=D1O(IABC)+D1T(IABC)+D1W(IABC)
C
  110 CONTINUE
C
CCC   WRITE(6,1)
CCC   CALL MATOUT(E1N,3,NATOMS,3,NATOMS,6)
      WRITE(6,2)
      CALL MATOUT(D1O,3,NATOMS,3,NATOMS,6)
      WRITE(6,3)
      CALL MATOUT(D1T,3,NATOMS,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
      WRITE(6,5)
      CALL MATOUT(E1T,3,NATOMS,3,NATOMS,6)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE ORB1ST(B0,CC,NABC,IFLAG)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION B0(NTRI),CC(NBASIS,NABC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      DATA ITAP25 / 25 /
    1 FORMAT(//,2X,' FIRST DERIVATIVES OF ORBITAL ENERGIES W.R.T. NUCLEA
     1R COORDINATE'/)
    2 FORMAT(2I5)
    3 FORMAT(3F20.10)
    4 FORMAT(//,2X,' FIRST DERIVATIVES OF ORBITAL ENERGIES W.R.T. ELECTR
     1IC FIELD'/)
C
C   FOR NUCLEAR COORDINATE PERTURBATION
      IF(IFLAG.EQ.2) GO TO 201
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 101 I=1,NBASIS
      II=IOFF(I+1)
      CC(I,IABC)=B0(II)
  101 CONTINUE
  102 CONTINUE
      WRITE(6,1)
      CALL MATOUT(CC,NBASIS,N3N,NBASIS,N3N,6)
      CALL SREW(ITAP44)
cets  call rfile(itap25)
      open(unit=25,file='file25',status='unknown')
cets  REWIND ITAP25
      WRITE(ITAP25,2) N3N,NBASIS
      WRITE(ITAP25,3) ((CC(I,J),J=1,N3N),I=1,NBASIS)
      REWIND ITAP25
      RETURN
C
C   FOR ELECTRIC FIELD PERTURBATION
  201 CONTINUE
      CALL SREW(ITAP44)
      DO 104 IXYZ=1,3
      IB=IBA(IXYZ+N3N)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 103 I=1,NBASIS
      II=IOFF(I+1)
      CC(I,IXYZ)=B0(II)
  103 CONTINUE
  104 CONTINUE
      WRITE(6,4)
      CALL MATOUT(CC,NBASIS,3,NBASIS,3,6)
      CALL SREW(ITAP44)
C
      RETURN
      END
      SUBROUTINE POLAR(OCC,HF,UF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),HF(NTRI,3),UF(NBASIS,NBASIS)
      DIMENSION POL(3,3),POLC(3,3),EIG(3),EIV(3,3),A(6),FV1(3),FV2(3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,HALF,FOUR / 0.0D+00 , 0.5D+00 , 4.0D+00 /
      DATA BOHR / 0.52917706D+00 /
    1 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    2 FORMAT(//,2X,' UF MATRIX, IXYZ = ',I5/)
    3 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    4 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
    5 FORMAT(//,2X,' A MATRIX'/)
    6 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    7 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
    8 FORMAT(//,2X,' EIGENVECTOR MATRIX'/)
C
      CALL SREW(ITAP44)
      A33=BOHR*BOHR*BOHR
C
C   CALL BACK HF MATRICES
      DO 101 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,HF(1,IXYZ),NTRI2,IH)
      IF(IPRNT.LE.3) GO TO 101
      WRITE(6,1) IXYZ
      CALL PRINT(HF(1,IXYZ),NTRI,NBASIS,6)
  101 CONTINUE
C
C:::B COORDINATE:::
      DO 105 IXYZ=1,3
      IU=IUA(N3N+IXYZ)
      CALL RREAD(ITAP44,UF,NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 301
      WRITE(6,2) IXYZ
      CALL MATOUT(UF,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
C:::A COORDINATES:::
  301 CONTINUE
      DO 103 JXYZ=1,3
      VALUP=ZERO
      DO 102 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 102 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-UF(J,I)*HF(IJ,JXYZ)*FAC
  102 CONTINUE
      POL(IXYZ,JXYZ)=VALUP*FOUR
  103 CONTINUE
  105 CONTINUE
      WRITE(6,3)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 106 I=1,3
      DO 106 J=1,3
      POLC(I,J)=POL(I,J)*A33
  106 CONTINUE
      WRITE(6,4)
      CALL MATOUT(POLC,3,3,3,3,6)
C
      IJ=0
      DO 107 I=1,3
      DO 107 J=1,I
      IJ=IJ+1
      A(IJ)=POL(I,J)
  107 CONTINUE
      WRITE(6,5)
      CALL PRINT(A,6,3,6)
      CALL RSP(3,3,6,A,EIG,1,EIV,FV1,FV2)
      DO 108 I=1,3
      DO 108 J=1,3
      POL(I,J)=ZERO
  108 CONTINUE
      DO 109 I=1,3
  109 POL(I,I)=EIG(I)
      WRITE(6,6)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 110 I=1,3
      DO 110 J=1,3
      POLC(I,J)=POL(I,J)*A33
  110 CONTINUE
      WRITE(6,7)
      CALL MATOUT(POLC,3,3,3,3,6)
      WRITE(6,8)
      CALL MATOUT(EIV,3,3,3,3,6)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE RDINT2(BUFO,NDIMB,ITAPE,MAXINT,LBLI,BUFI,IPRNT)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BUFO(NDIMB)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),JPRNT
      DATA ZERO / 0.0D+00 /
    1 FORMAT(2X,8I7,F15.8)
C
C   READ IN TWO ELECTRON INTEGRALS
      CALL RFILE(ITAPE)
      DO 101 I=1,MAXINT
  101 BUFO(I)=ZERO
      INTI=0
      NBLI=0
  200 IBL=0
      NBLI=NBLI+1
      CALL SREAD(ITAPE,LBLI,MAXBF4)
  201 IBL=IBL+1
      JA=LBLI(IBL+IBL-1)
      IF(JA.EQ.0) GO TO 205
      JAA=LBLI(IBL+IBL)
      CALL UNPACK(I,J,K,L,IX,JA,JAA)
      IJ=IOFF(I)+J
      KL=IOFF(K)+L
C     IJKL=IJ*(IJ-1)/2+KL
      IJKL=IOFF(IJ)+KL
      BUFO(IJKL)=BUFI(MAXBUF+IBL)
C     IF(IPRNT.LE.5) GO TO 202
C     WRITE(6,1) I,J,K,L,IJ,KL,IX,IJKL,BUFO(IJKL)
  202 IF(IBL.EQ.MAXBUF) GO TO 203
      GO TO 201
  203 INTI=INTI+MAXBUF
      GO TO 200
  205 INTI=INTI+IBL
C
      CALL RCLOSE(ITAPE,3)
      RETURN
      END
      SUBROUTINE REGIST(NIJ,KIJ)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' REGISTERED NUMBERS'/
     1 2X,' KVIR = ',I5/
     2 2X,' NIND = ',I5/
     3 2X,' NDEP = ',I5/
     4 2X,' NDIF = ',I5)
    2 FORMAT(//,2X,' NIJ MATRIX'/2X,' NO.',5X,' NIJ(1)',5X,' NIJ(2)'/)
    3 FORMAT(2X,I4,4X,I5,7X,I5)
    4 FORMAT(//,2X,' KIJ MATRIX'/2X,' NO.',5X,' KIJ(1)',5X,' KIJ(2)'/)
    5 FORMAT(//,2X,' NO.',8X,' FOCC',6X,' NSORB',7X,' NSTR',7X,' NEND'/)
    6 FORMAT(2X,I4,3X,F10.5,7X,I5,7X,I5,7X,I5)
C
      KVIR=NBASIS-JOCC
C
      NSTR(1)=1
      NEND(1)=NSORB(1)
      DO 101 ITYP=2,NTYPEP
      NSTR(ITYP)=NEND(ITYP-1)+1
      NEND(ITYP)=NSTR(ITYP)+NSORB(ITYP)-1
  101 CONTINUE
      DO 103 ITYP=1,NTYPEP
      DO 102 I=NSTR(ITYP),NEND(ITYP)
  102 MOTYP(I)=ITYP
  103 CONTINUE
C
      II=0
      DO 105 ITYP=2,NTYPEP
      DO 105 JTYP=1,ITYP-1
      DO 104 I=NSTR(ITYP),NEND(ITYP)
      DO 104 J=NSTR(JTYP),NEND(JTYP)
      II=II+1
      NIJ(II,1)=I
      NIJ(II,2)=J
  104 CONTINUE
  105 CONTINUE
      NIND=II
C
      II=0
      DO 107 ITYP=1,NTYPEP
C*C   IF(NSORB(ITYP).EQ.1) GO TO 107
C*C   DO 106 I=NSTR(ITYP)+1,NEND(ITYP)
C*C   DO 106 J=NSTR(ITYP),I-1
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=NSTR(ITYP),I
      II=II+1
      KIJ(II,1)=I
      KIJ(II,2)=J
  106 CONTINUE
  107 CONTINUE
      NDEP=II
C
      NDIF=NTRI-NIND-NDEP
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1) KVIR,NIND,NDEP,NDIF
      WRITE(6,2)
      DO 108 I=1,NIND
  108 WRITE(6,3) I,NIJ(I,1),NIJ(I,2)
      WRITE(6,4)
      DO 109 I=1,NDEP
  109 WRITE(6,3) I,KIJ(I,1),KIJ(I,2)
      WRITE(6,5)
      DO 110 I=1,NTYPEP
  110 WRITE(6,6) I,FOCC(I),NSORB(I),NSTR(I),NEND(I)
C
  205 RETURN
      END
      SUBROUTINE SCF2ND(ALPA,BETA,E2A,EPS,ZETA,E2M,E2P,EPA,UA,SA,WA,
     1                  CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E2A(N3N,N3N),E2M(N3N,N3N),E2P(N3N,N3N)
      DIMENSION ALPA(NTRI),BETA(NTRI),EPS(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION EPA(NBASIS,NBASIS),UA(NBASIS,NBASIS)
      DIMENSION SA(NTRI,N3N),WA(NBASIS,NBASIS,N3N),CC(NNC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,TWO,FOUR / 0.0D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' WA MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' E2P MATRIX'/)
    3 FORMAT(//,2X,' E2M MATRIX'/)
    4 FORMAT(///
     1 2X,'::::::::::::::::::::::::::::'/
     2 2X,':::SUMMARY OF CALCULATION:::'/
     3 2X,'::::::::::::::::::::::::::::'/)
    5 FORMAT(//,2X,' E2A MATRIX'/)
    6 FORMAT(//,2X,' E2M MATRIX'/)
    7 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    8 FORMAT(2I5)
    9 FORMAT(3F20.10)
C
C   READ IN TWO ELECTRON MO INTEGRALS
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   FORM WA MATRICES
C   SEE EQ.(6)
C
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      IE=IEA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      DO 101 I=1,NBASIS
      DO 101 J=1,NBASIS
      WA(I,J,IABC)=EPA(J,I)+EPA(J,I)
  101 CONTINUE
  102 CONTINUE
C
      DO 107 I=1,NBASIS
      DO 107 J=1,NBASIS
      NJ=MOTYP(J)
      DO 106 K=1,JOCC
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      FAC1=EPS(IK)+ZETA(IK,NJ)
      FAC2=EPS(JK)+EPS(JK)
      DO 105 IABC=1,N3N
      WA(I,J,IABC)=WA(I,J,IABC)-SA(JK,IABC)*FAC1-SA(IK,IABC)*FAC2
  105 CONTINUE
  106 CONTINUE
  107 CONTINUE
      DO 110 I=1,NBASIS
      DO 110 J=1,NBASIS
      DO 109 K=1,JOCC
      DO 109 L=1,JOCC
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      FAC3=ABIJ(I,J,K,L,ALPA,BETA,CC,NNC)
      DO 108 IABC=1,N3N
      WA(I,J,IABC)=WA(I,J,IABC)-SA(KL,IABC)*FAC3
  108 CONTINUE
  109 CONTINUE
  110 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      DO 115 IABC=1,N3N
      WRITE(6,1) IABC
      CALL MATOUT(WA(1,1,IABC),NBASIS,NBASIS,NBASIS,NBASIS,6)
  115 CONTINUE
C
C   CALCULATE SCF SECOND DERIVATIVES
C   SEE EQ.(5)
C
C   (B COORDINATE)
  201 CONTINUE
      DO 120 IABC=1,N3N
      IE=IEA(IABC)
      IU=IUA(IABC)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      CALL RREAD(ITAP44,UA,NBASQ,IU)
C
C   (A COORDINATE)
      DO 119 JABC=1,N3N
      VALUP=ZERO
      VALUM=valup
      DO 118 I=1,JOCC
      DO 116 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-SA(IJ,JABC)*EPA(I,J)
  116 CONTINUE
      DO 117 J=1,NBASIS
      VALUM=VALUM+WA(J,I,JABC)*UA(J,I)
  117 CONTINUE
  118 CONTINUE
      E2P(IABC,JABC)=VALUP*TWO
      E2M(IABC,JABC)=VALUM*TWO
  119 CONTINUE
  120 CONTINUE
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,2)
      CALL MATOUT(E2P,N3N,N3N,N3N,N3N,6)
      WRITE(6,3)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
C*******************************************
C***SUM UP CONTRIBUTIONS DUE TO MO CHANGE***
C*******************************************
  202 DO 125 I=1,N3N
      DO 125 J=1,N3N
      E2M(I,J)=E2p(I,J)+E2m(I,J)
  125 CONTINUE
C
      WRITE(6,4)
      WRITE(6,5)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
      WRITE(6,6)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
      DO 126 I=1,N3N
      DO 126 J=1,N3N
  126 E2M(I,J)=E2A(I,J)+E2M(I,J)
      WRITE(6,7)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
      ITAP15=15
      REWIND ITAP15
      N6N=N3N*2
      WRITE(ITAP15,8) NATOMS,N6N
      WRITE(ITAP15,9) ((E2M(I,J),J=1,N3N),I=1,N3N)
      REWIND ITAP15
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE WRBMAT(BB,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BB(NTRI,NABC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
    1 FORMAT(//,2X,' B0 MATRIX'/)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,BB(1,IABC),NTRI2,IB)
  101 CONTINUE
      WRITE(6,1)
      CALL MATOUT(BB,NTRI,NABC,NTRI,NABC,6)
      CALL SREW(ITAP44)
C
      RETURN
      END
      FUNCTION ZIJ(I,J,K,L,CC,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION CC(NNC)
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA FOUR / 4.0D+00 /
C
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      IJKL=IOFF(MAX0(IJ,KL))+MIN0(IJ,KL)
      ILJK=IOFF(MAX0(IL,JK))+MIN0(IL,JK)
      IKJL=IOFF(MAX0(IK,JL))+MIN0(IK,JL)
      ZIJ=CC(IJKL)*FOUR-CC(ILJK)-CC(IKJL)
      RETURN
      END
