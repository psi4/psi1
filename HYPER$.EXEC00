&TRACE ERR
*GLOBAL TXTLIB VEMLLIB VFORTLIB CMSLIB UTILITY
&IF .&2 EQ .  &GOTO  -BEGOFF
&IF  &2 EQ HYCLAO  &GOTO  -CALCTYPE
&IF  &2 EQ HYCLMO  &GOTO  -CALCTYPE
&IF  &2 EQ HYGRAO  &GOTO  -CALCTYPE
&IF  &2 EQ HYGRMO  &GOTO  -CALCTYPE
&IF  &2 EQ HYTCAO  &GOTO  -CALCTYPE
&IF  &2 EQ HYTCMO  &GOTO  -CALCTYPE
-BEGOFF
&BEGTYPE   -BEGEND
 
  AT THE PRESENT TIME,
  THE ONLY VALID ENTRIES FOR  "HYPER$ MOLECULE CALCTYPE"
  ARE:
 
       CALCTYPE:     HYCLAO    HYCLMO    HYGRAO    HYGRMO
                     HYTCAO    HYTCMO
 
        OPTIONS:     NONE !
 
       THERE ARE NO DEFAULT CALCTYPES OR OPTIONS !
 
-BEGEND
&GOTO  -JOBEND
-CALCTYPE
&A = HYPER
&B = *************
&C = &STRING OF *&B  &A  **********  &2  **********  &1  &B
&TYPE &C
&AOMOTYPE = &RIGHT OF &2 2
CP QUERY TIME
&ENDCOD = NOGO
EXEC HFSLINK$ INTS$ &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
EXEC HFSLINK$ SCF$ &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
EXEC HFSLINK$ MASTER$ &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
EXEC HFSLINK$ DIPDER$ &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
*  AO / MO  ----------------------- BRANCHING FOR TRANSYY
&IF &AOMOTYPE EQ AO  &GOTO  -NOTRANS
EXEC HFSLINK$ TRANSYY$ &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
-NOTRANS
&TYPEHYP = &CONCAT OF &2 $
EXEC HFSLINK$ &TYPEHYP &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
CP QUERY TIME
&EXIT
-JOBEND
CP QUERY TIME
&EXIT 99999
