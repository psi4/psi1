      SUBROUTINE FENTRY(CC,IA,MAXCOR)
C   ANALYTICAL THIRD DERIVATIVE PROGRAM FOR GENERAL OPEN-SHELL SCF
C***********************************************************************
C   MODIFICATION FOR IMS VERSION                                       *
C   BY YUKIO YAMAGUCHI                                                 *
C   DATE FEBRUARY 25, 1989                                             *
C***********************************************************************
C   MODIFIED ON JANUARY 11, 1989 BY YUKIO YAMAGUCHI                    *
C   DECRASED MAXCOR FROM 1200000 TO 650000                             *
C***********************************************************************
C   LAST UPDATED ON SEPTEMBER 29, 1987 BY YUKIO YAMAGUCHI              *
C   INCREASED ISAVE FROM (100,4) TO (500,4)                            *
C***********************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
C2-25-89  DIMENSION CC(650000),IA(1)
CCC   DIMENSION CC(1200000),IA(1)
      DIMENSION CC(MAXCOR),IA(1)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/GVBAB/ALPA(10,10),BETA(10,10)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/MFSEC/MASTER,NSECT
      COMMON/SAVES/IUNIT,IUNIT4,INTOT,ISAVE(500,4)
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IEF(150),IBF(150),IUF(150)
      COMMON/SECTS/ISS(465),IES(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1,JTAPE2
C2-25-89  EQUIVALENCE (CC,IA)
      DATA QUAR,HALF,ONE / 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' THIRD DERIVATIVE PROGRAM FOR GENERAL OPEN-SHELL SCF
     1 IN AO BASIS (VERSION OF 2/01/89)'/)
    2 FORMAT(5I5)
    3 FORMAT(2X,10I6)
    4 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' NBASIS = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NTRI   = ',I8/
     * 2X,' NTYPES = ',I8/
     * 2X,' NTYPEP = ',I8/
     * 2X,' NATOMS = ',I8/
     * 2X,' N3N    = ',I8/
     * 2X,' NATRI  = ',I8/
     * 2X,' N3TOT  = ',I8/
     * 2X,' N3NP3  = ',I8/
     * 2X,' N3NXX  = ',I8/
     * 2X,' IUNIT  = ',I8/
     * 2X,' INTOT  = ',I8/
     * 2X,' IPOL   = ',I8/
     * 2X,' IPRNT  = ',I8/)
    5 FORMAT(//,2X,' ALPA MATRIX'/)
    6 FORMAT(//,2X,' BETA MATRIX'/)
    7 FORMAT(//,2X,' NO.',8X,' FOCC',6X,' NSORB',7X,' NSTR',7X,' NEND'/)
    8 FORMAT(2X,I4,3X,F10.5,7X,I5,7X,I5,7X,I5)
    9 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
   10 FORMAT(2I5)
   11 FORMAT(3F20.10)
   12 FORMAT(//,2X,' SCF EIGENVECTOR (SO BASIS) MATRIX'/)
   13 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   14 FORMAT(//,2X,' REQUIRED MEMORY EXCEEDS MAXCOR'/
     1          2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
C
C   ITAP44 = TAPE FOR FIRST DERIVATIVES
C   JTAPE1 = WORKING TAPE FOR SECOND DERIVATIVES
C   JTAPE2 = WORKING TAPE FOR THIRD DERIVATIVES
C
      CALL TSTART(6)
      CALL NOUNFL
      CALL INITMF(1)
C
      ITAPE3=3
      ITAPE6=6
      INPUT=5
      ITAP20=20
      ITAP37=37
      ITAP38=38
      ITAP42=42
      ITAP44=44
      ITAP45=45
      ITAP46=46
      JTAPE1=91
      JTAPE2=92
      MAXBUF=4096
      MAXBF3=MAXBUF*3
      MAXBF6=MAXBUF*6
C2-25-89   MAXCOR=650000
CCC   MAXCOR=1200000
C
      CALL LOCATE(INPUT,'# SCF3RD #',IERR)
C
C   LOCATION OF MATRICES IN ITAP44
C   (1) SA (N3N*NTRIL)
C   (2) HA (N3N*NTRIL)
C   (3) EA (N3N*NBASQL)
C   (4) BA (N3N*NTRIL)
C   (5) UA (N3N*NBASQL)
C
C   LOCATION OF MATRICES IN JTAPE1
C   (1) SAB (NATRI*NTRIL)
C   (2) EAB (NATRI*NBASQL)
C   (3) SSAB (NATRI*NTRIL)
C   (4) BAB (NATRI*NTRIL)
C   (5) UAB (NATRI*NBASQL)
C
C
      IOFF(1)=0
      DO 101 I=1,465
  101 IOFF(I+1)=IOFF(I)+I
C
      WRITE(6,1)
      WRITE(3,1)
C
C   READIN PARAMETERS
      READ(5,2) IPOL,IPRNT
C
C   READIN SCF INFORMATION
      NBASIS=LPARA(3)
      NTRI=LPARA(4)
      NTRI2=NTRI*2
      NTOT=IOFF(NTRI+1)
      NATOMS=LPARA(10)
      NBFAO=LPARA(11)
      NBATRI=IOFF(NBFAO+1)
      N3N=LPARA(17)
      NTYPES=LPARA(18)
      NTYPEP=NTYPES+1
      NATRI=IOFF(N3N+1)
      N3TOT=N3N*(N3N+1)*(N3N+2)/6
      NTRIL=((NTRI2-1)/NSECT+1)
      NBASQ=NBASIS*NBASIS*2
      NBASQL=((NBASQ-1)/NSECT+1)
      N3NP3=N3N+3
      N3NXX=N3NP3
      IF(IPOL.NE.0) N3NXX=N3N
      N3TRL=N3NXX*NTRIL
      N3QRL=N3NXX*NBASQL
      N3TRTL=NATRI*NTRIL
      N3QTRL=NATRI*NBASQL
      DO 102 I=1,N3NXX
      ISF(I)=(I-1)*NTRIL+1
      IHF(I)=(I-1)*NTRIL+N3TRL+1
      IFF(I)=(I-1)*NTRIL+N3TRL+N3TRL+1
      IEF(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+1
      IBF(I)=(I-1)*NTRIL+N3TRL+N3TRL+N3TRL+N3QRL+1
      IUF(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+N3QRL+N3TRL+1
      WRITE(3,3) I,ISF(I),IHF(I),IFF(I),IEF(I),IBF(I),IUF(I)
  102 CONTINUE
      DO 103 I=1,NATRI
      ISS(I)=(I-1)*NTRIL+1
      IES(I)=(I-1)*NBASQL+N3TRTL+1
      ILS(I)=(I-1)*NTRIL+N3TRTL+N3QTRL+1
      IBS(I)=(I-1)*NTRIL+N3TRTL+N3QTRL+N3TRTL+1
      IUS(I)=(I-1)*NBASQL+N3TRTL+N3QTRL+N3TRTL+N3TRTL+1
      WRITE(3,3) I,ISS(I),IES(I),ILS(I),IBS(I),IUS(I)
  103 CONTINUE
C
      CALL RFILE(ITAP38)
      CALL RREAD(ITAP38,ISAVE,2000,1)
      IUNIT=ISAVE(1,1)
      INTOT=ISAVE(500,1)-1
      IUNIT2=IUNIT*2
      IUNIT4=IUNIT*4
C
      WRITE(6,4) NBASIS,NBFAO,NTRI,NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT,
     1           N3NP3,N3NXX,IUNIT,INTOT,IPOL,IPRNT
C
      DO 104 I=1,10
      NSORB(I)=LPARA(I+40)
      FOCC(I)=APARA(I+40)
  104 CONTINUE
      CALL ZERO(ALPA,NTYPEP*NTYPEP)
      CALL ZERO(BETA,NTYPEP*NTYPEP)
      IJ=0
      DO 106 I=1,NTYPES
      DO 106 J=1,I
      IJ=IJ+1
      ALPA(I,J)= (ONE-APARA(10+IJ))*FOCC(I)*FOCC(J)*HALF
      BETA(I,J)=-(ONE-APARA(25+IJ))*FOCC(I)*FOCC(J)*QUAR
      IF(I.EQ.J) GO TO 106
      ALPA(J,I)=ALPA(I,J)
      BETA(J,I)=BETA(I,J)
  106 CONTINUE
      WRITE(6,5)
      CALL MATOUT(ALPA,10,10,NTYPEP,NTYPEP,6)
      WRITE(6,6)
      CALL MATOUT(BETA,10,10,NTYPEP,NTYPEP,6)
      NSTR(1)=1
      NEND(1)=NSORB(1)
      DO 107 ITYP=2,NTYPEP
      NSTR(ITYP)=NEND(ITYP-1)+1
      NEND(ITYP)=NSTR(ITYP)+NSORB(ITYP)-1
  107 CONTINUE
      DO 109 ITYP=1,NTYPEP
      DO 108 I=NSTR(ITYP),NEND(ITYP)
  108 MOTYP(I)=ITYP
  109 CONTINUE
      WRITE(6,7)
      DO 110 I=1,NTYPEP
  110 WRITE(6,8) I,FOCC(I),NSORB(I),NSTR(I),NEND(I)
C
C   DYNAMIC ALLOCATION
C    1 : EIG        (NBASIS)
C    2 : OCC        (NBASIS)
C    3 : ESO & EAO  (NBFAO*NBASIS)
C    4 : F3A        (N3TOT)
C    5 : F3M        (N3TOT)
C    6 : F3X        (N3TOT)
C    7 : UA         (NBASIS,NBASIS,N3N)
C    8 : SA         (NTRI,N3N)
C    9 : DP         (NTRI,N3N,NTYPEP)
C   10 : ZTA        (NTRI,N3N,NTYPEP)
C   11 : ZETA       (NTRI,NTYPEP)
C
C   READ IN AO-MO EIGENVECTORS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      CALL MREAD(CC(IC1),17)
      CALL MREAD(CC(IC2),18)
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,9)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C   READ IN AO THIRD DERIVATIVES
  301 CONTINUE
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN AOPART'/)
      IC4=IC3+NBFAO*NBASIS
      IC5=IC4+N3TOT
      IC6=IC5+N3TOT
      ICMAX=IC6+N3TOT
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................F3A     F3X.....
      CALL AOPART(CC(IC4),CC(IC6))
C
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN EABMAT'/)
      IC7=IC6+N3TOT
      IC8=IC7+NTRI*NATRI
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NTRI*NATRI*NTYPEP
      IC12=IC11+NBASIS*NBASIS
      IC13=IC12+NBASIS*NBASIS
      ICMAX=IC13+NBASIS*NBASIS
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................ESO     HAB     SAB     TAB     FAB      EAB......
      CALL EABMAT(CC(IC3),CC(IC7),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C.................U        T........
     1            CC(IC12),CC(IC13))
C
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN SSMAT'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      ICMAX=IC9+NTRI*NATRI
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................UA      SA      SS......
      CALL SSMAT(CC(IC7),CC(IC8),CC(IC9))
C
C   READ IN SO-MO EIGENVECTORS
      CALL MREAD(CC(IC3),19)
      IF(IPRNT.LE.2) GO TO 303
      WRITE(6,12)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
  303 CONTINUE
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN DENSTY'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      IC10=IC9+NTRI*N3N*NTYPEP
      IC11=IC10+NTRI*N3N*NTYPEP
      IC12=IC11+NTRI*N3N*NTYPEP
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NBASIS*NBASIS
      IA14=IC14+IC14-1
      ICMAX=IC14+MAXBF3
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................ESO     UA      DP      DQ       DM       U........
      CALL DENSTY(CC(IC3),CC(IC7),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................T        LBLI     BUFI.....
     1            CC(IC13),IA(IA14),CC(IC14))
C
C   READ IN AO-MO EIGENVECTORS AGAIN
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 304
      WRITE(6,9)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
  304 CONTINUE
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      IC10=IC9+NTRI*N3N*NTYPEP
      IC11=IC10+N3N
      IC12=IC11+N3N*N3N
      IC13=IC12+NBATRI
      IC14=IC13+NBFAO*NBFAO
      ICMAX=IC14+NBFAO*NBFAO
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     E1A      E2A      SS       U        T........
      CALL DERMAT(CC(IC3),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14))
C
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN ZTAMAT'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      IC10=IC9+NTRI*N3N*NTYPEP
      IC11=IC10+NTRI*N3N*NTYPEP
      IC12=IC11+NTRI
      ICMAX=IC12+NTRI
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................ZTA      HH       TT.......
      CALL ZTAMAT(CC(IC10),CC(IC11),CC(IC12))
C
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN SCF3RD'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBASIS*NBASIS*N3N
      IC9=IC8+NTRI*N3N
      IC10=IC9+NTRI*N3N*NTYPEP
      IC11=IC10+NTRI*N3N*NTYPEP
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NBASIS*NBASIS
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NTRI
      IC17=IC16+NTRI
      IC18=IC17+NTRI
      IC19=IC18+NBASIS*NBASIS
      IC20=IC19+NBASIS*NBASIS
      IC21=IC20+NBASIS*NBASIS
      IC22=IC21+NTRI
      IC23=IC22+NTRI
      ICMAX=IC23+NTRI
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................F3A     F3M     UA      SA......
      CALL SCF3RD(CC(IC4),CC(IC5),CC(IC7),CC(IC8),
C.................DP      ZTA      ZETA     EA       EB       EC.......
     1            CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C.................SAB      SBC      SCA      EAB      EBC      ECA......
     2            CC(IC15),CC(IC16),CC(IC17),CC(IC18),CC(IC19),CC(IC20),
C.................SSAB     SSBC     SSCA.....
     3            CC(IC21),CC(IC22),CC(IC23))
C
      WRITE(3,28)
   28 FORMAT(//,2X, ' NOW YOU ARE IN SCFADD'/)
      IC7=IC6+N3TOT
      IC8=IC7+NBFAO*NBASIS*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+IUNIT*N3N
      IA11=IC11+IC11-1
      IC12=IC11+IUNIT2
      IC13=IC12+N3N
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      ICMAX=IC15+NTRI*NATRI*NTYPEP
      WRITE(3,13) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     F3A     F3M     F3X     UA      U.......
      CALL SCFADD(CC(IC3),CC(IC4),CC(IC5),CC(IC6),CC(IC7),CC(IC8),
C.................T       FDER     LABEL    XINT     DP       DQ.......
     1            CC(IC9),CC(IC10),IA(IA11),CC(IC12),CC(IC13),CC(IC14),
C.................DM.......
     2            CC(IC15))
C
C   STORE THIRD DERIVATIVES IN TAPE20
      REWIND ITAP20
      WRITE(ITAP20,10) NATOMS,N3TOT
      WRITE(ITAP20,11) (CC(IC6+I-1),I=1,N3TOT)
      REWIND ITAP20
      GO TO 400
C
  399 WRITE(6,14) ICMAX,MAXCOR
  400 CONTINUE
C
      CALL RCLOSE(ITAP37,3)
      CALL RCLOSE(ITAP38,3)
      CALL RCLOSE(ITAP42,3)
      CALL RCLOSE(ITAP44,3)
      CALL RCLOSE(ITAP45,3)
      CALL RCLOSE(ITAP46,3)
      CALL RCLOSE(JTAPE1,3)
      CALL RCLOSE(JTAPE2,3)
      CALL RCLOSE(MASTER,3)
      CALL TSTOP(6)
C
C2-25-89 STOP
      RETURN
      END
      SUBROUTINE AOPART(F3A,F3X)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION F3A(N3TOT),F3X(N3TOT)
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/POSIT/KST(7,7,7),IREV3(21,21,21)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
    1 FORMAT(2X,4I5,5X,F10.6)
C
      CALL RFILE(ITAP46)
      CALL SREAD(ITAP46,F3X,N3TOT*2,1)
C
C  FORM AN ARRAY FOR AO THIRD DERIVATIVE ALLOCATION
      CALL SETUP
C
      LABC=0
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::B COORDINATE:::
      DO 120 JABC=1,IABC
C
C:::C COORDINATE:::
      DO 110 KABC=1,JABC
      LABC=LABC+1
C
      IPOS=IREV3(IABC,JABC,KABC)
      VALU=F3X(IPOS)
      F3A(LABC)=VALU
      IF(IPRNT.LE.3) GO TO 110
      WRITE(6,1) IABC,JABC,KABC,LABC,VALU
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      CALL SREW(ITAP46)
      RETURN
      END
      SUBROUTINE SETUP
      IMPLICIT INTEGER (A-Z)
      DIMENSION IIIADD(10,3),IIJADD(18,3),IJKADD(27,3)
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/POSIT/KST(7,7,7),IREV3(21,21,21)
      DATA IIIADD / 1,2,2,2,3,3,3,3,3,3,
     *              1,1,2,2,1,2,2,3,3,3,
     *              1,1,1,2,1,1,2,1,2,3/
      DATA IIJADD / 1,1,1,2,2,2,3,3,3,2,2,2,3,3,3,3,3,3,
     *              1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,3,3,3,
     *              1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3/
      DATA IJKADD/1,1,1,1,1,1,1,1,1,2,2,2,2,2,2,2,2,2,3,3,3,3,3,3,3,3,3,
     *            1,1,1,2,2,2,3,3,3,1,1,1,2,2,2,3,3,3,1,1,1,2,2,2,3,3,3,
     *            1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3,1,2,3/
C
C SET UP THE KST ARRAY FOR THIRD DERIVATIVES
C
      KTOT=1
      IADD=10
      KST(1,1,1)=1
      DO 300 I=2,NATOMS
        DO 200 J=1,I
          DO 100 K=1,J
            KST(I,J,K)=KTOT + IADD
            IF(I .EQ. J) THEN
              IF(I .EQ. K) THEN
                IADD=10
              ELSE
                IADD=18
              END IF
            ELSE
              IF(J .EQ. K) THEN
                IADD=18
              ELSE
                IADD=27
              END IF
            END IF
            KTOT=KST(I,J,K)
  100     CONTINUE
  200   CONTINUE
  300 CONTINUE
C
      DO 450 IATOM=1,NATOMS
        II=(IATOM-1)*3
        IIISTR=KST(IATOM,IATOM,IATOM)-1
        DO 400 I=1,10
          I3=IIISTR + I
          IX=II + IIIADD(I,1)
          IY=II + IIIADD(I,2)
          IZ=II + IIIADD(I,3)
          IREV3(IX,IY,IZ)=I3
  400   CONTINUE
  450 CONTINUE
C
      DO 575 IATOM=2,NATOMS
        II=(IATOM-1)*3
        IATOM1=IATOM-1
        DO 550 JATOM=1,IATOM1
          JJ=(JATOM-1)*3
          IIJSTR=KST(IATOM,IATOM,JATOM)-1
          IJJSTR=KST(IATOM,JATOM,JATOM)-1
          DO 500 I=1,18
            I3=IIJSTR + I
            J3=IJJSTR + I
            IX=II + IIJADD(I,1)
            IY=II + IIJADD(I,2)
            IZ=JJ + IIJADD(I,3)
            IREV3(IX,IY,IZ)=I3
C
            IX=II + IIJADD(I,3)
            IY=JJ + IIJADD(I,1)
            IZ=JJ + IIJADD(I,2)
            IREV3(IX,IY,IZ)=J3
  500     CONTINUE
  550   CONTINUE
  575 CONTINUE
C
      DO 750 IATOM=3,NATOMS
        II=(IATOM-1)*3
        IATOM1=IATOM-1
        DO 700 JATOM=2,IATOM1
          JJ=(JATOM-1)*3
          JATOM1=JATOM-1
          DO 650 KATOM=1,JATOM1
            KK=(KATOM-1)*3
            IJKSTR=KST(IATOM,JATOM,KATOM)-1
            DO 600 I=1,27
              I3=IJKSTR + I
              IX=II + IJKADD(I,1)
              IY=JJ + IJKADD(I,2)
              IZ=KK + IJKADD(I,3)
              IREV3(IX,IY,IZ)=I3
  600       CONTINUE
  650     CONTINUE
  700   CONTINUE
  750 CONTINUE
C
      RETURN
      END
      SUBROUTINE EABMAT(EAO,HAB,SAB,TAB,FAB,EAB,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EAO(NBFAO,NBFAO),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION HAB(NTRI),SAB(NTRI),TAB(NTRI)
      DIMENSION FAB(NTRI,NATRI,NTYPEP),EAB(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTS/ISS(465),IES(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1,JTAPE2
      DATA HALF / 0.5D+00 /
    1 FORMAT(//,2X,' SAB MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' HAB MATRIX, IABC = ',I5/)
    3 FORMAT(//,2X,' TAB MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' FAB MATRIX, IABC = ',I5/)
C
C   CALL BACK HAB AND TAB MATRICES AND FORM EAB MATRICES
      CALL RFILE(ITAP45)
      CALL RFILE(JTAPE1)
C
      DO 101 IABC=1,NATRI
      ISX=ISS(IABC)
      CALL SREAD(ITAP45,SAB,NTRI2)
      CALL MOCONS(SAB,NTRI,SAB,NTRI,EAO,U,T)
      CALL RWRIT(JTAPE1,SAB,NTRI2,ISX)
  101 CONTINUE
C
      DO 105 IABC=1,NATRI
      CALL SREAD(ITAP45,HAB,NTRI2)
      CALL MOCONS(HAB,NTRI,HAB,NTRI,EAO,U,T)
      DO 103 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 102 I=1,NTRI
  102 FAB(I,IABC,ITYP)=HAB(I)*FAC
  103 CONTINUE
  105 CONTINUE
      DO 110 ITYP=1,NTYPES
      DO 108 IABC=1,NATRI
      CALL SREAD(ITAP45,TAB,NTRI2)
      CALL MOCONS(TAB,NTRI,TAB,NTRI,EAO,U,T)
      DO 107 I=1,NTRI
  107 FAB(I,IABC,ITYP)=FAB(I,IABC,ITYP)+TAB(I)
  108 CONTINUE
  110 CONTINUE
      DO 120 IABC=1,NATRI
      IE=IES(IABC)
      CALL ZERO(EAB,NBASIS*NBASIS)
C
      DO 113 ITYP=1,NTYPES
      DO 112 I=NSTR(ITYP),NEND(ITYP)
      DO 112 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EAB(I,J)=FAB(IJ,IABC,ITYP)
  112 CONTINUE
  113 CONTINUE
C
      CALL RWRIT(JTAPE1,EAB,NBASQ,IE)
  120 CONTINUE
C
      CALL SREW(ITAP45)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE SSMAT(UA,SA,SS)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION UA(NBASIS,NBASIS,N3N),SA(NTRI,N3N),SS(NTRI,NATRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IEF(150),IBF(150),IUF(150)
      COMMON/SECTS/ISS(465),IES(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1,JTAPE2
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' SS MATRIX'/)
C
      CALL RFILE(ITAP44)
      CALL SREW(JTAPE1)
C
      DO 101 IABC=1,N3N
      ISA=ISF(IABC)
      IUA=IUF(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,ISA)
      CALL RREAD(ITAP44,UA(1,1,IABC),NBASQ,IUA)
  101 CONTINUE
C
C   FORM SS MATRIX
      KABC=0
      DO 110 IABC=1,N3N
      DO 110 JABC=1,IABC
      KABC=KABC+1
      IABS=ISS(KABC)
      IABX=ILS(KABC)
      CALL RREAD(JTAPE1,SS(1,KABC),NTRI2,IABS)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      VALU1=A00
      VALU2=A00
      DO 103 M=1,NBASIS
      IM=IOFF(MAX0(I,M))+MIN0(I,M)
      JM=IOFF(MAX0(J,M))+MIN0(J,M)
      VALU1=VALU1+UA(I,M,IABC)*UA(J,M,JABC)+UA(J,M,IABC)*UA(I,M,JABC)
      VALU2=VALU2-SA(IM,IABC)*SA(JM,JABC)-SA(JM,IABC)*SA(IM,JABC)
  103 CONTINUE
      SS(IJ,KABC)=SS(IJ,KABC)+VALU1+VALU2
  105 CONTINUE
      CALL RWRIT(JTAPE1,SS(1,KABC),NTRI2,IABX)
C
  110 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(SS,NTRI,NATRI,NTRI,NATRI,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE DENSTY(ESO,UA,DP,DQ,DM,U,T,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ESO(NBFAO,NBASIS),UA(NBASIS,NBASIS,N3N)
      DIMENSION U(NBASIS,NBASIS),T(NBASIS,NBASIS)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,N3N,NTYPEP)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIGNS/IOFF(466),IPRNT
    1 FORMAT(//,2X,' DP MATRIX IN DENSTY'/)
C
C   FORM DENSITY LIKE MATRIX IN SO BASIS
      CALL PCMAT(UA,DP,DQ,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   DP AND DQ ARRAYS CONTAIN DENSITY LIKE MATRIX IN SO BASIS
C   DM ARRAY STORES HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,LBLI,BUFI,NTYPES*N3N)
C
C   FINAL TRANSFORMATION
      DO 102 IABC=1,N3N
      DO 101 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  101 CONTINUE
  102 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,N3N*NTYPEP,NTRI,N3N*NTYPEP,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE PCMAT(UA,DP,DQ,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION UA(NBASIS,NBASIS,NABC),ESO(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA TWO / 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
      CALL ZERO(DP,NABC*NTYPEP*NTRI)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 103 K=1,NBASIS
      DO 103 L=1,NBASIS
      KTYP=MOTYP(K)
      LTYP=MOTYP(L)
      FAC=ESO(I,K)*ESO(J,L)+ESO(I,L)*ESO(J,K)
      DO 102 ITYP=1,NTYPES
      AVAL=A(ITYP,LTYP)
      BVAL=B(ITYP,LTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 101 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)+UA(K,L,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)+UA(K,L,IABC)*FACB
  101 CONTINUE
  102 CONTINUE
  103 CONTINUE
  105 CONTINUE
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC*NTYPEP,NTRI,NABC*NTYPEP,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC*NTYPEP,NTRI,NABC*NTYPEP,6)
C
  201 CONTINUE
      DO 108 I=1,NBASIS
      II=IOFF(I+1)
      DO 107 ITYP=1,NTYPES
      DO 106 IABC=1,NABC
      DP(II,IABC,ITYP)=DP(II,IABC,ITYP)/TWO
      DQ(II,IABC,ITYP)=DQ(II,IABC,ITYP)/TWO
  106 CONTINUE
  107 CONTINUE
  108 CONTINUE
      CALL SCLMPY(DP,NTYPES*NABC*NTRI,TWO)
      CALL SCLMPY(DQ,NTYPES*NABC*NTRI,TWO)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC*NTYPEP,NTRI,NABC*NTYPEP,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC*NTYPEP,NTRI,NABC*NTYPEP,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE PQMAT(DP,DQ,DM,LBLI,BUFI,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),DM(NTRI,NABC)
      DIMENSION LBLI(MAXBF6),BUFI(MAXBF3)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF3,MAXBF6
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      DATA TWO / 2.0D+00 /
    1 FORMAT(2X,3I5,4F15.8)
    2 FORMAT(//,2X,' DM MATRIX'/)
C
C   DP :  COULOMB DENSITY TYPE SO MATRIX
C   DQ :  EXCHANGE DENSITY TYPE SO MATRIX
C   DM :  HALF TRANSFORMED SO MATRIX
C
C   FORM DM MATRICES
      CALL ZERO(DM,NABC*NTRI)
C
      CALL RFILE(ITAP37)
  200 CONTINUE
      CALL SREAD(ITAP37,LBLI,MAXBF6)
      NBUF=MOD(LBLI(1),MAXBUF+1)
      ILSTI=MOD(LBLI(2),MAXBUF+1)
      LBLI(1)=LBLI(1)/(MAXBUF+1)
      LBLI(2)=LBLI(2)/(MAXBUF+1)
      DO 102 I=1,NBUF
      IJ=LBLI(I+I-1)
      KL=LBLI(I+I)
      P=BUFI(I+MAXBUF)
      Q=BUFI(I+MAXBUF+MAXBUF)
      CJ=P+Q
      EK=Q+Q
C     IF(IPRNT.LE.9) GO TO 201
C     WRITE(6,1) I,IJ,KL,P,Q,CJ,EK
C 201 CONTINUE
      DO 101 IABC=1,NABC
      DM(IJ,IABC)=DM(IJ,IABC)+DP(KL,IABC)*CJ+DQ(KL,IABC)*EK
      DM(KL,IABC)=DM(KL,IABC)+DP(IJ,IABC)*CJ+DQ(IJ,IABC)*EK
  101 CONTINUE
  102 CONTINUE
      IF(ILSTI.EQ.0) GO TO 200
C
      CALL RCLOSE(ITAP37,3)
C
      IF (IPRNT.LE.4) GO TO 202
      WRITE(6,2)
      CALL MATOUT(DM,NTRI,NABC,NTRI,NABC,6)
C
  202 CONTINUE
      RETURN
      END
      SUBROUTINE DERMAT(EAO,E1A,E2A,SS,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E2A(N3N,N3N),SS(NBATRI)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1,JTAPE2
    1 FORMAT(//,2X,' SCF FIRST DERIVATIVE MATRIX'/)
    2 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SA (AO BASIS) MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' HA (AO BASIS) MATRIX, IABC = ',I5/)
    5 FORMAT(//,2X,' TA (AO BASIS) MATRIX, ITYP = ',I5,' IABC = ',I5/)
    6 FORMAT(//,2X,' TA (MO BASIS) MATRIX, ITYP = ',I5,' IABC = ',I5/)
C
      CALL RFILE(ITAP42)
      CALL RFILE(JTAPE2)
      NTREAD=NTYPES
      NBATR2=NBATRI*2
C
C   READ IN SCF FIRST DERIVATIVES
      CALL SREAD(ITAP42,E1A,N3N*2)
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
C
C   READ IN SCF SECOND DERIVATIVES
  201 CONTINUE
      CALL SREAD(ITAP42,E2A,N3N*N3N*2)
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,2)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
C
C   READ IN S FIRST DERIVATIVE INTEGRALS
  202 CONTINUE
      DO 101 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NBATR2)
CCC   IF(IPRNT.LE.4) GO TO 101
CCC   WRITE(6,3) IABC
CCC   CALL PRINT(SS,NBATRI,NBASIS,6)
  101 CONTINUE
C
C   READ IN ONE ELECTRON FIRST DERIVATIVE INTEGRALS
      DO 102 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NBATR2)
CCC   IF(IPRNT.LE.4) GO TO 102
CCC   WRITE(6,4) IABC
CCC   CALL PRINT(SS,NBATRI,NBFAO,6)
  102 CONTINUE
C
C   READ IN TWO ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 104 ITYP=1,NTREAD
      DO 103 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NBATR2)
CCC   IF(IPRNT.LE.4) GO TO 203
CCC   WRITE(6,5) ITYP,IABC
CCC   CALL PRINT(SS,NBATRI,NBFAO,6)
  203 CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL SWRIT(JTAPE2,SS,NTRI2)
CCC   IF(IPRNT.LE.4) GO TO 103
CCC   WRITE(6,6) ITYP,IABC
CCC   CALL PRINT(SS,NBATRI,NBASIS,6)
  103 CONTINUE
  104 CONTINUE
C
      CALL SREW(ITAP42)
      CALL SREW(JTAPE2)
      RETURN
      END
      SUBROUTINE ZTAMAT(ZTA,HH,TT)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION HH(NTRI),TT(NTRI)
      DIMENSION ZTA(NTRI,N3N,NTYPEP)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTF/ISA(150),IHA(150),IFA(150),IEA(150),IBA(150),IUA(150)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1,JTAPE2
      DATA HALF / 0.5D+00 /
    1 FORMAT(//,2X,' ZTA MATRIX'/)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE2)
C
C   FORM DERIVATIVE GENERALIZED LAGRANGIAN MATRICES
C   FOR NUCLEAR PERTURBATION
      CALL ZERO(ZTA,NTYPEP*N3N*NTRI)
      DO 104 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HH,NTRI2,IH)
      DO 103 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 102 I=1,NTRI
  102 ZTA(I,IABC,ITYP)=HH(I)*FAC
  103 CONTINUE
  104 CONTINUE
C
      DO 107 ITYP=1,NTYPES
      DO 106 IABC=1,N3N
      CALL SREAD(JTAPE2,TT,NTRI2)
      DO 105 I=1,NTRI
      ZTA(I,IABC,ITYP)=ZTA(I,IABC,ITYP)+TT(I)
  105 CONTINUE
  106 CONTINUE
  107 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1)
      CALL MATOUT(ZTA,NTRI,N3N*NTYPEP,NTRI,N3N*NTYPEP,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE2)
C
      RETURN
      END
      SUBROUTINE SCF3RD(F3A,F3M,UA,SA,DP,ZTA,ZETA,EA,EB,EC,
     1                  SAB,SBC,SCA,EAB,EBC,ECA,SSAB,SSBC,SSCA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION F3A(N3TOT),F3M(N3TOT),ZETA(NTRI,NTYPEP)
      DIMENSION SA(NTRI,N3N),UA(NBASIS,NBASIS,N3N)
      DIMENSION DP(NTRI,N3N,NTYPEP),ZTA(NTRI,N3N,NTYPEP)
      DIMENSION SAB(NTRI),SBC(NTRI),SCA(NTRI)
      DIMENSION EA(NBASIS,NBASIS),EB(NBASIS,NBASIS),EC(NBASIS,NBASIS)
      DIMENSION EAB(NBASIS,NBASIS),EBC(NBASIS,NBASIS),ECA(NBASIS,NBASIS)
      DIMENSION SSAB(NTRI),SSBC(NTRI),SSCA(NTRI)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SECTF/ISF(150),IHF(150),IFF(150),IEF(150),IBF(150),IUF(150)
      COMMON/SECTS/ISS(465),IES(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      COMMON/WORKS/JTAPE1,JTAPE2
      DATA A00,TWO,FOUR / 0.0D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' ZETA MATRIX, ITYP = ',I5/)
    2 FORMAT(2X,4I5,5X,10F10.6)
C
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
      IF(IPRNT.LE.3) GO TO 101
      WRITE(6,1) ITYP
      CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      LABC=0
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
      IEA=IEF(IABC)
      CALL RREAD(ITAP44,EA,NBASQ,IEA)
C
C:::B COORDINATE:::
      DO 120 JABC=1,IABC
      IEB=IEF(JABC)
      CALL RREAD(ITAP44,EB,NBASQ,IEB)
      IJABC=IOFF(IABC)+JABC
      ISAB=ISS(IJABC)
      IEAB=IES(IJABC)
      ILAB=ILS(IJABC)
      CALL RREAD(JTAPE1,SAB,NTRI2,ISAB)
      CALL RREAD(JTAPE1,EAB,NBASQ,IEAB)
      CALL RREAD(JTAPE1,SSAB,NTRI2,ILAB)
C
C:::C COORDINATE:::
      DO 110 KABC=1,JABC
      LABC=LABC+1
C
      IEC=IEF(KABC)
      CALL RREAD(ITAP44,EC,NBASQ,IEC)
      JKABC=IOFF(JABC)+KABC
      ISBC=ISS(JKABC)
      IEBC=IES(JKABC)
      ILBC=ILS(JKABC)
      CALL RREAD(JTAPE1,SBC,NTRI2,ISBC)
      CALL RREAD(JTAPE1,EBC,NBASQ,IEBC)
      CALL RREAD(JTAPE1,SSBC,NTRI2,ILBC)
C
      IKABC=IOFF(IABC)+KABC
      ISCA=ISS(IKABC)
      IECA=IES(IKABC)
      ILCA=ILS(IKABC)
      CALL RREAD(JTAPE1,SCA,NTRI2,ISCA)
      CALL RREAD(JTAPE1,ECA,NBASQ,IECA)
      CALL RREAD(JTAPE1,SSCA,NTRI2,ILCA)
C
      VALU1=F3A(LABC)
      VALU2=A00
      VALU3=A00
      VALU4=A00
      VALU5=A00
      VALU6=A00
      VALU7=A00
      VALU8=A00
C
      DO 107 I=1,NBASIS
      ITYP=MOTYP(I)
      DO 107 J=1,NBASIS
      JTYP=MOTYP(J)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU2=VALU2+UA(I,J,IABC)*EBC(J,I)
     1           +UA(I,J,JABC)*ECA(J,I)
     2           +UA(I,J,KABC)*EAB(J,I)
      VALU3=VALU3+SSAB(IJ)*EC(I,J)
     1           +SSBC(IJ)*EA(I,J)
     2           +SSCA(IJ)*EB(I,J)
      VALU4=VALU4+SSAB(IJ)*DP(IJ,KABC,ITYP)
     1           +SSBC(IJ)*DP(IJ,IABC,ITYP)
     2           +SSCA(IJ)*DP(IJ,JABC,ITYP)
      VAL1=A00
      VAL2=A00
      VAL3=A00
      DO 106 K=1,NBASIS
      KTYP=MOTYP(K)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      VALU5=VALU5+ZTA(IJ,IABC,KTYP)*UA(I,K,JABC)*UA(J,K,KABC)
     1           +ZTA(IJ,JABC,KTYP)*UA(I,K,KABC)*UA(J,K,IABC)
     2           +ZTA(IJ,KABC,KTYP)*UA(I,K,IABC)*UA(J,K,JABC)
      FAC1=(UA(J,K,KABC)*TWO+UA(K,J,KABC))*ZETA(IK,ITYP)
     1     +UA(K,I,KABC)*ZETA(JK,ITYP)
      FAC2=(UA(J,K,IABC)*TWO+UA(K,J,IABC))*ZETA(IK,ITYP)
     1     +UA(K,I,IABC)*ZETA(JK,ITYP)
      FAC3=(UA(J,K,JABC)*TWO+UA(K,J,JABC))*ZETA(IK,ITYP)
     1     +UA(K,I,JABC)*ZETA(JK,ITYP)
 
      VALU6=VALU6+SSAB(IJ)*FAC1+SSBC(IJ)*FAC2+SSCA(IJ)*FAC3
      VALU7=VALU7+UA(I,K,IABC)*UA(J,K,JABC)*DP(IJ,KABC,KTYP)
     1           +UA(I,K,JABC)*UA(J,K,KABC)*DP(IJ,IABC,KTYP)
     2           +UA(I,K,KABC)*UA(J,K,IABC)*DP(IJ,JABC,KTYP)
      DO 105 L=1,NBASIS
      VAL1=VAL1+UA(K,L,IABC)*(SA(IK,JABC)*UA(L,J,KABC)
     1                       +SA(IK,KABC)*UA(L,J,JABC))
      VAL2=VAL2+UA(K,L,JABC)*(SA(IK,KABC)*UA(L,J,IABC)
     1                       +SA(IK,IABC)*UA(L,J,KABC))
      VAL3=VAL3+UA(K,L,KABC)*(SA(IK,IABC)*UA(L,J,JABC)
     1                       +SA(IK,JABC)*UA(L,J,IABC))
  105 CONTINUE
  106 CONTINUE
      VALU8=VALU8+(VAL1+VAL2+VAL3)*ZETA(IJ,ITYP)
  107 CONTINUE
C
      VALU2=VALU2*FOUR
      VALU3=-VALU3*TWO
      VALU4=-VALU4*TWO
      VALU5=VALU5*FOUR
      VALU6=-VALU6*TWO
      VALU7=VALU7*FOUR
      VALU8=VALU8*FOUR
      VALUT=VALU2+VALU3+VALU4+VALU5+VALU6+VALU7+VALU8
      F3M(LABC)=VALUT
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,2) IABC,JABC,KABC,LABC,VALU1,VALU2,VALU3,VALU4,VALU5,
     1           VALU6,VALU7,VALU8,VALUT
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE SCFADD(EAO,F3A,F3M,F3X,UA,U,T,FDER,LABEL,XINT,DP,DQ,DM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EAO(NBFAO,NBASIS),UA(NBASIS,NBASIS,N3N)
      DIMENSION F3A(N3TOT),F3M(N3TOT),F3X(N3TOT)
      DIMENSION U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION FDER(N3N,IUNIT),LABEL(IUNIT4),XINT(N3N)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,NATRI,NTYPEP)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SAVES/IUNIT,IUNIT4,INTOT,ISAVE(500,4)
      COMMON/SECTS/ISS(465),IES(465),ILS(465),IBS(465),IUS(465)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX IN SCFADD'/)
    2 FORMAT(2X,4I5,10F10.6)
C
C   FORM DENSITY LIKE MATRIX IN AO BASIS
      CALL PCMAT(UA,DP,DQ,EAO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   DP AND DQ ARRAYS CONTAIN DENSITY LIKE MATRIX IN SO BASIS
C   DM ARRAY STORES HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PDDMAT(FDER,LABEL,XINT,DP,DQ,DM)
C
C   FINAL TRANSFORMATION
      DO 101 IABC=1,NATRI
      DO 101 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DM(1,IABC,ITYP),NTRI,EAO,U,T)
  101 CONTINUE
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DM,NTRI,NATRI*NTYPEP,NTRI,NATRI*NTYPEP,6)
C
  201 CONTINUE
      LABC=0
C
C:::A COORDINATE:::
      DO 130 IABC=1,N3N
C
C:::B COORDINATE:::
      DO 120 JABC=1,IABC
      IJABC=IOFF(IABC)+JABC
C
C:::C COORDINATE:::
      DO 110 KABC=1,JABC
      LABC=LABC+1
C
      JKABC=IOFF(JABC)+KABC
      IKABC=IOFF(IABC)+KABC
C
      VALU1=A00
      VALU2=A00
      VALU3=A00
C
      DO 102 I=1,NBASIS
      DO 102 J=1,NBASIS
      JTYP=MOTYP(J)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU1=VALU1+UA(I,J,IABC)*DM(IJ,JKABC,JTYP)
      VALU2=VALU2+UA(I,J,JABC)*DM(IJ,IKABC,JTYP)
      VALU3=VALU3+UA(I,J,KABC)*DM(IJ,IJABC,JTYP)
  102 CONTINUE
C
      VALU1=VALU1*TWO
      IF(JABC.EQ.KABC) VALU1=VALU1*TWO
      VALU2=VALU2*TWO
      IF(IABC.EQ.KABC) VALU2=VALU2*TWO
      VALU3=VALU3*TWO
      IF(IABC.EQ.JABC) VALU3=VALU3*TWO
      VALUT=VALU1+VALU2+VALU3
      F3M(LABC)=F3M(LABC)+VALUT
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,2) IABC,JABC,KABC,LABC,VALU1,VALU2,VALU3,VALUT
C
  110 CONTINUE
  120 CONTINUE
  130 CONTINUE
C
C   SUM UP BOTH CONTRIBUTIONS
      DO 140 I=1,N3TOT
  140 F3X(I)=F3A(I)+F3M(I)
C
  210 CONTINUE
      RETURN
      END
      SUBROUTINE PDDMAT(FDER,LABEL,XINT,DP,DQ,DM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION FDER(N3N,IUNIT),LABEL(IUNIT4),XINT(N3N)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,NATRI,NTYPEP)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NATOMS,N3N,NATRI,N3TOT
      COMMON/SAVES/IUNIT,IUNIT4,INTOT,ISAVE(500,4)
      COMMON/SIGNS/IOFF(466),IPRNT
      COMMON/TAPES/ITAP37,ITAP38,ITAP42,ITAP44,ITAP45,ITAP46
      DATA TWO / 2.0D+00 /
      DATA XLIMIT / 1.0D-10 /
    1 FORMAT(2X,7I4,10F10.5)
    2 FORMAT(//,2X,' DM MATRIX IN PDDMAT'/)
C
      ISIZED=IUNIT*N3N*2
      ISIZEL=IUNIT4
C
C   FORM DM MATRICES
      CALL ZERO(DM,NATRI*NTYPEP*NTRI)
C
      DO 800 IBUF=1,INTOT
      IPLUS=IBUF+1
      ISECD=ISAVE(IPLUS,1)
      CALL RREAD(ITAP38,FDER,ISIZED,ISECD)
      ISECL=ISAVE(IPLUS,4)
      CALL RREAD(ITAP38,LABEL,ISIZEL,ISECL)
      DO 700 JBUF=1,IUNIT
      JX=(JBUF-1)*4
      II=LABEL(JX+1)
      JJ=LABEL(JX+2)
      KK=LABEL(JX+3)
      LL=LABEL(JX+4)
      IF(II.EQ.0) GO TO 700
      DO 102 I=1,N3N
  102 XINT(I)=FDER(I,JBUF)
      CALL PACK(II,JJ,KK,LL,IX,JA,JAA)
      IF(IPRNT.LE.5) GO TO 401
      WRITE(6,1) IBUF,JBUF,II,JJ,KK,LL,IX,(XINT(I),I=1,N3N)
  401 CONTINUE
      GO TO (301,302,303,304,305,306,307,308,309,310,311,312,312,312),IX
C
C (II/II)   1=(11/11)
C
  301 IIII=IOFF(II+1)
      DO 113 ITYP=1,NTYPES
      DO 112 IABC=1,N3N
      DO 111 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 111
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC,ITYP)=DM(IIII,KABC,ITYP)
     *                 +(DP(IIII,IABC,ITYP)+DQ(IIII,IABC,ITYP))*VALU
  111 CONTINUE
  112 CONTINUE
  113 CONTINUE
      GO TO 600
C
C  (II/KK)  2=(22/11)
C
  302 IIII=IOFF(II+1)
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      DO 123 ITYP=1,NTYPES
      DO 122 IABC=1,N3N
      DO 121 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 121
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC,ITYP)=DM(IIII,KABC,ITYP)+DP(KKKK,IABC,ITYP)*VALU
      DM(KKKK,KABC,ITYP)=DM(KKKK,KABC,ITYP)+DP(IIII,IABC,ITYP)*VALU
      DM(IIKK,KABC,ITYP)=DM(IIKK,KABC,ITYP)+DQ(IIKK,IABC,ITYP)*VALU
  121 CONTINUE
  122 CONTINUE
  123 CONTINUE
      GO TO 600
C
C  (II/IL)  3=(22/21)
C
  303 IIII=IOFF(II+1)
      IILL=IOFF(II)+LL
      DO 133 ITYP=1,NTYPES
      DO 132 IABC=1,N3N
      DO 131 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 131
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC,ITYP)=DM(IIII,KABC,ITYP)
     *                 +(DP(IILL,IABC,ITYP)+DQ(IILL,IABC,ITYP))*VAL2
      DM(IILL,KABC,ITYP)=DM(IILL,KABC,ITYP)
     *                 +(DP(IIII,IABC,ITYP)+DQ(IIII,IABC,ITYP))*VALU
  131 CONTINUE
  132 CONTINUE
  133 CONTINUE
      GO TO 600
C
C  (IJ/JJ)  4=(21/11)
C
  304 IIJJ=IOFF(II)+JJ
      JJJJ=IOFF(JJ+1)
      DO 143 ITYP=1,NTYPES
      DO 142 IABC=1,N3N
      DO 141 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 141
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)
     *            +(DP(JJJJ,IABC,ITYP)+DQ(JJJJ,IABC,ITYP))*VALU
      DM(JJJJ,KABC,ITYP)=DM(JJJJ,KABC,ITYP)
     *            +(DP(IIJJ,IABC,ITYP)+DQ(IIJJ,IABC,ITYP))*VAL2
  141 CONTINUE
  142 CONTINUE
  143 CONTINUE
      GO TO 600
C
C  (IJ/IJ)  5=(21/21)
C
  305 IIJJ=IOFF(II)+JJ
      IIII=IOFF(II+1)
      JJJJ=IOFF(JJ+1)
      DO 153 ITYP=1,NTYPES
      DO 152 IABC=1,N3N
      DO 151 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 151
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)
     *                 +(DP(IIJJ,IABC,ITYP)*TWO+DQ(IIJJ,IABC,ITYP))*VALU
      DM(IIII,KABC,ITYP)=DM(IIII,KABC,ITYP)+DQ(JJJJ,IABC,ITYP)*VALU
      DM(JJJJ,KABC,ITYP)=DM(JJJJ,KABC,ITYP)+DQ(IIII,IABC,ITYP)*VALU
  151 CONTINUE
  152 CONTINUE
  153 CONTINUE
      GO TO 600
C
C  (II/KL)  6=(33/21)
C
  306 IIII=IOFF(II+1)
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      DO 163 ITYP=1,NTYPES
      DO 162 IABC=1,N3N
      DO 161 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 161
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIII,KABC,ITYP)=DM(IIII,KABC,ITYP)+DP(KKLL,IABC,ITYP)*VAL2
      DM(KKLL,KABC,ITYP)=DM(KKLL,KABC,ITYP)+DP(IIII,IABC,ITYP)*VALU
      DM(IIKK,KABC,ITYP)=DM(IIKK,KABC,ITYP)+DQ(IILL,IABC,ITYP)*VALU
      DM(IILL,KABC,ITYP)=DM(IILL,KABC,ITYP)+DQ(IIKK,IABC,ITYP)*VALU
  161 CONTINUE
  162 CONTINUE
  163 CONTINUE
      GO TO 600
C
C  (IJ/KK)  7=(32/11)
C
  307 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=IOFF(JJ)+KK
      DO 173 ITYP=1,NTYPES
      DO 172 IABC=1,N3N
      DO 171 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 171
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)+DP(KKKK,IABC,ITYP)*VALU
      DM(KKKK,KABC,ITYP)=DM(KKKK,KABC,ITYP)+DP(IIJJ,IABC,ITYP)*VAL2
      DM(IIKK,KABC,ITYP)=DM(IIKK,KABC,ITYP)+DQ(JJKK,IABC,ITYP)*VALU
      DM(JJKK,KABC,ITYP)=DM(JJKK,KABC,ITYP)+DQ(IIKK,IABC,ITYP)*VALU
  171 CONTINUE
  172 CONTINUE
  173 CONTINUE
      GO TO 600
C
C  (IJ/KK)  8=(31/22)
C
  308 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=JJ+IOFF(KK)
      DO 183 ITYP=1,NTYPES
      DO 182 IABC=1,N3N
      DO 181 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 181
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)+DP(KKKK,IABC,ITYP)*VALU
      DM(KKKK,KABC,ITYP)=DM(KKKK,KABC,ITYP)+DP(IIJJ,IABC,ITYP)*VAL2
      DM(IIKK,KABC,ITYP)=DM(IIKK,KABC,ITYP)+DQ(JJKK,IABC,ITYP)*VALU
      DM(JJKK,KABC,ITYP)=DM(JJKK,KABC,ITYP)+DQ(IIKK,IABC,ITYP)*VALU
  181 CONTINUE
  182 CONTINUE
  183 CONTINUE
      GO TO 600
C
C (IJ/IL)  9=(32/31)
C
  309 IIJJ=IOFF(II)+JJ
      IILL=IOFF(II)+LL
      IIII=IOFF(II+1)
      JJLL=IOFF(JJ)+LL
      DO 193 ITYP=1,NTYPES
      DO 192 IABC=1,N3N
      DO 191 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 191
      VAL2=VALU+VALU
      VAL3=VAL2+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)
     *                 +(DP(IILL,IABC,ITYP)*TWO+DQ(IILL,IABC,ITYP))*VALU
      DM(IILL,KABC,ITYP)=DM(IILL,KABC,ITYP)
     *                 +(DP(IIJJ,IABC,ITYP)*TWO+DQ(IIJJ,IABC,ITYP))*VALU
      DM(IIII,KABC,ITYP)=DM(IIII,KABC,ITYP)+DQ(JJLL,IABC,ITYP)*VAL2
      DM(JJLL,KABC,ITYP)=DM(JJLL,KABC,ITYP)+DQ(IIII,IABC,ITYP)*VALU
  191 CONTINUE
  192 CONTINUE
  193 CONTINUE
      GO TO 600
C
C  (IJ/JL)  10=(32/21)
C
  310 IIJJ=IOFF(II)+JJ
      JJLL=IOFF(JJ)+LL
      IILL=IOFF(II)+LL
      JJJJ=IOFF(JJ+1)
      DO 203 ITYP=1,NTYPES
      DO 202 IABC=1,N3N
      DO 201 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 201
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)
     *                 +(DP(JJLL,IABC,ITYP)*TWO+DQ(JJLL,IABC,ITYP))*VALU
      DM(JJLL,KABC,ITYP)=DM(JJLL,KABC,ITYP)
     *                 +(DP(IIJJ,IABC,ITYP)*TWO+DQ(IIJJ,IABC,ITYP))*VALU
      DM(IILL,KABC,ITYP)=DM(IILL,KABC,ITYP)+DQ(JJJJ,IABC,ITYP)*VALU
      DM(JJJJ,KABC,ITYP)=DM(JJJJ,KABC,ITYP)+DQ(IILL,IABC,ITYP)*VAL2
  201 CONTINUE
  202 CONTINUE
  203 CONTINUE
      GO TO 600
C
C  (IJ/KJ)  11=(31/21)
C
  311 IIJJ=IOFF(II)+JJ
      JJKK=JJ+IOFF(KK)
      IIKK=IOFF(II)+KK
      JJJJ=IOFF(JJ+1)
      DO 213 ITYP=1,NTYPES
      DO 212 IABC=1,N3N
      DO 211 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 211
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)
     *                 +(DP(JJKK,IABC,ITYP)*TWO+DQ(JJKK,IABC,ITYP))*VALU
      DM(JJKK,KABC,ITYP)=DM(JJKK,KABC,ITYP)
     *                 +(DP(IIJJ,IABC,ITYP)*TWO+DQ(IIJJ,IABC,ITYP))*VALU
      DM(IIKK,KABC,ITYP)=DM(IIKK,KABC,ITYP)+DQ(JJJJ,IABC,ITYP)*VALU
      DM(JJJJ,KABC,ITYP)=DM(JJJJ,KABC,ITYP)+DQ(IIKK,IABC,ITYP)*VAL2
  211 CONTINUE
  212 CONTINUE
  213 CONTINUE
      GO TO 600
C
C  (IJ/KL)  12=(43/21)
C  (IJ/KL)  13=(42/31)
C  (IJ/KL)  14=(41/32)
C
  312 CONTINUE
      IIJJ=IOFF(II)+JJ
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      JJLL=IOFF(MAX0(JJ,LL))+MIN0(JJ,LL)
      JJKK=IOFF(MAX0(JJ,KK))+MIN0(JJ,KK)
      DO 223 ITYP=1,NTYPES
      DO 222 IABC=1,N3N
      DO 221 JABC=1,N3N
      VALU=XINT(JABC)
      IF(DABS(VALU).LT.XLIMIT) GO TO 221
      VAL2=VALU+VALU
      KABC=IOFF(MAX0(IABC,JABC))+MIN0(IABC,JABC)
      DM(IIJJ,KABC,ITYP)=DM(IIJJ,KABC,ITYP)+DP(KKLL,IABC,ITYP)*VAL2
      DM(KKLL,KABC,ITYP)=DM(KKLL,KABC,ITYP)+DP(IIJJ,IABC,ITYP)*VAL2
      DM(IIKK,KABC,ITYP)=DM(IIKK,KABC,ITYP)+DQ(JJLL,IABC,ITYP)*VALU
      DM(JJLL,KABC,ITYP)=DM(JJLL,KABC,ITYP)+DQ(IIKK,IABC,ITYP)*VALU
      DM(IILL,KABC,ITYP)=DM(IILL,KABC,ITYP)+DQ(JJKK,IABC,ITYP)*VALU
      DM(JJKK,KABC,ITYP)=DM(JJKK,KABC,ITYP)+DQ(IILL,IABC,ITYP)*VALU
  221 CONTINUE
  222 CONTINUE
  223 CONTINUE
C
  600 CONTINUE
  700 CONTINUE
  800 CONTINUE
C
      CALL SREW(ITAP38)
C
      IF(IPRNT.LE.3) GO TO 405
      WRITE(6,2)
      CALL MATOUT(DM,NTRI,NATRI*NTYPEP,NTRI,NATRI*NTYPEP,6)
C
  405 CONTINUE
      RETURN
      END
      SUBROUTINE MOCONS(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(466),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBASIS
      DO 101 JJ=1,NBASIS
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      T(II,JJ)=SA(IIJJ)
  101 CONTINUE
      DO 103 II=1,NBASIS
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBASIS
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBASIS
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE MOCONV(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NBFAO,NTRI,NTRI2,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(466),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBFAO
      DO 101 JJ=1,NBFAO
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      SAX=SA(IIJJ)
      T(II,JJ)=SAX
  101 CONTINUE
      DO 103 II=1,NBFAO
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBFAO
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBFAO
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE SCLMPY(A,NDIM,SCALE)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(1)
C
      DO 101 I=1,NDIM
      A(I)=A(I)*SCALE
  101 CONTINUE
C
      RETURN
      END
