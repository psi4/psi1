&TRACE ERR
*GLOBAL TXTLIB CMSLIB VEMLLIB VFORTLIB UTILITY
*GLOBAL LOADLIB VFLODLIB
EXEC HFSINIT
C  THIS EXEC RUNS SCFGRAD$ IN A LOOP, UP TO "&ITERATE" NUMBER OF TIMES,
C  OVER GEOMETRIES WHICH ARE IN FILES OF FILETYPE "INFIL1", "INFIL2",
C  ... "INFIL&ITERATE".  THIS EXEC IS SPECIFICALLY DESIGNED TO GENERATE
C  A "FILE11" FULL OF GRADIENTS FOR FINITE-DISPLACEMENT APPLICATIONS.
C  IN ORDER TO REDUCE SCF ITERATIONS, THE UNPERTURBED OR STARTING
C  VECTOR SHOULD BE IN A FILE OF FILETYPE "OPTVEC30".  ALL OF THE
C  OUTPUTS ARE COLLECTED INTO A FILE WITH FILETYPE "TOTALOUT &TMP1".
C  IT IS BEST TO COPY AND MODIFY THIS EXEC TO SUIT YOUR OWN NEEDS.
&A = GEOMFREQ
&B = ******************
&C = &STRING OF &B  &A  &B  &1  &B
&TYPE &C
&IF &N < 2  &GOTO -JOBSTART
&TMP1 = &CONCAT OF &2 1
&TMP2 = &CONCAT OF &2 2
&TMP4 = &CONCAT OF &2 4
-JOBSTART
CP QUERY TIME
&ITERATE = 10
&ITERNUM = 1
&LOOP -ENDITER  &ITERATE
&INFIL = &CONCAT OF INFIL &ITERNUM
&ITERNUM = &ITERNUM + 1
&FILENAM = &STRING OF   &1  &INFIL  A
&TYPE &FILENAM
&TYPE
ERASE  &1 FILE30 A4
COPYFILE &1 OPTVEC30 A4 = FILE30 =
FI  3 DISK &1 CHECK  A  (LRECL 132 BLOCK 132
FI  5 DISK &1 INPUT  A
FI  6 DISK &1 FILE6  A  (LRECL 132 BLOCK 132
FI 14 DISK &1 &INFIL A
LOAD GEOM IOMR NOUNFL (START CLEAR
EXEC SCFGRAD$ &1
STATE  &1 TOTALOUT &TMP1
&IF &RETCODE = 0  &GOTO -NEXTCOPY
COPYFILE   &1 OUTPUT A   = TOTALOUT &TMP1
&GOTO  -ENDITER
-NEXTCOPY
COPYFILE &1 OUTPUT A = TOTALOUT &TMP1 (APPEND
-ENDITER
&EXIT
