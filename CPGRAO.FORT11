      SUBROUTINE FENTRY(CC,IA,MAXCOR)
C                ( "B" FOR BIG NON-CORE CASES )
C   COUPLED PERTURBED HARTREE-FOCK PROGRAM
C   FOR THE GENERAL OPEN-SHELL SYSTEM IN AO BASIS
C**********************************************************************
C*   NOTICE OF PROGRAM MODIFICATION                                   *
C**********************************************************************
C*  MODIFICATION FOR FENTRY                                           *
C*  BY:  YUKIO YAMAGUCHI                                              *
C*  DATE:  FEBRUARY 21, 1989                                          *
C**********************************************************************
C*  MODIFICATION FOR IMS VERSION                                      *
C*  BY:  YUKIO YAMAGUCHI                                              *
C*  DATE:  FEBRUARY 01, 1989                                          *
C**********************************************************************
C*  BY:  RICHARD REMINGTON                         search:  c8-08-88  *
C*  BY:  RICHARD REMINGTON                         search:  c3-24-88  *
C*  DATE:  August  8,  1988                                           *
C*  DATE:  MARCH  24,  1988                                           *
C*  REASON: DECREASE CORE TO RUN IN 7MB ON 9370                       *
C**********************************************************************
C*  LAST UPDATED ON FEB 13, 1987 BY RICHARD REMINGTON                 *
C*  CHANGE THE LARGE CASE "CPGRAOB" VERSION TO THE STD. "CPGRAO" AND  *
C*  INCREASE MAXVEC,MAXVX FROM 500,12 TO 600,20 AND                   *
C*  INCREASE CC(1200000) TO CC(1750000) AND MAXCOR TO 1750000         *
C*  AND ZERO OUT CC USING VXINIT                                      *
C**********************************************************************
C*  LAST UPDATED ON JULY 19, 1985 BY YUKIO YAMAGUCHI                  *
C**********************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
c3-24-88  DIMENSION CC(1750000),IA(1)
c8-08-88  DIMENSION CC(775000),IA(1)
C2-16-89  DIMENSION CC(700000),IA(1)
      DIMENSION CC(MAXCOR),IA(1)
      CHARACTER*80 ALABEL
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/GVBAB/ALPA(10,10),BETA(10,10)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/MFSEC/MASTER,NSECT
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/SIZES/MAXVEC,MAXVX
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/TOLER/ICONV
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
C2-16-89 EQUIVALENCE (CC,IA)
      DATA ALABEL / ' SCF FIRST DERIVATIVE BROUGHT YOU BY Y & Y CO. EST.
     1 IN MARCH , 1982             ' /
      DATA A00,QURT,HALF,ONE / 0.0D+00 , 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' COUPLED PERTURBED HARTREE-FOCK PROGRAM FOR GENERAL
     1OPEN-SHELL SCF IN AO BASIS (VERSION OF 2/01/89)'/)
    2 FORMAT(8I5)
    3 FORMAT(2X,10I5)
    4 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' NBASIS = ',I8/
     * 2X,' NTRI   = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NBATRI = ',I8/
     * 2X,' NATOMS = ',I8/
     * 2X,' N3N    = ',I8/
     * 2X,' N3NP3  = ',I8/
     * 2X,' N3NX3  = ',I8/
     * 2X,' N3NXX  = ',I8/
     * 2X,' IOCC   = ',I8/
     * 2X,' JOCC   = ',I8/
     * 2X,' KOCC   = ',I8)
    5 FORMAT(2X,' NTYPES = ',I8/
     * 2X,' NTYPEP = ',I8/
     * 2X,' IPOL   = ',I8/
     * 2X,' ICONV  = ',I8/
     * 2X,' IPRNT  = ',I8/)
    6 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
    7 FORMAT(//,2X,' ALPA MATRIX'/)
    8 FORMAT(//,2X,' BETA MATRIX'/)
    9 FORMAT(//,2X,' SCF EIGENVECTOR (SO BASIS) MATRIX'/)
   10 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   11 FORMAT(//,2X,' REQUIRED MEMORY EXCEEDS MAXCOR'/
     1          2X,' ICMAX = ',I7,5X,' MAXCOR = ',I7/)
C
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C   REFERENCE :                                            :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, D.J.FOX, M.A.VINCENT,  :
C   AND H.F.SCHAFER III, J.MOL.STRUC. 103,183 (1983)       :
C:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
C   ITAP11 = GEOMETRY AND GRADIENT
C   ITAP15 = SCF SECOND DERIVATIVE
C   ITAP36 = TWO ELECTRON AO INTEGRALS
C   ITAP42 = FIRST AND SECOND DERIVATIVE INFORMATION
C   ITAP43 = DERIVATIVE DIPOLE MOMENTS
C   ITAP44 = FIRST DERIVATIVES AND U'S
C   JTAPE1 = WORKING TAPE FOR THE CPHF PROCEDURE
C
      CALL TSTART(6)
      CALL NOUNFL
      CALL INITMF(1)
C
C::::::::::::::::::::::::::::::::::::::::
C:::DEFINE MACHINE-DEPENDENT CONSTANTS:::
C::::::::::::::::::::::::::::::::::::::::
c3-24-88  MAXCOR=1750000
c8-08-88  MAXCOR=775000
C2-16-89  MAXCOR= 700000
      MAXVEC=    600
      MAXVX =     20
C
C     ZERO OUT CORE WITH AN INITIALIZATION OF CC TO "0"
C
CCC   MAXBYT = MAXCOR * 8
CCC   CALL VXINIT(CC,0,MAXBYT)
C
      ITAPE3=3
      INPUT=5
      ITAPE6=6
      ITAP11=11
      ITAP15=15
      ITAP36=36
      ITAP42=42
      ITAP43=43
      ITAP44=44
      JTAPE1=91
C
      CALL LOCATE(INPUT,'# CPHFAO #',IERR)
C
C   LOCATION OF MATRICES IN ITAP44
C   (1) SA (N3NXX*NTRIL)
C   (2) HA (N3NXX*NTRIL)
C   (3) TA AND FA (N3NXX*NTRIL)
C   (4) EA (N3NXX*NBASQL)
C   (5) BA (N3NXX*NTRIL)
C   (6) UA (N3NXX*NBASQL)
C
      IOFF(1)=0
      DO 101 I=1,255
  101 IOFF(I+1)=IOFF(I)+I
C
      WRITE(6,1)
      WRITE(3,1)
C
C   READ IN PARAMETERS
      READ(5,2) IPOL,ICONV,IPRNT
C
C   READ IN SCF INFORMATION
      NBASIS=LPARA(3)
      NBFAO=LPARA(11)
      NTRI=LPARA(4)
      NTRI2=NTRI*2
      NBATRI=IOFF(NBFAO+1)
      NBATR2=NBATRI*2
      IOCC=LPARA(7)
      KOCC=LPARA(8)
      JOCC=LPARA(9)
      NATOMS=LPARA(10)
      N3N=LPARA(17)
      NATRI=IOFF(N3N+1)
      N3NP3=N3N+3
      N3NX3=N3N*3
      N3NXX=N3NP3
      IF(IPOL.NE.0) N3NXX=N3N
      NTRIL=((NTRI2-1)/NSECT+1)
      NBASQ=NBASIS*NBASIS*2
      NBASQL=((NBASQ-1)/NSECT+1)
      N3TRL=N3NXX*NTRIL
      N3QRL=N3NXX*NBASQL
      NTYPES=LPARA(18)
      NTYPEP=NTYPES+1
      DO 102 I=1,N3NXX
      ISA(I)=(I-1)*NTRIL+1
      IHA(I)=(I-1)*NTRIL+N3TRL+1
      IFA(I)=(I-1)*NTRIL+N3TRL+N3TRL+1
      IEA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+1
      IBA(I)=(I-1)*NTRIL+N3TRL+N3TRL+N3TRL+N3QRL+1
      IUA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+N3QRL+N3TRL+1
      WRITE(3,3) I,ISA(I),IHA(I),IFA(I),IEA(I),IBA(I),IUA(I)
  102 CONTINUE
      ENUC=APARA(1)
      ESCF=APARA(2)
      DO 103 I=1,10
      NSORB(I)=LPARA(I+40)
      FOCC(I)=APARA(I+40)
  103 CONTINUE
      WRITE(6,4) NBASIS,NTRI,NBFAO,NBATRI,NATOMS,N3N,N3NP3,N3NX3,N3NXX,
     1 IOCC,JOCC,KOCC
      WRITE(6,5) NTYPES,NTYPEP,IPOL,ICONV,IPRNT
C
C   READ IN AO-MO EIGENVECTORS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      CALL MREAD(CC(IC1),17)
      CALL MREAD(CC(IC2),18)
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,6)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C   CALCULATE ALPA AND BETA MATRICES
  301 CONTINUE
      CALL ZERO(ALPA,NTYPEP*NTYPEP)
      CALL ZERO(BETA,NTYPEP*NTYPEP)
      IJ=0
      DO 105 I=1,NTYPES
      DO 105 J=1,I
      IJ=IJ+1
      ALPA(I,J)= (ONE-APARA(10+IJ))*FOCC(I)*FOCC(J)*HALF
      BETA(I,J)=-(ONE-APARA(25+IJ))*FOCC(I)*FOCC(J)*QURT
      IF(I.EQ.J) GO TO 105
      ALPA(J,I)=ALPA(I,J)
      BETA(J,I)=BETA(I,J)
  105 CONTINUE
C     IF(IPRNT.LE.2) GO TO 302
      WRITE(6,7)
      CALL MATOUT(ALPA,10,10,NTYPEP,NTYPEP,6)
      WRITE(6,8)
      CALL MATOUT(BETA,10,10,NTYPEP,NTYPEP,6)
C
C**************************************
C***PREPARATION FOR FIRST ORDER CPHF***
C**************************************
C
C   FORM REGISTER MATRIX
  302 CONTINUE
      WRITE(3,20)
   20 FORMAT(//,2X,' NOW YOU ARE IN REGIST'/)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC4=IC3+NBFAO*NBASIS
      IA4=IC4+IC4-1
      IC5=IC4+NTRI
      IA5=IC5+IC5-1
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C.................NIJ     KIJ.....
      CALL REGIST(IA(IA4),IA(IA5))
C
C   CHECK SIZE OF THE CORE MEMORY
      CALL CORSIZ(MAXCOR,N3NXX,NADD,MAXVC,IPOL)
C
C   READ IN FIRST AND SECOND DERIVATIVE INFORMATION
C   AND FORM SM, HM AND FM MATRICES
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC6=IC5+NTRI
      IC7=IC6+N3N
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC8=IC7+N3N*N3N
      IC9=IC8+NBATRI
      IC10=IC9+NBFAO*NBFAO
      ICMAX=IC10+NBFAO*NBFAO
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     E1A     E2A     SS      U       T........
      CALL DERMAT(CC(IC3),CC(IC6),CC(IC7),CC(IC8),CC(IC9),CC(IC10))
C
C   READ IN SO-MO EIGENVECTORS
      CALL MREAD(CC(IC3),19)
      IF(IPRNT.LE.2) GO TO 305
      WRITE(6,9)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C:::::::::::::::::::::::::::::::::::
C:::TEST ROUTINES FOR INPUT CHECK:::
C:::::::::::::::::::::::::::::::::::
  305 CONTINUE
C
C   CALCULATE SCF ENERGY FOR A TEST
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN E0CAL'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NTRI
      IC12=IC11+NBFAO*NBFAO
      IC13=IC12+NBFAO*NBFAO
      IC14=IC13+NTRI*NTYPES
      IC15=IC14+NTRI*NTYPES
      IC16=IC15+NTRI*NTYPES
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF2
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ESO     HSO     HMO     TM       U........
      CALL E0CAL(CC(IC2),CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................T        DP       DQ       DM       LBLI     BUFI.....
     1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),IA(IA16),CC(IC16))
C
C   CALCULATE SCF FIRST ENERGY DERIVATIVES
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN EADER'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+N3N
      IC10=IC9+N3N
      IC11=IC10+N3N
      IC12=IC11+N3N
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      ICMAX=IC15+NTRI*N3N*NTYPES
      WRITE(3,10) ICMAX,MAXCOR
C................OCC     D1O     D1T     D1W      E1T      SM.......
      CALL EADER(CC(IC2),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................HM       EPS      TM.......
     1           CC(IC13),CC(IC14),CC(IC15))
C
C   FORM DERIVATIVE LAGRANGIAN MATRIX
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN FAMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NBASIS*NBASIS
      ICMAX=IC11+NTRI*N3N*NTYPEP
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................HH      TT      EPA      FM.......
      CALL FAMAT(CC(IC8),CC(IC9),CC(IC10),CC(IC11))
C
C   CALCULATE BA MATRIX
C   FOR INDEPENDENT PAIRS
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN BAIND'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI*NTYPEP
      IC12=IC11+NTRI
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF2
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................ESO     NIJ     U       T       ZETA     B0.......
      CALL BAIND(CC(IC3),IA(IA4),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................EPA      DP       DQ       DM       SM.......
     1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC15),
C................LBLI     BUFI.....
     2           IA(IA16),CC(IC16))
      IF(IPRNT.LE.4) GO TO 308
      CALL WRBMAT(CC(IC15),N3N)
C
C   FOR DEPENDENT PAIRS
  308 CONTINUE
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN BADEP'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI
      IC13=IC12+NTRI*N3N
      IC14=IC13+NTRI*N3N
      IC15=IC14+NTRI*N3N
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF2
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     OCC     ESO     KIJ     U       T.......
      CALL BADEP(CC(IC1),CC(IC2),CC(IC3),IA(IA5),CC(IC8),CC(IC9),
C................B0       FF       DP       DQ       DM.......
     1           CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI.....
     2           IA(IA15),CC(IC15))
      IF(IPRNT.LE.4) GO TO 309
      CALL WRBMAT(CC(IC13),N3N)
C
C   FORM BF MATRIX
  309 CONTINUE
      IF(IPOL.NE.0) GO TO 310
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN BFMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI
      IC10=IC9+NTRI*NTYPES*3
      IC11=IC10+NBASIS*NBASIS
      ICMAX=IC11+NTRI
      WRITE(3,10) ICMAX,MAXCOR
C................NIJ     KIJ     HF      HM      EPF      BF.......
      CALL BFMAT(IA(IA4),IA(IA5),CC(IC8),CC(IC9),CC(IC10),CC(IC11))
      IF(IPRNT.LE.2) GO TO 310
      CALL WRBMAT(CC(IC11),N3NXX)
C
C********************************
C***FIRST ORDER CPHF PROCEDURE***
C********************************
C
  310 CONTINUE
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN CPHF'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI*NTYPEP
      IC12=IC11+NTRI
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTYPEP*(NTRI*NADD)
      IC16=IC15+NTYPEP*(NTRI*NADD)
      IC17=IC16+NTYPEP*(NTRI*NADD)
      IC18=IC17+MAXVC*(MAXVC+NADD)
      IC19=IC18+NIND*MAXVC*2
      IA19=IC19+IC19-1
      ICMAX=IC19+MAXBF2
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C...............ESO     NIJ     KIJ     U       A       T.......
      CALL CPHF(CC(IC3),IA(IA4),IA(IA5),CC(IC8),CC(IC8),CC(IC9),
C...............ZETA     B0       AA       TT       DP.......
     1          CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C...............DQ       DM       W        BB       LBLI     BUFI.....
     2          CC(IC15),CC(IC16),CC(IC17),CC(IC18),IA(IA19),CC(IC19),
C...........................
     3          MAXVC,NADD,N3NXX)
      IF(IPRNT.LE.4) GO TO 311
      CALL WRBMAT(CC(IC18),N3NXX)
C
C   CALCULATE DEPENDENT PAIRS OF U MATRIX
  311 CONTINUE
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN UCMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI*N3NXX
      IC13=IC12+NTRI*N3NXX
      IC14=IC13+NTRI*N3NXX
      IC15=IC14+NTRI
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF2
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     OCC     ESO     NIJ     KIJ     U.......
      CALL UCMAT(CC(IC1),CC(IC2),CC(IC3),IA(IA4),IA(IA5),CC(IC8),
C................T       B        DP       DQ       DM       DEL......
     1           CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI...........
     2           IA(IA15),CC(IC15),N3NXX)
      IF(IPRNT.LE.2) GO TO 312
      CALL WRBMAT(CC(IC13),N3NXX)
C
C   COMPLETE U MATRIX
  312 CONTINUE
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN UXMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI*N3NXX
      IC10=IC9+NTRI*N3NXX
      ICMAX=IC10+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
C................B       SM      U..............
      CALL UXMAT(CC(IC8),CC(IC9),CC(IC10),N3NXX)
C     CALL WRBMAT(CC(IC10),N3NXX)
C
C   CALCULATE SCF SECOND DERIVATIVES
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN WAMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBASIS*NBASIS
      IC10=IC9+NBASIS*NBASIS
      IC11=IC10+NBASIS*NBASIS
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF2
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................ESO     U       T       EPA      ZETA     WA.......
      CALL WAMAT(CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................SA       DP       DQ       DM       LBLI     BUFI.....
     1           CC(IC13),CC(IC14),CC(IC15),CC(IC13),IA(IA16),CC(IC16))
C
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN SCF2ND'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+N3N*N3N
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI*N3N
      IC12=IC11+NBASIS*NBASIS*N3N
      IC13=IC12+NBASIS*NBASIS
      ICMAX=IC13+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................E2A     E2M     E2P     SA       WA       EPA......
      CALL SCF2ND(CC(IC7),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................UA.......
     1            CC(IC13))
C
      IF(IPOL.NE.0) GO TO 320
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN DIPMO'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS*3
      WRITE(3,10) ICMAX,MAXCOR
C................OCC     DIP     UA......
      CALL DIPMO(CC(IC2),CC(IC8),CC(IC9))
C
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN POLAR'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS
      WRITE(3,10) ICMAX,MAXCOR
C................OCC     HF      UF......
      CALL POLAR(CC(IC2),CC(IC8),CC(IC9))
  320 CONTINUE
      GO TO 400
C
  399 WRITE(6,11) ICMAX,MAXCOR
  400 CONTINUE
C
      CALL RCLOSE(ITAP42,3)
      CALL RCLOSE(ITAP43,3)
      CALL RCLOSE(ITAP44,3)
      CALL RCLOSE(MASTER,3)
      CALL TSTOP(6)
C
      RETURN
C2-16-89 STOP
      END
      SUBROUTINE REGIST(NIJ,KIJ)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' REGISTERED NUMBERS'/
     1 2X,' KVIR = ',I5/
     2 2X,' NIND = ',I5/
     3 2X,' NDEP = ',I5/
     4 2X,' NDIF = ',I5)
    2 FORMAT(//,2X,' NIJ MATRIX'/2X,' NO.',5X,' NIJ(1)',5X,' NIJ(2)'/)
    3 FORMAT(2X,I4,4X,I5,7X,I5)
    4 FORMAT(//,2X,' KIJ MATRIX'/2X,' NO.',5X,' KIJ(1)',5X,' KIJ(2)'/)
    5 FORMAT(//,2X,' NO.',8X,' FOCC',6X,' NSORB',7X,' NSTR',7X,' NEND'/)
    6 FORMAT(2X,I4,3X,F10.5,7X,I5,7X,I5,7X,I5)
C
      KVIR=NBASIS-JOCC
C
      NSTR(1)=1
      NEND(1)=NSORB(1)
      DO 101 ITYP=2,NTYPEP
      NSTR(ITYP)=NEND(ITYP-1)+1
      NEND(ITYP)=NSTR(ITYP)+NSORB(ITYP)-1
  101 CONTINUE
      DO 103 ITYP=1,NTYPEP
      DO 102 I=NSTR(ITYP),NEND(ITYP)
  102 MOTYP(I)=ITYP
  103 CONTINUE
C
      II=0
      DO 105 ITYP=2,NTYPEP
      DO 105 JTYP=1,ITYP-1
      DO 104 I=NSTR(ITYP),NEND(ITYP)
      DO 104 J=NSTR(JTYP),NEND(JTYP)
      II=II+1
      NIJ(II,1)=I
      NIJ(II,2)=J
  104 CONTINUE
  105 CONTINUE
      NIND=II
C
      II=0
      DO 107 ITYP=1,NTYPEP
C*C   IF(NSORB(ITYP).EQ.1) GO TO 107
C*C   DO 106 I=NSTR(ITYP)+1,NEND(ITYP)
C*C   DO 106 J=NSTR(ITYP),I-1
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=NSTR(ITYP),I
      II=II+1
      KIJ(II,1)=I
      KIJ(II,2)=J
  106 CONTINUE
  107 CONTINUE
      NDEP=II
C
      NDIF=NTRI-NIND-NDEP
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1) KVIR,NIND,NDEP,NDIF
      WRITE(6,2)
      DO 108 I=1,NIND
  108 WRITE(6,3) I,NIJ(I,1),NIJ(I,2)
      WRITE(6,4)
      DO 109 I=1,NDEP
  109 WRITE(6,3) I,KIJ(I,1),KIJ(I,2)
      WRITE(6,5)
      DO 110 I=1,NTYPEP
  110 WRITE(6,6) I,FOCC(I),NSORB(I),NSTR(I),NEND(I)
C
  205 RETURN
      END
      SUBROUTINE CORSIZ(MAXCOR,N3NXX,NADD,MAXVC,IPOL)
      IMPLICIT REAL*8 (A-H,O-Z)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIZES/MAXVEC,MAXVX
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%'/
     1          2X,' %%%NOW YOU ARE IN CORSIZ%%%'/
     2          2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%'/)
    2 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
    3 FORMAT(//,2X,' *************************************************'/
     1          2X,' ***REQUIRED MEMORY EXCEEDS MAXCOR-----STOP !!!***'/
     2          2X,' *************************************************'/
     3          2X,' IGMAX = ',I10,5X,' MAXCOR = ',I10/)
    4 FORMAT(//,2X,' ::::::::::::::::::::::::::::::::::'/
     1          2X,' :::REQUIRED MEMORY FOR THIS RUN:::'/
     2          2X,' ::::::::::::::::::::::::::::::::::'/
     3          2X,' NADD  = ',I10,5X,' MAXVC  = ',I10/
     4          2X,' IGMAX = ',I10,5X,' MAXCOR = ',I10/)
    5 FORMAT(//,2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'/
     1          2X,' %%%NOW YOU ARE LEAVING CORSIZ%%%'/
     2          2X,' %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%'/)
C
C   THIS SUBROUTINE WILL CHECK THE MAXIMUM CORE MEMORY REQUIRED
C   FOR THE CALCULATION
C
C   DYNAMIC ALLOCATION FOR THE CORE MEMORY
C   1 : EIG        (NBASIS)
C   2 : OCC        (NBASIS)
C   3 : ESO & EAO  (NBFAO*NBASIS)
C   4 : E1A        (N3N)
C   5 : E2A        (N3N*N3N)
C   6 : NIJ        (NTRI)
C   7 : KIJ        (NTRI)
C
      WRITE(3,1)
      NADD=N3NXX*2
C
C   READ IN AO-MO EIGENVECTORS
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
C
C   FORM REGISTER MATRIX
      WRITE(3,20)
   20 FORMAT(//,2X,' NOW YOU ARE IN REGIST'/)
      IC4=IC3+NBFAO*NBASIS
      IA4=IC4+IC4-1
      IC5=IC4+NTRI
      IA5=IC5+IC5-1
C.................NIJ     KIJ.....
C***  CALL REGIST(IA(IA4),IA(IA5))
C
C   READ IN FIRST AND SECOND DERIVATIVE INFORMATION
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
      IC6=IC5+NTRI
      IC7=IC6+N3N
      IC8=IC7+N3N*N3N
      IC9=IC8+NBATRI
      IC10=IC9+NBFAO*NBFAO
      ICMAX=IC10+NBFAO*NBFAO
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=ICMAX
C.................EAO     E1A     E2A     SS      U       T........
C***  CALL DERMAT(CC(IC3),CC(IC6),CC(IC7),CC(IC8),CC(IC9),CC(IC10))
C
C   CALCULATE SCF ENERGY FOR A TEST
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN E0CAL'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NTRI
      IC12=IC11+NBFAO*NBFAO
      IC13=IC12+NBFAO*NBFAO
      IC14=IC13+NTRI*NTYPES
      IC15=IC14+NTRI*NTYPES
      IC16=IC15+NTRI*NTYPES
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF2
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     ESO     HSO     HMO     TM       U........
C***  CALL E0CAL(CC(IC2),CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................T        DP       DQ       DM       LBLI     BUFI.....
C*** 1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),IA(IA16),CC(IC16))
C
C   CALCULATE SCF FIRST ENERGY DERIVATIVES
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN EADER'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+N3N
      IC10=IC9+N3N
      IC11=IC10+N3N
      IC12=IC11+N3N
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTRI
      ICMAX=IC15+NTRI*N3N*NTYPES
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     D1O     D1T     D1W      E1T      SM.......
C***  CALL EADER(CC(IC2),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................HM       EPS      TM.......
C*** 1           CC(IC13),CC(IC14),CC(IC15))
C
C   FORM DERIVATIVE LAGRANGIAN MATRIX
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN FAMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI
      IC10=IC9+NTRI
      IC11=IC10+NBASIS*NBASIS
      ICMAX=IC11+NTRI*N3N*NTYPEP
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................HH      TT      EPA      FM.......
C***  CALL FAMAT(CC(IC8),CC(IC9),CC(IC10),CC(IC11))
C
C   CALCULATE BA MATRIX
C   FOR INDEPENDENT PAIRS
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN BAIND'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI*NTYPEP
      IC12=IC11+NTRI
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF2
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................ESO     NIJ     U       T       ZETA     B0.......
C***  CALL BAIND(CC(IC3),IA(IA4),CC(IC8),CC(IC9),CC(IC10),CC(IC11),
C................EPA      DP       DQ       DM       SM.......
C*** 1           CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC15),
C................LBLI     BUFI.....
C*** 2           IA(IA16),CC(IC16))
C
C   FOR DEPENDENT PAIRS
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN BADEP'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI
      IC13=IC12+NTRI*N3N
      IC14=IC13+NTRI*N3N
      IC15=IC14+NTRI*N3N
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF2
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................EIG     OCC     ESO     KIJ     U       T.......
C***  CALL BADEP(CC(IC1),CC(IC2),CC(IC3),IA(IA5),CC(IC8),CC(IC9),
C................B0       FF       DP       DQ       DM.......
C*** 1           CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI.....
C*** 2           IA(IA15),CC(IC15))
C
C   FORM BF MATRIX
      IF(IPOL.NE.0) GO TO 310
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN BFMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI
      IC10=IC9+NTRI*NTYPES*3
      IC11=IC10+NBASIS*NBASIS
      ICMAX=IC11+NTRI
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................NIJ     KIJ     HF      HM      EPF      BF.......
C***  CALL BFMAT(IA(IA4),IA(IA5),CC(IC8),CC(IC9),CC(IC10),CC(IC11))
C
  310 CONTINUE
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN CPHF'/)
  200 CONTINUE
      NADD=NADD/2
      MAXVC=MIN0(MAXVEC,NADD*MAXVX)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI*NTYPEP
      IC12=IC11+NTRI
      IC13=IC12+NTRI
      IC14=IC13+NTRI
      IC15=IC14+NTYPEP*(NTRI*NADD)
      IC16=IC15+NTYPEP*(NTRI*NADD)
      IC17=IC16+NTYPEP*(NTRI*NADD)
      IC18=IC17+MAXVC*(MAXVC+NADD)
      IC19=IC18+NIND*MAXVC*2
      IA19=IC19+IC19-1
      ICMAX=IC19+MAXBF2
      WRITE(3,29) NIND,N3NXX,NADD,MAXVC,ICMAX
   29 FORMAT(2X,' NIND  = ',I6,3X,' N3NXX = ',I6,3X,' NADD = ',I6/
     1       2X,' MAXVC = ',I6,3X,' ICMAX = ',I6/)
      IF(NADD.EQ.1) GO TO 201
      IF(ICMAX.LT.MAXCOR) GO TO 201
      GO TO 200
  201 CONTINUE
      IGMAX=MAX0(ICMAX,IGMAX)
C...............ESO     NIJ     KIJ     U       A       T.......
C***  CALL CPHF(CC(IC3),IA(IA4),IA(IA5),CC(IC8),CC(IC8),CC(IC9),
C...............ZETA     B0       AA       TT       DP.......
C*** 1          CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C...............DQ       DM       W        BB       LBLI     BUFI.....
C*** 2          CC(IC15),CC(IC16),CC(IC17),CC(IC18),IA(IA19),CC(IC19),
C...........................
C*** 3          MAXVC,N3NXX)
C
C   CALCULATE DEPENDENT PAIRS OF U MATRIX
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN UCMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBFAO*NBFAO
      IC10=IC9+NBFAO*NBFAO
      IC11=IC10+NTRI
      IC12=IC11+NTRI*N3NXX
      IC13=IC12+NTRI*N3NXX
      IC14=IC13+NTRI*N3NXX
      IC15=IC14+NTRI
      IA15=IC15+IC15-1
      ICMAX=IC15+MAXBF2
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................EIG     OCC     ESO     NIJ     KIJ     U.......
C***  CALL UCMAT(CC(IC1),CC(IC2),CC(IC3),IA(IA4),IA(IA5),CC(IC8),
C................T       B        DP       DQ       DM       DEL......
C*** 1           CC(IC9),CC(IC10),CC(IC11),CC(IC12),CC(IC13),CC(IC14),
C................LBLI     BUFI...........
C*** 2           IA(IA15),CC(IC15),N3NXX)
C
C   COMPLETE U MATRIX
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN UXMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI*N3NXX
      IC10=IC9+NTRI*N3NXX
      ICMAX=IC10+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................B       SM      U..............
C***  CALL UXMAT(CC(IC8),CC(IC9),CC(IC10),N3NXX)
C
C   CALCULATE SCF SECOND DERIVATIVES
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN WAMAT'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NBASIS*NBASIS
      IC10=IC9+NBASIS*NBASIS
      IC11=IC10+NBASIS*NBASIS
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NBASIS*NBASIS
      IC14=IC13+NTRI*N3N*NTYPEP
      IC15=IC14+NTRI*N3N*NTYPEP
      IC16=IC15+NTRI*N3N*NTYPEP
      IA16=IC16+IC16-1
      ICMAX=IC16+MAXBF2
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................ESO     U       T       EPA      ZETA     WA.......
C***  CALL WAMAT(CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C................SA       DP       DQ       DM       LBLI     BUFI.....
C*** 1           CC(IC13),CC(IC14),CC(IC15),CC(IC13),IA(IA16),CC(IC16))
C
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN SCF2ND'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+N3N*N3N
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI*N3N
      IC12=IC11+NBASIS*NBASIS*N3N
      IC13=IC12+NBASIS*NBASIS
      ICMAX=IC13+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C.................E2A     E2M     E2P     SA       WA       EPA......
C***  CALL SCF2ND(CC(IC7),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................UA.......
C*** 1            CC(IC13))
C
      IF(IPOL.NE.0) GO TO 320
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN DIPMO'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS*3
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     DIP     UA......
C***  CALL DIPMO(CC(IC2),CC(IC8),CC(IC9))
C
      WRITE(3,35)
   35 FORMAT(//,2X,' NOW YOU ARE IN POLAR'/)
      IC8=IC7+N3N*N3N
      IC9=IC8+NTRI*3
      ICMAX=IC9+NBASIS*NBASIS
      WRITE(3,2) ICMAX,MAXCOR
      IGMAX=MAX0(ICMAX,IGMAX)
C................OCC     HF      UF......
C***  CALL POLAR(CC(IC2),CC(IC8),CC(IC9))
  320 CONTINUE
C
      IF(IGMAX.LT.MAXCOR) GO TO 400
C
C   REQUIRED MEMORY EXCEEDS MAXCOR-----STOP !!!
      WRITE(6,3) IGMAX,MAXCOR
      WRITE(3,3) IGMAX,MAXCOR
      STOP
C
  400 CONTINUE
      WRITE(3,4) NADD,MAXVC,IGMAX,MAXCOR
      WRITE(6,4) NADD,MAXVC,IGMAX,MAXCOR
      WRITE(3,5)
C
      RETURN
      END
      SUBROUTINE DERMAT(EAO,E1A,E2A,SS,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E2A(N3N,N3N),SS(NBATRI)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
    1 FORMAT(//,2X,' SCF FIRST DERIVATIVE MATRIX'/)
    2 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SA (AO BASIS) MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' SA (MO BASIS) MATRIX, IABC = ',I5/)
    5 FORMAT(//,2X,' HA (AO BASIS) MATRIX, IABC = ',I5/)
    6 FORMAT(//,2X,' HA (MO BASIS) MATRIX, IABC = ',I5/)
    7 FORMAT(//,2X,' TA (AO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
    8 FORMAT(//,2X,' TA (MO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
C
      CALL RFILE(ITAP42)
      CALL RFILE(ITAP44)
      CALL RFILE(JTAPE1)
      NBSET=NTYPES
      NTREAD=NBSET
C
C   READ IN SCF FIRST DERIVATIVES
      CALL SREAD(ITAP42,E1A,N3N*2)
      WRITE(6,1)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
C
C   READ IN SCF SECOND DERIVATIVES
      CALL SREAD(ITAP42,E2A,N3N*N3N*2)
      WRITE(6,2)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
C
C   READ IN S FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,3) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  201 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IS)
      IF(IPRNT.LE.4) GO TO 101
      WRITE(6,4) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  101 CONTINUE
C
C   READ IN ONE ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 102 IABC=1,N3N
      IH=IHA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,5) IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  202 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IH)
      IF(IPRNT.LE.4) GO TO 102
      WRITE(6,6) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  102 CONTINUE
C
C   READ IN TWO ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 104 ITYP=1,NBSET
      DO 103 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 203
      WRITE(6,7) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  203 CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL SWRIT(JTAPE1,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,8) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  103 CONTINUE
  104 CONTINUE
C
      CALL SREW(ITAP42)
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE MOCONV(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBFAO
      DO 101 JJ=1,NBFAO
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      SAX=SA(IIJJ)
      T(II,JJ)=SAX
  101 CONTINUE
      DO 103 II=1,NBFAO
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBFAO
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBFAO
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE MOCONS(SA,NNA,SM,NNM,ESO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      DATA A00 / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM SO TO MO BASIS
      DO 101 II=1,NBASIS
      DO 101 JJ=1,II
      IIJJ=IOFF(II)+JJ
      SAX=SA(IIJJ)
      T(II,JJ)=SAX
      IF(II.EQ.JJ) GO TO 101
      T(JJ,II)=SAX
  101 CONTINUE
      DO 103 II=1,NBASIS
      DO 103 J=1,NBASIS
      SUM=A00
      DO 102 JJ=1,NBASIS
      SUM=SUM+ESO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=A00
      DO 104 II=1,NBASIS
      SUM=SUM+ESO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE E0CAL(OCC,ESO,HSO,HMO,TM,U,T,DP,DQ,DM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),HSO(NTRI),HMO(NTRI),TM(NTRI)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION DP(NTRI,NTYPES),DQ(NTRI,NTYPES),DM(NTRI,NTYPES)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' H (SO BASIS) MATRIX'/)
    2 FORMAT(//,2X,' H (MO BASIS) MATRIX'/)
    3 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
C
C   CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(1)
C
C   READ IN ONE ELCTRON INTEGRALS (SO BASIS)
      CALL MREAD(HSO,15)
      CALL MOCONS(HSO,NTRI,HMO,NTRI,ESO,U,T)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL PRINT(HSO,NTRI,NBASIS,6)
      WRITE(6,2)
      CALL PRINT(HMO,NTRI,NBASIS,6)
C
C   CALCULATE ONE-ELCTRON ENERGY
  201 CONTINUE
      ELEC1=A00
      DO 101 I=1,JOCC
      II=IOFF(I+1)
      ELEC1=ELEC1+HMO(II)*OCC(I)
  101 CONTINUE
C
C   FORM DENSITY MATRIX IN SO BASIS
      CALL PQAMAT(DP,DQ,ESO,NTYPES)
C
C   FORM HALF-TRANSFORMED TWO ELECTRON MATRIX
      CALL PQMAT(DP,DQ,DM,NTYPES,LBLI,BUFI)
C
C   CALCULATE TWO-ELECTRON ENERGY
      ELEC2=A00
      DO 103 ITYP=1,NTYPES
      CALL MOCONS(DM(1,ITYP),NTRI,TM,NTRI,ESO,U,T)
      DO 102 I=NSTR(ITYP),NEND(ITYP)
      II=IOFF(I+1)
      ELEC2=ELEC2+TM(II)
  102 CONTINUE
  103 CONTINUE
C
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,3) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
      RETURN
      END
      SUBROUTINE PQAMAT(DP,DQ,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 104 ITYP=1,NTYPES
      SUMP=A00
      SUMQ=A00
      DO 103 JTYP=1,NTYPES
      DO 102 K=NSTR(JTYP),NEND(JTYP)
      FAC=ESO(I,K)*ESO(J,K)
      SUMP=SUMP+A(ITYP,JTYP)*FAC
      SUMQ=SUMQ+B(ITYP,JTYP)*FAC
  102 CONTINUE
  103 CONTINUE
      DP(IJ,ITYP)=SUMP
      DQ(IJ,ITYP)=SUMQ
  104 CONTINUE
  105 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES,NTRI,NTYPES,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES,NTRI,NTYPES,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE PQMAT(DP,DQ,DM,NABC,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),DM(NTRI,NABC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(2X,6I5,F15.8)
    2 FORMAT(//,2X,' DM MATRIX'/)
C
C   DP :  COULOMB DENSITY TYPE SO MATRIX
C   DQ :  EXCHANGE DENSITY TYPE SO MATRIX
C   DM :  HALF TRANSFORMED SO MATRIX
C
C   FORM DM MATRICES
      CALL ZERO(DM,NTRI*NABC)
C
      CALL RFILE(ITAP36)
  500 CALL SREAD(ITAP36,LBLI,MAXBF4)
      DO 160 I=1,MAXBUF
      IBA=LBLI(I+I-1)
      IBB=LBLI(I+I)
      IF(IBA.EQ.0) GO TO 501
      VALU=BUFI(MAXBUF+I)
      CALL UNPACK(II,JJ,KK,LL,IX,IBA,IBB)
C     IF(IPRNT.LE.9) GO TO 301
C     WRITE(6,1) I,II,JJ,KK,LL,IX,VALU
  301 GO TO (201,202,203,204,205,206,207,208,209,210,211,212,212,212),IX
C
C (II/II)   1=(11/11)
C
  201 IIII=IOFF(II+1)
      DO 112 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+(DP(IIII,IABC)+DQ(IIII,IABC))*VALU
  112 CONTINUE
      GO TO 160
C
C  (II/KK)  2=(22/11)
C
  202 IIII=IOFF(II+1)
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      DO 116 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+DP(KKKK,IABC)*VALU
      DM(KKKK,IABC)=DM(KKKK,IABC)+DP(IIII,IABC)*VALU
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(IIKK,IABC)*VALU
  116 CONTINUE
      GO TO 160
C
C  (II/IL)  3=(22/21)
C
  203 IIII=IOFF(II+1)
      IILL=IOFF(II)+LL
      VAL2=VALU+VALU
      DO 120 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+(DP(IILL,IABC)+DQ(IILL,IABC))*VAL2
      DM(IILL,IABC)=DM(IILL,IABC)+(DP(IIII,IABC)+DQ(IIII,IABC))*VALU
  120 CONTINUE
      GO TO 160
C
C  (IJ/JJ)  4=(21/11)
C
  204 IIJJ=IOFF(II)+JJ
      JJJJ=IOFF(JJ+1)
      VAL2=VALU+VALU
      DO 124 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(JJJJ,IABC)+DQ(JJJJ,IABC))*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+(DP(IIJJ,IABC)+DQ(IIJJ,IABC))*VAL2
  124 CONTINUE
      GO TO 160
C
C  (IJ/IJ)  5=(21/21)
C
  205 IIJJ=IOFF(II)+JJ
      IIII=IOFF(II+1)
      JJJJ=IOFF(JJ+1)
      DO 128 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IIII,IABC)=DM(IIII,IABC)+DQ(JJJJ,IABC)*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+DQ(IIII,IABC)*VALU
  128 CONTINUE
      GO TO 160
C
C  (II/KL)  6=(33/21)
C
  206 IIII=IOFF(II+1)
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      VAL2=VALU+VALU
      DO 132 IABC=1,NABC
      DM(IIII,IABC)=DM(IIII,IABC)+DP(KKLL,IABC)*VAL2
      DM(KKLL,IABC)=DM(KKLL,IABC)+DP(IIII,IABC)*VALU
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(IILL,IABC)*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+DQ(IIKK,IABC)*VALU
  132 CONTINUE
      GO TO 160
C
C  (IJ/KK)  7=(32/11)
C
  207 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=IOFF(JJ)+KK
      VAL2=VALU+VALU
      DO 136 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+DP(KKKK,IABC)*VALU
      DM(KKKK,IABC)=DM(KKKK,IABC)+DP(IIJJ,IABC)*VAL2
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJKK,IABC)*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+DQ(IIKK,IABC)*VALU
  136 CONTINUE
      GO TO 160
C
C  (IJ/KK)  8=(31/22)
C
  208 IIJJ=IOFF(II)+JJ
      KKKK=IOFF(KK+1)
      IIKK=IOFF(II)+KK
      JJKK=JJ+IOFF(KK)
      VAL2=VALU+VALU
      DO 140 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+DP(KKKK,IABC)*VALU
      DM(KKKK,IABC)=DM(KKKK,IABC)+DP(IIJJ,IABC)*VAL2
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJKK,IABC)*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+DQ(IIKK,IABC)*VALU
  140 CONTINUE
      GO TO 160
C
C (IJ/IL)  9=(32/31)
C
  209 IIJJ=IOFF(II)+JJ
      IILL=IOFF(II)+LL
      IIII=IOFF(II+1)
      JJLL=IOFF(JJ)+LL
      VAL2=VALU+VALU
      DO 144 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(IILL,IABC)*TWO+DQ(IILL,IABC))*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IIII,IABC)=DM(IIII,IABC)+DQ(JJLL,IABC)*VAL2
      DM(JJLL,IABC)=DM(JJLL,IABC)+DQ(IIII,IABC)*VALU
  144 CONTINUE
      GO TO 160
C
C  (IJ/JL)  10=(32/21)
C
  210 IIJJ=IOFF(II)+JJ
      JJLL=IOFF(JJ)+LL
      IILL=IOFF(II)+LL
      JJJJ=IOFF(JJ+1)
      VAL2=VALU+VALU
      DO 148 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(JJLL,IABC)*TWO+DQ(JJLL,IABC))*VALU
      DM(JJLL,IABC)=DM(JJLL,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+DQ(JJJJ,IABC)*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+DQ(IILL,IABC)*VAL2
  148 CONTINUE
      GO TO 160
C
C  (IJ/KJ)  11=(31/21)
C
  211 IIJJ=IOFF(II)+JJ
      JJKK=JJ+IOFF(KK)
      IIKK=IOFF(II)+KK
      JJJJ=IOFF(JJ+1)
      VAL2=VALU+VALU
      DO 152 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+(DP(JJKK,IABC)*TWO+DQ(JJKK,IABC))*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+(DP(IIJJ,IABC)*TWO+DQ(IIJJ,IABC))*VALU
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJJJ,IABC)*VALU
      DM(JJJJ,IABC)=DM(JJJJ,IABC)+DQ(IIKK,IABC)*VAL2
  152 CONTINUE
      GO TO 160
C
C  (IJ/KL)  12=(43/21)
C  (IJ/KL)  13=(42/31)
C  (IJ/KL)  14=(41/32)
C
  212 IIJJ=IOFF(II)+JJ
      KKLL=IOFF(KK)+LL
      IIKK=IOFF(II)+KK
      IILL=IOFF(II)+LL
      JJLL=IOFF(MAX0(JJ,LL))+MIN0(JJ,LL)
      JJKK=IOFF(MAX0(JJ,KK))+MIN0(JJ,KK)
      VAL2=VALU+VALU
      DO 156 IABC=1,NABC
      DM(IIJJ,IABC)=DM(IIJJ,IABC)+DP(KKLL,IABC)*VAL2
      DM(KKLL,IABC)=DM(KKLL,IABC)+DP(IIJJ,IABC)*VAL2
      DM(IIKK,IABC)=DM(IIKK,IABC)+DQ(JJLL,IABC)*VALU
      DM(JJLL,IABC)=DM(JJLL,IABC)+DQ(IIKK,IABC)*VALU
      DM(IILL,IABC)=DM(IILL,IABC)+DQ(JJKK,IABC)*VALU
      DM(JJKK,IABC)=DM(JJKK,IABC)+DQ(IILL,IABC)*VALU
  156 CONTINUE
C
  160 CONTINUE
      GO TO 500
C
  501 CALL RCLOSE(ITAP36,3)
C
      IF (IPRNT.LE.4) GO TO 502
      WRITE(6,2)
      CALL MATOUT(DM,NTRI,NABC,NTRI,NABC,6)
C
  502 CONTINUE
      RETURN
      END
      SUBROUTINE EADER(OCC,D1O,D1T,D1W,E1T,SM,HM,EPS,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),SM(NTRI),HM(NTRI),EPS(NTRI)
      DIMENSION TM(NTRI,N3N,NTYPES)
      DIMENSION D1O(N3N),D1T(N3N),D1W(N3N),E1T(N3N)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' LAGRANGIAN MATRIX'/)
    2 FORMAT(//,2X,' NUCLEAR REPULSION FIRST DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SCF ONE-ELECTRON FIRST DERIVATIVE MATRIX'/)
    4 FORMAT(//,2X,' SCF TWO-ELECTRON FIRST DERIVATIVE MATRIX'/)
    5 FORMAT(//,2X,' SCF OVERLAP CONTRIBUTION MATRIX'/)
    6 FORMAT(//,2X,' TOTAL SCF FIRST DERIVATIVE MATRIX'/)
C
C   CALCULATE SCF FIRST DERIVATIVES FOR A TEST
C   SEE EQ.(2)
C
      CALL MREAD(EPS,25)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL PRINT(EPS,NTRI,NBASIS,6)
C
C   CALL BACK DERIVATIVE TM MATRICES
  201 CONTINUE
      CALL SREW(JTAPE1)
      DO 102 ITYP=1,NTYPES
      DO 101 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  101 CONTINUE
  102 CONTINUE
C
      CALL SREW(ITAP44)
      DO 110 IABC=1,N3N
      IS=ISA(IABC)
      IH=IHA(IABC)
      CALL RREAD(ITAP44,SM,NTRI2,IS)
      CALL RREAD(ITAP44,HM,NTRI2,IH)
C  ONE-ELECTRON CONTRIBUTION
      VALU=A00
      DO 106 I=1,JOCC
      II=IOFF(I+1)
      VALU=VALU+HM(II)*OCC(I)
  106 CONTINUE
      D1O(IABC)=VALU
C  TWO-ELECTRON CONTRIBUTION
      VALU=A00
      DO 108 ITYP=1,NTYPES
      DO 107 I=NSTR(ITYP),NEND(ITYP)
      II=IOFF(I+1)
      VALU=VALU+TM(II,IABC,ITYP)
  107 CONTINUE
      D1T(IABC)=VALU
  108 CONTINUE
C   OVERLAP CONTRIBUTION
      VALU=A00
      DO 109 I=1,JOCC
      DO 109 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU=VALU-SM(IJ)*EPS(IJ)
  109 CONTINUE
      D1W(IABC)=VALU*TWO
C
C   SUM UP ALL CONTRIBUTIONS
      E1T(IABC)=D1O(IABC)+D1T(IABC)+D1W(IABC)
C
  110 CONTINUE
C
C     WRITE(6,2)
C     CALL MATOUT(E1N,3,NATOMS,3,NATOMS,6)
      WRITE(6,3)
      CALL MATOUT(D1O,3,NATOMS,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(D1T,3,NATOMS,3,NATOMS,6)
      WRITE(6,5)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
      WRITE(6,6)
      CALL MATOUT(E1T,3,NATOMS,3,NATOMS,6)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE FAMAT(HH,TT,EPA,FM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION HH(NTRI),TT(NTRI),FM(NTRI,N3N,NTYPEP)
      DIMENSION EPA(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' EPA MATRIX, IABC = ',I5/)
C
C   CALL BACK HM AND TM MATRICES AND FORM FA MATRICES
C   SEE EQ.(7)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      DO 103 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HH,NTRI2,IH)
      DO 102 ITYP=1,NTREAD
      FAC=FOCC(ITYP)*HALF
      DO 101 I=1,NTRI
  101 FM(I,IABC,ITYP)=HH(I)*FAC
  102 CONTINUE
  103 CONTINUE
C
      DO 106 ITYP=1,NTREAD
      DO 105 IABC=1,N3N
      CALL SREAD(JTAPE1,TT,NTRI2)
      DO 104 I=1,NTRI
      FM(I,IABC,ITYP)=FM(I,IABC,ITYP)+TT(I)
  104 CONTINUE
  105 CONTINUE
  106 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 120 IABC=1,N3N
      IE=IEA(IABC)
      CALL ZERO(EPA,NBASIS*NBASIS)
C
      DO 112 ITYP=1,NTYPES
      DO 111 I=NSTR(ITYP),NEND(ITYP)
      DO 111 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EPA(I,J)=FM(IJ,IABC,ITYP)
  111 CONTINUE
  112 CONTINUE
C
      CALL RWRIT(ITAP44,EPA,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 120
      WRITE(6,1) IABC
      CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  120 CONTINUE
C
C   STORE DERIVATIVE AVERAGED FOCK
CC*   IF(ICORE.NE.0) GO TO 201
CC    CALL ZERO(TT,NTRI)
CC*   DO 122 I=1,N3N
CC*   IT=IFA(IABC)
CC*   CALL RWRIT(ITAP44,TT,NTRI2,IT)
CC122 CONTINUE
CC*   GO TO 205
CC201 CONTINUE
      DO 125 IABC=1,N3N
      IT=IFA(IABC)
      CALL RWRIT(ITAP44,FM(1,IABC,NTREAD),NTRI2,IT)
  125 CONTINUE
  205 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE BAIND(ESO,NIJ,U,T,ZETA,B0,EPA,DP,DQ,DM,SM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION NIJ(NTRI,2),ZETA(NTRI,NTYPEP)
      DIMENSION B0(NTRI),EPA(NBASIS,NBASIS)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,N3N,NTYPEP),SM(NTRI,N3N*NTYPEP)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' ZETA MATRIX, ITYP = ',I5/)
    2 FORMAT(//,2X,' FM MATRIX, IABC = ',I5/)
    3 FORMAT(2X,3I5,4F15.8)
    4 FORMAT(//,2X,' B0 MATRIX IN BAIND'/)
    5 FORMAT(//,2X,' DP MATRIX IN BAIND'/)
C
C   FORM B0 MATRIX
C   SEE EQ.(13)
C
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
      IF(IPRNT.LE.2) GO TO 101
      WRITE(6,1) ITYP
      CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
      CALL SREW(ITAP44)
      DO 120 IABC=1,N3N
      IS=ISA(IABC)
      IE=IEA(IABC)
      IB=IBA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      CALL ZERO(B0,NTRI)
C
C   ELEMENTS FOR INDEPENDENT PAIRS
C     IF(IPRNT.LE.4) GO TO 301
C     WRITE(6,2) IABC
C     CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  301 DO 110 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      VALU1=EPA(I,J)-EPA(J,I)
      VALU2=A00
C
      DO 107 K=2,NBASIS
      LIM=MIN0(JOCC,K-1)
      DO 107 L=1,LIM
      KL=IOFF(K)+L
      FAC2=A00
      IF(K.NE.J) GO TO 205
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      FAC2=FAC2+ZETA(IL,ITYP)-ZETA(IL,JTYP)
  205 IF(K.NE.I) GO TO 206
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      FAC2=FAC2-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  206 VALU2=VALU2-SM(KL,IABC)*FAC2
  107 CONTINUE
      VALUT=VALU1+VALU2
      B0(IJ)=VALUT
C     IF(IPRNT.LE.4) GO TO 110
C     WRITE(6,3) II,I,J,VALU1,VALU2,VALUT
  110 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
C     IF(IPRNT.LE.4) GO TO 120
C     WRITE(6,4)
C     CALL PRINT(B0,NTRI,NBASIS,6)
  120 CONTINUE
C
C   FORM DENSITY LIKE MATRIX IN SO BASIS
      CALL PQBMAT(DP,DQ,SM,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY MATRIX
      CALL PQMAT(DP,DQ,DM,N3N*NTYPES,LBLI,BUFI)
C
C   FORM DENSITY LIKE MATRIX IN MO BASIS
      DO 125 IABC=1,N3N
      DO 125 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  125 CONTINUE
C
C     IF(IPRNT.LE.4) GO TO 303
C     WRITE(6,5)
C     CALL MATOUT(DP,NTRI,NTYPEP*N3N,NTRI,N3N*NTYPEP,6)
C
C   COMPLETE B0 MATRIX
  303 CONTINUE
      DO 130 IABC=1,N3N
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 129 II=1,NIND
      I=NIJ(II,1)
      ITYP=MOTYP(I)
      J=NIJ(II,2)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      B0(IJ)=B0(IJ)+DP(IJ,IABC,ITYP)-DP(IJ,IABC,JTYP)
  129 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
  130 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE PQBMAT(DP,DQ,SM,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION SM(NTRI,NABC*NTYPEP)
      DIMENSION ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   FORM DENSITY LIKE MATRIX
C   SEE EQ.(A-2)
C
      CALL ZERO(DP,NABC*NTYPEP*NTRI)
      CALL ZERO(DQ,NABC*NTYPEP*NTRI)
      IJ=0
      DO 110 I=1,NBASIS
      DO 110 J=1,I
      IJ=IJ+1
      DO 106 K=1,JOCC
      KTYP=MOTYP(K)
      DO 106 L=1,K
      KL=IOFF(K)+L
      FAC=ESO(I,K)*ESO(J,L)+ESO(J,K)*ESO(I,L)
      IF(K.EQ.L) FAC=ESO(I,K)*ESO(J,K)
      DO 105 ITYP=1,NTYPES
      AVAL=A(ITYP,KTYP)
      BVAL=B(ITYP,KTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 104 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)-SM(KL,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)-SM(KL,IABC)*FACB
  104 CONTINUE
  105 CONTINUE
  106 CONTINUE
  110 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE BADEP(EIG,OCC,ESO,KIJ,U,T,B0,FF,DP,DQ,DM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),OCC(NBASIS),KIJ(NTRI,2)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION B0(NTRI),FF(NTRI)
      DIMENSION DP(NTRI,N3N),DQ(NTRI,N3N),DM(NTRI,N3N)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' B0 MATRIX IN BADEP'/)
    2 FORMAT(//,2X,' DP MATRIX IN BADEP'/)
C
      CALL SREW(ITAP44)
C   DM ARRAY STORES DERIVATIVE OVERLAP MATRIX
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      IT=IFA(IABC)
      IB=IBA(IABC)
      CALL RREAD(ITAP44,DM(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,FF,NTRI2,IT)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      IJ=0
      DO 101 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      B0(IJ)=FF(IJ)-EIG(J)*DM(IJ,IABC)
  101 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      IF(IPRNT.LE.4) GO TO 102
      WRITE(6,1)
      CALL PRINT(B0,NTRI,NBASIS,6)
  102 CONTINUE
C
C   FORM DENSITY LIKE MATRIX
C   DM ARRAY CONTAINES DERIVATIVE OVERLAP MATRIX
C   DP ARRAY STORES DENSITY LIKE MATRIX IN SO BASIS
  301 CONTINUE
      CALL PQCMAT(DP,DQ,DM,OCC,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   AND TRANSFORM THEM INTO MO BASIS
C   DP ARRAY CONTAINS DENSITY LIKE MATRIX IN SO BASIS
C   DM ARRAY STORES HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,N3N,LBLI,BUFI)
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,2)
      CALL MATOUT(DP,NTRI,N3N,NTRI,N3N,6)
C
C   COMPLETE B0 MATRICES
  302 CONTINUE
      CALL SREW(ITAP44)
      DO 105 IABC=1,N3N
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      CALL MOCONS(DM(1,IABC),NTRI,DP(1,IABC),NTRI,ESO,U,T)
      DO 104 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      B0(IJ)=B0(IJ)+DP(IJ,IABC)
  104 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      IF(IPRNT.LE.4) GO TO 105
      WRITE(6,1)
      CALL PRINT(B0,NTRI,N3N,NTRI,N3N,6)
  105 CONTINUE
C
  303 CONTINUE
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE PQCMAT(DP,DQ,SM,OCC,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),SM(NTRI,NABC)
      DIMENSION OCC(NBASIS),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   FORM DENSITY LIKE MATRICES
C
      CALL ZERO(DP,NABC*NTRI)
      CALL ZERO(DQ,NABC*NTRI)
C
      IJ=0
      DO 107 I=1,NBASIS
      DO 107 J=1,I
      IJ=IJ+1
      DO 106 K=1,JOCC
      FACO=OCC(K)*HALF
      DO 106 L=1,K
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      FACV=ESO(I,K)*ESO(J,L)+ESO(J,K)*ESO(I,L)
      IF(K.EQ.L) FACV=ESO(I,K)*ESO(J,K)
      FAC=FACO*FACV
      DO 105 IABC=1,NABC
      FACT=-FAC*SM(KL,IABC)
      FAC2=FACT+FACT
      DP(IJ,IABC)=DP(IJ,IABC)+FAC2
      DQ(IJ,IABC)=DQ(IJ,IABC)-FACT
  105 CONTINUE
  106 CONTINUE
  107 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC,NTRI,NABC,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE BFMAT(NIJ,KIJ,HF,HM,EPF,BF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION HF(NTRI),HM(NTRI,3,NTYPES),EPF(NBASIS,NBASIS),BF(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/DIPOL/DIPDA(135),DIPDM(135),DIPDN(135),DIPDT(135)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/CI102/NIND,NDEP
      DATA A00,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' DIPDA MATRIX'/)
    2 FORMAT(//,2X,' DIPDN MATRIX'/)
    3 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    4 FORMAT(//,2X,' EPF MATRIX, IXYZ = ',I5/)
C
      CALL RFILE(ITAP43)
      CALL SREAD(ITAP43,DIPDA,270)
      CALL SREAD(ITAP43,DIPDN,270)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,2)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      DO 103 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      CALL SREAD(ITAP43,HF,NTRI2)
      CALL RWRIT(ITAP44,HF,NTRI2,IH)
      DO 102 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 101 I=1,NTRI
  101 HM(I,IXYZ,ITYP)=HF(I)*FAC
  102 CONTINUE
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,3) IXYZ
      CALL PRINT(HF,NTRI,NBASIS,6)
  103 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 110 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      IE=IEA(IXYZ+N3N)
      IB=IBA(IXYZ+N3N)
      CALL RREAD(ITAP44,HF,NTRI2,IH)
      CALL ZERO(BF,NTRI)
      CALL ZERO(EPF,NBASIS*NBASIS)
C
      DO 107 ITYP=1,NTYPES
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EPF(I,J)=HM(IJ,IXYZ,ITYP)
  106 CONTINUE
  107 CONTINUE
C
C   ELEMENTS FOR INDEPENDENT PAIRS
      DO 108 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=EPF(I,J)-EPF(J,I)
  108 CONTINUE
C
C   ELEMENTS FOR DEPENDENT PAIRS
      DO 109 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=HF(IJ)
  109 CONTINUE
C
  202 CONTINUE
      CALL RWRIT(ITAP44,BF,NTRI2,IB)
      CALL RWRIT(ITAP44,EPF,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,4) IXYZ
      CALL MATOUT(EPF,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
      CALL SREW(ITAP43)
      CALL SREW(ITAP44)
C
      RETURN
      END
      SUBROUTINE CPHF(ESO,NIJ,KIJ,U,A,T,ZETA,B0,AA,TT,DP,DQ,DM,W,BB,
     1                LBLI,BUFI,NMAX,NADD,N3NXX)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*16 DLIM,DETERM
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION A(NBFAO,NBFAO),ZETA(NTRI,NTYPEP)
      DIMENSION BB(NIND,NMAX*2),W(NMAX,NMAX+NADD)
      DIMENSION DP(NTRI,NADD,NTYPEP),DQ(NTRI,NADD,NTYPEP)
      DIMENSION DM(NTRI,NADD,NTYPEP)
      DIMENSION AA(NTRI),TT(NTRI),B0(NTRI),NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),BNORM(1024)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/TOLER/ICONV
      COMMON/WORKS/JTAPE1
      COMMON/CI102/NIND,NDEP
      DATA A00,ONE,TEN / 0.0D+00 , 1.0D+00 , 10.0D+00 /
    1 FORMAT(//,2X,' A MATRIX'/)
    2 FORMAT(//,2X,' IGROUP = ',I5,5X,' IFIRST = ',I5,5X,
     1             ' NABC   = ',I5/)
    3 FORMAT(//,2X,' BB MATRIX'/)
    4 FORMAT(//,2X,' SCALED BB MATRIX'/)
    5 FORMAT(//,2X,' ORTHONORMALIZED BB MATRIX'/)
    6 FORMAT(//,2X,' ORTHONORMALITY OF BB MATRIX'/)
    7 FORMAT(//,2X,' ITERATION NO. = ',I5/)
    8 FORMAT(//,2X,' (1-A)*B MATRIX'/)
    9 FORMAT(//,2X,' KVEC = ',I5,' K = ',I5,' TT = ',(10F10.5))
   10 FORMAT(/,2X,' NITER  = ',I5,5X,' NVEC   = ',I5,5X,' NADDV  = ',
     1         I5/)
   11 FORMAT(//,2X,' NORM OF ADDED VECTOR',(5(I5,F15.8)))
   12 FORMAT(//,2X,' BB MATRIX'/)
   13 FORMAT(/,2X,' END OF ITERATION    NVTOT  = ',I5/)
   14 FORMAT(//,2X,'***NVTOT EXCEEDS NMAX***',5X,' NMAX = ',I5/)
   15 FORMAT(//,2X,' EXPANDED VECTORS BB AND A*BB MATRICES'/)
   16 FORMAT(//,2X,' W MATRIX'/)
   17 FORMAT(//,2X,' SCALED W MATRIX'/)
   18 FORMAT(//,2X,'***SIMULTANEOUS EQUATION CANNOT BE SOLVED***',
     1 5X,' DETERM = ',Q20.10/)
   19 FORMAT(/,2X,' LINEAR EQUATION HAS BEEN SOLVED',
     1 5X,' DETERM = ',Q20.10/)
   20 FORMAT(//,2X,' SOLUTION MATRIX OF SCALED W MATRIX'/)
   21 FORMAT(//,2X,' SOLUTION MATRIX'/)
   22 FORMAT(//,2X,' B0 MATRIX WITH INDEP. OF UA, IABC = ',I5/)
C
      CRIT=1.0D-10
      IF(ICONV.NE.0) CRIT=ONE/(TEN**ICONV)
      DLIM=1.0Q-76
      NIND2=NIND*2
C
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
C     CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
C   FORM A MATRIX
C   SEE EQ.(A-1)
C
      CALL ZERO(A,NBASIS*NBASIS)
      DO 104 I=1,NBASIS
      DO 104 J=1,NBASIS
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      II=IOFF(I+1)
      JJ=IOFF(J+1)
      A(I,J)=A(I,J)-ZETA(II,ITYP)+ZETA(II,JTYP)
     1             -ZETA(JJ,JTYP)+ZETA(JJ,ITYP)
  104 CONTINUE
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1)
      CALL MATOUT(A,NBFAO,NBFAO,NBASIS,NBASIS,6)
C
C   SCALING MATRIX
C   SEE EQS.(24) AND (25)
C
  301 CONTINUE
      DO 105 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      AVAL=A(I,J)
      AVAL=DABS(AVAL)
      AA(II)=ONE/DSQRT(AVAL)
  105 CONTINUE
C
C   START GROUPING THE B VECTORS
      IGROUP=0
      ILAST=0
  500 CONTINUE
      IGROUP=IGROUP+1
      IFIRST=ILAST+1
      ILAST=IFIRST+NADD-1
      IF(ILAST.GT.N3NXX) ILAST=N3NXX
      NABC=ILAST-IFIRST+1
      WRITE(6,2) IGROUP,IFIRST,NABC
      CALL SREW(JTAPE1)
C
C   CALL BACK B0 MATRICES
      CALL SREW(ITAP44)
      DO 107 IABC=1,NABC
      III=IABC+IFIRST-1
      IB=IBA(III)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 106 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BB(II,IABC)=B0(IJ)
  106 CONTINUE
  107 CONTINUE
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,3)
      CALL MATOUT(BB,NIND,NABC,NIND,NABC,6)
C
C   SCALE B0 MATRICES
  302 CONTINUE
      DO 109 II=1,NIND
      DO 108 IABC=1,NABC
  108 BB(II,IABC)=BB(II,IABC)*AA(II)
  109 CONTINUE
      IF(IPRNT.LE.4) GO TO 303
      WRITE(6,4)
      CALL MATOUT(BB,NIND,NABC,NIND,NABC,6)
C
C   ORTHONORMALIZE B0 MATRIX
C
  303 CONTINUE
      CALL ORTHOG(BB,BB(1,NABC+1),NIND,NABC,NIND,NABC,NVEC)
      IF(IPRNT.LE.4) GO TO 304
      WRITE(6,5)
      CALL MATOUT(BB,NIND,NVEC,NIND,NVEC,6)
  304 CONTINUE
      DO 110 IABC=1,NVEC
  110 BNORM(IABC)=ONE
C
C   CHECK ORTHONORMALITY
      IF(IPRNT.LE.4) GO TO 305
      DO 112 I=1,NVEC
      DO 112 J=1,NVEC
      VALU=A00
      DO 111 K=1,NIND
  111 VALU=VALU+BB(K,I)*BB(K,J)
      W(I,J)=VALU
  112 CONTINUE
      WRITE(6,6)
      CALL MATOUT(W,NMAX,NMAX+NABC,NVEC,NVEC,6)
C
  305 NPVEC=0
      NITER=0
C:::::::::::::::::::::
C:::START ITERATION:::
C:::::::::::::::::::::
  200 NITER=NITER+1
      IF(IPRNT.GT.2)
     *WRITE(6,7) NITER
C
      DO 114 II=1,NIND
      DO 113 IABC=1,NVEC
  113 BB(II,NPVEC+IABC)=BB(II,NPVEC+IABC)*AA(II)
  114 CONTINUE
C
C   FORM A*B MATRIX
      CALL MAKEAB(BB(1,NPVEC+1),DP,DQ,DM,BB(1,NPVEC+NVEC+1),NVEC,
     1 ZETA,NIJ,ESO,U,T,LBLI,BUFI)
C
      DO 116 II=1,NIND
      DO 115 IABC=1,NVEC
      BB(II,NPVEC+IABC)=BB(II,NPVEC+IABC)/AA(II)
      BB(II,NPVEC+NVEC+IABC)=BB(II,NPVEC+NVEC+IABC)*AA(II)
  115 CONTINUE
  116 CONTINUE
C
C   STORE A*B MATRIX
      DO 117 IABC=NPVEC+NVEC+1,NPVEC+NVEC*2
      CALL SWRIT(JTAPE1,BB(1,IABC),NIND2)
  117 CONTINUE
C
C   FORM (1-A)*B
      DO 120 IABC=NPVEC+1,NPVEC+NVEC
      DO 119 II=1,NIND
C*119 BB(II,NVEC+IABC)=-BB(II,IABC)-BB(II,NVEC+IABC)
  119 BB(II,NVEC+IABC)=BB(II,IABC)-BB(II,NVEC+IABC)
  120 CONTINUE
      IF(IPRNT.LE.4) GO TO 306
      WRITE(6,8)
      CALL MATOUT(BB(1,NPVEC+NVEC+1),NIND,NVEC,NIND,NVEC,6)
C
C   FORM ADDITIONAL EXPANSION VECTORS
  306 NADDV=0
      K=NPVEC+NVEC
      DO 130 IABC=NPVEC+NVEC+1,NPVEC+NVEC*2
C
      DO 121 II=1,NIND
  121 TT(II)=BB(II,IABC)
      DO 124 L=1,K
      CALL NORM(XBB,BB(1,L),BB(1,IABC),NIND)
      FAC=XBB/BNORM(L)
      DO 123 II=1,NIND
  123 TT(II)=TT(II)-BB(II,L)*FAC
  124 CONTINUE
      IF(IPRNT.LE.4) GO TO 307
      WRITE(6,9) IABC,K,(TT(I),I=1,NIND)
  307 CONTINUE
      CALL NORM(XTT,TT,TT,NIND)
      IF(DABS(XTT).LT.CRIT) GO TO 130
      NADDV=NADDV+1
      K=K+1
      BNORM(K)=XTT
      DO 126 II=1,NIND
C*126 BB(II,K)=-TT(II)
  126 BB(II,K)=TT(II)
C
  130 CONTINUE
C
      WRITE(6,10) NITER,NVEC,NADDV
      WRITE(3,10) NITER,NVEC,NADDV
      NPVEC=NPVEC+NVEC
      NVEC=NADDV
      IF(NADDV.EQ.0) GO TO 310
      IF(IPRNT.LE.4) GO TO 308
      IST=NPVEC+1
      IED=NPVEC+NVEC
      WRITE(6,11) (I,BNORM(I),I=IST,IED)
      WRITE(6,12)
      CALL MATOUT(BB(1,NPVEC+1),NIND,NVEC,NIND,NVEC,6)
  308 GO TO 200
C
  310 NVTOT=NPVEC
      WRITE(6,13) NVTOT
      WRITE(3,13) NVTOT
      IF(NVTOT.LE.NMAX) GO TO 400
      WRITE(6,14) NMAX
      STOP
C
C   CALL BACK A*B MATRICES
  400 CONTINUE
      CALL SREW(JTAPE1)
      DO 131 IABC=NVTOT+1,NVTOT*2
  131 CALL SREAD(JTAPE1,BB(1,IABC),NIND2)
      IF(IPRNT.LE.4) GO TO 311
      WRITE(6,15)
      CALL MATOUT(BB,NIND,NVTOT*2,NIND,NVTOT*2,6)
C
  311 CONTINUE
      DO 133 I=1,NVTOT
      DO 133 J=1,NVTOT
      SUM=A00
      DO 132 II=1,NIND
C*132 SUM=SUM-BB(II,I)*BB(II,NVTOT+J)
  132 SUM=SUM+BB(II,I)*BB(II,NVTOT+J)
      W(I,J)=SUM
  133 CONTINUE
C
C   READ BACK ORIGINAL B0 MATRICES
      CALL SREW(ITAP44)
      DO 135 IABC=1,NABC
      III=IABC+IFIRST-1
      IB=IBA(III)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 134 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BB(II,NVTOT+IABC)=B0(IJ)*AA(II)
  134 CONTINUE
  135 CONTINUE
      IF(IPRNT.LE.4) GO TO 312
      WRITE(6,4)
      CALL MATOUT(BB(1,NVTOT+1),NIND,NABC,NIND,NABC,6)
C
C   SET B0*B ON W
  312 CONTINUE
      DO 137 I=1,NVTOT
      DO 137 J=1,NABC
      SUM=A00
      DO 136 II=1,NIND
  136 SUM=SUM+BB(II,I)*BB(II,NVTOT+J)
      W(I,NVTOT+J)=SUM
  137 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 313
      WRITE(6,16)
      CALL MATOUT(W,NMAX,NMAX+NABC,NVTOT,NVTOT+NABC,6)
  313 CONTINUE
      DO 138 I=1,NVTOT
      AVAL=ONE/DSQRT(BNORM(I))
      DO 138 J=1,NVTOT
      BVAL=ONE/DSQRT(BNORM(J))
      W(I,J)=W(I,J)*AVAL*BVAL
  138 CONTINUE
      DO 139 I=1,NVTOT
      AVAL=ONE/DSQRT(BNORM(I))
      DO 139 J=1,NABC
      W(I,NVTOT+J)=W(I,NVTOT+J)*AVAL
  139 CONTINUE
      IF(IPRNT.LE.3) GO TO 315
      WRITE(6,17)
      CALL MATOUT(W,NMAX,NMAX+NABC,NVTOT,NVTOT+NABC,6)
C
C   SOLVE SMALL SIMULTANEOUS EQUATION
  315 CONTINUE
      CALL FLINQ(W,NMAX,NVTOT,NABC,DETERM)
      IF(QABS(DETERM).GT.DLIM) GO TO 320
C
C   SIMULTANEOUS EQUATION CANNOT BE SOLVED---STOP!!!
      WRITE(6,18) DETERM
      STOP
C
C::::::::::::::::::::::::::::::::
C:::LINEAR EQUATION WAS SOLVED:::
C::::::::::::::::::::::::::::::::
  320 WRITE(6,19) DETERM
      IF(IPRNT.LE.4) GO TO 322
      WRITE(6,20)
      CALL MATOUT(W(1,NVTOT+1),NMAX,NMAX+NABC,NVTOT,NABC,6)
  322 CONTINUE
      DO 151 I=1,NVTOT
      AVAL=ONE/DSQRT(BNORM(I))
      DO 151 J=1,NABC
      W(I,NVTOT+J)=W(I,NVTOT+J)*AVAL
  151 CONTINUE
      IF(IPRNT.LE.4) GO TO 323
      WRITE(6,21)
      CALL MATOUT(W(1,NVTOT+1),NMAX,NMAX+NABC,NVTOT,NABC,6)
C
C   FORM INDEPENDENT ELEMENTS OF UA MATRIX
  323 CONTINUE
      DO 153 II=1,NIND
      DO 153 IABC=1,NABC
      SUM=A00
      DO 152 I=1,NVTOT
C*152 SUM=SUM-BB(II,I)*W(I,NVTOT+IABC)
  152 SUM=SUM+BB(II,I)*W(I,NVTOT+IABC)
      BB(II,NVTOT+IABC)=SUM*AA(II)
  153 CONTINUE
C
      CALL SREW(ITAP44)
      DO 155 IABC=1,NABC
      III=IABC+IFIRST-1
      IB=IBA(III)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 154 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      B0(IJ)=BB(II,NVTOT+IABC)
  154 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      IF(IPRNT.LE.3) GO TO 155
      WRITE(6,22) IABC
      CALL PRINT(B0,NTRI,NBASIS,6)
  155 CONTINUE
C
      IF(ILAST.GE.N3NXX) GO TO 501
      GO TO 500
C
  501 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE MAKEAB(B0,DP,DQ,DM,B1,NABC,ZETA,NIJ,ESO,U,T,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION B0(NIND,NABC),B1(NIND,NABC)
      DIMENSION ZETA(NTRI,NTYPEP),NIJ(NTRI,2)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION DM(NTRI,NABC,NTYPEP)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI102/NIND,NDEP
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' B1 MATRIX'/)
    2 FORMAT(//,2X,' FINAL B1 MATRIX'/)
C
C   SEE EQ.(21)
C   FORM DENSITY LIKE MATRIX IN SO BASIS
C
      CALL PQDMAT(DP,DQ,B0,NIJ,ESO,NABC)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,NTYPES*NABC,LBLI,BUFI)
C
C   COMPLETE TRANSFORMATION
      DO 101 IABC=1,NABC
      DO 101 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  101 CONTINUE
C
      DO 103 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      DO 102 IABC=1,NABC
  102 B1(II,IABC)=DP(IJ,IABC,ITYP)-DP(IJ,IABC,JTYP)
  103 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1)
      CALL MATOUT(B1,NIND,NABC,NIND,NABC,6)
C
C   SEE EQ.(12)
C   REST OF EQ.(11)
C
  301 CONTINUE
      DO 106 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      DO 105 JJ=1,NIND
      K=NIJ(JJ,1)
      L=NIJ(JJ,2)
      KTYP=MOTYP(K)
      LTYP=MOTYP(L)
      ZVAL1=A00
      ZVAL2=A00
      ZVAL3=A00
      ZVAL4=A00
      IF(J.NE.K) GO TO 201
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      ZVAL1=ZETA(IL,ITYP)-ZETA(IL,JTYP)
  201 IF(I.NE.K) GO TO 202
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      ZVAL2=-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  202 IF(J.NE.L) GO TO 203
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      ZVAL3=-ZETA(IK,ITYP)+ZETA(IK,JTYP)
  203 IF(I.NE.L) GO TO 204
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      ZVAL4=ZETA(JK,JTYP)-ZETA(JK,ITYP)
  204 ZVALT=ZVAL1+ZVAL2+ZVAL3+ZVAL4
C
      DO 104 IABC=1,NABC
      B1(II,IABC)=B1(II,IABC)+B0(JJ,IABC)*ZVALT
  104 CONTINUE
  105 CONTINUE
  106 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,2)
      CALL MATOUT(B1,NIND,NABC,NIND,NABC,6)
C
  302 CONTINUE
      RETURN
      END
      SUBROUTINE PQDMAT(DP,DQ,B0,NIJ,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION B0(NIND,NABC),NIJ(NTRI,2),ESO(NBFAO,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI102/NIND,NDEP
      DATA A00 / 0.0D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   SEE EQS.(22) AND (23)
C
      CALL ZERO(DP,NABC*NTYPEP*NTRI)
      CALL ZERO(DQ,NABC*NTYPEP*NTRI)
C
      IJ=0
      DO 106 I=1,NBASIS
      DO 106 J=1,I
      IJ=IJ+1
      DO 105 II=1,NIND
      K=NIJ(II,1)
      L=NIJ(II,2)
      KTYP=MOTYP(K)
      LTYP=MOTYP(L)
      FAC=ESO(I,K)*ESO(J,L)+ESO(I,L)*ESO(J,K)
      DO 104 ITYP=1,NTYPES
      AVAL=A(ITYP,KTYP)-A(ITYP,LTYP)
      BVAL=B(ITYP,KTYP)-B(ITYP,LTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 103 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)+B0(II,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)+B0(II,IABC)*FACB
  103 CONTINUE
  104 CONTINUE
  105 CONTINUE
  106 CONTINUE
  110 CONTINUE
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NTYPES*NABC,NTRI,NTYPES*NABC,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE UCMAT(EIG,OCC,ESO,NIJ,KIJ,U,T,B,DP,DQ,DM,DEL,
     1                 LBLI,BUFI,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),OCC(NBASIS)
      DIMENSION ESO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2),B(NTRI)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),DM(NTRI,NABC)
      DIMENSION DEL(NTRI),LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/CI102/NIND,NDEP
      DATA DLIMIT / 1.0D-8 /
      DATA A00,ONE / 0.0D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' B0 MATRIX'/)
    2 FORMAT(//,2X,' DP MATRIX'/)
    3 FORMAT(//,2X,' DM MATRIX'/)
C
C   CALL BACK B0 MATRICES
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,DM(1,IABC),NTRI2,IB)
  101 CONTINUE
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1)
      CALL MATOUT(B,NTRI,NABC,NTRI,NABC,6)
C
C   FORM DENSITY LIKE MATRIX IN SO BASIS
  301 CONTINUE
      CALL PQEMAT(DP,DQ,DM,NIJ,OCC,ESO,NABC)
      IF(IPRNT.LE.4) GO TO 302
      WRITE(6,2)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
C   AND TRANSFORM THEM INTO MO BASIS
  302 CONTINUE
      CALL PQMAT(DP,DQ,DM,NABC,LBLI,BUFI)
      DO 102 IABC=1,NABC
      CALL MOCONS(DM(1,IABC),NTRI,DP(1,IABC),NTRI,ESO,U,T)
  102 CONTINUE
      IF(IPRNT.LE.4) GO TO 303
      WRITE(6,3)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
C
  303 CONTINUE
      DO 103 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      DEL(II)=A00
      DELJI=EIG(J)-EIG(I)
      IF(I.EQ.J) DELJI=ONE
      IF(DABS(DELJI).LE.DLIMIT) GO TO 103
      DEL(II)=ONE/DELJI
  103 CONTINUE
C
      DO 105 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B,NTRI2,IB)
      DO 104 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      TDEL=DEL(II)
      IF(TDEL.EQ.A00) GO TO 104
      B(IJ)=(B(IJ)+DP(IJ,IABC))*TDEL
  104 CONTINUE
  105 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE PQEMAT(DP,DQ,B,NIJ,OCC,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC),DQ(NTRI,NABC),B(NTRI,NABC)
      DIMENSION OCC(NBASIS),ESO(NBFAO,NBASIS),NIJ(NTRI,2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/CI102/NIND,NDEP
      DATA A00,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' DP MATRIX'/)
    2 FORMAT(//,2X,' DQ MATRIX'/)
C
C   FORM DENSITY MATRICES
C
C   FORM DP MATRIX
      CALL ZERO(DP,NABC*NTRI)
      CALL ZERO(DQ,NABC*NTRI)
C
      IJ=0
      DO 107 I=1,NBASIS
      DO 107 J=1,I
      IJ=IJ+1
      DO 106 II=1,NIND
      K=NIJ(II,1)
      L=NIJ(II,2)
      KL=IOFF(K)+L
      FAC=(ESO(I,K)*ESO(J,L)+ESO(I,L)*ESO(J,K))*(OCC(L)-OCC(K))*HALF
      DO 105 IABC=1,NABC
      FACT=B(KL,IABC)*FAC
      FAC2=FACT+FACT
      DP(IJ,IABC)=DP(IJ,IABC)+FAC2
      DQ(IJ,IABC)=DQ(IJ,IABC)-FACT
  105 CONTINUE
  106 CONTINUE
  107 CONTINUE
C
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DP,NTRI,NABC,NTRI,NABC,6)
      WRITE(6,2)
      CALL MATOUT(DQ,NTRI,NABC,NTRI,NABC,6)
C
  201 CONTINUE
      RETURN
      END
      SUBROUTINE ORTHOG(U,W,NDIM,MDIM,N,NTEMP,NVEC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NDUM(360)
      DIMENSION U(NDIM,MDIM),W(NDIM)
      DATA A00 / 0.0D+00 /
C
      CALL IZERO(NDUM,NTEMP)
C   N=1
      DO 102 I=1,N
  102 W(I)=U(I,1)
      CALL NORMLZ(W,NDIM,N,IFLAG)
      DO 103 I=1,N
  103 U(I,1)=W(I)
C
C   N=>2
C
      NVEC=1
      IF(NTEMP.EQ.1) RETURN
      DO 110 II=2,NTEMP
      DO 104 I=1,N
  104 W(I)=U(I,II)
      DO 107 K=1,II-1
      IF(NDUM(K).NE.0) GO TO 107
      EU=A00
      DO 105 I=1,N
  105 EU=EU+U(I,K)*U(I,II)
      DO 106 I=1,N
  106 W(I)=W(I)-EU*U(I,K)
  107 CONTINUE
      CALL NORMLZ(W,NDIM,N,IFLAG)
      IF(IFLAG.NE.0) NDUM(II)=1
      IF(IFLAG.NE.0) GO TO 110
      NVEC=NVEC+1
      DO 108 I=1,N
  108 U(I,II)=W(I)
  110 CONTINUE
C
      IX=0
      DO 120 II=1,NTEMP
      IF(NDUM(II).NE.0) GO TO 120
      IX=IX+1
      DO 115 I=1,N
  115 W(I)=U(I,II)
      DO 116 I=1,N
  116 U(I,IX)=W(I)
  120 CONTINUE
C
      RETURN
      END
      SUBROUTINE NORMLZ(V,NDIM,N,IFLAG)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION V(NDIM)
    1 FORMAT(//,2X,'***ERROR IN NORMALIZATION***'/)
      DATA RLIM / 1.0D-38 /
      DATA VLIM / 1.0D-6 /
      DATA A00 / 0.0D+00 /
C
      IFLAG=0
      SQL=A00
      DO 101 I=1,N
  101 SQL=SQL+V(I)*V(I)
      R=DSQRT(SQL)
      IF(R.LE.RLIM) GO TO 202
      IF(R.LE.VLIM) GO TO 204
C
  201 DO 102 I=1,N
  102 V(I)=V(I)/R
      GO TO 205
  202 WRITE(6,1)
      STOP
C
  204 IFLAG=1
  205 RETURN
      END
      SUBROUTINE NORM(XAB,A,B,N)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION A(1),B(1)
      DATA A00 / 0.0D+00 /
C
      XAB=A00
      DO 101 I=1,N
  101 XAB=XAB+A(I)*B(I)
      RETURN
      END
      SUBROUTINE UXMAT(B,SM,U,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION B(NTRI,NABC),SM(NTRI,NABC),U(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      DATA A00,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' U MATRIX, IABC= ',I5/)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B(1,IABC),NTRI2,IB)
      IF(IABC.GT.N3N) GO TO 101
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
  101 CONTINUE
C
C   FILL UP THE REST OF U MATRIX
      DO 105 IABC=1,NABC
      IU=IUA(IABC)
      IF(IABC.GT.N3N) GO TO 301
C
      IJ=0
      DO 102 I=1,NBASIS
      DO 102 J=1,I
      IJ=IJ+1
      IF(I.EQ.J) GO TO 201
      U(I,J)=B(IJ,IABC)
      U(J,I)=-U(I,J)-SM(IJ,IABC)
      GO TO 102
  201 U(I,I)=-SM(IJ,IABC)*HALF
  102 CONTINUE
      GO TO 302
C
  301 CONTINUE
      IJ=0
      DO 103 I=1,NBASIS
      DO 103 J=1,I
      IJ=IJ+1
      IF(I.EQ.J) GO TO 202
      U(I,J)=B(IJ,IABC)
      U(J,I)=-U(I,J)
      GO TO 103
  202 U(I,I)=A00
  103 CONTINUE
C
  302 CONTINUE
      CALL RWRIT(ITAP44,U,NBASQ,IU)
      IF(IPRNT.LE.2) GO TO 105
      WRITE(6,1) IABC
      CALL MATOUT(U,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  105 CONTINUE
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE WAMAT(ESO,U,T,EPA,ZETA,WA,SA,DP,DQ,DM,LBLI,BUFI)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ESO(NBFAO,NBASIS),U(NBASIS,NBASIS),T(NBASIS,NBASIS)
      DIMENSION EPA(NBASIS,NBASIS)
      DIMENSION ZETA(NTRI,NTYPEP),WA(NBASIS,NBASIS),SA(NTRI,N3N*NTYPEP)
      DIMENSION DP(NTRI,N3N,NTYPEP),DQ(NTRI,N3N,NTYPEP)
      DIMENSION DM(NTRI,N3N,NTYPEP)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' WA MATRIX, IABC = ',I5/)
C
C   FORM WA MATRICES
C   SEE EQ.(6)
C
C   CALL BACK LAGRANGIAN MATRICES
      DO 101 ITYP=1,NTYPES
      CALL MREAD(ZETA(1,ITYP),26+ITYP)
  101 CONTINUE
      DO 102 I=1,NTRI
  102 ZETA(I,NTYPEP)=A00
C
C   CALL BACK DERIVATIVE OVERLAP INTEGRALS
      CALL SREW(ITAP44)
      DO 103 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
  103 CONTINUE
C
C   FORM DENSITY LIKE MATRIX
      CALL PQFMAT(DP,DQ,SA,ESO,N3N)
C
C   FORM HALF-TRANSFORMED DENSITY LIKE MATRIX
      CALL PQMAT(DP,DQ,DM,NTYPES*N3N,LBLI,BUFI)
C
      DO 104 IABC=1,N3N
      DO 104 ITYP=1,NTYPES
      CALL MOCONS(DM(1,IABC,ITYP),NTRI,DP(1,IABC,ITYP),NTRI,ESO,U,T)
  104 CONTINUE
C
C   COMPLETE WA MATRICES
      CALL SREW(JTAPE1)
      DO 110 IABC=1,N3N
      IS=ISA(IABC)
      IE=IEA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
C
      DO 107 I=1,NBASIS
      ITYP=MOTYP(I)
      DO 107 J=1,NBASIS
      JTYP=MOTYP(J)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU1=EPA(J,I)+EPA(J,I)
      VALU2=DP(IJ,IABC,JTYP)
      VALU3=A00
      DO 106 K=1,JOCC
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      FAC1=ZETA(IK,ITYP)+ZETA(IK,JTYP)
      FAC2=ZETA(JK,JTYP)+ZETA(JK,JTYP)
      VALU3=VALU3+SA(JK,IABC)*FAC1+SA(IK,IABC)*FAC2
  106 CONTINUE
      WA(I,J)=VALU1-VALU2-VALU3
  107 CONTINUE
      CALL SWRIT(JTAPE1,WA,NBASQ)
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,1) IABC
      CALL MATOUT(WA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE PQFMAT(DP,DQ,SA,ESO,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION DP(NTRI,NABC,NTYPEP),DQ(NTRI,NABC,NTYPEP)
      DIMENSION ESO(NBFAO,NBASIS),SA(NTRI,NABC*NTYPEP)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/GVBAB/A(10,10),B(10,10)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00 / 0.0D+00 /
C
      CALL ZERO(DP,NABC*NTYPEP*NTRI)
      CALL ZERO(DQ,NABC*NTYPEP*NTRI)
C
      IJ=0
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IJ+1
      DO 104 K=1,JOCC
      KTYP=MOTYP(K)
      DO 104 L=1,JOCC
      LTYP=MOTYP(L)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      FAC=ESO(I,K)*ESO(J,L)+ESO(J,K)*ESO(I,L)
      DO 103 ITYP=1,NTYPES
      AVAL=A(ITYP,LTYP)
      BVAL=B(ITYP,LTYP)
      FACA=AVAL*FAC
      FACB=BVAL*FAC
      DO 102 IABC=1,NABC
      DP(IJ,IABC,ITYP)=DP(IJ,IABC,ITYP)+SA(KL,IABC)*FACA
      DQ(IJ,IABC,ITYP)=DQ(IJ,IABC,ITYP)+SA(KL,IABC)*FACB
  102 CONTINUE
  103 CONTINUE
  104 CONTINUE
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE SCF2ND(E2A,E2M,E2P,SA,WA,EPA,UA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E2A(N3N,N3N),E2M(N3N,N3N),E2P(N3N,N3N)
      DIMENSION SA(NTRI,N3N),WA(NBASIS,NBASIS,N3N)
      DIMENSION EPA(NBASIS,NBASIS),UA(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' WA MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' E2P MATRIX'/)
    3 FORMAT(//,2X,' E2M MATRIX'/)
    4 FORMAT(///
     1 2X,':::::::::::::::::::::::::::::::::::::::::::::'/
     2 2X,':::SUMMARY OF FIRST ORDER CPHF CALCULATION:::'/
     3 2X,':::::::::::::::::::::::::::::::::::::::::::::'/)
    5 FORMAT(//,2X,' E2A MATRIX'/)
    6 FORMAT(//,2X,' E2M MATRIX'/)
    7 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    8 FORMAT(2I5)
    9 FORMAT(3F20.10)
C
C   CALCULATE SCF SECOND DERIVATIVES
C   SEE EQ.(5)
C
C   CALL BACK SA MATRICES
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
      CALL SREAD(JTAPE1,WA(1,1,IABC),NBASQ)
CCC   WRITE(6,1) IABC
CCC   CALL MATOUT(WA(1,1,IABC),NBASIS,NBASIS,NBASIS,NBASIS,6)
  101 CONTINUE
C
C   (B COORDINATE)
      DO 110 IABC=1,N3N
      IE=IEA(IABC)
      IU=IUA(IABC)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      CALL RREAD(ITAP44,UA,NBASQ,IU)
C
C   (A COORDINATE)
      DO 105 JABC=1,N3N
      VALUP=A00
      VALUM=A00
C
      DO 104 I=1,JOCC
      DO 102 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-SA(IJ,JABC)*EPA(I,J)
  102 CONTINUE
      DO 103 J=1,NBASIS
      VALUM=VALUM+WA(J,I,JABC)*UA(J,I)
  103 CONTINUE
  104 CONTINUE
      E2M(IABC,JABC)=VALUM*TWO
      E2P(IABC,JABC)=VALUP*TWO
  105 CONTINUE
  110 CONTINUE
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,2)
      CALL MATOUT(E2P,N3N,N3N,N3N,N3N,6)
      WRITE(6,3)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
  201 CONTINUE
      DO 115 I=1,N3N
      DO 115 J=1,N3N
  115 E2M(I,J)=E2M(I,J)+E2P(I,J)
      WRITE(6,4)
      WRITE(6,5)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
      WRITE(6,6)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
      DO 116 I=1,N3N
      DO 116 J=1,N3N
  116 E2M(I,J)=E2A(I,J)+E2M(I,J)
      WRITE(6,7)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
      ITAP15=15
      REWIND ITAP15
      N6N=N3N*2
      WRITE(ITAP15,8) NATOMS,N6N
      WRITE(ITAP15,9) ((E2M(I,J),J=1,N3N),I=1,N3N)
      REWIND ITAP15
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE DIPMO(OCC,DIP,UA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),DIP(NTRI,3),UA(NBASIS,NBASIS,3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),DIPDE(3,45),DUM(889)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,HALF,ONE,FOUR / 0.0D+00 , 0.5D+00 , 1.0D+00 , 4.0D+00 /
      DATA DEBYE,BOHR / 2.541765480D+00 , 0.52917706D+00 /
    1 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS'/)
    2 FORMAT(//,2X,' UA MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    3 FORMAT(//,2X,' DIPDA MATRIX'/)
    4 FORMAT(//,2X,' DIPDM MATRIX'/)
    5 FORMAT(//,2X,' DIPDE MATRIX'/)
    6 FORMAT(//,2X,' DIPDN MATRIX'/)
    7 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/BOHR)
     *'/)
    8 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/A)'/)
    9 FORMAT(2I5)
   10 FORMAT(3F20.10)
C
      CALL SREW(ITAP44)
      DEB4=DEBYE*FOUR
      RBOHR=ONE/BOHR
C     WRITE(6,1)
C
      CALL ZERO(DIPDM,3*N3N)
C
      DO 102 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,DIP(1,IXYZ),NTRI2,IH)
  102 CONTINUE
C
      KABC=0
      DO 105 IABC=1,NATOMS
      I00=(IABC-1)*3
      IXX=I00+1
      IYY=I00+2
      IZZ=I00+3
      DO 103 IXYZ=1,3
      KABC=KABC+1
      IU=IUA(KABC)
      CALL RREAD(ITAP44,UA(1,1,IXYZ),NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 103
      WRITE(6,2) IABC,IXYZ
      CALL MATOUT(UA(1,1,IXYZ),NBASIS,NBASIS,NBASIS,NBASIS,6)
  103 CONTINUE
C
      DO 104 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 104 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      DIPDM(1,IXX)=DIPDM(1,IXX)+UA(J,I,1)*DIP(IJ,1)*FAC
      DIPDM(1,IYY)=DIPDM(1,IYY)+UA(J,I,2)*DIP(IJ,1)*FAC
      DIPDM(1,IZZ)=DIPDM(1,IZZ)+UA(J,I,3)*DIP(IJ,1)*FAC
      DIPDM(2,IXX)=DIPDM(2,IXX)+UA(J,I,1)*DIP(IJ,2)*FAC
      DIPDM(2,IYY)=DIPDM(2,IYY)+UA(J,I,2)*DIP(IJ,2)*FAC
      DIPDM(2,IZZ)=DIPDM(2,IZZ)+UA(J,I,3)*DIP(IJ,2)*FAC
      DIPDM(3,IXX)=DIPDM(3,IXX)+UA(J,I,1)*DIP(IJ,3)*FAC
      DIPDM(3,IYY)=DIPDM(3,IYY)+UA(J,I,2)*DIP(IJ,3)*FAC
      DIPDM(3,IZZ)=DIPDM(3,IZZ)+UA(J,I,3)*DIP(IJ,3)*FAC
  104 CONTINUE
  105 CONTINUE
C
      DO 106 I=1,3
      DO 106 J=1,N3N
      DIPDM(I,J)=-DIPDM(I,J)*DEB4
  106 CONTINUE
C
C   SUM UP CONTRIBUTIONS FROM MO CHANGE
      DO 107 I=1,3
      DO 107 J=1,N3N
      DIPDE(I,J)=DIPDA(I,J)+DIPDM(I,J)
  107 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,3)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,4)
      CALL MATOUT(DIPDM,3,45,3,N3N,6)
C
C   SUM UP ALL CONTRIBUTIONS
  201 CONTINUE
      DO 108 I=1,3
      DO 108 J=1,N3N
      DIPDT(I,J)=DIPDE(I,J)+DIPDN(I,J)
  108 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,5)
      CALL MATOUT(DIPDE,3,45,3,N3N,6)
      WRITE(6,6)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
  202 CONTINUE
      WRITE(6,7)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      DO 109 I=1,3
      DO 109 J=1,N3N
      DIPDT(I,J)=DIPDT(I,J)*RBOHR
  109 CONTINUE
      WRITE(6,8)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      ITAP17=17
      REWIND ITAP17
      WRITE(ITAP17,9) NATOMS,N3N
      WRITE(ITAP17,10) ((DIPDT(I,J),J=1,N3N),I=1,3)
      REWIND ITAP17
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE POLAR(OCC,HF,UF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),HF(NTRI,3),UF(NBASIS,NBASIS)
      DIMENSION POL(3,3),POLC(3,3),EIG(3),EIV(3,3),A(6),FV1(3),FV2(3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3,N3NX3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(256),IPRNT
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA A00,HALF,FOUR / 0.0D+00 , 0.5D+00 , 4.0D+00 /
      DATA BOHR / 0.52917706D+00 /
    1 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    2 FORMAT(//,2X,' UF MATRIX, IXYZ = ',I5/)
    3 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    4 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
    5 FORMAT(//,2X,' A MATRIX'/)
    6 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    7 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
    8 FORMAT(//,2X,' EIGENVECTOR MATRIX'/)
C
      CALL SREW(ITAP44)
      A33=BOHR*BOHR*BOHR
C
C   CALL BACK HF MATRICES
      DO 101 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,HF(1,IXYZ),NTRI2,IH)
      IF(IPRNT.LE.3) GO TO 101
      WRITE(6,1) IXYZ
      CALL PRINT(HF(1,IXYZ),NTRI,NBASIS,6)
  101 CONTINUE
C
C:::B COORDINATE:::
      DO 105 IXYZ=1,3
      IU=IUA(N3N+IXYZ)
      CALL RREAD(ITAP44,UF,NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 301
      WRITE(6,2) IXYZ
      CALL MATOUT(UF,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
C:::A COORDINATES:::
  301 CONTINUE
      DO 103 JXYZ=1,3
      VALUP=A00
      DO 102 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 102 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-UF(J,I)*HF(IJ,JXYZ)*FAC
  102 CONTINUE
      POL(IXYZ,JXYZ)=VALUP*FOUR
  103 CONTINUE
  105 CONTINUE
      WRITE(6,3)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 106 I=1,3
      DO 106 J=1,3
      POLC(I,J)=POL(I,J)*A33
  106 CONTINUE
      WRITE(6,4)
      CALL MATOUT(POLC,3,3,3,3,6)
C
      IJ=0
      DO 107 I=1,3
      DO 107 J=1,I
      IJ=IJ+1
      A(IJ)=POL(I,J)
  107 CONTINUE
      WRITE(6,5)
      CALL PRINT(A,6,3,6)
      CALL RSP(3,3,6,A,EIG,1,EIV,FV1,FV2)
      CALL ZERO(POL,9)
      DO 109 I=1,3
  109 POL(I,I)=EIG(I)
      WRITE(6,6)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 110 I=1,3
      DO 110 J=1,3
      POLC(I,J)=POL(I,J)*A33
  110 CONTINUE
      WRITE(6,7)
      CALL MATOUT(POLC,3,3,3,3,6)
      WRITE(6,8)
      CALL MATOUT(EIV,3,3,3,3,6)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE WRBMAT(BB,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BB(NTRI,NABC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/TAPES/ITAP36,ITAP42,ITAP43,ITAP44
    1 FORMAT(//,2X,' B0 MATRIX'/)
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,BB(1,IABC),NTRI2,IB)
  101 CONTINUE
      WRITE(6,1)
      CALL MATOUT(BB,NTRI,NABC,NTRI,NABC,6)
      CALL SREW(ITAP44)
C
      RETURN
      END
