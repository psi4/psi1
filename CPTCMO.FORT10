      PROGRAM CPTCMO
C   COUPLED PERTURBED HARTREE-FOCK PROGRAM
C   FOR THE CLOSED SHELL TCSCF SYSTEM IN MO BASIS
C**********************************************************************
C*   NOTICE OF PROGRAM MODIFICATION                                   *
C**********************************************************************
c Moved to PSI distribution disk on 020289 - clj.
C**********************************************************************
C   BY:  RICHARD REMINGTON                         search:  c8-08-88  *
C   DATE:  August  8,  1988                                           *
C   REASON: DECREASE CORE TO RUN IN 7MB ON 9370                       *
C**********************************************************************
C   LAST UPDATED ON AUGUST 02, 1985 BY YUKIO YAMAGUCHI                *
C**********************************************************************
C
      IMPLICIT REAL*8 (A-H,O-Z)
c8-08-88  DIMENSION CC(1200000),IA(1)
      DIMENSION CC(600000),IA(1)
      CHARACTER*80 ALABEL
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),APARA(1024)
      COMMON/CIBAF/BAF(45,2)
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/COUPL/ALPC(15),BETC(15)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/MFSEC/MASTER,NSECT
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      COMMON/CI105/A22(2,2)
      EQUIVALENCE (CC,IA)
      DATA ALABEL / ' SCF FIRST DERIVATIVE BROUGHT YOU BY Y & Y CO. EST.
     1 IN MARCH , 1982             ' /
    1 FORMAT(//,2X,' COUPLED PERTURBED HARTREE-FOCK PROGRAM FOR TCSCF IN
     1 MO BASIS'/)
    2 FORMAT(10I5)
    3 FORMAT(2X,10I5)
    4 FORMAT(//,2X,' PARAMETERS'/
     * 2X,' NBASIS = ',I8/
     * 2X,' NTRI   = ',I8/
     * 2X,' NBFAO  = ',I8/
     * 2X,' NBATRI = ',I8/
     * 2X,' NATOMS = ',I8/
     * 2X,' N3N    = ',I8/
     * 2X,' N3NP3  = ',I8/
     * 2X,' N3NXX  = ',I8/
     * 2X,' IOCC   = ',I8/
     * 2X,' JOCC   = ',I8/
     * 2X,' KOCC   = ',I8/
     * 2X,' NTYPES = ',I8/
     * 2X,' NTYPEP = ',I8/
     * 2X,' ITEST  = ',I8/
     * 2X,' IORB   = ',I8/
     * 2X,' IPOL   = ',I8/
     * 2X,' ICORE  = ',I8/
     * 2X,' IPRNT  = ',I8)
    5 FORMAT(//,2X,' ALPC MATRIX'/)
    6 FORMAT(//,2X,' BETC MATRIX'/)
    7 FORMAT(//,2X,' SCF EIGENVECTOR (AO BASIS) MATRIX'/)
    8 FORMAT(//,2X,' SCF EIGENVECTOR (SO BASIS) MATRIX'/)
    9 FORMAT(//,2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
   10 FORMAT(//,2X,' REQUIRED MEMORY EXCEEDS MAXCOR'/
     1          2X,' ICMAX = ',I10,5X,' MAXCOR = ',I10/)
C
C::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C   REFERENCES FOR TCSCF & GRSCF :                        :
C   Y.YAMAGUCHI, Y.OSAMURA, AND H.F.SCHAEFER III,         :
C   J.AM.CHEM.SOC. 105,7506(1983)                         :
C                 &                                       :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, D.J.FOX, M.A.VINCENT, :
C   AND H.F.SCHAEFER III, J.MOL.STRUC. 103,183 (1983)     :
C                 &                                       :
C   Y.OSAMURA, Y.YAMAGUCHI, P.SAXE, M.A.VINCENT, J.F.GAW, :
C   AND H.F.SCHAEFER III, CHEM.PHYS. 72,131(1982)         :
C::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
C
C   ITAP11 = GEOMETRY AND GRADIENTS
C   ITAP15 = SCF SECOND DERIVATIVES
C   ITAP17 = DIPOLE DERIVATIVES
C   ITAP25 = FIRST DERIVATIVES OF ORBITAL ENERGIES
C   ITAP35 = TWO ELECTRON MO INTEGRALS
C   ITAP42 = FIRST AND SECOND DERIVATIVE INFORMATION
C   ITAP43 = FIRST DERIVATIVES OF DIPOLE MOMENTS
C   ITAP44 = FIRST DERIVATIVE MATRICES AND U'S
C   JTAPE1 = WORKING TAPE FOR CPHF
C   JTAPE2 = WORKING TAPE FOR E MATRICES
C
      CALL TSTART(6)
      CALL NOUNFL
      CALL INITMF(1)
C
      ITAPE3=3
      INPUT=5
      ITAPE6=6
      ITAP11=11
      ITAP15=15
      ITAP17=17
      ITAP25=25
      ITAP35=35
      ITAP42=42
      ITAP43=43
      ITAP44=44
      JTAPE1=91
      JTAPE2=92
c8-08-88  MAXCOR=1200000
      MAXCOR= 600000
C
      IOFF(1)=0
      DO 101 I=1,1378
  101 IOFF(I+1)=IOFF(I)+I
C
      CALL LOCATE(INPUT,'# CPHFMO #',IERR)
      WRITE(6,1)
      WRITE(3,1)
C
C   READIN PARAMETERS
      READ(5,2) ITEST,IORB,IPOL,ICORE,IPRNT
C
C   READIN SCF INFORMATION
      NBASIS=LPARA(3)
      NBFAO=LPARA(11)
      NTRI=LPARA(4)
      NTRI2=NTRI*2
      NTOT=IOFF(NTRI+1)
      NBASQ=NBASIS*NBASIS*2
      NBATRI=IOFF(NBFAO+1)
      NBATR2=NBATRI*2
      IOCC=LPARA(7)
      KOCC=LPARA(8)
      JOCC=LPARA(9)
      NATOMS=LPARA(10)
      N3N=LPARA(17)
      NATRI=IOFF(N3N+1)
      N3NP3=N3N+3
      N3NXX=N3NP3
      IF(IPOL.EQ.0) N3NXX=N3N
      NTYPES=LPARA(18)
      NTYPEP=NTYPES+1
      NTRIL=((NTRI2-1)/NSECT+1)
      NBASQL=((NBASQ-1)/NSECT+1)
      N3TRL=N3NXX*NTRIL
      N3QRL=N3NXX*NBASQL
      ENUC=APARA(1)
      ESCF=APARA(2)
      DO 102 I=1,10
      NSORB(I)=LPARA(I+40)
      FOCC(I)=APARA(I+40)
  102 CONTINUE
      DO 103 I=1,15
      ALPC(I)=APARA(I+10)
      BETC(I)=APARA(I+25)
  103 CONTINUE
      DO 104 I=1,N3NXX
      ISA(I)=(I-1)*NTRIL+1
      IHA(I)=(I-1)*NTRIL+N3TRL+1
      IFA(I)=(I-1)*NTRIL+N3TRL+N3TRL+1
      IEA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+1
      IBA(I)=(I-1)*NTRIL+N3TRL+N3TRL+N3TRL+N3QRL+1
      IUA(I)=(I-1)*NBASQL+N3TRL+N3TRL+N3TRL+N3QRL+N3TRL+1
      WRITE(3,3) I,ISA(I),IHA(I),IFA(I),IEA(I),IBA(I),IUA(I)
  104 CONTINUE
      WRITE(6,4) NBASIS,NTRI,NBFAO,NBATRI,NATOMS,N3N,N3NP3,N3NXX,
     1 IOCC,JOCC,KOCC,NTYPES,NTYPEP,ITEST,IORB,IPOL,ICORE,IPRNT
      WRITE(6,5)
      CALL PRINT(ALPC,15,NTYPEP,6)
      WRITE(6,6)
      CALL PRINT(BETC,15,NTYPEP,6)
C
C     IC1  = EIG(NBASIS)
C     IC2  = OCC(NBASIS)
C     IC3  = EAO OR ESO(NBFAO,NBASIS)
C     IC4  = NIJ(NTRI,2)
C     IC5  = KIJ(NTRI,2)
C     IC6  = ALPA(NTRI)
C     IC7  = BETA(NTRI)
C     IC8  = E1A(N3N)
C     IC9  = E2A(N3N,N3N)
C     IC10 = EPS(NTRI)
C     IC11 = ZETA(NTRI,NTYPEP)
C     IC12 = A12(NIND,2)
C
C   READ IN AO-MO EIGENVECTORS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC1=1
      IC2=IC1+NBASIS
      IC3=IC2+NBASIS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      CALL MREAD(CC(IC1),17)
      CALL MREAD(CC(IC2),18)
      CALL MREAD(CC(IC3),20)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,7)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C**************************
C***PREPARATION FOR CPHF***
C**************************
C
C   FORM REGISTER MATRICES
  301 CONTINUE
      WRITE(3,20)
   20 FORMAT(//,2X,' NOW YOU ARE IN REGIST'/)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC4=IC3+NBFAO*NBASIS
      IA4=IC4+IC4-1
      IC5=IC4+NTRI
      IA5=IC5+IC5-1
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C.................NIJ     KIJ.....
      CALL REGIST(IA(IA4),IA(IA5))
C
C   FORM ALPA AND BETA MATRICES
      WRITE(3,21)
   21 FORMAT(//,2X,' NOW YOU ARE IN ABMAT'/)
      IC6=IC5+NTRI
      IC7=IC6+NTRI
C................ALPA    BETA....
      CALL ABMAT(CC(IC6),CC(IC7))
C
C   READ IN FIRST AND SECOND DERIVATIVE INFORMATION
C   AND FORM SM, HM AND FM MATRICES
      WRITE(3,22)
   22 FORMAT(//,2X,' NOW YOU ARE IN DERMAT'/)
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC8=IC7+NTRI
      IC9=IC8+N3N
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      IC10=IC9+N3N*N3N
      IC11=IC10+NBATRI
      IC12=IC11+NBFAO*NBFAO
      ICMAX=IC12+NBFAO*NBFAO
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................EAO     E1A     E2A     SS       U        T........
      CALL DERMAT(CC(IC3),CC(IC8),CC(IC9),CC(IC10),CC(IC11),CC(IC12))
C
C   READ IN SO-MO EIGENVECTORS
      CALL MREAD(CC(IC3),19)
      IF(IPRNT.LE.2) GO TO 302
      WRITE(6,8)
      CALL EIGOUT(CC(IC3),CC(IC1),CC(IC2),NBFAO,NBASIS,NBFAO,NBASIS,6)
C
C:::::::::::::::::::::::::::::::::::
C:::TEST ROUTINES FOR INPUT CHECK:::
C:::::::::::::::::::::::::::::::::::
  302 CONTINUE
      IF(ITEST.EQ.0) GO TO 303
      WRITE(3,23)
   23 FORMAT(//,2X,' NOW YOU ARE IN E0CAL'/)
C   CALCULATE SCF ENERGY FOR A TEST
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTOT
      IA12=IC12+IC12-1
      ICMAX=IC12+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ALPA    BETA    H        CC       LBLI.....
      CALL E0CAL(CC(IC2),CC(IC6),CC(IC7),CC(IC10),CC(IC11),IA(IA12),
C................BUFI..........
     1           CC(IC12),NTOT)
C
C   CALCULATE LAGRANGIAN AND ZETA MATRICES
  303 CONTINUE
      WRITE(3,24)
   24 FORMAT(//,2X,' NOW YOU ARE IN E0LAG'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTOT
      IA14=IC14+IC14-1
      ICMAX=IC14+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ALPA    BETA    EPS      ZETA     H........
      CALL E0LAG(CC(IC2),CC(IC6),CC(IC7),CC(IC10),CC(IC11),CC(IC12),
C................CC       LBLI     BUFI..........
     1           CC(IC13),IA(IA14),CC(IC14),NTOT)
C
C   CALCULATE SCF FIRST ENERGY DERIVATIVES
      IF(ITEST.EQ.0) GO TO 304
      WRITE(3,25)
   25 FORMAT(//,2X,' NOW YOU ARE IN MODER'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+N3N
      IC14=IC13+N3N
      IC15=IC14+N3N
      IC16=IC15+N3N
      IC17=IC16+NTRI*N3N
      IC18=IC17+NTRI*N3N
      ICMAX=IC18+NTRI*N3N*NTREAD
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     ALPA    BETA    EPS      D1O      D1T......
      CALL MODER(CC(IC2),CC(IC6),CC(IC7),CC(IC10),CC(IC12),CC(IC13),
C................D1W,     E1T      SM       HM       TM.......
     1           CC(IC14),CC(IC15),CC(IC16),CC(IC17),CC(IC18))
C
C   FORM HA MATRICES
  304 CONTINUE
      WRITE(3,26)
   26 FORMAT(//,2X,' NOW YOU ARE IN HAMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+N3N
      IC14=IC13+N3N
      IC15=IC14+N3N
      IC16=IC15+NTRI*N3N
      ICMAX=IC16+NTRI*N3N*NTREAD
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................BETA    D1W      D1T      D1W      HM       TM.......
      CALL HAMAT(CC(IC7),CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC16))
C
C   FORM DERIVATIVE LAGRANGIAN MATRICES
      WRITE(3,27)
   27 FORMAT(//,2X,' NOW YOU ARE IN FAMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NBASIS*NBASIS
      IC15=IC14+NTRI*N3N
      ICMAX=IC15+NTRI*N3N*NTREAD
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................ALPA    BETA    FF       EPA      HM       TM.......
      CALL FAMAT(CC(IC6),CC(IC7),CC(IC12),CC(IC13),CC(IC14),CC(IC15))
C
C   CALCULATE BA MATRICES
      WRITE(3,28)
   28 FORMAT(//,2X,' NOW YOU ARE IN BAMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI*N3N
      IC14=IC13+NTRI*N3N
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NTRI
      IC17=IC16+NTOT
      IA17=IC17+IC17-1
      ICMAX=IC17+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     OCC     NIJ     KIJ     ALPA    BETA....
      CALL BAMAT(CC(IC1),CC(IC2),IA(IA4),IA(IA5),CC(IC6),CC(IC7),
C................ZETA     SM       B0       EPA      FA       CC.......
     1           CC(IC11),CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC16),
C................LBLI     BUFI..........
     2           IA(IA17),CC(IC17),NTOT)
C
C   CALCULATE H MATRIX
      WRITE(3,29)
   29 FORMAT(//,2X,' NOW YOU ARE IN HMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      IC14=IC13+NTOT
      IA14=IC14+IC14-1
      ICMAX=IC14+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C...............H        CC       LBLI     BUFI..........
      CALL HMAT(CC(IC12),CC(IC13),IA(IA14),CC(IC14),NTOT)
C
C   CALCULATE A MATRIX
      WRITE(3,30)
   30 FORMAT(//,2X,' NOW YOU ARE IN AMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTOT
      IA13=IC13+IC13-1
      ICMAX=IC13+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C...............EIG     NIJ     KIJ     ALPA    BETA    ZETA.....
      CALL AMAT(CC(IC2),IA(IA4),IA(IA5),CC(IC6),CC(IC7),CC(IC11),
C...............CC       LBLI     BUFI..........
     1          CC(IC12),IA(IA13),CC(IC13),NTOT)
C
C   CALCULATE E MATRICES
      WRITE(3,31)
   31 FORMAT(//,2X,' NOW YOU ARE IN EMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NIND*2
      IC14=IC13+NBASIS*NBASIS
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NBASIS*NBASIS
      IC17=IC16+NBASIS*NBASIS
      IC18=IC17+NTRI
      IC19=IC18+NTOT
      IA19=IC19+IC19-1
      ICMAX=IC19+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C...............NIJ     A12      E11      E22      E12      EE.......
      CALL EMAT(IA(IA4),CC(IC12),CC(IC13),CC(IC14),CC(IC15),CC(IC16),
C...............H        CC       LBLI     BUFI..........
     1          CC(IC17),CC(IC18),IA(IA19),CC(IC19),NTOT)
C
C   FORM BF MATRICES
      WRITE(3,32)
   32 FORMAT(//,2X,' NOW YOU ARE IN BFMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NIND*2
      IC14=IC13+NTRI
      IC15=IC14+NTRI*NTYPES*3
      IC16=IC15+NBASIS*NBASIS
      ICMAX=IC16+NTRI
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................NIJ     KIJ     HF       HM       EPF      BF.......
      CALL BFMAT(IA(IA4),IA(IA5),CC(IC13),CC(IC14),CC(IC15),CC(IC16))
C
C   FORM BAF MATRICES
      WRITE(3,33)
   33 FORMAT(//,2X,' NOW YOU ARE IN BAFMAT'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NIND*2
      IC14=IC13+NTRI*N3N
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NBASIS*NBASIS
      ICMAX=IC16+NBASIS*NBASIS
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................SA       E11      E22      E12............
      CALL BAFMAT(CC(IC13),CC(IC14),CC(IC15),CC(IC16),N3NXX)
C
C********************
C***CPHF PROCEDURE***
C********************
C
C   SOLVE SIMULTANEOUS EQUATIONS DIRECTLY
      WRITE(3,34)
   34 FORMAT(//,2X,' NOW YOU ARE IN CPHF'/)
      NINDP2=NIND+2
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NIND*2
      IC14=IC13+NTRI
      IC15=IC14+NBASIS*NBASIS
      IC16=IC15+NBASIS*NBASIS
      IC17=IC16+NBASIS*NBASIS
      IC18=IC17+NBASIS*NBASIS
      IC19=IC18+NTRI*N3NXX
      IC20=IC19+NTRI*N3NXX
      IC21=IC20+NINDP2*(NINDP2+N3NXX)
      IA21=IC21+IC21-1
      ICMAX=IC21+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................EIG     NIJ     KIJ     A12      B0       UA.......
       CALL CPHF(CC(IC1),IA(IA4),IA(IA5),CC(IC12),CC(IC13),CC(IC14),
C................E11      E22      E12      SA       BA       C........
     1           CC(IC15),CC(IC16),CC(IC17),CC(IC18),CC(IC19),CC(IC20),
C................LBLI     BUFI..................
     2           IA(IA21),CC(IC21),NINDP2,N3NXX)
C
C   CALCULATE SCF SECOND DERIVATIVES
      WRITE(3,35)
   35 FORMAT(//,2X,' NOW YOU ARE IN SCF2ND'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+N3N*N3N
      IC14=IC13+N3N*N3N
      IC15=IC14+N3N*N3N
      IC16=IC15+NBASIS*NBASIS
      IC17=IC16+NBASIS*NBASIS
      IC18=IC17+NBASIS*NBASIS
      IC19=IC18+NBASIS*NBASIS
      IC20=IC19+NBASIS*NBASIS
      IC21=IC20+NTRI*N3N
      IC22=IC21+NBASIS*NBASIS*N3N
      IC23=IC22+NTOT
      IA23=IC23+IC23-1
      ICMAX=IC23+MAXBF2
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................ALPA    BETA    E2A     EPS      ZETA     E2M......
      CALL SCF2ND(CC(IC6),CC(IC7),CC(IC9),CC(IC10),CC(IC11),CC(IC12),
C.................E2C      E2P      EPA      UA       E11      E22......
     1            CC(IC13),CC(IC14),CC(IC15),CC(IC16),CC(IC17),CC(IC18),
C.................E12      SA       WA       CC       LBLI     BUFI.....
     2            CC(IC19),CC(IC20),CC(IC21),CC(IC22),IA(IA23),CC(IC23),
C......................
     3            NTOT)
C
C   CALCULATE DIPOLE MOMENT DERIVATIVES
      WRITE(3,36)
   36 FORMAT(//,2X,' NOW YOU ARE IN DIPMO'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI*3
      ICMAX=IC13+NBASIS*NBASIS*3
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     DIP      UA.......
      CALL DIPMO(CC(IC2),CC(IC12),CC(IC13))
C
C   FIND ORBITAL ENERGY DERIVATIVES
      IF(IORB.EQ.0) GO TO 305
      WRITE(3,37)
   37 FORMAT(//,2X,' NOW YOU ARE IN ORB1ST'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI
      ICMAX=IC13+NBASIS*N3N
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C.................B0       CC.........
      CALL ORB1ST(CC(IC12),CC(IC13),1)
      IF(IPOL.EQ.0) GO TO 305
C.................B0       CC.........
      CALL ORB1ST(CC(IC12),CC(IC13),2)
C
C   CALCULATE ELECTRIC POLARIZABILITIES
  305 CONTINUE
      IF(IPOL.EQ.0) GO TO 310
      WRITE(3,38)
   38 FORMAT(//,2X,' NOW YOU ARE IN POLAR'/)
      IC10=IC9+N3N*N3N
      IC11=IC10+NTRI
      IC12=IC11+NTRI*NTYPEP
      IC13=IC12+NTRI*3
      ICMAX=IC13+NBASIS*NBASIS
      WRITE(3,9) ICMAX,MAXCOR
      IF(ICMAX.GT.MAXCOR) GO TO 399
C................OCC     HF       UF.......
      CALL POLAR(CC(IC2),CC(IC12),CC(IC13))
C
  310 CONTINUE
      GO TO 400
  399 CONTINUE
      WRITE(6,10) ICMAX,MAXCOR
      WRITE(3,10) ICMAX,MAXCOR
  400 CONTINUE
      CALL RCLOSE(MASTER,3)
      CALL RCLOSE(ITAP42,3)
      CALL RCLOSE(ITAP43,3)
      CALL RCLOSE(ITAP44,3)
      CALL RCLOSE(JTAPE1,3)
      CALL RCLOSE(JTAPE2,3)
C
      CALL TSTOP(6)
      STOP
      END
      SUBROUTINE REGIST(NIJ,KIJ)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
    1 FORMAT(//,2X,' REGISTERED NUMBERS'/
     1 2X,' KVIR = ',I5/
     2 2X,' NIND = ',I5/
     3 2X,' NDEP = ',I5/
     4 2X,' NDIF = ',I5)
    2 FORMAT(//,2X,' NIJ MATRIX'/2X,' NO.',5X,' NIJ(1)',5X,' NIJ(2)'/)
    3 FORMAT(2X,I4,4X,I5,7X,I5)
    4 FORMAT(//,2X,' KIJ MATRIX'/2X,' NO.',5X,' KIJ(1)',5X,' KIJ(2)'/)
    5 FORMAT(//,2X,' NO.',8X,' FOCC',6X,' NSORB',7X,' NSTR',7X,' NEND'/)
    6 FORMAT(2X,I4,3X,F10.5,7X,I5,7X,I5,7X,I5)
C
      KVIR=NBASIS-JOCC
C
      NSTR(1)=1
      NEND(1)=NSORB(1)
      DO 101 ITYP=2,NTYPEP
      NSTR(ITYP)=NEND(ITYP-1)+1
      NEND(ITYP)=NSTR(ITYP)+NSORB(ITYP)-1
  101 CONTINUE
      DO 103 ITYP=1,NTYPEP
      DO 102 I=NSTR(ITYP),NEND(ITYP)
  102 MOTYP(I)=ITYP
  103 CONTINUE
C
      II=0
      DO 105 ITYP=2,NTYPEP
      DO 105 JTYP=1,ITYP-1
      DO 104 I=NSTR(ITYP),NEND(ITYP)
      DO 104 J=NSTR(JTYP),NEND(JTYP)
      II=II+1
      NIJ(II,1)=I
      NIJ(II,2)=J
  104 CONTINUE
  105 CONTINUE
      NIND=II
C
      II=0
      DO 107 ITYP=1,NTYPEP
C*C   IF(NSORB(ITYP).EQ.1) GO TO 107
C*C   DO 106 I=NSTR(ITYP)+1,NEND(ITYP)
C*C   DO 106 J=NSTR(ITYP),I-1
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=NSTR(ITYP),I
      II=II+1
      KIJ(II,1)=I
      KIJ(II,2)=J
  106 CONTINUE
  107 CONTINUE
      NDEP=II
C
      NDIF=NTRI-NIND-NDEP
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1) KVIR,NIND,NDEP,NDIF
      WRITE(6,2)
      DO 108 I=1,NIND
  108 WRITE(6,3) I,NIJ(I,1),NIJ(I,2)
      WRITE(6,4)
      DO 109 I=1,NDEP
  109 WRITE(6,3) I,KIJ(I,1),KIJ(I,2)
      WRITE(6,5)
      DO 110 I=1,NTYPEP
  110 WRITE(6,6) I,FOCC(I),NSORB(I),NSTR(I),NEND(I)
C
  205 RETURN
      END
      SUBROUTINE DERMAT(EAO,E1A,E2A,SS,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E2A(N3N,N3N),SS(NBATRI)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
    1 FORMAT(//,2X,' SCF FIRST DERIVATIVE MATRIX'/)
    2 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    3 FORMAT(//,2X,' SA (AO BASIS) MATRIX, IABC = ',I5/)
    4 FORMAT(//,2X,' SA (MO BASIS) MATRIX, IABC = ',I5/)
    5 FORMAT(//,2X,' HA (AO BASIS) MATRIX, IABC = ',I5/)
    6 FORMAT(//,2X,' HA (MO BASIS) MATRIX, IABC = ',I5/)
    7 FORMAT(//,2X,' TA (AO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
    8 FORMAT(//,2X,' TA (MO BASIS) MATRIX, IABC = ',I5,' ITYP = ',I5/)
C
      CALL RFILE(ITAP42)
      CALL RFILE(ITAP44)
      CALL RFILE(JTAPE1)
      NBSET=6
      NTREAD=NBSET
C
C   READ IN SCF FIRST DERIVATIVES
      CALL SREAD(ITAP42,E1A,N3N*2)
      WRITE(6,1)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
C
C   READ IN SCF SECOND DERIVATIVES
      CALL SREAD(ITAP42,E2A,N3N*N3N*2)
      WRITE(6,2)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
C
C   READ IN S FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,3) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  201 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IS)
      IF(IPRNT.LE.4) GO TO 101
      WRITE(6,4) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  101 CONTINUE
C
C   READ IN ONE ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 102 IABC=1,N3N
      IH=IHA(IABC)
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 202
      WRITE(6,5) IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  202 CONTINUE
      CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL RWRIT(ITAP44,SS,NTRI2,IH)
      IF(IPRNT.LE.4) GO TO 102
      WRITE(6,6) IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  102 CONTINUE
C
C   READ IN TWO ELECTRON FIRST DERIVATIVE INTEGRALS
C   AND TRANSFORM THEM FROM AO TO MO BASIS
      DO 104 ITYP=1,NBSET
      DO 103 IABC=1,N3N
      CALL SREAD(ITAP42,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 203
      WRITE(6,7) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBFAO,6)
  203 CALL MOCONV(SS,NBATRI,SS,NBATRI,EAO,U,T)
      CALL SWRIT(JTAPE1,SS,NTRI2)
      IF(IPRNT.LE.4) GO TO 103
      WRITE(6,8) ITYP,IABC
      CALL PRINT(SS,NBATRI,NBASIS,6)
  103 CONTINUE
  104 CONTINUE
C
      CALL SREW(ITAP42)
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE MOCONV(SA,NNA,SM,NNM,EAO,U,T)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NNA),SM(NNM)
      DIMENSION EAO(NBFAO,NBASIS),U(NBFAO,NBFAO),T(NBFAO,NBFAO)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA ZERO / 0.0D+00 /
C
C   TRANSFORM INTEGRALS FROM AO TO MO BASIS
      DO 101 II=1,NBFAO
      DO 101 JJ=1,NBFAO
      IIJJ=IOFF(MAX0(II,JJ))+MIN0(II,JJ)
      T(II,JJ)=SA(IIJJ)
  101 CONTINUE
      DO 103 II=1,NBFAO
      DO 103 J=1,NBASIS
      SUM=ZERO
      DO 102 JJ=1,NBFAO
      SUM=SUM+EAO(JJ,J)*T(II,JJ)
  102 CONTINUE
      U(II,J)=SUM
  103 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      SUM=ZERO
      DO 104 II=1,NBFAO
      SUM=SUM+EAO(II,I)*U(II,J)
  104 CONTINUE
      SM(IJ)=SUM
  105 CONTINUE
C
      RETURN
      END
      SUBROUTINE ABMAT(ALPA,BETA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/COUPL/ALPC(15),BETC(15)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      DATA ZERO,QUAR,HALF,ONE / 0.0D+00 , 0.25D+00 , 0.5D+00 , 1.0D+00 /
    1 FORMAT(//,2X,' ALPA MATRIX'/)
    2 FORMAT(//,2X,' BETA MATRIX'/)
C
C   SET UP ALPHA AND BETA MATRICES
      DO 101 I=1,NTRI
      ALPA(I)=ZERO
      BETA(I)=ZERO
  101 CONTINUE
C
      NSTRI=0
      NENDI=0
      DO 105 ITYP=1,NTYPES
      MM=NSORB(ITYP)
      IF(MM.EQ.0) GO TO 105
      FI=FOCC(ITYP)
      NSTRI=NENDI+1
      NENDI=NSTRI+MM-1
      NSTRJ=0
      NENDJ=0
      DO 104 JTYP=1,ITYP
      NN=NSORB(JTYP)
      IF(NN.EQ.0) GO TO 104
      FJ=FOCC(JTYP)
      NSTRJ=NENDJ+1
      NENDJ=NSTRJ+NN-1
      DO 102 I=NSTRI,NENDI
      DO 102 J=NSTRJ,NENDJ
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      IIJJ=IOFF(MAX0(ITYP,JTYP))+MIN0(ITYP,JTYP)
      ALPA(IJ)=(ONE-ALPC(IIJJ))*FI*FJ*HALF
      BETA(IJ)=-(ONE-BETC(IIJJ))*FI*FJ*QUAR
  102 CONTINUE
  104 CONTINUE
  105 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 205
      WRITE(6,1)
      CALL PRINT(ALPA,NTRI,NBASIS,6)
      WRITE(6,2)
      CALL PRINT(BETA,NTRI,NBASIS,6)
C
  205 RETURN
      END
      SUBROUTINE E0CAL(OCC,ALPA,BETA,H,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),ALPA(NTRI),BETA(NTRI),H(NTRI),CC(NNC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,ONE,TWO / 0.0D+00 , 1.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' H MATRIX (MO BASIS)'/)
    2 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
C
C   CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(4)
C
      CALL MREAD(H,16)
      IF(IPRNT.LE.3) GO TO 201
      WRITE(6,1)
      CALL PRINT(H,NTRI,NBASIS,6)
C
C   READ IN TWO ELECTRON MO INTEGRALS
  201 CONTINUE
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   CALCULATE ONE-ELECTRON ENERGY
      ELEC1=ZERO
      DO 101 I=1,JOCC
      II=IOFF(I+1)
      ELEC1=ELEC1+OCC(I)*H(II)
  101 CONTINUE
C   CALCULATE TWO-ELECTRON ENRGY
      ELEC2=ZERO
      DO 102 I=1,JOCC
      DO 102 J=1,I
      IJ=IOFF(I)+J
      II=IOFF(I+1)
      JJ=IOFF(J+1)
      IIJJ=IOFF(II)+JJ
      IJIJ=IOFF(IJ+1)
      FAC=TWO
      IF(I.EQ.J) FAC=ONE
      ELEC2=ELEC2+(ALPA(IJ)*CC(IIJJ)+BETA(IJ)*CC(IJIJ))*FAC
  102 CONTINUE
C
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,2) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
      RETURN
      END
      SUBROUTINE E0LAG(OCC,ALPA,BETA,EPS,ZETA,H,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),ALPA(NTRI),BETA(NTRI),H(NTRI),CC(NNC)
      DIMENSION EPS(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,HALF,TWO / 0.0D+00 , 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' LAGRANGIAN MATRIX'/)
    2 FORMAT(//,2X,' SCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' ELEC-ONE   = ',F20.10/
     * 2X,' ELEC-TWO   = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10)
    3 FORMAT(//,2X,' ZETA MATRIX  , ITYP = ',I5/)
C
C   FORM LAGRANGIAN MATRIX AND CALCULATE SCF ENERGY FOR A TEST
C   SEE EQ.(11)
C
      CALL MREAD(H,16)
C   READ IN TWO ELECTRON MO INTEGRALS
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   ONE-ELECTRON TERMS
      DO 101 I=1,NBASIS
      FAC=OCC(I)*HALF
      DO 101 J=1,I
      IJ=IOFF(I)+J
      EPS(IJ)=H(IJ)*FAC
  101 CONTINUE
      ELEC1=ZERO
      DO 102 I=1,JOCC
      II=IOFF(I+1)
  102 ELEC1=ELEC1+EPS(II)*TWO
C
C   TWO-ELECTRON TERMS
      DO 105 I=1,NBASIS
      DO 105 J=1,I
      IJ=IOFF(I)+J
      VALU=ZERO
      DO 104 K=1,JOCC
      KK=IOFF(K+1)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IJKK=IOFF(MAX0(IJ,KK))+MIN0(IJ,KK)
      IKJK=IOFF(MAX0(IK,JK))+MIN0(IK,JK)
      VALU=VALU+ALPA(IK)*CC(IJKK)+BETA(IK)*CC(IKJK)
  104 CONTINUE
      EPS(IJ)=EPS(IJ)+VALU
  105 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,1)
      CALL PRINT(EPS,NTRI,NBASIS,6)
  201 ELEC2=ZERO
      DO 106 I=1,JOCC
      II=IOFF(I+1)
  106 ELECT=ELECT+EPS(II)
C
      ELEC2=ELECT-ELEC1*HALF
      ELECT=ELEC1+ELEC2
      ETOT=ENUC+ELECT
      WRITE(6,2) ESCF,ENUC,ELEC1,ELEC2,ELECT,ETOT
C
C   CALCULATE ZETA MATRICES
C   SEE EQ.(16)
C
      DO 110 ITYP=1,NTYPES
      L=NSTR(ITYP)
      FAC=FOCC(ITYP)*HALF
      DO 109 I=1,NBASIS
      DO 109 J=1,I
      IJ=IOFF(I)+J
      VALU=H(IJ)*FAC
      DO 108 K=1,JOCC
      KK=IOFF(K+1)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IJKK=IOFF(MAX0(IJ,KK))+MIN0(IJ,KK)
      IKJK=IOFF(MAX0(IK,JK))+MIN0(IK,JK)
      LK=IOFF(MAX0(L,K))+MIN0(L,K)
      VALU=VALU+ALPA(LK)*CC(IJKK)+BETA(LK)*CC(IKJK)
  108 CONTINUE
      ZETA(IJ,ITYP)=VALU
  109 CONTINUE
      IF(IPRNT.LE.2) GO TO 110
      WRITE(6,3) ITYP
      CALL PRINT(ZETA(1,ITYP),NTRI,NBASIS,6)
  110 CONTINUE
C
C   FOR VIRTUAL SPACE
      DO 115 I=1,NTRI
  115 ZETA(I,NTYPEP)=ZERO
C
      RETURN
      END
      SUBROUTINE MODER(OCC,ALPA,BETA,EPS,D1O,D1T,D1W,E1T,SM,HM,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),ALPA(NTRI),BETA(NTRI),EPS(NTRI)
      DIMENSION D1O(N3N),D1T(N3N),D1W(N3N),E1T(N3N)
      DIMENSION SM(NTRI,N3N),HM(NTRI,N3N),TM(NTRI,N3N,NTREAD)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,TWO / 0.0D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' D1N MATRIX'/)
    2 FORMAT(//,2X,' D1O MATRIX'/)
    3 FORMAT(//,2X,' D1T MATRIX'/)
    4 FORMAT(//,2X,' D1W MATRIX'/)
    5 FORMAT(//,2X,' EGRAD MATRIX'/)
C
C   CALCULATE SCF FIRST DERIVATIVES FOR A TEST
C   SEE EQ.(9)
C
C   READ BACK DERIVATIVE SM AND HM MATRICES
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      IH=IHA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,HM(1,IABC),NTRI2,IH)
  102 CONTINUE
C
C   READ BACK DERIVATIVE TM MATRICES
      CALL SREW(JTAPE1)
      DO 105 ITYP=1,NTREAD
      DO 104 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  104 CONTINUE
  105 CONTINUE
C
      DO 110 IABC=1,N3N
C  ONE-ELECTRON CONTRIBUTION
      VALU=ZERO
      DO 106 I=1,JOCC
      II=IOFF(I+1)
      VALU=VALU+HM(II,IABC)*OCC(I)
  106 CONTINUE
      D1O(IABC)=VALU
C  TWO-ELECTRON CONTRIBUTION
      VALU=ZERO
      DO 108 ITYP=1,NTYPES
      I=NSTR(ITYP)
      DO 108 JTYP=1,NTYPES
      DO 107 J=NSTR(JTYP),NEND(JTYP)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      JJ=IOFF(J+1)
      VALU=VALU+ALPA(IJ)*TM(JJ,IABC,ITYP*2-1)
     1         +BETA(IJ)*TM(JJ,IABC,ITYP*2)
  107 CONTINUE
  108 CONTINUE
      D1T(IABC)=VALU
C   OVERLAP CONTRIBUTION
      VALU=ZERO
      DO 109 I=1,JOCC
      DO 109 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU=VALU-SM(IJ,IABC)*EPS(IJ)
  109 CONTINUE
      D1W(IABC)=VALU*TWO
C
C   SUM UP ALL CONTRIBUTIONS
      E1T(IABC)=D1O(IABC)+D1T(IABC)+D1W(IABC)
C
  110 CONTINUE
C
CCC   WRITE(6,1)
CCC   CALL MATOUT(E1N,3,NATOMS,3,NATOMS,6)
      WRITE(6,2)
      CALL MATOUT(D1O,3,NATOMS,3,NATOMS,6)
      WRITE(6,3)
      CALL MATOUT(D1T,3,NATOMS,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
      WRITE(6,5)
      CALL MATOUT(E1T,3,NATOMS,3,NATOMS,6)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE HAMAT(BETA,D1E,D1T,D1W,HM,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BETA(NTRI)
      DIMENSION D1E(N3N),D1T(N3N),D1W(N3N)
      DIMENSION HM(NTRI,N3N),TM(NTRI,N3N,NTREAD)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA ZERO,HALF,TWO,FOUR / 0.0D+00 , 0.5D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' HA MATRICES'/)
    2 FORMAT(//,2X,I5,5F15.8)
    3 FORMAT(//,2X,' H11A MATRIX'/)
    4 FORMAT(//,2X,' H22A MATRIX'/)
    5 FORMAT(//,2X,' H12A MATRIX'/)
    6 FORMAT(//,2X,' D1N MATRIX'/)
    7 FORMAT(//,2X,' D1E MATRIX'/)
    8 FORMAT(//,2X,' D1W MATRIX'/)
    9 FORMAT(//,2X,' EGRAD MATRIX'/)
C
C   CALL BACK HM AND TM MATRICES AND FORM HA MATRICES
C   SEE EQS.(33)-(35)
C
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HM(1,IABC),NTRI2,IH)
  102 CONTINUE
C
      CALL SREW(JTAPE1)
      DO 106 ITYP=1,NTREAD
      DO 105 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  105 CONTINUE
  106 CONTINUE
C
C*    WRITE(6,1)
      DO 115 IABC=1,N3N
      ELEC1=ZERO
      DO 110 I=1,IOCC
      II=IOFF(I+1)
      ELEC1=ELEC1+HM(II,IABC)*TWO
  110 CONTINUE
      ELEC2=ZERO
      DO 111 I=1,IOCC
      II=IOFF(I+1)
      ELEC2=ELEC2+TM(II,IABC,1)*TWO-TM(II,IABC,2)
  111 CONTINUE
C
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      H11=HM(MM,IABC)*TWO+TM(MM,IABC,3)
      H22=HM(NN,IABC)*TWO+TM(NN,IABC,5)
      H12=TM(MM,IABC,6)
      DO 112 I=1,IOCC
      II=IOFF(I+1)
      H11=H11+TM(II,IABC,3)*FOUR-TM(II,IABC,4)*TWO
      H22=H22+TM(II,IABC,5)*FOUR-TM(II,IABC,6)*TWO
  112 CONTINUE
      H11A(IABC)=ELEC1+ELEC2+H11
      H22A(IABC)=ELEC1+ELEC2+H22
      H12A(IABC)=H12
      IF(IPRNT.LE.3) GO TO 115
      WRITE(6,2) IABC,ELEC1,ELEC2,H11,H22,H12
  115 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,3)
      CALL MATOUT(H11A,3,15,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(H22A,3,15,3,NATOMS,6)
      WRITE(6,5)
      CALL MATOUT(H12A,3,15,3,NATOMS,6)
C
C   CALCULATE SCF DERIVATIVES FOR A TEST
  201 C1SQ=FOCC(2)*HALF
      C2SQ=FOCC(3)*HALF
      MN=M+IOFF(N)
      C12P=BETA(MN)*TWO
      C1=DSQRT(C1SQ)
      C2=DSQRT(C2SQ)
      IF(BETA(MN).LT.ZERO) C2=-C2
      DO 120 IABC=1,N3N
      D1E(IABC)=H11A(IABC)*C1SQ+H22A(IABC)*C2SQ+H12A(IABC)*C12P
      D1T(IABC)=D1E(IABC)+D1W(IABC)
      E1T(IABC)=D1T(IABC)
  120 CONTINUE
      IF(IPRNT.LE.2) GO TO 202
C***  WRITE(6,6)
C***  CALL MATOUT(D1N,3,NATOMS,3,NATOMS,6)
      WRITE(6,7)
      CALL MATOUT(D1E,3,NATOMS,3,NATOMS,6)
      WRITE(6,8)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
      WRITE(6,9)
      CALL MATOUT(D1T,3,NATOMS,3,NATOMS,6)
C
  202 CONTINUE
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE FAMAT(ALPA,BETA,FF,EPA,HM,TM)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI)
      DIMENSION FF(NTRI),HM(NTRI,N3N),TM(NTRI,N3N,NTREAD)
      DIMENSION EPA(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' EPA MATRIX, IABC = ',I5/)
C
C   CALL BACK HM AND TM MATRICES AND FORM FA MATRICES
C   SEE EQS.(15) AND (32)
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
C
      DO 101 IABC=1,N3N
      IH=IHA(IABC)
      CALL RREAD(ITAP44,HM(1,IABC),NTRI2,IH)
  101 CONTINUE
C
      DO 103 ITYP=1,NTREAD
      DO 102 IABC=1,N3N
      CALL SREAD(JTAPE1,TM(1,IABC,ITYP),NTRI2)
  102 CONTINUE
  103 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 110 IABC=1,N3N
      IE=IEA(IABC)
      DO 105 I=1,NBASIS
      ITYP=MOTYP(I)
      FAC=FOCC(ITYP)*HALF
      DO 105 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU=HM(IJ,IABC)*FAC
      DO 104 KTYP=1,NTYPES
      K=NSTR(KTYP)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      VALU=VALU+ALPA(IK)*TM(IJ,IABC,KTYP*2-1)
     1         +BETA(IK)*TM(IJ,IABC,KTYP*2)
  104 CONTINUE
      EPA(I,J)=VALU
  105 CONTINUE
C
      CALL RWRIT(ITAP44,EPA,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,1) IABC
      CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
C
C   STORE DERIVATIVE AVERAGED FOCK
      DO 115 IABC=1,N3N
      IT=IFA(IABC)
      DO 114 I=1,NTRI
  114 FF(I)=HM(I,IABC)+TM(I,IABC,NTREAD)
      CALL RWRIT(ITAP44,FF,NTRI2,IT)
  115 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      FUNCTION GSAI(I,J,K,L,ALPA,BETA,CC,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI),CC(NNC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA TWO / 2.0D+00 /
C
C FORM GSAI MATRIX
C
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IJKL=IOFF(MAX0(IJ,KL))+MIN0(IJ,KL)
      IKJL=IOFF(MAX0(IK,JL))+MIN0(IK,JL)
      ILJK=IOFF(MAX0(IL,JK))+MIN0(IL,JK)
      GSAI=(ALPA(IK)-ALPA(JK))*CC(IJKL)*TWO
     1    +(BETA(IK)-BETA(JK))*(CC(IKJL)+CC(ILJK))
C
      RETURN
      END
      SUBROUTINE BAMAT(EIG,OCC,NIJ,KIJ,ALPA,BETA,ZETA,SM,B0,EPA,FA,CC,
     1                 LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION EIG(NBASIS),OCC(NBASIS),NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION ALPA(NTRI),BETA(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION SM(NTRI,N3N),B0(NTRI,N3N),EPA(NBASIS,NBASIS),FA(NTRI)
      DIMENSION CC(NNC),LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' FM MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' B0 MATRIX '/)
    3 FORMAT(2X,3I5,4F15.8)
C
C   FORM B0 MATRICES
C   SEE EQ.(26)
C
C   READ IN TWO ELECTRON MO INTEGRALS AGAIN
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   CALL BACK SM MATRICES
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SM(1,IABC),NTRI2,IS)
      DO 101 I=1,NTRI
      B0(I,IABC)=ZERO
  101 CONTINUE
  102 CONTINUE
C
C   ELEMENTS FOR INDEPENDENT PAIRS
      DO 120 IABC=1,N3N
      IT=IFA(IABC)
      IE=IEA(IABC)
      IB=IBA(IABC)
      CALL RREAD(ITAP44,FA,NTRI2,IT)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1) IABC
      CALL MATOUT(EPA,NBASIS,NBASIS,NBASIS,NBASIS,6)
      WRITE(6,2) IABC
  301 CONTINUE
      DO 110 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
      VALU1=EPA(I,J)-EPA(J,I)
      VALU2=ZERO
      VALU3=ZERO
C
      DO 107 K=1,NBASIS
      LIM=MIN0(JOCC,K)
      DO 107 L=1,LIM
      KL=IOFF(K)+L
      FAC2=GSAI(I,J,K,L,ALPA,BETA,CC,NNC)
      IF(K.EQ.L) FAC2=FAC2*HALF
      VALU2=VALU2-SM(KL,IABC)*FAC2
      FAC3=ZERO
      IF(K.NE.J) GO TO 205
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      FAC3=FAC3+ZETA(IL,ITYP)-ZETA(IL,JTYP)
  205 IF(K.NE.I) GO TO 206
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      FAC3=FAC3-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  206 VALU3=VALU3-SM(KL,IABC)*FAC3
  107 CONTINUE
      VALUT=VALU1+VALU2+VALU3
      B0(IJ,IABC)=VALUT
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,3) II,I,J,VALU1,VALU2,VALU3,VALUT
  110 CONTINUE
C
C   ELEMENTS FOR DEPENDENT PAIRS
      DO 115 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      VALU1=FA(IJ)-EIG(J)*SM(IJ,IABC)
      VALU2=ZERO
      DO 114 K=1,JOCC
      DO 114 L=1,K
      KL=IOFF(K)+L
      FAC=ZIJ(I,J,K,L,CC,NNC)*OCC(K)*HALF
      IF(K.EQ.L) FAC=FAC*HALF
      VALU2=VALU2-SM(KL,IABC)*FAC
  114 CONTINUE
      B0(IJ,IABC)=VALU1+VALU2
  115 CONTINUE
      CALL RWRIT(ITAP44,B0(1,IABC),NTRI2,IB)
  120 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 302
      WRITE(6,2)
      CALL MATOUT(B0,NTRI,N3N,NTRI,N3N,6)
C
  302 CONTINUE
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE HMAT(H,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION H(NTRI),CC(NNC),LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/ENRGY/ESCF,ENUC
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      DATA ZERO,HALF,ONE,TWO,FOUR / 0.0D+00 , 0.5D+00 , 1.0D+00 ,
     *                              2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' HMAT ELEMENTS'/
     * 2X,' ELEC1C     = ',F20.10/
     * 2X,' ELEC1M     = ',F20.10/
     * 2X,' ELEC1N     = ',F20.10/
     * 2X,' ELEC2C     = ',F20.10/
     * 2X,' ELEC2M     = ',F20.10/
     * 2X,' ELEC2N     = ',F20.10/
     * 2X,' H11        = ',F20.10/
     * 2X,' H22        = ',F20.10/
     * 2X,' H12        = ',F20.10/
     * 2X,' H21        = ',F20.10/)
    2 FORMAT(//,2X,' TCSCF ENERGIES'/
     * 2X,' ESCF       = ',F20.10/
     * 2X,' ENUC       = ',F20.10/
     * 2X,' C1         = ',F20.10/
     * 2X,' C2         = ',F20.10/
     * 2X,' E1         = ',F20.10/
     * 2X,' E2         = ',F20.10/
     * 2X,' C1SQ       = ',F20.10/
     * 2X,' C2SQ       = ',F20.10/
     * 2X,' C12P       = ',F20.10/
     * 2X,' ELEC-TOTAL = ',F20.10/
     * 2X,' ETOT       = ',F20.10/)
C
C   CALCULATE H MATRIX
C   SEE EQS.(5)-(7)
C
C   CALCULATE ONE-ELECTRON ENERGY
      CALL MREAD(H,16)
      ELEC1C=ZERO
      DO 101 I=1,IOCC
      II=IOFF(I+1)
      ELEC1C=ELEC1C+H(II)*TWO
  101 CONTINUE
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      ELEC1M=H(MM)*TWO
      ELEC1N=H(NN)*TWO
C
C   CALCULATE TWO-ELECTRON ENRGY
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
      ELEC2C=ZERO
      DO 102 I=1,IOCC
      DO 102 J=1,I
      IJ=IOFF(I)+J
      II=IOFF(I+1)
      JJ=IOFF(J+1)
      IIJJ=IOFF(II)+JJ
      IJIJ=IOFF(IJ+1)
      FAC=TWO
      IF(I.EQ.J) FAC=ONE
      ELEC2C=ELEC2C+(CC(IIJJ)*TWO-CC(IJIJ))*FAC
  102 CONTINUE
C
      MMMM=IOFF(MM+1)
      NNNN=IOFF(NN+1)
      MN=M+IOFF(N)
      MNMN=IOFF(MN+1)
      ELEC2M=ZERO
      ELEC2N=ZERO
      DO 103 I=1,IOCC
      MI=IOFF(M)+I
      NI=IOFF(N)+I
      II=IOFF(I+1)
      MMII=IOFF(MM)+II
      NNII=IOFF(NN)+II
      MIMI=IOFF(MI+1)
      NINI=IOFF(NI+1)
      ELEC2M=ELEC2M+CC(MMII)*FOUR-CC(MIMI)*TWO
      ELEC2N=ELEC2N+CC(NNII)*FOUR-CC(NINI)*TWO
  103 CONTINUE
      ELEC2M=ELEC2M+CC(MMMM)
      ELEC2N=ELEC2N+CC(NNNN)
      H11=ELEC1C+ELEC1M+ELEC2C+ELEC2M
      H22=ELEC1C+ELEC1N+ELEC2C+ELEC2N
      H12=CC(MNMN)
      H21=CC(MNMN)
      WRITE(6,1) ELEC1C,ELEC1M,ELEC1N,ELEC2C,ELEC2M,ELEC2N,H11,H22,
     1           H12,H21
C
C   CALCULATE SCF ENERGY FOR A TEST
      B=H11+H22
      C=H11*H22-H12*H12
      SQBC=DSQRT(B*B-C*FOUR)
      E1=(B-SQBC)*HALF
      E2=(B+SQBC)*HALF
      ELEC=H11*C1SQ+H22*C2SQ+H12*C12P
      ETOT=ENUC+ELEC
      WRITE(6,2) ESCF,ENUC,C1,C2,E1,E2,C1SQ,C2SQ,C12P,ELEC,ETOT
C
      RETURN
      END
      SUBROUTINE AMAT(OCC,NIJ,KIJ,ALPA,BETA,ZETA,CC,LBLO,BUFO,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION ALPA(NTRI),BETA(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION OCC(NBASIS),CC(NNC)
      DIMENSION LBLO(MAXBF4),BUFO(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA ZERO,HALF / 0.0D+00 , 0.5D+00 /
    1 FORMAT(//,2X,' A MATRIX'/)
    2 FORMAT(2X,6I5,3F15.8)
    3 FORMAT(2X,6I5,F15.8)
C
C   FORM A MATRIX
C   SEE EQ.(23)
C
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLO,BUFO,IPRNT)
      CALL SREW(JTAPE1)
      IF(IPRNT.LE.4) GO TO 301
      WRITE(6,1)
C
C   ELEMENTS FOR INDEPENDENT PAIRS
  301 IBL=0
      DO 102 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      ITYP=MOTYP(I)
      JTYP=MOTYP(J)
      IJ=IOFF(I)+J
C
      DO 101 JJ=1,II
      K=NIJ(JJ,1)
      L=NIJ(JJ,2)
      KL=IOFF(K)+L
      VALU1=ZERO
      VALU2=ZERO
      ZVAL1=ZERO
      ZVAL2=ZERO
      ZVAL3=ZERO
      ZVAL4=ZERO
      VALU1=VALU1+GSAI(I,J,K,L,ALPA,BETA,CC,NNC)
      IF(L.GT.JOCC) GO TO 201
      VALU1=VALU1-GSAI(I,J,L,K,ALPA,BETA,CC,NNC)
  201 IF(J.NE.K) GO TO 202
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      ZVAL1=ZETA(IL,ITYP)-ZETA(IL,JTYP)
  202 IF(I.NE.K) GO TO 203
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      ZVAL2=-ZETA(JL,JTYP)+ZETA(JL,ITYP)
  203 IF(J.NE.L) GO TO 204
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      ZVAL3=-ZETA(IK,ITYP)+ZETA(IK,JTYP)
  204 IF(I.NE.L) GO TO 205
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      ZVAL4=ZETA(JK,JTYP)-ZETA(JK,ITYP)
  205 VALU2=ZVAL1+ZVAL2+ZVAL3+ZVAL4
      VALUT=VALU1+VALU2
C     IF(IPRNT.LE.4) GO TO 206
C     WRITE(6,2) II,JJ,I,J,K,L,VALU1,VALU2,VALUT
  206 IBL=IBL+1
      LBLO(IBL+IBL-1)=II
      LBLO(IBL+IBL)=JJ
      BUFO(MAXBUF+IBL)=VALUT
      IF(IBL.LT.MAXBUF) GO TO 101
      IBL=0
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
  101 CONTINUE
  102 CONTINUE
C
C   STORE THE LAST BUFFER
      IBL=IBL+1
      LBLO(IBL+IBL-1)=0
      LBLO(IBL+IBL)=0
      BUFO(MAXBUF+IBL)=ZERO
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
C
C   ELEMENTS FOR DEPENDENT PAIRS
CCCCC IF(ICI.EQ.0) GO TO 210
      IBL=0
      DO 105 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      DO 104 JJ=1,NIND
      K=NIJ(JJ,1)
      L=NIJ(JJ,2)
      A=ZIJ(I,J,K,L,CC,NNC)*(OCC(L)-OCC(K))*HALF
      IBL=IBL+1
      LBLO(IBL+IBL-1)=256*I+J
      LBLO(IBL+IBL)=256*K+L
      BUFO(MAXBUF+IBL)=A
C     IF(IPRNT.LE.4) GO TO 207
C     WRITE(6,3) II,JJ,I,J,K,L,A
  207 CONTINUE
      IF(IBL.LT.MAXBUF) GO TO 104
      IBL=0
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
  104 CONTINUE
  105 CONTINUE
C
C   STORE THE LAST BUFFER
      IBL=IBL+1
      LBLO(IBL+IBL-1)=0
      LBLO(IBL+IBL)=0
      BUFO(MAXBUF+IBL)=ZERO
      CALL SWRIT(JTAPE1,LBLO,MAXBF4)
C
  210 CALL SREW(JTAPE1)
      RETURN
      END
      FUNCTION ZIJ(I,J,K,L,CC,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION CC(NNC)
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA FOUR / 4.0D+00 /
C
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      IJKL=IOFF(MAX0(IJ,KL))+MIN0(IJ,KL)
      ILJK=IOFF(MAX0(IL,JK))+MIN0(IL,JK)
      IKJL=IOFF(MAX0(IK,JL))+MIN0(IK,JL)
      ZIJ=CC(IJKL)*FOUR-CC(ILJK)-CC(IKJL)
      RETURN
      END
      SUBROUTINE EMAT(NIJ,A12,E11,E22,E12,EE,H,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),A12(NIND,2)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      DIMENSION EE(NBASIS,NBASIS),H(NTRI),CC(NNC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA ZERO,HALF,TWO / 0.0D+00 , 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' E11 MATRIX'/)
    2 FORMAT(//,2X,' E22 MATRIX'/)
    3 FORMAT(//,2X,' E12 MATRIX'/)
    4 FORMAT(//,2X,' EE MATRIX'/)
C
C   FORM E MATRICES
C   SEE EQS.(18)-(21)
C
      CALL MREAD(H,16)
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
      DO 101 I=1,NBASIS
      DO 101 J=1,NBASIS
      E11(I,J)=ZERO
      E22(I,J)=ZERO
      E12(I,J)=ZERO
  101 CONTINUE
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      MN=M+IOFF(N)
      DO 103 I=1,NBASIS
      IM=IOFF(MAX0(I,M))+MIN0(I,M)
      IN=IOFF(MAX0(I,N))+MIN0(I,N)
      INMN=IOFF(MAX0(IN,MN))+MIN0(IN,MN)
      IMMN=IOFF(MAX0(IM,MN))+MIN0(IM,MN)
      E12(I,M)=CC(INMN)*HALF
      E12(I,N)=CC(IMMN)*HALF
      DO 103 K=1,JOCC
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      KM=IOFF(MAX0(K,M))+MIN0(K,M)
      KN=IOFF(MAX0(K,N))+MIN0(K,N)
      IKMM=IOFF(MAX0(IK,MM))+MIN0(IK,MM)
      IKNN=IOFF(MAX0(IK,NN))+MIN0(IK,NN)
      IMKM=IOFF(MAX0(IM,KM))+MIN0(IM,KM)
      INKN=IOFF(MAX0(IN,KN))+MIN0(IN,KN)
      VALU0=ZERO
      VALU1=H(IK)+CC(IKMM)*TWO-CC(IMKM)
      VALU2=H(IK)+CC(IKNN)*TWO-CC(INKN)
      DO 102 J=1,IOCC
      JJ=IOFF(J+1)
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      KJ=IOFF(MAX0(K,J))+MIN0(K,J)
      IKJJ=IOFF(MAX0(IK,JJ))+MIN0(IK,JJ)
      IJKJ=IOFF(MAX0(IJ,KJ))+MIN0(IJ,KJ)
      VALU0=VALU0+CC(IKJJ)*TWO-CC(IJKJ)
  102 CONTINUE
      IF(K.EQ.N) GO TO 201
      E11(I,K)=VALU0+VALU1
  201 IF(K.EQ.M) GO TO 103
      E22(I,K)=VALU0+VALU2
  103 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 301
      WRITE(6,1)
      CALL MATOUT(E11,NBASIS,NBASIS,NBASIS,NBASIS,6)
      WRITE(6,2)
      CALL MATOUT(E22,NBASIS,NBASIS,NBASIS,NBASIS,6)
      WRITE(6,3)
      CALL MATOUT(E12,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
C   CALCULATE THE LAGRANGIAN MATRIX FOR A TEST
  301 CONTINUE
      DO 105 I=1,NBASIS
      DO 105 J=1,NBASIS
      EE(I,J)=E11(I,J)*C1SQ+E22(I,J)*C2SQ+E12(I,J)*C12P
  105 CONTINUE
      IF(IPRNT.LE.3) GO TO 302
      WRITE(6,4)
      CALL MATOUT(EE,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
C   CALCULATE A12 MATRIX ELEMENTS
  302 CONTINUE
      DO 106 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      A1=(E11(I,J)-E11(J,I))*C1+(E12(I,J)-E12(J,I))*C2
      A2=(E12(I,J)-E12(J,I))*C1+(E22(I,J)-E22(J,I))*C2
      A12(II,1)=A1*TWO
      A12(II,2)=A2*TWO
  106 CONTINUE
C
C   STORE E MATRICES IN JTAPE3
      CALL RFILE(JTAPE2)
      CALL SWRIT(JTAPE2,E11,NBASQ)
      CALL SWRIT(JTAPE2,E22,NBASQ)
      CALL SWRIT(JTAPE2,E12,NBASQ)
      CALL SREW(JTAPE2)
C
      RETURN
      END
      SUBROUTINE BFMAT(NIJ,KIJ,HF,HM,EPF,BF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2)
      DIMENSION HF(NTRI),HM(NTRI,3,NTYPES),EPF(NBASIS,NBASIS),BF(NTRI)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/DIPOL/DIPDA(135),DIPDM(135),DIPDN(135),DIPDT(135)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      DATA ZERO,HALF,TWO / 0.0D+00 , 0.5D+00 , 2.0D+00 /
    1 FORMAT(//,2X,' DIPDA MATRIX'/)
    2 FORMAT(//,2X,' DIPDN MATRIX'/)
    3 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    4 FORMAT(//,2X,' EPF MATRIX, IXYZ = ',I5/)
C
      CALL RFILE(ITAP43)
      CALL SREAD(ITAP43,DIPDA,270)
      CALL SREAD(ITAP43,DIPDN,270)
      IF(IPRNT.LE.4) GO TO 201
      WRITE(6,1)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,2)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
C
  201 CONTINUE
      CALL SREW(ITAP44)
      M=IOCC+1
      N=IOCC+2
      MM=IOFF(M+1)
      NN=IOFF(N+1)
      DO 104 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      CALL SREAD(ITAP43,HF,NTRI2)
      CALL RWRIT(ITAP44,HF,NTRI2,IH)
      DO 102 ITYP=1,NTYPES
      FAC=FOCC(ITYP)*HALF
      DO 101 I=1,NTRI
  101 HM(I,IXYZ,ITYP)=HF(I)*FAC
  102 CONTINUE
      DPM(IXYZ)=HF(MM)
      DPN(IXYZ)=HF(NN)
      VALC=ZERO
      DO 103 I=1,IOCC
      II=IOFF(I+1)
      VALC=VALC+HF(II)
  103 CONTINUE
      H11F(IXYZ)=(VALC+HF(MM))*TWO
      H22F(IXYZ)=(VALC+HF(NN))*TWO
      IF(IPRNT.LE.4) GO TO 104
      WRITE(6,3) IXYZ
      CALL PRINT(HF,NTRI,NBASIS,6)
  104 CONTINUE
C
C   STORE DERIVATIVE LAGRANGIAN MATRICES
      DO 110 IXYZ=1,3
      IH=IHA(IXYZ+N3N)
      IE=IEA(IXYZ+N3N)
      IB=IBA(IXYZ+N3N)
      CALL RREAD(ITAP44,HF,NTRI2,IH)
      DO 105 I=1,NBASIS
      DO 105 J=1,NBASIS
  105 EPF(I,J)=ZERO
C
      DO 107 ITYP=1,NTYPES
      DO 106 I=NSTR(ITYP),NEND(ITYP)
      DO 106 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      EPF(I,J)=HM(IJ,IXYZ,ITYP)
  106 CONTINUE
  107 CONTINUE
C
C   ELEMENTS FOR INDEPENDENT PAIRS
      DO 108 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=EPF(I,J)-EPF(J,I)
  108 CONTINUE
C
C   ELEMENTS FOR DEPENDENT PAIRS
      DO 109 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      BF(IJ)=HF(IJ)
  109 CONTINUE
C
      CALL RWRIT(ITAP44,BF,NTRI2,IB)
      CALL RWRIT(ITAP44,EPF,NBASQ,IE)
      IF(IPRNT.LE.4) GO TO 110
      WRITE(6,4) IXYZ
      CALL MATOUT(EPF,NBASIS,NBASIS,NBASIS,NBASIS,6)
  110 CONTINUE
      CALL SREW(ITAP43)
      CALL SREW(ITAP44)
C
      RETURN
      END
      SUBROUTINE BAFMAT(SA,E11,E22,E12,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION SA(NTRI,N3N)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIBAF/BAF(45,2)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      COMMON/CI105/A22(2,2)
      DATA HALF,TWO,FOUR /  0.5D+00 , 2.0D+00 , 4.0D+00 /
C
C   CALCUATE BAF MATRICES
C   CALL BACK E MATRICES
      CALL SREW(JTAPE2)
      CALL SREAD(JTAPE2,E11,NBASQ)
      CALL SREAD(JTAPE2,E22,NBASQ)
      CALL SREAD(JTAPE2,E12,NBASQ)
      CALL SREW(JTAPE2)
C
C   CALL BACK SA MATRICES
      CALL SREW(ITAP44)
      DO 101 IABC=1,N3N
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
  101 CONTINUE
C
      FAC1=-C12P*C2
      FAC2= C12P*C1
      DO 105 IABC=1,NABC
      IF(IABC.GT.N3N) GO TO 201
      B11=-H11A(IABC)+E1T(IABC)
      B12=-H12A(IABC)
      B21=-H12A(IABC)
      B22=-H22A(IABC)+E1T(IABC)
      DO 103 I=1,JOCC
      DO 103 J=1,I
      IJ=IOFF(I)+J
      FAC4=FOUR
      IF(I.EQ.J) FAC4=TWO
      B11=B11+SA(IJ,IABC)*E11(J,I)*FAC4
      B22=B22+SA(IJ,IABC)*E22(J,I)*FAC4
      B12=B12+SA(IJ,IABC)*E12(J,I)*FAC4
      B21=B21+SA(IJ,IABC)*E12(J,I)*FAC4
  103 CONTINUE
      BA1=(B11*C1+B12*C2)*HALF
      BA2=(B21*C1+B22*C2)*HALF
      BAF(IABC,1)=BA1
      BAF(IABC,2)=BA2
      GO TO 105
  201 CONTINUE
      IXYZ=IABC-N3N
      BAF(IABC,1)=(DPM(IXYZ)-DPN(IXYZ))*FAC1
      BAF(IABC,2)=(DPM(IXYZ)-DPN(IXYZ))*FAC2
  105 CONTINUE
C
C   CALCULATE A22 MATRIX ELEMENTS
      A22(1,1)=(H11-ELEC+C1*C1)*HALF
      A22(2,2)=(H22-ELEC+C2*C2)*HALF
      A22(1,2)=(H12+C1*C2)*HALF
      A22(2,1)=A22(1,2)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE CPHF(EIG,NIJ,KIJ,A12,B0,UA,E11,E22,E12,SA,BA,C,
     1                LBLI,BUFI,NNX,NABC)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*16 DLIM,DETERM
      DIMENSION EIG(NBASIS),UA(NBASIS,NBASIS)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      DIMENSION NIJ(NTRI,2),KIJ(NTRI,2),A12(NIND,2),B0(NTRI)
      DIMENSION SA(NTRI,NABC),BA(NTRI,NABC),C(NNX,NNX+NABC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIBAF/BAF(45,2)
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI102/NIND,NDEP
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI105/A22(2,2)
      DATA CRIT / 1.0D-6 /
      DATA DLIM / 1.0Q-76 /
      DATA ZERO,HALF,ONE,TWO,FOUR / 0.0D+00 , 0.5D+00 , 1.0D+00 ,
     1                              2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' C MATRIX'/)
    2 FORMAT(//,2X,' DETERM = ',Q20.10/)
    3 FORMAT(//,2X,'***THE VALUE OF DETERMINANT IS TOO SMALL***',
     1 5X,' DETERM = ',Q20.10/)
    4 FORMAT(2X,I4,3X,F15.8,3X,F15.8)
    5 FORMAT(//,2X,' B MATRIX, IABC = ',I5/)
    6 FORMAT(//,2X,' U MATRIX, IABC = ',I5/)
C
C***********************************
C***SET UP SIMULTANEOUS EQUATIONS***
C***********************************
C
      CALL SREW(ITAP44)
      DO 101 IABC=1,NABC
      IB=IBA(IABC)
      CALL RREAD(ITAP44,BA(1,IABC),NTRI2,IB)
      IF(IABC.GT.N3N) GO TO 101
      IS=ISA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
  101 CONTINUE
C
C"""""""""""""""""""
C"""FORM A MATRIX"""
C"""""""""""""""""""
C
C:::ORBITAL SPACE:::
      CALL SREW(JTAPE1)
  500 CALL SREAD(JTAPE1,LBLI,MAXBF4)
      DO 102 M=1,MAXBUF
      II=LBLI(M+M-1)
      IF(II.EQ.0) GO TO 600
      JJ=LBLI(M+M)
      C(II,JJ)=BUFI(MAXBUF+M)
      C(JJ,II)=BUFI(MAXBUF+M)
  102 CONTINUE
      GO TO 500
  600 CONTINUE
C
C:::CI SPACE:::
C   SEE EQS.(24) AND (25)
      DO 103 II=1,NIND
      C(II,NIND+1)=A12(II,1)
      C(NIND+1,II)=A12(II,1)
      C(II,NIND+2)=A12(II,2)
      C(NIND+2,II)=A12(II,2)
  103 CONTINUE
      C(NIND+1,NIND+1)=A22(1,1)
      C(NIND+2,NIND+2)=A22(2,2)
      C(NIND+1,NIND+2)=A22(1,2)
      C(NIND+2,NIND+1)=A22(2,1)
C
C"""""""""""""""""""
C"""FORM B MATRIX"""
C"""""""""""""""""""
C
C:::ORBITAL SPACE:::
      DO 105 IABC=1,NABC
      DO 104 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      C(II,NIND+2+IABC)=BA(IJ,IABC)
  104 CONTINUE
  105 CONTINUE
C
C:::CI SPACE:::
C   SEE EQ.(27)
      DO 107 IABC=1,NABC
      C(NIND+1,NIND+2+IABC)=BAF(IABC,1)
      C(NIND+2,NIND+2+IABC)=BAF(IABC,2)
  107 CONTINUE
C
      IF(IPRNT.LE.3) GO TO 202
      WRITE(6,1)
      CALL MATOUT(C,NNX,NNX+NABC,NIND+2,NIND+2+NABC,6)
C
C""""""""""""""""""""""""""""""""""""""
C"""SOLVE THE SIMULTANEOUS EQUATIONS"""
C""""""""""""""""""""""""""""""""""""""
  202 CALL FLINQ(C,NNX,NIND+2,NABC,DETERM)
      WRITE(6,2) DETERM
      IF(IPRNT.GT.3)
     * CALL MATOUT(C,NNX,NNX+NABC,NIND+2,NIND+2+NABC,6)
      IF(QABS(DETERM).LE.DLIM) GO TO 203
      GO TO 205
  203 WRITE(6,3) DETERM
      STOP
C
C"""""""""""""""""""""""""""""""""""""""""""""""
C"""OBTAIN INDEPENDENT ELEMENTS OF U MATRICES"""
C"""""""""""""""""""""""""""""""""""""""""""""""
C
C:::ORBITAL SPACE:::
  205 DO 110 IABC=1,NABC
      DO 109 II=1,NIND
      I=NIJ(II,1)
      J=NIJ(II,2)
      IJ=IOFF(I)+J
      BA(IJ,IABC)=C(II,NIND+2+IABC)
  109 CONTINUE
C
C:::CI SPACE:::
      C1A(IABC)=C(NIND+1,NIND+2+IABC)
      C2A(IABC)=C(NIND+2,NIND+2+IABC)
      IF(IPRNT.LE.3) GO TO 110
      WRITE(6,4) IABC,C1A(IABC),C2A(IABC)
      IF(IPRNT.LE.5) GO TO 110
      WRITE(6,5) IABC
      CALL PRINT(BA(1,IABC),NTRI,NBASIS,6)
  110 CONTINUE
C
C   CALCULATE DEPENDENT ELEMENTS OF U MATRICES
CCCCC IF(ICI.EQ.0) GO TO 210
  700 CALL SREAD(JTAPE1,LBLI,MAXBF4)
      DO 115 M=1,MAXBUF
      IB1=LBLI(M+M-1)
      IF(IB1.EQ.0) GO TO 800
      IB2=LBLI(M+M)
      J=MOD(IB1,256)
      I=IB1/256
      L=MOD(IB2,256)
      K=IB2/256
      IJ=IOFF(I)+J
      KL=IOFF(K)+L
      VALU=BUFI(MAXBUF+M)
      DO 114 IABC=1,NABC
      BA(IJ,IABC)=BA(IJ,IABC)+VALU*BA(KL,IABC)
  114 CONTINUE
  115 CONTINUE
      GO TO 700
  800 CALL SREW(JTAPE1)
C
      DO 125 IABC=1,NABC
      IB=IBA(IABC)
      IU=IUA(IABC)
C
      DO 116 II=1,NDEP
      I=KIJ(II,1)
      J=KIJ(II,2)
      IJ=IOFF(I)+J
      DELJI=EIG(J)-EIG(I)
      IF(I.EQ.J) DELJI=ONE
      IF(DABS(DELJI).LT.CRIT) GO TO 206
      BA(IJ,IABC)=BA(IJ,IABC)/DELJI
      GO TO 116
  206 BA(IJ,IABC)=ZERO
  116 CONTINUE
C
      IF(IABC.GT.N3N) GO TO 208
      DO 118 I=1,NBASIS
      DO 118 J=1,I
      IJ=IOFF(I)+J
      B0(IJ)=BA(IJ,IABC)
      IF(I.EQ.J) GO TO 207
      UA(I,J)=BA(IJ,IABC)
      UA(J,I)=-UA(I,J)-SA(IJ,IABC)
      GO TO 118
  207 UA(I,I)=-SA(IJ,IABC)*HALF
  118 CONTINUE
      GO TO 210
C
  208 CONTINUE
      DO 119 I=1,NBASIS
      DO 119 J=1,I
      IJ=IOFF(I)+J
      B0(IJ)=BA(IJ,IABC)
      IF(I.EQ.J) GO TO 209
      UA(I,J)=BA(IJ,IABC)
      UA(J,I)=-UA(I,J)
      GO TO 119
  209 UA(I,I)=ZERO
  119 CONTINUE
C
  210 CONTINUE
      CALL RWRIT(ITAP44,B0,NTRI2,IB)
      CALL RWRIT(ITAP44,UA,NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 125
      WRITE(6,6) IABC
      CALL MATOUT(UA,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  125 CONTINUE
C
      CALL SREW(ITAP44)
      CALL SREW(JTAPE1)
      RETURN
      END
      SUBROUTINE SCF2ND(ALPA,BETA,E2A,EPS,ZETA,E2M,E2C,E2P,EPA,UA,
     1                  E11,E22,E12,SA,WA,CC,LBLI,BUFI,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI),EPS(NTRI),ZETA(NTRI,NTYPEP)
      DIMENSION E2A(N3N,N3N),E2M(N3N,N3N),E2C(N3N,N3N),E2P(N3N,N3N)
      DIMENSION EPA(NBASIS,NBASIS),UA(NBASIS,NBASIS)
      DIMENSION E11(NBASIS,NBASIS),E22(NBASIS,NBASIS),E12(NBASIS,NBASIS)
      DIMENSION SA(NTRI,N3N),WA(NBASIS,NBASIS,N3N),CC(NNC)
      DIMENSION LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIMAT/H11A(45),H22A(45),H12A(45),E1T(45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/START/FOCC(10),NSORB(10),NSTR(10),NEND(10),MOTYP(256)
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/WORKS/JTAPE1,JTAPE2
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      COMMON/CI104/H11,H22,H12,ELEC
      DATA ZERO,TWO,FOUR / 0.0D+00 , 2.0D+00 , 4.0D+00 /
    1 FORMAT(//,2X,' WA MATRIX, IABC = ',I5/)
    2 FORMAT(//,2X,' E2P MATRIX'/)
    3 FORMAT(//,2X,' E2M MATRIX'/)
    4 FORMAT(///
     1 2X,'::::::::::::::::::::::::::::'/
     2 2X,':::SUMMARY OF CALCULATION:::'/
     3 2X,'::::::::::::::::::::::::::::'/)
    5 FORMAT(//,2X,' E2A MATRIX'/)
    6 FORMAT(//,2X,' E2M MATRIX'/)
    7 FORMAT(//,2X,' E2C MATRIX'/)
    8 FORMAT(//,2X,' SCF SECOND DERIVATIVE MATRIX'/)
    9 FORMAT(2I5)
   10 FORMAT(3F20.10)
C
C   READ IN TWO ELECTRON MO INTEGRALS
      CALL RDINT2(CC,NNC,ITAP35,NNC,LBLI,BUFI,IPRNT)
C
C   FORM WA MATRICES
C   SEE EQ.(13)
C
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IS=ISA(IABC)
      IE=IEA(IABC)
      CALL RREAD(ITAP44,SA(1,IABC),NTRI2,IS)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      DO 101 I=1,NBASIS
      DO 101 J=1,NBASIS
      WA(I,J,IABC)=EPA(J,I)+EPA(J,I)
  101 CONTINUE
  102 CONTINUE
C
      DO 107 I=1,NBASIS
      DO 107 J=1,NBASIS
      NJ=MOTYP(J)
      DO 106 K=1,JOCC
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      FAC1=EPS(IK)+ZETA(IK,NJ)
      FAC2=EPS(JK)+EPS(JK)
      DO 105 IABC=1,N3N
      WA(I,J,IABC)=WA(I,J,IABC)-SA(JK,IABC)*FAC1-SA(IK,IABC)*FAC2
  105 CONTINUE
  106 CONTINUE
  107 CONTINUE
      DO 110 I=1,NBASIS
      DO 110 J=1,NBASIS
      DO 109 K=1,JOCC
      DO 109 L=1,JOCC
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      FAC3=ABIJ(I,J,K,L,ALPA,BETA,CC,NNC)
      DO 108 IABC=1,N3N
      WA(I,J,IABC)=WA(I,J,IABC)-SA(KL,IABC)*FAC3
  108 CONTINUE
  109 CONTINUE
  110 CONTINUE
      IF(IPRNT.LE.3) GO TO 201
      DO 115 IABC=1,N3N
      WRITE(6,1) IABC
      CALL MATOUT(WA(1,1,IABC),NBASIS,NBASIS,NBASIS,NBASIS,6)
  115 CONTINUE
C
C   CALCULATE SCF SECOND DERIVATIVES
C   SEE EQ.(12)
C
C   (B COORDINATE)
  201 CONTINUE
      DO 120 IABC=1,N3N
      IE=IEA(IABC)
      IU=IUA(IABC)
      CALL RREAD(ITAP44,EPA,NBASQ,IE)
      CALL RREAD(ITAP44,UA,NBASQ,IU)
C
C   (A COORDINATE)
      DO 119 JABC=1,N3N
      VALUP=ZERO
      VALUM=ZERO
      DO 118 I=1,JOCC
      DO 116 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-SA(IJ,JABC)*EPA(I,J)
  116 CONTINUE
      DO 117 J=1,NBASIS
      VALUM=VALUM+WA(J,I,JABC)*UA(J,I)
  117 CONTINUE
  118 CONTINUE
      E2P(IABC,JABC)=VALUP*TWO
      E2M(IABC,JABC)=VALUM*TWO
  119 CONTINUE
  120 CONTINUE
      IF(IPRNT.LE.3) GO TO 202
      WRITE(6,2)
      CALL MATOUT(E2P,N3N,N3N,N3N,N3N,6)
      WRITE(6,3)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
C*******************************************
C***SUM UP CONTRIBUTIONS DUE TO MO CHANGE***
C*******************************************
  202 DO 121 I=1,N3N
      DO 121 J=1,N3N
      E2M(I,J)=E2M(I,J)+E2P(I,J)
  121 CONTINUE
C
C***********************************************
C***CONTRIBUTION DUE TO CI COEFFICIENT CHANGE***
C***********************************************
      CALL SREW(JTAPE2)
      CALL SREAD(JTAPE2,E11,NBASQ)
      CALL SREAD(JTAPE2,E22,NBASQ)
      CALL SREAD(JTAPE2,E12,NBASQ)
      CALL SREW(JTAPE2)
      DO 125 IABC=1,N3N
      DO 125 JABC=1,N3N
      VALU11=H11A(JABC)
      VALU22=H22A(JABC)
      VALU12=H12A(JABC)
      DO 124 I=1,JOCC
      DO 124 J=1,JOCC
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALU11=VALU11-SA(IJ,JABC)*E11(I,J)*TWO
      VALU22=VALU22-SA(IJ,JABC)*E22(I,J)*TWO
      VALU12=VALU12-SA(IJ,JABC)*E12(I,J)*TWO
  124 CONTINUE
      E2C(IABC,JABC)=(C1A(IABC)*C1*VALU11+C1A(IABC)*C2*VALU12
     1               +C2A(IABC)*C1*VALU12+C2A(IABC)*C2*VALU22)*TWO
  125 CONTINUE
C
CCC   DO 125 IABC=1,N3N
CCC   DO 125 JABC=1,N3N
CCC   E2C(I,J)=-(C1A(IABC)*C1A(JABC))*(H11-ELEC)*TWO
CCC  1         -(C2A(IABC)*C2A(JABC))*(H22-ELEC)*TWO
CCC  2         -(C1A(IABC)*C2A(JABC)+C1A(JABC)*C2A(IABC))*H12*TWO
CC125 CONTINUE
      WRITE(6,4)
      WRITE(6,5)
      CALL MATOUT(E2A,N3N,N3N,N3N,N3N,6)
      WRITE(6,6)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
      WRITE(6,7)
      CALL MATOUT(E2C,N3N,N3N,N3N,N3N,6)
      DO 126 I=1,N3N
      DO 126 J=1,N3N
  126 E2M(I,J)=E2A(I,J)+E2M(I,J)+E2C(I,J)
      WRITE(6,8)
      CALL MATOUT(E2M,N3N,N3N,N3N,N3N,6)
C
      ITAP15=15
      REWIND ITAP15
      N6N=N3N*2
      WRITE(ITAP15,9) NATOMS,N6N
      WRITE(ITAP15,10) ((E2M(I,J),J=1,N3N),I=1,N3N)
      REWIND ITAP15
C
      CALL SREW(ITAP44)
      RETURN
      END
      FUNCTION ABIJ(I,J,K,L,ALPA,BETA,CC,NNC)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION ALPA(NTRI),BETA(NTRI),CC(NNC)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/SIGNS/IOFF(1379),IPRNT
      DATA TWO / 2.0D+00 /
C
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      KL=IOFF(MAX0(K,L))+MIN0(K,L)
      IL=IOFF(MAX0(I,L))+MIN0(I,L)
      JK=IOFF(MAX0(J,K))+MIN0(J,K)
      IK=IOFF(MAX0(I,K))+MIN0(I,K)
      JL=IOFF(MAX0(J,L))+MIN0(J,L)
      IJKL=IOFF(MAX0(IJ,KL))+MIN0(IJ,KL)
      ILJK=IOFF(MAX0(IL,JK))+MIN0(IL,JK)
      IKJL=IOFF(MAX0(IK,JL))+MIN0(IK,JL)
      ABIJ=ALPA(JL)*CC(IJKL)*TWO+BETA(JL)*(CC(IKJL)+CC(ILJK))
      RETURN
      END
      SUBROUTINE CIGRAD(E1A,E1T,D1W,U,W)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION E1A(N3N),E1T(N3N),D1W(N3N)
      DIMENSION U(NBASIS,NBASIS),W(NBASIS,NBASIS)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      DATA ZERO / 0.0D+00 /
    1 FORMAT(//,2X,' LAGRANGIAN MATRIX'/)
    2 FORMAT(//,2X,' U MATRIX, IABC = ',I5/)
    3 FORMAT(//,2X,' EA1 MATRIX'/)
    4 FORMAT(//,2X,' EA2 MATRIX'/)
    5 FORMAT(//,2X,' E1T MATRIX'/)
C
      CALL MREAD(W,32)
      IF(IPRNT.LE.2) GO TO 301
      WRITE(6,1)
      CALL MATOUT(W,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
  301 CONTINUE
      CALL SREW(ITAP44)
      DO 105 IABC=1,N3N
      IU=IUA(IABC)
      CALL RREAD(ITAP44,U,NBASQ,IU)
      IF(IPRNT.LE.2) GO TO 302
      WRITE(6,2) IABC
      CALL MATOUT(U,NBASIS,NBASIS,NBASIS,NBASIS,6)
  302 VALU=ZERO
      DO 103 I=1,NBASIS
      DO 103 J=1,NBASIS
  103 VALU=VALU+U(J,I)*W(I,J)
      D1W(IABC)=VALU
  105 CONTINUE
      WRITE(6,3)
      CALL MATOUT(E1A,3,NATOMS,3,NATOMS,6)
      WRITE(6,4)
      CALL MATOUT(D1W,3,NATOMS,3,NATOMS,6)
C
C   SUM UP ALL CONTRIBUTIONS
      DO 107 IABC=1,N3N
  107 E1T(IABC)=E1A(IABC)+D1W(IABC)
      WRITE(6,5)
      CALL MATOUT(E1T,3,NATOMS,3,NATOMS,6)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE DIPMO(OCC,DIP,UA)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),DIP(NTRI,3),UA(NBASIS,NBASIS,3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CALIF/LPARA(1024),DIPDE(3,45),DIPDC(3,45),DUM(754)
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/DIPOL/DIPDA(3,45),DIPDM(3,45),DIPDN(3,45),DIPDT(3,45)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA ZERO,HALF,ONE,TWO,FOUR / 0.0D+00 , 0.5D+00 , 1.0D+00 ,
     1                              2.0D+00 , 4.0D+00 /
      DATA DEBYE,BOHR / 2.541765480D+00 , 0.52917706D+00 /
    1 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS'/)
    2 FORMAT(//,2X,' UA MATRIX, IABC = ',I5,' IXYZ = ',I5/)
    3 FORMAT(//,2X,' DIPDA MATRIX'/)
    4 FORMAT(//,2X,' DIPDM MATRIX'/)
    5 FORMAT(//,2X,' DIPDC MATRIX'/)
    6 FORMAT(//,2X,' DIPDE MATRIX'/)
    7 FORMAT(//,2X,' DIPDN MATRIX'/)
    8 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/BOHR)
     *'/)
    9 FORMAT(//,2X,' FIRST DERIVATIVES OF DIPOLE MOMENTS (IN DEBYE/A)'/)
   10 FORMAT(2I5)
   11 FORMAT(3F20.10)
C
      CALL SREW(ITAP44)
      DEB4=DEBYE*FOUR
      DEB2=DEBYE*TWO
      RBOHR=ONE/BOHR
C     WRITE(6,1)
C
      DO 101 I=1,3
      DO 101 J=1,N3N
      DIPDM(I,J)=ZERO
      DIPDC(I,J)=ZERO
  101 CONTINUE
C
      DO 102 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,DIP(1,IXYZ),NTRI2,IH)
  102 CONTINUE
C
      KABC=0
      DO 105 IABC=1,NATOMS
      I00=(IABC-1)*3
      IXX=I00+1
      IYY=I00+2
      IZZ=I00+3
      DO 103 IXYZ=1,3
      KABC=KABC+1
      IU=IUA(KABC)
      CALL RREAD(ITAP44,UA(1,1,IXYZ),NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 103
      WRITE(6,2) IABC,IXYZ
      CALL MATOUT(UA(1,1,IXYZ),NBASIS,NBASIS,NBASIS,NBASIS,6)
  103 CONTINUE
C
      DO 104 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 104 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      DIPDM(1,IXX)=DIPDM(1,IXX)+UA(J,I,1)*DIP(IJ,1)*FAC
      DIPDM(1,IYY)=DIPDM(1,IYY)+UA(J,I,2)*DIP(IJ,1)*FAC
      DIPDM(1,IZZ)=DIPDM(1,IZZ)+UA(J,I,3)*DIP(IJ,1)*FAC
      DIPDM(2,IXX)=DIPDM(2,IXX)+UA(J,I,1)*DIP(IJ,2)*FAC
      DIPDM(2,IYY)=DIPDM(2,IYY)+UA(J,I,2)*DIP(IJ,2)*FAC
      DIPDM(2,IZZ)=DIPDM(2,IZZ)+UA(J,I,3)*DIP(IJ,2)*FAC
      DIPDM(3,IXX)=DIPDM(3,IXX)+UA(J,I,1)*DIP(IJ,3)*FAC
      DIPDM(3,IYY)=DIPDM(3,IYY)+UA(J,I,2)*DIP(IJ,3)*FAC
      DIPDM(3,IZZ)=DIPDM(3,IZZ)+UA(J,I,3)*DIP(IJ,3)*FAC
  104 CONTINUE
      DIPDC(1,IXX)=C1A(IXX)*H11F(1)*C1+C2A(IXX)*H22F(1)*C2
      DIPDC(1,IYY)=C1A(IYY)*H11F(1)*C1+C2A(IYY)*H22F(1)*C2
      DIPDC(1,IZZ)=C1A(IZZ)*H11F(1)*C1+C2A(IZZ)*H22F(1)*C2
      DIPDC(2,IXX)=C1A(IXX)*H11F(2)*C1+C2A(IXX)*H22F(2)*C2
      DIPDC(2,IYY)=C1A(IYY)*H11F(2)*C1+C2A(IYY)*H22F(2)*C2
      DIPDC(2,IZZ)=C1A(IZZ)*H11F(2)*C1+C2A(IZZ)*H22F(2)*C2
      DIPDC(3,IXX)=C1A(IXX)*H11F(3)*C1+C2A(IXX)*H22F(3)*C2
      DIPDC(3,IYY)=C1A(IYY)*H11F(3)*C1+C2A(IYY)*H22F(3)*C2
      DIPDC(3,IZZ)=C1A(IZZ)*H11F(3)*C1+C2A(IZZ)*H22F(3)*C2
  105 CONTINUE
C
      DO 106 I=1,3
      DO 106 J=1,N3N
      DIPDM(I,J)=-DIPDM(I,J)*DEB4
      DIPDC(I,J)=-DIPDC(I,J)*DEB2
  106 CONTINUE
C
C   SUM UP CONTRIBUTIONS FROM MO CHANGE
      DO 107 I=1,3
      DO 107 J=1,N3N
      DIPDE(I,J)=DIPDA(I,J)+DIPDM(I,J)+DIPDC(I,J)
  107 CONTINUE
      IF(IPRNT.LE.2) GO TO 201
      WRITE(6,3)
      CALL MATOUT(DIPDA,3,45,3,N3N,6)
      WRITE(6,4)
      CALL MATOUT(DIPDM,3,45,3,N3N,6)
      WRITE(6,5)
      CALL MATOUT(DIPDC,3,45,3,N3N,6)
C
C   SUM UP ALL CONTRIBUTIONS
  201 CONTINUE
      DO 108 I=1,3
      DO 108 J=1,N3N
      DIPDT(I,J)=DIPDE(I,J)+DIPDN(I,J)
  108 CONTINUE
C
      IF(IPRNT.LE.2) GO TO 202
      WRITE(6,6)
      CALL MATOUT(DIPDE,3,45,3,N3N,6)
      WRITE(6,7)
      CALL MATOUT(DIPDN,3,45,3,N3N,6)
  202 CONTINUE
      WRITE(6,8)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      DO 109 I=1,3
      DO 109 J=1,N3N
      DIPDT(I,J)=DIPDT(I,J)*RBOHR
  109 CONTINUE
      WRITE(6,9)
      CALL MATOUT(DIPDT,3,45,3,N3N,6)
C
      ITAP17=17
      REWIND ITAP17
      WRITE(ITAP17,10) NATOMS,N3N
      WRITE(ITAP17,11) ((DIPDT(I,J),J=1,N3N),I=1,3)
      REWIND ITAP17
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE POLAR(OCC,HF,UF)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION OCC(NBASIS),HF(NTRI,3),UF(NBASIS,NBASIS)
      DIMENSION POM(3,3),POC(3,3)
      DIMENSION POL(3,3),POLC(3,3),EIG(3),EIV(3,3),A(6),FV1(3),FV2(3)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/CIDER/C1A(45),C2A(45)
      COMMON/CIDIP/DPM(3),DPN(3),H11F(3),H22F(3)
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
      COMMON/CI101/IOCC,JOCC,KOCC
      COMMON/CI103/C1SQ,C2SQ,C12P,C1,C2
      DATA ZERO,HALF,TWO,FOUR / 0.0D+00 , 0.5D+00 , 2.0D+00 , 4.0D+00 /
      DATA BOHR / 0.52917706D+00 /
    1 FORMAT(//,2X,' HF MATRIX, IXYZ = ',I5/)
    2 FORMAT(//,2X,' UF MATRIX, IXYZ = ',I5/)
    3 FORMAT(//,2X,' POM MATRIX'/)
    4 FORMAT(//,2X,' POC MATRIX'/)
    5 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    6 FORMAT(//,2X,' POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
    7 FORMAT(//,2X,' A MATRIX'/)
    8 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A.U.)'/)
    9 FORMAT(//,2X,' PRINCIPAL POLARIZABILITY TENSOR MATRIX (IN A**3)'/)
   10 FORMAT(//,2X,' EIGENVECTOR MATRIX'/)
C
      CALL SREW(ITAP44)
      A33=BOHR*BOHR*BOHR
C
C   CALL BACK HF MATRICES
      DO 101 IXYZ=1,3
      IH=IHA(N3N+IXYZ)
      CALL RREAD(ITAP44,HF(1,IXYZ),NTRI2,IH)
      IF(IPRNT.LE.3) GO TO 101
      WRITE(6,1) IXYZ
      CALL PRINT(HF(1,IXYZ),NTRI,NBASIS,6)
  101 CONTINUE
C
C:::B (G) COORDINATE:::
      DO 105 IXYZ=1,3
      C1G=C1A(N3N+IXYZ)
      C2G=C2A(N3N+IXYZ)
      IU=IUA(N3N+IXYZ)
      CALL RREAD(ITAP44,UF,NBASQ,IU)
      IF(IPRNT.LE.3) GO TO 301
      WRITE(6,2) IXYZ
      CALL MATOUT(UF,NBASIS,NBASIS,NBASIS,NBASIS,6)
C
C:::A (F) COORDINATES:::
  301 CONTINUE
      DO 103 JXYZ=1,3
      VALUP=ZERO
      DO 102 I=1,JOCC
      FAC=OCC(I)*HALF
      DO 102 J=1,NBASIS
      IJ=IOFF(MAX0(I,J))+MIN0(I,J)
      VALUP=VALUP-UF(J,I)*HF(IJ,JXYZ)*FAC
  102 CONTINUE
      POM(IXYZ,JXYZ)=VALUP*FOUR
      POC(IXYZ,JXYZ)=-(C1G*C1*H11F(JXYZ)+C2G*C2*H22F(JXYZ))*TWO
  103 CONTINUE
  105 CONTINUE
C
      DO 106 IXYZ=1,3
      DO 106 JXYZ=1,3
      POL(IXYZ,JXYZ)=POM(IXYZ,JXYZ)+POC(IXYZ,JXYZ)
  106 CONTINUE
      WRITE(6,3)
      CALL MATOUT(POM,3,3,3,3,6)
      WRITE(6,4)
      CALL MATOUT(POC,3,3,3,3,6)
      WRITE(6,5)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 107 I=1,3
      DO 107 J=1,3
      POLC(I,J)=POL(I,J)*A33
  107 CONTINUE
      WRITE(6,6)
      CALL MATOUT(POLC,3,3,3,3,6)
C
      IJ=0
      DO 108 I=1,3
      DO 108 J=1,I
      IJ=IJ+1
      A(IJ)=POL(I,J)
  108 CONTINUE
      WRITE(6,7)
      CALL PRINT(A,6,3,6)
      CALL RSP(3,3,6,A,EIG,1,EIV,FV1,FV2)
      DO 109 I=1,3
      DO 109 J=1,3
      POL(I,J)=ZERO
  109 CONTINUE
      DO 110 I=1,3
  110 POL(I,I)=EIG(I)
      WRITE(6,8)
      CALL MATOUT(POL,3,3,3,3,6)
      DO 111 I=1,3
      DO 111 J=1,3
      POLC(I,J)=POL(I,J)*A33
  111 CONTINUE
      WRITE(6,9)
      CALL MATOUT(POLC,3,3,3,3,6)
      WRITE(6,10)
      CALL MATOUT(EIV,3,3,3,3,6)
C
      CALL SREW(ITAP44)
      RETURN
      END
      SUBROUTINE ORB1ST(B0,CC,IFLAG)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION B0(NTRI),CC(NBASIS,1)
      COMMON/BASIS/NBASIS,NTRI,NTRI2,NBFAO,NBATRI,NBASQ
      COMMON/FUNCS/NTYPES,NTYPEP,NTREAD,NATOMS,N3N,N3NP3
      COMMON/POSIT/ISA(153),IHA(153),IFA(153),IEA(153),IBA(153),IUA(153)
      COMMON/SIGNS/IOFF(1379),IPRNT
      COMMON/TAPES/ITAP35,ITAP42,ITAP43,ITAP44
    1 FORMAT(//,2X,' FIRST DERIVATIVES OF ORBITAL ENERGIES W.R.T. NUCLEA
     1R COORDINATE'/)
    2 FORMAT(//,2X,' FIRST DERIVATIVES OF ORBITAL ENERGIES W.R.T. ELECTR
     1IC FIELD'/)
C
C   FOR NUCLEAR COORDINATE PERTURBATION
      IF(IFLAG.EQ.2) GO TO 201
      CALL SREW(ITAP44)
      DO 102 IABC=1,N3N
      IB=IBA(IABC)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 101 I=1,NBASIS
      II=IOFF(I+1)
      CC(I,IABC)=B0(II)
  101 CONTINUE
  102 CONTINUE
      WRITE(6,1)
      CALL MATOUT(CC,NBASIS,N3N,NBASIS,N3N,6)
      CALL SREW(ITAP44)
      RETURN
C
C   FOR ELECTRIC FIELD PERTURBATION
  201 CONTINUE
      CALL SREW(ITAP44)
      DO 104 IXYZ=1,3
      IB=IBA(IXYZ+N3N)
      CALL RREAD(ITAP44,B0,NTRI2,IB)
      DO 103 I=1,NBASIS
      II=IOFF(I+1)
      CC(I,IXYZ)=B0(II)
  103 CONTINUE
  104 CONTINUE
      WRITE(6,2)
      CALL MATOUT(CC,NBASIS,3,NBASIS,3,6)
      CALL SREW(ITAP44)
C
      RETURN
      END
      SUBROUTINE RDINT2(BUFO,NDIMB,ITAPE,MAXINT,LBLI,BUFI,IPRNT)
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION BUFO(NDIMB),LBLI(MAXBF4),BUFI(MAXBF2)
      COMMON/LIMIT/MAXBAS,MAXBUF,MAXBF2,MAXBF4
      COMMON/SIGNS/IOFF(1379),JPRNT
      DATA ZERO / 0.0D+00 /
    1 FORMAT(2X,8I7,F15.8)
C
C   READ IN TWO ELECTRON INTEGRALS
      CALL RFILE(ITAPE)
      DO 101 I=1,MAXINT
  101 BUFO(I)=ZERO
      INTI=0
      NBLI=0
  200 IBL=0
      NBLI=NBLI+1
      CALL SREAD(ITAPE,LBLI,MAXBF4)
  201 IBL=IBL+1
      JA=LBLI(IBL+IBL-1)
      IF(JA.EQ.0) GO TO 205
      JAA=LBLI(IBL+IBL)
      CALL UNPACK(I,J,K,L,IX,JA,JAA)
      IJ=IOFF(I)+J
      KL=IOFF(K)+L
C     IJKL=IJ*(IJ-1)/2+KL
      IJKL=IOFF(IJ)+KL
      BUFO(IJKL)=BUFI(MAXBUF+IBL)
C     IF(IPRNT.LE.5) GO TO 202
C     WRITE(6,1) I,J,K,L,IJ,KL,IX,IJKL,BUFO(IJKL)
  202 IF(IBL.EQ.MAXBUF) GO TO 203
      GO TO 201
  203 INTI=INTI+MAXBUF
      GO TO 200
  205 INTI=INTI+IBL
C
      CALL RCLOSE(ITAPE,3)
      RETURN
      END
