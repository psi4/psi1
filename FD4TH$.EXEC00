&TRACE ERR
GLOBAL TXTLIB VEMLLIB VFORTLIB CMSLIB UTILITY
GLOBAL LOADLIB VFLODLIB
*  THIS EXEC RUNS SCF3RD$ IN A LOOP, UP TO "&ITERATE" NUMBER OF TIMES,
*  OVER GEOMETRIES WHICH ARE IN FILES OF FILETYPE "INFIL1", "INFIL2",
*  ... "INFIL&ITERATE".  THIS EXEC IS SPECIFICALLY DESIGNED TO GENERATE
*  A "FILE11" FULL OF GRADIENTS FOR FINITE-DISPLACEMENT APPLICATIONS.
*  IN ORDER TO REDUCE SCF ITERATIONS, THE UNPERTURBED OR STARTING
*  VECTOR SHOULD BE IN A FILE OF FILETYPE "OPTVEC30".  ALL OF THE
*  OUTPUTS ARE COLLECTED INTO A FILE WITH FILETYPE "TOTALOUT &TMP1".
*  IT IS BEST TO COPY AND MODIFY THIS EXEC TO SUIT YOUR OWN NEEDS.
&A = FD4TH
&B = ******************
&C = &STRING OF &B  &A  ***&B  &1  &B
&TYPE &C
&IF &N < 2  &GOTO -JOBSTART
&TMP1 = &CONCAT OF &2 1
&TMP2 = &CONCAT OF &2 2
&TMP4 = &CONCAT OF &2 4
-JOBSTART
*
&TRACE ALL
CP QUERY TIME
&ITERATE = 31
&ITERNUM = 1
*
STATE  &1 FILE31 *
&IF &RETCODE = 0  &SKIP 1
COPYFILE &1 FILE30 A4 = FILE31 = (REPLACE
*
STATE  &1 OPTVEC *
&IF &RETCODE = 0  &SKIP 1
COPYFILE &1 FILE31 A4 = OPTVEC = (REPLACE
*
EXEC  SCF3RD$ &1 &2 &3 &4
*
STATE  &1 GOOD15 *
&IF &RETCODE = 0  &SKIP 2
ERASE &1 GOOD15 A
COPYFILE &1 FILE15 A  = GOOD15  = (REPLACE
*
STATE  &1 GOOD17 *
&IF &RETCODE = 0  &SKIP 2
ERASE &1 GOOD17 A
COPYFILE &1 FILE17 A  = GOOD17  = (REPLACE
*
STATE  &1 GOOD20 *
&IF &RETCODE = 0  &SKIP 2
ERASE &1 GOOD20 A
COPYFILE &1 FILE20 A  = GOOD20  = (REPLACE
*
STATE  &1 GOODOUT *
&IF &RETCODE = 0  &SKIP 2
ERASE &1 GOODOUT A
COPYFILE &1 OUTPUT A  = GOODOUT  = (REPLACE
*
&LOOP -ENDITER  &ITERATE
*
&TYPE ITERATION NUMBER &ITERNUM
&ITERNUM = &ITERNUM + 1
*
EXEC  FDGEOM$ &1 &11
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
COPYFILE &1 FILE31 A4 = FILE30  = (REPLACE
COPYFILE &1 FILE15 A  = TOTAL20 A (APPEND
*
EXEC  GEOM$ &1
&READ VARS &ENDCOD
&IF &ENDCOD NE GOGO &GOTO -JOBEND
*
EXEC  SCF3RD$ &1 &2 &3 &4
COPYFILE &1 FILE20 A4 = TOTAL20 A (APPEND
*
-ENDITER
*
-JOBEND
*
COPYFILE &1 FILE6  A  = FDGEOM6 A (REPLACE
ERASE &1 FILE6 A
***ERASE &1 FILE30 A4
COPYFILE &1 OPTVEC A4 = FILE30  = (REPLACE
COPYFILE &1 GOOD15 A  = FILE15  = (REPLACE
***ERASE &1 GOOD15 A
COPYFILE &1 GOOD17 A  = FILE17  = (REPLACE
***ERASE &1 GOOD17 A
COPYFILE &1 GOOD20 A  = FILE20  = (REPLACE
***ERASE &1 GOOD20 A
COPYFILE &1 GOODOUT A  = OUTPUT = (REPLACE
***ERASE &1 GOODOUT A
*
EXEC  WRIT24$ &1
*
EXEC  ANHARM$ &1
*
&EXIT
